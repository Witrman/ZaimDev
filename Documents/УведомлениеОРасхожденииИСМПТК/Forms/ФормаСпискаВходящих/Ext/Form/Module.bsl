////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьКнопокИзмененияСтатуса();
	
	Если Параметры.Отбор.Свойство("Организация") Тогда
		Организация = Параметры.Отбор.Организация;
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		ЭтаФорма.Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ИнтеграцияИСМПТККлиентСервер.ИмяСобытияЗаписьУведомлениеОРасхождении() Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)	
	
	УстановитьБыстрыйОтбор("Организация", Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	УстановитьБыстрыйОтбор("Контрагент", Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПриИзменении(Элемент)
	
	УстановитьБыстрыйОтбор("Состояние", Состояние);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УстановитьВидимостьКнопокИзмененияСтатуса();
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокДокументУОРОбновить", "Доступность", Ложь);
	Иначе
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокДокументУОРОбновить", "Доступность", Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура УстановитьБыстрыйОтбор(ВидЭлемента, ЗначениеЭлемента)
	
	ИнтеграцияИСМПТККлиентСерверПереопределяемый.ИзменитьЭлементОтбораСписка(Список, ВидЭлемента, ЗначениеЭлемента, ЗначениеЗаполнено(ЗначениеЭлемента));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКнопокИзмененияСтатуса()
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() > 0 Тогда		
		
		ПервыйВыделенныйУОР = Элементы.Список.ВыделенныеСтроки[0];
		СоответвствиеСтатусов = ИнтеграцияИСМПТК.РазрешенныеДействияПоСтатусамУведомленияОРасхождении(ПервыйВыделенныйУОР.Направление);
		УстановитьВидимостьКнопки(СоответвствиеСтатусов, "СписокУОРПодтвердить", ПервыйВыделенныйУОР, ИнтеграцияИСМПТККлиентСервер.ДействиеПодтверждение());
		УстановитьВидимостьКнопки(СоответвствиеСтатусов, "СписокУОРОтклонить",   ПервыйВыделенныйУОР, ИнтеграцияИСМПТККлиентСервер.ДействиеОтклонение());
		
	Иначе
		
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокУОРПодтвердить",  "Видимость", Ложь);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокУОРОтклонить",  "Видимость", Ложь);
		
	КонецЕсли;
	
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокУОРОтправить",  "Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Функция УстановитьВидимостьКнопки(СоответвствиеСтатусов, НазваниеКнопки, ПервыйВыделенныйУОР, Действие) 
	
	ВидимостьКнопки = СоответвствиеСтатусов[Действие][ПервыйВыделенныйУОР.Статус];
	Если ВидимостьКнопки <> Неопределено Тогда
	
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, НазваниеКнопки, "Видимость", ВидимостьКнопки);
	
	Иначе
	
		ТекстСообщения = НСтр("ru='Тех. ошибка! Для действия %1 и статуса Уведомления о расхождениях %2 не указано соответствие для отображения кнопки.'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, ИнтеграцияИСМПТККлиентСервер.ДействиеОтзыв(), ПервыйВыделенныйУОР.Статус);
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	
	КонецЕсли;
		
КонецФункции


