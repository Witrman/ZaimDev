
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.РегНомерНаБумажномНосителе) 
		ИЛИ Объект.ДатаВыпискиНаБумажномНосителе <> '00010101000000' Тогда
		ВыпискаБумажногоАкта = Истина;
	Иначе
		ВыпискаБумажногоАкта = Ложь;
	КонецЕсли;
	
	РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйВызовСервера.ПриСозданииНаСервере_НастроитьПодключаемоеОборудование(ЭтаФорма);
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
		ИнтерфейсИСМПТК.УстановитьЗаголовокФормыДокумента(Строка(Объект.ВидОперации), Объект.Ссылка, ЭтаФорма);
		Объект.НомерИСМПТ = "";
		Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") Тогда
			//Требуется сверка полученных товаров и данных Акта
			Объект.Проверен = Ложь;
		Иначе
			Объект.Проверен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") 
		И ПравоДоступа("Просмотр", Метаданные.Документы.УведомлениеОВыводеИзОборотаИСМПТК) Тогда
		СписаниеКМ = ПолучитьУВИО();
	КонецЕсли;
	
	СобытияФормИСМПТКПереопределяемый.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	МенеджерОборудованияКлиентПереопределяемый.НачатьПодключениеОборудованиеПриОткрытииФормы(ЭтаФорма, "СканерШтрихкода");
	СформироватьДеревоКодовМаркировки();
	ЗапретитьРедактированиеПолей();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы И ИспользуютсяСканерыШтрихкода Тогда
		МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	ИнтерфейсИСМПТК.УстановитьЗаголовокФормыДокумента(Строка(Объект.ВидОперации), Объект.Ссылка, ЭтаФорма);
	
	СобытияФормИСМПТКПереопределяемый.ПриЧтенииНаСервере(ТекущийОбъект, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	//Запоняем признак исправленного документа для передачи на сервер ИС МПТ
	Если Объект.ТипАкта = ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исправленный") Тогда
		Объект.ЭтоКорректировка = Истина;
	Иначе
		Объект.ЭтоКорректировка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИнтеграцияИСМПТККлиент.ОповеститьФормы(ИнтеграцияИСМПТККлиентСервер.ИмяСобытияЗаписьАктПриемаПередачи());
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УправлениеФормой();
	ИнтерфейсИСМПТК.УстановитьЗаголовокФормыДокумента(Строка(Объект.ВидОперации), Объект.Ссылка, ЭтаФорма);
	СобытияФормИСМПТКПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ИзменитьДокументОснование = Ложь;
	
	//Сначала проверяем, если ли в выбранном основании товары, по которым необходимо заполнять Акт
	ЕстьМаркируемыйТовар = ПроверитьНаличиеМаркируемогоТовара(ВыбранноеЗначение);
	
	Если Не ЕстьМаркируемыйТовар Тогда
		//Если в выбранном основании нет маркируемого товара, то заполнять Акт по нему не имеет смысла. 
		//Сообщаем пользователю, отменяем выбор.
		ТекстСообщения = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияВДокументеОтсутствуетМаркируемаяпродукция();
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДокументОснование%", ВыбранноеЗначение);
		Сообщить(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		
		Если ЗначениеЗаполнено(Объект.ДокументОснование)
			И Объект.ДокументОснование <> ВыбранноеЗначение 
			И СтрНайти(ИсточникВыбора.ИмяФормы, "ФормаВыбораИзКлассификатора") = 0 Тогда
			
			ТекстВопроса = НСтр("ru = 'Документ уже отражен в учете с помощью ""%СтарыйДокументОснование%"".
								|Отразить документ в учете с помощью выбранного документа?'");
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%СтарыйДокументОснование%", Объект.ДокументОснование);
			
			ИзменитьДокументОснованиеЗавершение = Новый ОписаниеОповещения("ИзменитьДокументОснованиеЗавершение", ЭтаФорма, Новый Структура("ВыбранноеЗначение", ВыбранноеЗначение));
			ПоказатьВопрос(ИзменитьДокументОснованиеЗавершение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
			
		Иначе
			
			ИзменитьДокументОснование = Истина;
			
		КонецЕсли;
		
		Если ИзменитьДокументОснование Тогда
			
			Структура_СвязанныйАкт = ПроверитьСвязанныйАкт(ВыбранноеЗначение);
			
			Если Структура_СвязанныйАкт = Неопределено Тогда
				//Если у выбранного документа-основания нет связанного Акта, заполняем его без вопросов
				Объект.ДокументОснование = ВыбранноеЗначение;
				СформироватьПредставлениеДокументаОснования(ЭтаФорма);
				Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") 
					И Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик") Тогда
					ТекстВопроса = НСтр("ru = 'Перезаполнить Акт по выбранному основанию?'");
					ПерезаполнитьДокументПоОснованию = Новый ОписаниеОповещения("ПерезаполнитьДокументПоОснованию", ЭтаФорма);
					ПоказатьВопрос(ПерезаполнитьДокументПоОснованию, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
				Иначе 
					УправлениеФормой();
				КонецЕсли;
				Модифицированность = Истина;
			Иначе
				//Если связанный Акт есть, предупреждаем об этом пользователя и даем возможность отказаться от выбора
				ТекстВопросаВыборОснования = НСтр("ru = 'На основании документа ""%ДокументОснование%"" уже введен Акт према/передачи. Привязать текущий Акт к выбранному документу без отсоединения ранее связанного Акта?'");
				ТекстВопросаВыборОснования = СтрЗаменить(ТекстВопросаВыборОснования, "%ДокументОснование%", ВыбранноеЗначение);
				
				ПродолжитьЗаполнениеДокументаОснования = Новый ОписаниеОповещения("ПродолжитьЗаполнениеДокументаОснования", ЭтаФорма, Новый Структура("ВыбранноеЗначение", ВыбранноеЗначение));
				ПоказатьВопрос(ПродолжитьЗаполнениеДокументаОснования, ТекстВопросаВыборОснования, РежимДиалогаВопрос.ОКОтмена);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ИнтеграцияИСМПТККлиентСервер.ИмяСобытияЗаписьАктПриемаПередачи() Тогда		
		
		Если Не Объект.Ссылка.Пустая() Тогда
			ЭтаФорма.Прочитать();	
		КонецЕсли;
		
		СформироватьДеревоКодовМаркировки();
		ЗапретитьРедактированиеПолей();
		
	ИначеЕсли ИмяСобытия = ИнтеграцияИСМПТККлиентСервер.ИмяСобытияЗаписьУведомлениеОРасхождении() Тогда		
		
		ОбработкаОповещенияЗаписиУОРНаСервере();
		
	ИначеЕсли Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			ОбработкаСобытияСканераОбщая(Параметр);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗакрытиеФормыСверкиТоваров" Тогда
		
		ПолучитьСтруктурыРасхожденийИзХранилище(Параметр);
				
	ИначеЕсли ИмяСобытия = "Запись_УведомлениеОВыводеИзОборота" Тогда
		
		СписаниеКМ = ПолучитьУВИО();
				
	Иначе
		ИнтеграцияИСМПТККлиентПереопределяемый.ОбработкаОповещенияДокументыИСМПТ(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);		
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Не СобытияФормИСМПТККлиентПереопределяемый.ОбрабатыватьВнешнееСобытиеТО() Тогда
		Возврат;
	КонецЕсли;
	
	Параметр = Новый Массив;
	Параметр.Добавить(Данные);
	Параметр.Добавить(Неопределено);
	
	Если ВводДоступен() Тогда
		Если Событие = "Штрихкод" ИЛИ Событие = "ПолученШтрихкод" Тогда
			ОбработкаСобытияСканераОбщая(Параметр);
		КонецЕсли;
	Иначе			
		ИнтеграцияИСМПТККлиентПереопределяемый.ОбработкаОповещенияДокументыИСМПТ(ЭтаФорма, Объект.Ссылка, Событие, Параметр, Источник);		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
		
	Если ВыпискаБумажногоАкта И Не ЗначениеЗаполнено(Объект.ДатаВыпискиНаБумажномНосителе) Тогда
		ТекстСообщения = НСтр("ru = 'A. Не указана дата Акта, выписанного на бумаге.'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если ВыпискаБумажногоАкта И Не ЗначениеЗаполнено(Объект.РегНомерНаБумажномНосителе) 
		И Объект.ДатаВыпискиНаБумажномНосителе = '00010101000000' Тогда
		ТекстСообщения = НСтр("ru = 'A. Не указан номер Акта, выписанного на бумаге.'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаСобытияСканераОбщая(Параметр)
	
	ОчиститьСообщения();
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") Тогда 
		ТекстСообщения = НСтр("ru = 'Корректировка кодов идентификации входящего документа невозможна. Воспользуйтесь формой сверки кодов идентификации.'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	Иначе
		Если НЕ Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик") Тогда
			ТекстСообщения = НСтр("ru = 'Добавление кодов маркировки возможно только в документе со статусом Черновик.'");
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);	
		Иначе
			Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() 
				И Не ЗначениеЗаполнено(Объект.Организация) Тогда
				ТекстСообщения = НСтр("ru = 'Включена опция автоматической проверки состояния кодов маркировки по данным сервера. Для получения токена сеанса требуется указать Организацию!'");
				ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
				Возврат;
			КонецЕсли;
			Если Не ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПроверкаФорматаТранспортногоКодаПройденаУспешно(РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.ПреобразоватьДанныеСоСканераВСтруктуру(Параметр)) Тогда
				Возврат;
			КонецЕсли;			
			ОбработатьКодМаркировки(РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.ПреобразоватьДанныеСоСканераВСтруктуру(Параметр));
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ОтражениеВУчете

&НаКлиенте
Процедура ПредставлениеАктНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ПоказатьЗначение(, Объект.ДокументОснование);
	Иначе
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("Создать", НСтр("ru = 'Создать'"));
		Кнопки.Добавить("Выбрать", НСтр("ru = 'Выбрать'"));
		Кнопки.Добавить("Отмена",  НСтр("ru = 'Отмена'"));
		ОтразитьДокументВУчетеЗавершение = Новый ОписаниеОповещения("ОтразитьДокументВУчетеЗавершение", ЭтаФорма);
		ПоказатьВопрос(ОтразитьДокументВУчетеЗавершение, НСтр("ru = 'Отразить документ в учете?'"), Кнопки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьДокументВУчетеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = "Выбрать" Тогда
		ВыбратьДокументОснование();
	ИначеЕсли РезультатВопроса = "Создать" Тогда
		СоздатьДокументОснованиеНаКлиентеПослеЗаписи(КодВозвратаДиалога.ОК, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеВыбрать(Команда)
	
	ВыбратьДокументОснование();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДокументОснование()
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.ПустаяСсылка") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указано направление документа.'"));
		Возврат;
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСМПТК.ПустаяСсылка") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан вид операции документа.'"));
		Возврат;
	КонецЕсли;
	
	ДокументыВыбора = ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПолучитьСписокДокументовОснованияДляАктаПриемаПередачи(Объект.Направление, Объект.ВидОперации, "Выбрать");
	
	Если ДокументыВыбора.Количество() > 1 Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ДокументыВыбора", ДокументыВыбора);
					
		Оповещение = Новый ОписаниеОповещения("ВыборДокументаОснованияЗавершение", ЭтотОбъект);
		ОткрытьФорму("Обработка.ОбменИСМПТК.Форма.ФормаВыбораДокументаОснования", ПараметрыФормы,,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ДокументыВыбора.Количество() = 1 Тогда
		
		ВыбранныйДокумент = ДокументыВыбора[0].Значение;
		ВыборДокументаОснованияЗавершение(ВыбранныйДокумент, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборДокументаОснованияЗавершение(ВыбранныйДокумент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйДокумент <> "" И ТипЗнч(ВыбранныйДокумент) <> Тип("Неопределено") Тогда
		
		ИмяДокумента = ВыбранныйДокумент;
		
		СтруктураОтбора = Новый Структура();
		Если ЗначениеЗаполнено(Объект.Организация) Тогда
			СтруктураОтбора.Вставить("Организация", Объект.Организация);
		КонецЕсли;
		Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") Тогда
			Если ЗначениеЗаполнено(Объект.Поставщик) Тогда
				СтруктураОтбора.Вставить("Контрагент", Объект.Поставщик);
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Объект.Получатель) Тогда
				СтруктураОтбора.Вставить("Контрагент", Объект.Получатель);
			КонецЕсли;
		КонецЕсли;
		
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.ДополнитьЗначенияОтбораПриВыбореДокументаОснованияДляАПП(СтруктураОтбора, ИмяДокумента, Объект.Направление, Объект.ВидОперации);
		СтруктураПараметров = Новый Структура("Отбор, РежимВыбора, МножественныйВыбор", СтруктураОтбора, Истина, Ложь);
		
		ИмяФормыВыбора = "Документ." + ИмяДокумента + ".ФормаВыбора"; 
		ОткрытьФорму(ИмяФормыВыбора, СтруктураПараметров , ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументОснование(Команда)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ПоказатьЗначение(, Объект.ДокументОснование);
	Иначе
		ПоказатьПредупреждение(, РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияОтказДокументНеОтраженВУчете());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтсоединитьДокументОснование(Команда)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Объект.ДокументОснование = Неопределено;
		СформироватьПредставлениеДокументаОснования(ЭтаФорма);
		Модифицированность = Истина;
	Иначе
		ПоказатьПредупреждение(, РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияОтказДокументНеОтраженВУчете());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеСоздать(Команда)
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") Тогда
		НужноСообщитьОНезаполненнойНоменклатуре =  ПроверитьЗаполненностьНоменклатурыТоварахВоВходящемАПП();
		Если НужноСообщитьОНезаполненнойНоменклатуре Тогда
			ТекстСообщения = НСтр("ru = 'В таблице Марки есть строки с не определенной Номенклатурой! Создание документа-основания невозможно! Сначала зарегистрируйте все неопознанные штрихкоды номенклатуры или получите с сервера состав упаковок.'");
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() ИЛИ ЭтаФорма.Модифицированность Тогда
		
		СоздатьДокументОснованиеНаКлиентеПослеЗаписи = Новый ОписаниеОповещения("СоздатьДокументОснованиеНаКлиентеПослеЗаписи", ЭтаФорма);	
		ИнтеграцияИСМПТККлиент.ВопросЗаписатьОбъектПередВыполнением(СоздатьДокументОснованиеНаКлиентеПослеЗаписи);
		
	Иначе
		СоздатьДокументОснованиеНаКлиентеПослеЗаписи(КодВозвратаДиалога.ОК, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументОснованиеНаКлиентеПослеЗаписи(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		
		ДокументыВыбора = ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПолучитьСписокДокументовОснованияДляАктаПриемаПередачи(Объект.Направление, Объект.ВидОперации, "Создать");
		
		Если ДокументыВыбора.Количество() > 1 Тогда
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("ДокументыВыбора", ДокументыВыбора);
			
			Оповещение = Новый ОписаниеОповещения("СозданиеДокументаОснованияЗавершение", ЭтотОбъект);
			ОткрытьФорму("Обработка.ОбменИСМПТК.Форма.ФормаВыбораДокументаОснования", ПараметрыФормы,,,,,Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		ИначеЕсли ДокументыВыбора.Количество() = 1 Тогда
			
			ВыбранныйДокумент = ДокументыВыбора[0].Значение;
			СозданиеДокументаОснованияЗавершение(ВыбранныйДокумент, Неопределено);
			
		КонецЕсли;
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура СозданиеДокументаОснованияЗавершение(ВыбранныйДокумент, ДополнительныеПараметры) Экспорт
	
	Если Не ВыбранныйДокумент = Неопределено Тогда
		Массив = Новый Массив;
		Массив.Добавить(Объект.Ссылка);
		
		ДанныеДляСоздания = Новый Структура();
		ДанныеДляСоздания.Вставить("ИсходныйДокумент", 	  Массив);
		ДанныеДляСоздания.Вставить("СоздаваемыйДокумент", ВыбранныйДокумент);

		ИнтеграцияИСМПТККлиент.СоздатьПервичныйДокумент(ДанныеДляСоздания);
		СформироватьПредставлениеДокументаОснования(ЭтаФорма);
		Модифицированность = Истина;
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область СостояниеДокумента

&НаКлиенте
Процедура ПроверенПриИзменении(Элемент)
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") 
		И Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.ОжидаетПриемку") Тогда
		Объект.Состояние = ?(Объект.Проведен, ПредопределенноеЗначение("Перечисление.СостоянияДокументовИСМПТК.ПринятПроверен"), ПредопределенноеЗначение("Перечисление.СостоянияДокументовИСМПТК.ПринятОтПоставщика"));
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеСостояния()
	
	Если Объект.Направление = Перечисления.НаправленияДокументовИСМПТК.Исходящий
		И Объект.Состояние = Перечисления.СостоянияДокументовИСМПТК.Сформирован Тогда
		
		ПредставлениеСостояния = НСтр("ru = 'Документ готов к отправке получателю через ИС МПТ'");
		
	ИначеЕсли Объект.Направление = Перечисления.НаправленияДокументовИСМПТК.Входящий
		И Объект.Состояние = Перечисления.СостоянияДокументовИСМПТК.ПринятПроверен Тогда
		
		ПредставлениеСостояния = НСтр("ru = 'Документ принят от поставщика'");
		
	ИначеЕсли Объект.Направление = Перечисления.НаправленияДокументовИСМПТК.Исходящий
		И (Объект.Состояние = Перечисления.СостоянияДокументовИСМПТК.ПринятСервером
		ИЛИ Объект.Состояние = Перечисления.СостоянияДокументовИСМПТК.ДоставленПолучателю)Тогда
		
		ПредставлениеСостояния = НСтр("ru = 'Документ успешно принят сервером ИС МПТ'");
		
	ИначеЕсли Объект.Состояние = Перечисления.СостоянияДокументовИСМПТК.ОбрабатываетсяСервером Тогда
		
		ПредставлениеСостояния = НСтр("ru = 'Документ обрабатывается сервером ИС МПТ'");
		
	ИначеЕсли Объект.Состояние = Перечисления.СостоянияДокументовИСМПТК.ОтклоненСервером Тогда
		
		ПредставлениеСостояния = НСтр("ru = 'Документ отклонен сервером ИС МПТ, т.к. содержит ошибки'");
		
	ИначеЕсли Объект.Состояние = Перечисления.СостоянияДокументовИСМПТК.Отозван
		ИЛИ Объект.Состояние = Перечисления.СостоянияДокументовИСМПТК.Аннулирован Тогда
		
		ПредставлениеСостояния = НСтр("ru = 'Документ %Состояние%'");
		ПредставлениеСостояния = СтрЗаменить(ПредставлениеСостояния, "%Состояние%", НРег(Объект.Состояние));
				
	ИначеЕсли Объект.Состояние = Перечисления.СостоянияДокументовИСМПТК.Сформирован
		И ВыпискаБумажногоАкта И Не Объект.Ссылка.Пустая() Тогда
		
		ПредставлениеСостояния = НСтр("ru = 'Документ выписан в бумажном виде. Ожидает отправки на сервер ИС МПТ'");
		
	Иначе
		
		Если Объект.Ссылка.Пустая() Тогда
			ПредставлениеСостояния = НСтр("ru = 'Новый документ'");
		Иначе
			ПредставлениеСостояния = НСтр("ru = 'Документ %Состояние%'");
			ПредставлениеСостояния = СтрЗаменить(ПредставлениеСостояния, "%Состояние%", НРег(Объект.Состояние));
		КонецЕсли;
		
	КонецЕсли;
	
	//Если документ Входящий и по нему выполнена сверка реально полученного товара с данными ИС МПТ,
	//т.е. установлен признак проверки на вкладке Товары, дополняем состояние:
	Если Объект.Направление = Перечисления.НаправленияДокументовИСМПТК.Входящий
		И Объект.Проверен 
		И Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.ОжидаетПриемку") Тогда
		ПредставлениеСостояния = ПредставлениеСостояния + НСтр("ru = ', выполнена проверка товаров'");
	КонецЕсли;
	
	Элементы.ПредставлениеСостояния.ЦветТекста = ИнтеграцияИСМПТККлиентСервер.ЦветСостояния(Объект.Состояние);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСостоянияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий")
		И Объект.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияДокументовИСМПТК.Сформирован") Тогда
		
		ОтправитьПолучателюЗаписьФормы = Новый ОписаниеОповещения("ОтправитьПолучателюЗаписьФормы", ЭтаФорма);
		ПоказатьВопрос(ОтправитьПолучателюЗаписьФормы, НСтр("ru = 'Отправить получателю?'"), РежимДиалогаВопрос.ОКОтмена);
		
	ИначеЕсли Объект.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияДокументовИСМПТК.ОбрабатываетсяСервером") Тогда	
		
		ОбновитьНаСервереЗавершение = Новый ОписаниеОповещения("ОбновитьНаСервереЗавершение", ЭтаФорма);
		ПоказатьВопрос(ОбновитьНаСервереЗавершение, НСтр("ru = 'Получить результат обработки документа из ИС МПТ?'"), РежимДиалогаВопрос.ОКОтмена);
		
	Иначе
		ПоказатьПредупреждение(, ПредставлениеСостояния);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПолучателюЗаписьФормы(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		Если Объект.Ссылка.Пустая() ИЛИ ЭтаФорма.Модифицированность Тогда
			ОтправитьПолучателюЗавершение = Новый ОписаниеОповещения("ОтправитьПолучателюЗавершение", ЭтаФорма);	
			ИнтеграцияИСМПТККлиент.ВопросЗаписатьОбъектПередВыполнением(ОтправитьПолучателюЗавершение,, "Записать");
		Иначе
			ОтправитьПолучателюЗавершение(КодВозвратаДиалога.ОК, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПолучателюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		Если ИнтеграцияИСМПТККлиент.ОбъектЗаписан(ЭтаФорма) Тогда
			МассивАктов = Новый Массив;
			МассивАктов.Добавить(Объект.Ссылка);
			ИнтеграцияИСМПТККлиент.ОтправитьИсходящиеДокументыИСМПТ(МассивАктов, Новый Структура("ЗапускатьФоновоеЗадание", Ложь));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаСервереЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		Если ИнтеграцияИСМПТККлиент.ОбъектЗаписан(ЭтаФорма) Тогда
			МассивАктов = Новый Массив;
			МассивАктов.Добавить(Объект.Ссылка);
			ИнтеграцияИСМПТККлиент.ОбновитьДокументыИзИСМПТ(МассивАктов, Новый Структура("ЗапускатьФоновоеЗадание, ТолькоОбновитьСтатус", Ложь, Истина));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УведомлениеОРасхождении

&НаКлиенте
Процедура ПредставлениеУведомлениеОРасхожденииНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.УведомлениеОРасхождении) Тогда
		
		Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") Тогда
			
			СписокДействийУОР = Новый СписокЗначений();
			СписокДействийУОР.Добавить("ОткрытьСуществующийДокумент", "Открыть");
			СписокДействийУОР.Добавить("ОтвязатьДокумент", "Отвязать");
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("УОР", Объект.УведомлениеОРасхождении);
			ДополнительныеПараметры.Вставить("Основание", ЭтаФорма.Параметры.Ключ);
			ДополнительныеПараметры.Вставить("Владелец", ЭтаФорма);
			
			Оповещение = Новый ОписаниеОповещения("ПослеВыбораПредставлениеУОР", ЭтотОбъект, ДополнительныеПараметры);
			ЭтаФорма.ПоказатьВыборИзМеню(Оповещение, СписокДействийУОР);
			
		Иначе
			ПоказатьЗначение(,Объект.УведомлениеОРасхождении);
		КонецЕсли;
		
	КонецЕсли;
				
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПредставлениеУОР(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Значение = "ОткрытьСуществующийДокумент" Тогда
		ПоказатьЗначение(,Объект.УведомлениеОРасхождении);
	Иначе
		
		//Можно отвязать Уведомление только если оно еще не отправлено в ИС МПТ
		МожноОтвязатьУведомление = ИнтеграцияИСМПТКВызовСервера.ПроверитьСтатусУведомления(Объект.УведомлениеОРасхождении);
		
		Если Не МожноОтвязатьУведомление Тогда
			ТекстСообщения = НСтр("ru = 'Уведомление можно отвязать только до его отправки в ИС МПТ.'");
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		    Возврат;
		КонецЕсли;
		
		//Разрываем связь между АПП и УОР
		РазорватьСвязьАППиУОРНаСервере();
		ИнтеграцияИСМПТККлиент.ОповеститьФормы("Отвязать_УОР");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РазорватьСвязьАППиУОРНаСервере()
	
	УОР = Объект.УведомлениеОРасхождении.ПолучитьОбъект();
	УОР.ДокументОснование = Неопределено;
	УОР.СвязанныйАкт = Неопределено;
	УОР.Записать();
		
	Объект.УведомлениеОРасхождении = Неопределено;
	СформироватьПредставлениеУведомленияОРасхождении(ЭтаФорма);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СобытияФормИСМПТККлиентПереопределяемый.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");

КонецПроцедуры

#Область Общее

&НаКлиенте
Процедура СвязанныйАктПриИзменении(Элемент)
	
	СвязанныйАктПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СвязанныйАктПриИзмененииНаСервере()
	
	Объект.РегистрационныйНомерСвязанногоАкта = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.ЗначениеРеквизитаОбъекта(Объект.СвязанныйАкт, "НомерИСМПТ");
			
КонецПроцедуры

&НаКлиенте
Процедура СвязанныйАктНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормыВыбора = Новый Структура("РежимВыбора", Истина);
	Отбор = Новый Структура;
			
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
		//Выбираемый АПП - исходный, оформояем корректировку:
		Отбор.Вставить("Направление", ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий"));
		Отбор.Вставить("ТипАкта", 	  ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исходный"));
		Отбор.Вставить("Получатель",  ?(ЗначениеЗаполнено(Объект.Контрагент),  Объект.Контрагент,  ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка")));
		Отбор.Вставить("Поставщик",   ?(ЗначениеЗаполнено(Объект.Организация), Объект.Организация, ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка")));
		
	Иначе
		
		Отбор.Вставить("Направление", ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий"));
		Отбор.Вставить("Поставщик",  ?(ЗначениеЗаполнено(Объект.Организация), Объект.Организация, ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка")));
		Отбор.Вставить("Получатель", ?(ЗначениеЗаполнено(Объект.Контрагент),  Объект.Контрагент,  ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка")));
		
	КонецЕсли;
	
	Если Отбор.Количество() <> 0 Тогда
		
		ПараметрыФормыВыбора.Вставить("Отбор", Отбор);
		
		ОткрытьФорму("Документ.АктПриемаПередачиИСМПТК.ФормаВыбора", ПараметрыФормыВыбора, Элемент);
		СтандартнаяОбработка = Ложь;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипАктаПриИзменении(Элемент)
	
	ТипАктаПриИзмененииНаСервере();	
	
КонецПроцедуры

&НаСервере
Процедура ТипАктаПриИзменениинаСервере()
	
	Объект.ЭтоКорректировка = ?(Объект.ТипАкта = ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исправленный"), Истина, Ложь);
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыпискаБумажногоАктаПриИзменении(Элемент)
	
	Если Не ВыпискаБумажногоАкта Тогда
		Объект.РегНомерНаБумажномНосителе = "";
		Объект.ДатаВыпискиНаБумажномНосителе = '00010101000000';
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область Поставщик

&НаСервере
Процедура ПоставщикПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.НомерИСМПТ) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = ПолучитьРеквизитыОрганизацииКонтрагента(Объект.Поставщик, "Поставщик");
	ЗаполнитьЗначенияСвойств(Объект, СтруктураДанных, "ПоставщикИдентификационныйНомер, ПоставщикНаименование");
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
		Объект.Организация = Объект.Поставщик;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикПриИзменении(Элемент)
	
	ПоставщикПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область Получатель

&НаСервере
Процедура ПолучательПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.НомерИСМПТ) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = ПолучитьРеквизитыОрганизацииКонтрагента(Объект.Получатель, "Получатель");
	ЗаполнитьЗначенияСвойств(Объект, СтруктураДанных, "ПолучательИдентификационныйНомер, ПолучательНаименование");
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") Тогда 
		Объект.Организация = Объект.Получатель;
	Иначе
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСМПТК.ВвозИзЕАЭС") 
			Или Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСМПТК.ВвозИзТретьихСтран") Тогда
			Объект.Организация = Объект.Получатель;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	
	ПолучательПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ЗаполнитьЗначенияПоОрганизации()
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") Тогда
		
		Объект.Получатель = Объект.Организация;
						
	ИначеЕсли Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
		
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСМПТК.ВвозИзЕАЭС") 
			Или Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСМПТК.ВвозИзТретьихСтран") Тогда
			Объект.Получатель = Объект.Организация;
		Иначе
			Объект.Поставщик  = Объект.Организация;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.НомерИСМПТ) Тогда 
			СтруктураДанных = ПолучитьРеквизитыОрганизацииКонтрагента(Объект.Поставщик, "Поставщик");
			ЗаполнитьЗначенияСвойств(Объект, СтруктураДанных, "ПоставщикИдентификационныйНомер, ПоставщикНаименование");
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Направление) Тогда
		ЗаполнитьЗначенияПоОрганизации();
	КонецЕсли;
	   
КонецПроцедуры

&НаСервере
Процедура НаправлениеПриИзмененииНаСервере()
	
	Если Объект.Направление = Перечисления.НаправленияДокументовИСМПТК.Исходящий Тогда
		
		Объект.Получатель = Объект.Поставщик;
		Объект.Контрагент = Объект.Поставщик;
		Объект.Поставщик = Объект.Организация;
		
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодменюПоставщик", "Видимость", Ложь);
		
		Объект.ПолучательИдентификационныйНомер = Объект.ПоставщикИдентификационныйНомер;
		Объект.ПолучательНаименование  			= Объект.ПоставщикНаименование;
		СтруктураДанных = ПолучитьРеквизитыОрганизацииКонтрагента(Объект.Организация, "Поставщик");
		ЗаполнитьЗначенияСвойств(Объект, СтруктураДанных, "ПоставщикИдентификационныйНомер, ПоставщикНаименование");
				
	ИначеЕсли Объект.Направление = Перечисления.НаправленияДокументовИСМПТК.Входящий Тогда
		
		Объект.Поставщик = Объект.Получатель;
		Объект.Контрагент = Объект.Получатель;
		Объект.Получатель = Объект.Организация;
		
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодменюПоставщик", "Видимость", Истина);
		
		Объект.ПоставщикИдентификационныйНомер = Объект.ПолучательИдентификационныйНомер;
		Объект.ПоставщикНаименование 		   = Объект.ПолучательНаименование;
		СтруктураДанных = ПолучитьРеквизитыОрганизацииКонтрагента(Объект.Организация, "Получатель");
		ЗаполнитьЗначенияСвойств(Объект, СтруктураДанных, "ПолучательИдентификационныйНомер, ПолучательНаименование");
			
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура НаправлениеПриИзменении(Элемент)
	
	НаправлениеПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

#Область Товары

&НаКлиенте
Процедура ПослеЗакрытияВопросаЗаписатьШтрихкодНоменклатуры(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
		
	Попытка
		
		Номенклатура = Параметры.Номенклатура;
		EAN 		 = Параметры.EAN;
		GTIN         = Параметры.GTIN;
		ЕдиницаИзмерения = Параметры.ЕдиницаИзмерения;
		Характеристика	 = Параметры.Характеристика;
		
		ИнтеграцияИСМПТКПереопределяемый.ЗаписатьШтрихкодНоменклатуры(Номенклатура, EAN, ЕдиницаИзмерения, Характеристика);
		ЗаписатьНоменклатуруВМарки(Номенклатура, GTIN);
		ТекстСообщения = НСтр("ru='Для корректного отображения данных в таблице Марки необходимо записать документ.'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, , , "Форма.Объект");
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗарегистрироватьНовыйGTIN", "Доступность", Ложь);
		
	Исключение
		
		Информация = ИнформацияОбОшибке();
		ТекстСообщения = НСтр("ru='Не удалось записать штрихкод номенклатуры по причине: %1'");
		ТекстСообщения = ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, КраткоеПредставлениеОшибки(Информация));
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, , , "Форма.Объект");
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗарегистрироватьНовыйGTIN", "Доступность", Истина);
		
	КонецПопытки;

КонецПроцедуры

&НаСервере
Функция ЗаписатьНоменклатуруВМарки(Номенклатура, GTIN)
	
	Отбор = Новый Структура();
	Отбор.Вставить("GTIN", GTIN);
	
	ВидПродукции = ИнтеграцияИСМПТКПереопределяемый.ПолучитьВидПродукцииПоНоменклатуре(Номенклатура);
	
	Строки = Объект.Марки.НайтиСтроки(Отбор);
	Если Строки.Количество() > 0 Тогда
		Для Каждого Строка Из Строки Цикл
			Строка.Номенклатура   = Номенклатура;
			Строка.ВидПродукцииИС = ВидПродукции;
		КонецЦикла;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПроверитьНаличиеШтрихкода(Номенклатура, EAN)
	
	Возврат ИнтеграцияИСМПТКПереопределяемый.ПроверитьНаличиеШтрихкодаУноменклатуры(Номенклатура, EAN);
	
КонецФункции

&НаСервере
Функция ПолучитьСведенияОНоменклатуре(Номенклатура)
	
	Возврат ИнтеграцияИСМПТКПереопределяемый.ПолучитьСведенияОНоменклатуре(Номенклатура);
	
КонецФункции

&НаКлиенте
Процедура ДеревоМаркированнойПродукцииНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Выполнение = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область УведомлениеОРасхождении

&НаКлиенте
Процедура ВвестиУведомлениеОРасхождении(Команда)
	
	ТекстВопроса = "";
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Параметры.Ключ) ИЛИ ЭтаФорма.Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Уведомление нельзя вводить на основании не записанного документа. Записать Акт?'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		
		ДополнительныеПараметры = Новый Структура("Форма", ЭтаФорма);
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение   = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаписьПриСозданииУведомленияОРасхождениях", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Основание", Объект.Ссылка);
		ОткрытьФорму("Документ.УведомлениеОРасхожденииИСМПТК.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаЗаписьПриСозданииУведомленияОРасхождениях(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Форма = Параметры.Форма;
	
	Попытка
		// выполняем запись документа
		ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись);
		
		Если Форма.Записать(ПараметрыЗаписи) Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Основание", Объект.Ссылка);
			ОткрытьФорму("Документ.УведомлениеОРасхожденииИСМПТК.ФормаОбъекта", ПараметрыФормы);
		КонецЕсли;
		
	Исключение
		
		Информация = ИнформацияОбОшибке();
		ТекстСообщения = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияНеУдалосьЗаписатьДокументМаркировки();
		ТекстСообщения = ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, КраткоеПредставлениеОшибки(Информация));
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, , , "Форма.Объект");
		
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область УведомлениеОВыводеИзОборота

&НаКлиенте
Процедура ВвестиУведомлениеОСписании(Команда)
	
	ТекстВопроса = "";
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Параметры.Ключ) ИЛИ ЭтаФорма.Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Уведомление нельзя вводить на основании не записанного документа. Записать Акт?'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		
		ДополнительныеПараметры = Новый Структура("Форма", ЭтаФорма);
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение   = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаписьПриСозданииУведомленияОВыбытии", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Основание", Объект.Ссылка);
		ОткрытьФорму("Документ.УведомлениеОВыводеИзОборотаИСМПТК.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаЗаписьПриСозданииУведомленияОВыбытии(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Форма = Параметры.Форма;
	
	Попытка
		// выполняем запись документа
		ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись);
		
		Если Форма.Записать(ПараметрыЗаписи) Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Основание", Объект.Ссылка);
			ОткрытьФорму("Документ.УведомлениеОВыводеИзОборотаИСМПТК.ФормаОбъекта", ПараметрыФормы);
		КонецЕсли;
		
	Исключение
		
		Информация = ИнформацияОбОшибке();
		ТекстСообщения = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияНеУдалосьЗаписатьДокументМаркировки();
		ТекстСообщения = ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, КраткоеПредставлениеОшибки(Информация));
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, , , "Форма.Объект");
		
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СверкаПолученныхКодовМаркировки(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	ТЗМарки = ПолучитьМассивИдентификаторов();
	ПараметрыФормы.Вставить("ТаблицаКодовМаркировки", ТЗМарки);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	
	ОткрытьФорму("Документ.АктПриемаПередачиИСМПТК.Форма.ФормаСверкиВходящихКодовМаркировки", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивИдентификаторов()
	Возврат ОбщегоНазначения.ВыгрузитьКолонку(Объект.Марки, "КодИдентификации");
КонецФункции

#Область ЗапросСоставаУпаковокССервера

&НаКлиенте
Процедура ЗаполнитьДанныеПоКоличествуГрупповыхУпаковок(Команда)
	
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик") Тогда
		//У не отправленного документа команда работает в полном функционале и без уточнения у пользователя
		ЗаполнитьДанныеПоКоличествуГрупповыхУпаковокПродолжить();
	Иначе
		//У отправленного - предварительно предупреждаем, что будут внесены программные изменения в документ, откатить которые нельзя, т.к. документ уже заблокирован.
		ТекстВопроса = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияЗапросСоставаУпаковокВОтправленномДокументе();
		ЗаполнитьДанныеПоКоличествуГрупповыхУпаковокПослеВопроса = Новый ОписаниеОповещения("ЗаполнитьДанныеПоКоличествуГрупповыхУпаковокПослеВопроса", ЭтаФорма);
		ПоказатьВопрос(ЗаполнитьДанныеПоКоличествуГрупповыхУпаковокПослеВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеПоКоличествуГрупповыхУпаковокПослеВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьДанныеПоКоличествуГрупповыхУпаковокПродолжить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеПоКоличествуГрупповыхУпаковокПродолжить()
	
	ЗавершениеПолучитьДанныеПоГорупповымКодамМаркировки = Новый ОписаниеОповещения("ЗавершениеПолучитьДанныеПоГорупповымКодамМаркировки", ЭтаФорма);
	ПолучитьКлючАвторизации(ЗавершениеПолучитьДанныеПоГорупповымКодамМаркировки);
	
КонецПроцедуры

&НаСервере
Процедура ЗавершениеПолучитьДанныеПоГорупповымКодамМаркировки(РезультатВыполнения, Параметры) Экспорт
	
	НеОтправлен = Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик");
	ИнтеграцияИСМПТК.ЗавершениеПолучитьДанныеПоГорупповымКодамМаркировки_Общая(ЭтаФорма, РезультатВыполнения, Объект.Направление, НеОтправлен);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПоискПоШтрихкодуВыполнить(Команда)
	
	ОчиститьСообщения();
	Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() 
		И Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Включена опция автоматической проверки состояния кодов маркировки по данным сервера. Для получения токена сеанса требуется указать Организацию!'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
			
	Оповещение = Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект);
	РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.ПоказатьВводШтрихкода(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(ДанныеШтрихкода, ДополнительныеПараметры) Экспорт
	
	Если Не ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПроверкаФорматаТранспортногоКодаПройденаУспешно(ДанныеШтрихкода) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьКодМаркировки(ДанныеШтрихкода);
	
КонецПроцедуры

&НаКлиенте
Процедура СверитьДанныеСДокументомОснованием(Команда)
	
	СопоставитьСДокументомОснованием();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеВТСД(Команда)
	
	ОчиститьСообщения();
	СобытияФормИСМПТККлиентПереопределяемый.ВыгрузитьДанныеВТСД(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	ОчиститьСообщения();
	Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() 
		И Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Включена опция автоматической проверки состояния кодов маркировки по данным сервера. Для получения токена сеанса требуется указать Организацию!'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоОпределениеЛогистическихКодовИСМПТК() Тогда 
		ПолучитьТокенДляЗакгрузкиИзТСД = Новый ОписаниеОповещения("ЗагрузитьИзТСДЗавершениеТокен", ЭтаФорма);
		ПолучитьКлючАвторизации(ПолучитьТокенДляЗакгрузкиИзТСД);
	Иначе
		МенеджерОборудованияКлиент.НачатьЗагрузкуДанныеИзТСД(Новый ОписаниеОповещения("ЗагрузитьИзТСДЗавершение", ЭтотОбъект), УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьНовыйGTIN(Команда)
	
	Номенклатура 	 = Элементы.Товары.ТекущиеДанные.Номенклатура;
	EAN 			 = Элементы.Товары.ТекущиеДанные.EAN;
	GTIN 			 = Элементы.Товары.ТекущиеДанные.GTIN;
	ЕдиницаИзмерения = Элементы.Товары.ТекущиеДанные.ЕдиницаИзмерения;
	Характеристика 	 = Элементы.Товары.ТекущиеДанные.Характеристика;
	
	Если Не ЗначениеЗаполнено(Характеристика) 
		И ИнтеграцияИСМПТКПереопределяемый.ПроверитьВедениеУчетаПоХарактеристикамУНоменклатуры(Номенклатура)
		И ИнтеграцияИСМПТКПереопределяемый.ПроверитьИспользованиеХарактеристик() Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнена Характеристика!'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	 	Возврат;
	КонецЕсли;
		
	ТекстВопроса = НСтр("ru = 'Добавить выбранной номенклатуре новый штрихкод?'");
	
	Дополнительныепараметры = Новый Структура();
	
	ДополнительныеПараметры.Вставить("Номенклатура",     Номенклатура);
	ДополнительныеПараметры.Вставить("EAN", 		     EAN);
	ДополнительныеПараметры.Вставить("GTIN", 		     GTIN);
	ДополнительныеПараметры.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	ДополнительныеПараметры.Вставить("Характеристика", 	 Характеристика);
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаписатьШтрихкодНоменклатуры", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьИзТСДЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	СобытияФормИСМПТККлиентПереопределяемый.ПриПолученииДанныхИзТСД(Новый ОписаниеОповещения("Подключаемый_ПолученыДанныеИзТСД", ЭтотОбъект), ЭтотОбъект, РезультатВыполнения);
	
	Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() Тогда 
		
		ТокенАвторизацииВрем = Неопределено;
		ЗавершениеПроверитьСостояниеКодовНаСервере = Новый ОписаниеОповещения("ЗавершениеПроверитьСостояниеКодовНаСервере", ЭтаФорма);
		ПолучитьКлючАвторизации(ЗавершениеПроверитьСостояниеКодовНаСервере);
		
		//Если по какой-то причине не удалось выполнить получение Токена, то и запрос на проверку статуса КМ не выполнялся,
		//об этом нужно предупредить и отметить непроверенные коды.
		Если ТокенАвторизацииВрем = Неопределено Тогда 
			СообщитьОНевозможностиВыполнитьПроверкуСтатусаКМ("Фоновый");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзТСДЗавершениеТокен(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	МенеджерОборудованияКлиент.НачатьЗагрузкуДанныеИзТСД(Новый ОписаниеОповещения("ЗагрузитьИзТСДЗавершение", ЭтотОбъект), УникальныйИдентификатор);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКлючАвторизации(СобытиеПослеАвторизации) Экспорт
	
	РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.ПолучитьКлючАвторизацииОбщая(СобытиеПослеАвторизации, Объект.Организация);
		
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтруктурыРасхожденийИзХранилище(РезультатВыбора)
	
	СтруктурыРасхожденийИзХранилище = ПолучитьИзВременногоХранилища(РезультатВыбора.АдресРасхожденийВХранилище);
	Если СтруктурыРасхожденийИзХранилище = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Расхождения.Очистить();
	
	//Признак, необходимый для проверки результатов сверки товаров. Если по итогу сверки все коды будут найдены, 
	//заполнять Расхождения не имеет смысла, вводить УОР тоже
	ВсеКодыНайдены = Истина;
	
	Для Каждого СтрокаКодаИдентификации Из СтруктурыРасхожденийИзХранилище Цикл
		
		СтруктураКода = Новый Структура("Штрихкод, СтатусСверкиКода", СтрокаКодаИдентификации.Ключ, СтрокаКодаИдентификации.Значение);
		
		Если Не СтруктураКода.СтатусСверкиКода = ПредопределенноеЗначение("Перечисление.СтатусыСверкиКодаИСМПТК.Найден") Тогда
			ВсеКодыНайдены = Ложь;
		КонецЕсли;
		
		ДанныеПоКоду  = ИнтеграцияИСМПТКВызовСервера.ПолучитьКодыМаркировки(СтруктураКода);  
		НоваяСтрока   = Объект.Расхождения.Добавить();
		
		//Если удалось разобрать код, вернется структура. Если нет - текст сообщения об ошибке.
		Если ТипЗнч(ДанныеПоКоду) = Тип("Строка") Тогда
			Возврат;
		КонецЕсли;
		
		Если ДанныеПоКоду.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда
			//Для групповых поиск по регистру не выполняем, т.к. EAN в стркое относится к вложенному товару, а номенклатура для групповых 
			//должна заполняться по верхнему уровню, т.е. по реквизиту GTINВерхнегоУровня.
			EANВерхнегоУровня  = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодEANИзGTIN(ДанныеПоКоду.GTINВерхнегоУровня);
			ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(EANВерхнегоУровня);
		Иначе
			ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(ДанныеПоКоду.EAN);
		КонецЕсли;
		
		Если ТипЗнч(ДанныеНоменклатуры) = Тип("Структура") Тогда
			Номенклатура   = ДанныеНоменклатуры.Номенклатура;
			Характеристика = ДанныеНоменклатуры.Характеристика;
		Иначе
			Номенклатура   = ДанныеНоменклатуры;
			Характеристика = Неопределено;
		КонецЕсли;
		
		НоваяСтрока.КодИдентификации 	= ДанныеПоКоду.КодИдентификации;
		НоваяСтрока.ВидУпаковки 		= ДанныеПоКоду.ВидУпаковки;
		
		//Для корректного заполнения количества нужно сопоставить данные с ТЧ Марки, т.к. только там можем получить 
		//инф-ю о вложенном количестве групповых упаковок, которую прислал сервер (запросить состав во вхд. документе сами не можем).
		СтрокаВМарках = Объект.Марки.НайтиСтроки(Новый Структура("КодИдентификации", ДанныеПоКоду.КодИдентификации));
		Если Не СтрокаВМарках.Количество() = 0 Тогда 
			КоличествоМарки = СтрокаВМарках[0].Количество;
		Иначе
		    КоличествоМарки = 1;
		КонецЕсли;
		
		Если ДанныеПоКоду.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Логистическая") 
			Или ДанныеПоКоду.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда 
			GTIN = "";
			GTINВерхнегоУровня = ДанныеПоКоду.GTINВерхнегоУровня;
			EAN = ДанныеПоКоду.EAN;
		ИначеЕсли ДанныеПоКоду.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда 
			GTIN = ДанныеПоКоду.GTIN;
			GTINВерхнегоУровня = ДанныеПоКоду.GTINВерхнегоУровня;
			EAN = ДанныеПоКоду.EAN;
		КонецЕсли;
		
		НоваяСтрока.GTIN 				= GTIN;
		НоваяСтрока.GTINВерхнегоУровня 	= GTINВерхнегоУровня;
				
		НоваяСтрока.EAN 				= EAN;
		НоваяСтрока.СтатусСверкиКода 	= СтруктураКода.СтатусСверкиКода;
		НоваяСтрока.Номенклатура		= Номенклатура;
		НоваяСтрока.Количество 			= КоличествоМарки;
		
		НоваяСтрока.ВидПродукцииИС 		= ДанныеПоКоду.ВидПродукцииИС;
		
	КонецЦикла;
	
	Если ВсеКодыНайдены Тогда
		Объект.Расхождения.Очистить();
	КонецЕсли;
	
	Если Не ВсеКодыНайдены Тогда
		ТекстСообщения = НСтр("ru = 'При сверке поступившего товара были обнаружены расхождения с Актом. Требуется оформление Уведомления о расхождениях!'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Объект.Проверен = Истина;
	УправлениеФормой();
	
	Модифицированность = Истина;
		
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполненностьНоменклатурыТоварахВоВходящемАПП() 
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
		Возврат Ложь;
	Иначе
		Для Каждого Строка Из Объект.Марки Цикл
			//Проверяем, зарегистрирован ли ШК в регистре. Может возникнуть ситуация, когда в ТЧ Марки полученного Акта
			//номенклатура заполнена, т.к. в момент получения документа был зарегистрирован штриход. 
			//Но затем штрихкод был изменен или удален и в момент ввода основания для АПП номенклатура в таблице уже не соответствует 
			//кодам макрировки по штрихкоду.
			EAN = Строка.EAN;
			Если Не ЗначениеЗаполнено(EAN) Тогда
				EAN = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодEANИзGTIN(Строка.GTINВерхнегоУровня);
			КонецЕсли;
			ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(EAN);
			Если Не ЗначениеЗаполнено(ДанныеНоменклатуры.Номенклатура) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СопоставитьСДокументомОснованием()
	
	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) Тогда 
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияНеВыбранДокументОснование());
		Возврат;
	КонецЕсли;
	
	ДанныеДокументаОснования 	= ПолучитьДанныеДокументаОснованияНаСервере();
	ПодобранныеМарки 			= Объект.Марки.Выгрузить();
	НомерСтроки 				= 1;
	
	ПодобранныеМарки.Свернуть("Номенклатура", "Количество");
	ПодобранныеМарки.Колонки.Добавить("КоличествоОснование", Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3)));
	
	БылаНедостача = Ложь;
	БылПересорт = Ложь;
	
	ТекстОшибкиЕстьНовые = "";
	
	Для Каждого ТоварОснования Из ДанныеДокументаОснования.Товары Цикл 
		
		НайденныеКодыМаркировки = ПодобранныеМарки.Найти(ТоварОснования.Номенклатура, "Номенклатура");
		
		Если Не НайденныеКодыМаркировки = Неопределено Тогда
			НайденныеКодыМаркировки.КоличествоОснование = НайденныеКодыМаркировки.КоличествоОснование + ТоварОснования.КоличествоИсточник;
		Иначе 
			НоваяСтрока = ПодобранныеМарки.Добавить();
			НоваяСтрока.Номенклатура 			= ТоварОснования.Номенклатура;
			НоваяСтрока.КоличествоОснование 	= ТоварОснования.КоличествоИсточник;
			НоваяСтрока.Количество 				= 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Товар Из ПодобранныеМарки Цикл
		
		ТекстОшибки = "";
		
		Если Товар.КоличествоОснование = 0 Тогда
			
			ТекстОшибки = НСтр("ru = 'В Акте приема/передачи имеется номенклатура %Ном, отсутствующая в документе-основании.'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Ном", Товар.Номенклатура);
			
		ИначеЕсли Товар.Количество = 0 Тогда
			
			ТекстОшибки = НСтр("ru = 'В Акте приема/передачи отсутствуют данные по номенклатуре %Ном, присутствующей в документе-основании.'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Ном", Товар.Номенклатура);
			
		ИначеЕсли Не Товар.Количество = Товар.КоличествоОснование Тогда
			
			ТекстОшибки = ТекстОшибки + НСтр("ru = 'Количество подобранных кодов маркировки для номенклатуры %Ном (%ККодов) %Отличие, чем количество товаров в основании (%КНом).
			|'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Ном", Товар.Номенклатура);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ККодов", Товар.Количество);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%КНом", Товар.КоличествоОснование);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Отличие", ?(Товар.КоличествоОснование < Товар.Количество, "больше", "меньше"));
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда 
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
		Иначе
			ТекстНетОшибок = НСтр("ru = 'Расхождений по количеству не найдено.'");
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстНетОшибок);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Входящий  = Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий");
	Исходящий = Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий");
	Первичный 	 = Объект.ТипАкта  = ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исходный");
	Исправленный = Объект.ТипАкта  = ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исправленный");
	НеОтправлен  = Объект.Статус   = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик");
	
	//////// Общий раздел ////////
	//Группа СвязанныйАктПриемаПередачи доступна только в корректировочном Акте
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СвязанныйАктПриемаПередачи", "Видимость", Исправленный);
	//Реквизиты бумажного документа доступны только если установлена опция выписки на бумаге
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаБумажныйДокумент", 	"Видимость", ВыпискаБумажногоАкта);
	//////////////////////////////	
	
	//////// Работа с Уведомлением о расхождениях ////////	
	//УведомлениеОРасхождении как реквизит на странице Общий раздел отображается только если оформляется корректировочный АПП. 
	//В остальных случаях информация отображается в шапке ссылкой на документ
	//(по схожему принципу со ссылкой Отражен в учете).
	Если Исходящий И Исправленный Тогда 
		НужноСкрытьУОР = Ложь;
		НужноСкрытьПредставлениеУОР = Истина;
	Иначе
		НужноСкрытьУОР = Истина;
		Если ЗначениеЗаполнено(Объект.УведомлениеОРасхождении) Тогда
			НужноСкрытьПредставлениеУОР = Ложь;
			Если Исходящий Тогда
				ЗаголовокУведомления = НСтр("ru='Получено Уведомление'"); 
			Иначе
				ЗаголовокУведомления = НСтр("ru='Сформировано Уведомление'"); 
			КонецЕсли;
			ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредставлениеУведомления", "Заголовок", ЗаголовокУведомления);
		Иначе
			НужноСкрытьПредставлениеУОР = Истина;
		КонецЕсли;
	КонецЕсли;
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "УведомлениеОРасхождении", 		"Видимость", Не НужноСкрытьУОР);
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредставлениеУведомления", "Видимость", Не НужноСкрытьПредставлениеУОР);
	
	//Команда ВвестиУведомление доступна только для полученных первичных АПП
	Если Входящий И Первичный Тогда
		Если ЗначениеЗаполнено(Объект.УведомлениеОРасхождении) 
			ИЛИ (Объект.Проверен И Объект.Расхождения.Количество() = 0) Тогда
			//УОР уже введено ИЛИ выполнена проверка и расхожденя не обнаружены - вводить УОР не нужно
			НужноСкрытьКомандуВводаУОР = Истина;
		Иначе
			Если Объект.Проверен Тогда
				//Проверка выполнена, есть расхождения - можем оформить УОР
				НужноСкрытьКомандуВводаУОР = Ложь;
			Иначе 
				//Проверка не выполнена - не можем вводить УОР
				НужноСкрытьКомандуВводаУОР = Истина;
			КонецЕсли;
		КонецЕсли;
	Иначе
		//УОР не требуется
		НужноСкрытьКомандуВводаУОР = Истина;
	КонецЕсли;
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВвестиУведомлениеОРасхождении", "Видимость",  Не НужноСкрытьКомандуВводаУОР);
	//////////////////////////////	
	
	//////// Редактирование данных строки ////////	
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДеревоМаркированнойПродукцииРедактироватьСтрокиТранспортныеКоды", "Видимость", Не Входящий);
	
	Если Исходящий Тогда
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДеревоМаркированнойПродукцииПодобратьНоменклатуруИЗарегистрироватьGTIN", "Доступность", НеОтправлен);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДеревоМаркированнойПродукцииРедактироватьСтрокиТранспортныеКоды", 		"Доступность", НеОтправлен);
	КонецЕсли;
	//////////////////////////////	
	
	//////// Импорт/Экспорт ////////	
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТоварыВыгрузитьДанныеВТСД", "Видимость", Ложь); //Временно не используется
	Если Исходящий И Первичный И НеОтправлен Тогда
		//В этом случае оставляем возможнотсь редактирования
		НужноСкрытьКомандыИмпортЭкспорт = Ложь;
	Иначе
		НужноСкрытьКомандыИмпортЭкспорт = Истина;
	КонецЕсли;
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "КомандыЗагрузкиТЧ", "Доступность", Не НужноСкрытьКомандыИмпортЭкспорт); 
	//Ручной ввод ШК
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ТоварыПоискПоШтрихкоду", "Видимость", Не НужноСкрытьКомандыИмпортЭкспорт);
	//Команда Разбить документ по ТГ
	Если Объект.Марки.Количество() = 0 Тогда
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаОбщаяКомандаРазбитьНаНесколькоДокументовИСМПТК", "Видимость", Ложь);
	Иначе
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаОбщаяКомандаРазбитьНаНесколькоДокументовИСМПТК", "Видимость", Не НужноСкрытьКомандыИмпортЭкспорт);
	КонецЕсли;
	
	//Команда Проверить документ
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаОбщаяКомандаПроверитьИСМПТК", "Видимость", Исходящий);
	
	//Мобильное приложение
	Если ПравоДоступа("Просмотр", Метаданные.РегистрыСведений.НастройкиПодключенияМобильноеПриложениеИСМПТК) Тогда 
		ИспользоватьПриложение = Константы.ИспользоватьМобильноеПриложениеИСМПТК.Получить();
		Если Не ИспользоватьПриложение Тогда
			НужноСкрыватьМобильноеПриложение = Истина;
		Иначе
			Если Исходящий И Первичный Тогда
				НужноСкрыватьМобильноеПриложение = Ложь;
			Иначе
				НужноСкрыватьМобильноеПриложение = Истина;
			КонецЕсли;
		КонецЕсли;
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДеревоМаркированнойПродукцииМобильноеПриложениеЗагрузитьТовары", "Видимость", Не НужноСкрыватьМобильноеПриложение);
	Иначе
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДеревоМаркированнойПродукцииМобильноеПриложениеЗагрузитьТовары", "Видимость", Ложь);
	КонецЕсли;
	//////////////////////////////	
	
	//////// Сверка товаров ////////
	//Сверка полученных товаров и данных документа.
	//Может применяться только во входящем первичном документе, у которого статус позволяет выполнять приемку и по документу не оформлено УОР.
	Если Входящий И Первичный 
		И Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.ОжидаетПриемку")
		И Не ЗначениеЗаполнено(Объект.УведомлениеОРасхождении) Тогда
		НужноСкрыватьКомандуСверкиПолученныхТоваров = Ложь;
	Иначе
		НужноСкрыватьКомандуСверкиПолученныхТоваров = Истина;
	КонецЕсли;
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДеревоМаркированнойПродукцииСверкаПолученныхКодовМаркировки", "Видимость", Не НужноСкрыватьКомандуСверкиПолученныхТоваров);
		
	//Признак выполненной сверки и доступность команды Подтвердить
	ПравоПодтвержденияАктаБезПроверки = РольДоступна("ПравоПодтвержденияАктаБезПроверкиИСМПТК") ИЛИ Пользователи.ЭтоПолноправныйПользователь(,Ложь);
	Если Входящий И Первичный Тогда
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Проверен", "ТолькоПросмотр", Не ПравоПодтвержденияАктаБезПроверки);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Проверен", "Видимость", 		Не НужноСкрыватьКомандуСверкиПолученныхТоваров);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументАктПриемаПередачиИСМПТКПодтвердить", "Доступность", Объект.Проверен);
	ИначеЕсли Входящий И Исправленный Тогда
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Проверен", "Видимость", Ложь);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументАктПриемаПередачиИСМПТКПодтвердить", "Доступность", Истина);
	Иначе 
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Проверен", "Видимость", Ложь);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументАктПриемаПередачиИСМПТКПодтвердить", "Видимость", Ложь);
	КонецЕсли;
	
	//Сверка данных документа с документом-основанием
	//На текущий момент из-за неопределённости с пересчетом для различных единиц измерения основания команда безусловно скрывается.
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДеревоМаркированнойПродукцииСверитьДанныеСДокументомОснованием", "Видимость", Ложь);
	
	//Данные о товарах основания в ТЧ Марки
	Если ЗначениеЗаполнено(Объект.ДокументОснование) 
		И Исходящий И Не Исправленный Тогда
		ВидимостьКоличествоОснования = Истина;
	Иначе
		ВидимостьКоличествоОснования = Ложь;
	КонецЕсли;
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДеревоМаркированнойПродукцииГруппаКоличествоОснования", "Видимость", ВидимостьКоличествоОснования);
	Если НеОтправлен Тогда 
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаОбщаяКомандаПерезаполнитьПоПервичномуДокументуИСМПТК", "Видимость", ВидимостьКоличествоОснования);
	Иначе
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаОбщаяКомандаПерезаполнитьПоПервичномуДокументуИСМПТК", "Видимость", Ложь);
	КонецЕсли;
	//////////////////////////////	

	//////// Уведомление о списании (вывод из оборота) ////////
	Если ПравоДоступа("Просмотр", Метаданные.Документы.УведомлениеОВыводеИзОборотаИСМПТК) Тогда
		СписаниеКМ = ПолучитьУВИО();
		ВведеноУведомлениеОВыводеИзОборота = ЗначениеЗаполнено(СписаниеКМ);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаУведомлениеОВыводеИзОборота", "Видимость", ВведеноУведомлениеОВыводеИзОборота);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписаниеКМ", "Видимость", ВведеноУведомлениеОВыводеИзОборота);
		
		Если Входящий И Объект.Расхождения.Количество() = 0 
			И Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.ПринятПодтвержден")
			И Не ВведеноУведомлениеОВыводеИзОборота Тогда
			НужноСкрыватьКомандуВводаСписания = Ложь;
		Иначе
			НужноСкрыватьКомандуВводаСписания = Истина;
		КонецЕсли;
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВвестиУведомлениеОСписании", "Видимость", Не НужноСкрыватьКомандуВводаСписания);
	Иначе
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаУведВИО", "Видимость", Ложь);
	КонецЕсли;
	//////////////////////////////	
	
	//Команда проверки статуса и владельца кода по данным сервера актуальна только при заполнении исходящего Акта до его отправки
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДеревоМаркированнойПродукцииПроверитьСостояниеКодовНаСервере", "Видимость", НеОтправлен);
	
	//////// Прочее ////////
	//ТЧ Ошибки выводится только если в ней есть какие-то записи
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Ошибки", "Видимость", ?(Объект.Ошибки.Количество() = 0, Ложь, Истина));
	//Управление доступностью кнопок Отправить, Обновить, Создать основание и т.д. в командной панели	
	УстановитьВидимостьКнопокИзмененияСтатуса();
	ИзменитьКоманднуюПанельДокумента();
	//Формирование поля Состояние по текущему Статусу документа
	СформироватьПредставлениеСостояния();
	//Формирование представления ссылки на связанный документ
	СформироватьПредставлениеДокументаОснования(ЭтаФорма);
	СформироватьПредставлениеУведомленияОРасхождении(ЭтаФорма);
	//////////////////////////////	
			
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Если Объект.ТипАкта = Неопределено ИЛИ Объект.ТипАкта = ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.ПустаяСсылка") Тогда
		Объект.ТипАкта = ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исходный");
	КонецЕсли;
		
	Если Объект.Направление.Пустая() Тогда
		Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий");
	КонецЕсли;
	
	Если Объект.ВидОперации.Пустая() Тогда
		Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперацииИСМПТК.Реализация");
	КонецЕсли;
	
	Если Объект.Статус.Пустая() Тогда
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик");
	КонецЕсли;
	
	Если ИспользоватьПодключаемоеОборудование Тогда
		ИспользуютсяСканерыШтрихкода = (МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам("СканерШтрихкода").Количество() > 0);
	Иначе
		ИспользуютсяСканерыШтрихкода = Ложь;
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРеквизитыОрганизацииКонтрагента(Ссылка, Реквизит) Экспорт
	
	Возврат ИнтеграцияИСМПТКПереопределяемый.ПолучитьРеквизитыОрганизацииКонтрагента(Ссылка, Реквизит);
	
КонецФункции

&НаКлиенте
Процедура ИзменитьДокументОснованиеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		Объект.ДокументОснование = ДополнительныеПараметры.ВыбранноеЗначение;
		СформироватьПредставлениеДокументаОснования(ЭтаФорма);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНовуюНоменклатуру(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		
		Если Не ДополнительныеПараметры.СтруктураКодовМаркировки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда 
			
			ПараметрыОткрытияФормы = Новый Структура("Номенклатура, СтруктураКодовМаркировки, Организация", ДополнительныеПараметры.Номенклатура, ДополнительныеПараметры.СтруктураКодовМаркировки, Объект.Организация);
			
			ОткрытьФорму(
			"Обработка.ОбменИСМПТК.Форма.ФормаУточненияДанныхИС",
			ПараметрыОткрытияФормы, ЭтотОбъект,,,,
			Новый ОписаниеОповещения("ЗавершитьДобавлениеКодаМаркировки", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Иначе 
			
			СтруктураКодовМаркировки = ДополнительныеПараметры.СтруктураКодовМаркировки;
			Номенклатура			 = ДополнительныеПараметры.Номенклатура;
			ДобавитьКодМаркировкиВДерево(Номенклатура, СтруктураКодовМаркировки);
			
			НоваяСтрока = Объект.Марки.Добавить();
			НоваяСтрока.КодМаркировки 		= СтруктураКодовМаркировки.КодМаркировки;
			НоваяСтрока.КодИдентификации 	= СтруктураКодовМаркировки.КодИдентификации;
			НоваяСтрока.GTIN 				= СтруктураКодовМаркировки.GTIN;
			Если СтруктураКодовМаркировки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда 
				НоваяСтрока.GTINВерхнегоУровня 	= СтруктураКодовМаркировки.GTINВерхнегоУровня;
			КонецЕсли;
			НоваяСтрока.EAN 				= СтруктураКодовМаркировки.EAN;
			НоваяСтрока.Номенклатура 		= Номенклатура;
			НоваяСтрока.Количество 			= СтруктураКодовМаркировки.Количество;
			НоваяСтрока.ВидУпаковки			= СтруктураКодовМаркировки.ВидУпаковки;
			НоваяСтрока.ВидПродукцииИС		= СтруктураКодовМаркировки.ВидПродукцииИС;
			
			Для Каждого Строка ИЗ ДеревоМаркированнойПродукции.ПолучитьЭлементы() Цикл
				Элементы.ДеревоМаркированнойПродукции.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
			КонецЦикла;
			
			Модифицированность = Истина;
			
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗаполнениеДокументаОснования(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		Объект.ДокументОснование = ДополнительныеПараметры.ВыбранноеЗначение;
		СформироватьПредставлениеДокументаОснования(ЭтаФорма);
		Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") 
			И Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик") Тогда
			
			ТекстВопроса = НСтр("ru = 'Перезаполнить Акт по выбранному основанию?'");
			ПерезаполнитьДокументПоОснованию = Новый ОписаниеОповещения("ПерезаполнитьДокументПоОснованию", ЭтаФорма);
			ПоказатьВопрос(ПерезаполнитьДокументПоОснованию, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		КонецЕсли;
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьПредставлениеДокументаОснования(Форма)
	
	Если ЗначениеЗаполнено(Форма.Объект.ДокументОснование) Тогда
		Форма.ПредставлениеАкт = Форма.Объект.ДокументОснование;
		Форма.Элементы.ПредставлениеАкт.ЦветТекста = ИнтеграцияИСМПТККлиентСервер.ЦветСиний();
	Иначе
		Форма.ПредставлениеАкт = НСтр("ru = 'Документ не отражен в учете'");
		Форма.Элементы.ПредставлениеАкт.ЦветТекста = ИнтеграцияИСМПТККлиентСервер.ЦветКрасный();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьПредставлениеУведомленияОРасхождении(Форма)
	
	Если ЗначениеЗаполнено(Форма.Объект.УведомлениеОРасхождении) Тогда
		Форма.ПредставлениеУведомлениеОРасхождении = Форма.Объект.УведомлениеОРасхождении;
		Форма.Элементы.ПредставлениеУведомлениеОРасхождении.ЦветТекста = ИнтеграцияИСМПТККлиентСервер.ЦветСиний();
	Иначе
		Форма.ПредставлениеУведомлениеОРасхождении = НСтр("ru = 'Нет связанных Уведомлений'");
		Форма.Элементы.ПредставлениеУведомлениеОРасхождении.ЦветТекста = ИнтеграцияИСМПТККлиентСервер.ЦветКрасный();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьСвязанныйАкт(ВыбранноеЗначение)
	
	Возврат ИнтеграцияИСМПТК.ПроверитьНаличиеСвязанногоАкта(ВыбранноеЗначение, Истина);
	
КонецФункции

&НаСервере
Функция ПроверитьНаличиеМаркируемогоТовара(ВыбранноеЗначение) 
	
	Возврат ИнтеграцияИСМПТКПереопределяемый.ПроверитьНаличиеМаркируемогоТовара(ВыбранноеЗначение);
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьАктПоДокументуОснованию()
	
	ДанныеДокументаОснования = ПолучитьДанныеДокументаОснованияНаСервере();
	
	РеквизитыШапки = ДанныеДокументаОснования.Реквизиты;
	ДанныеПоТоварам = ДанныеДокументаОснования.Товары;
	
	ЗаполнитьЗначенияСвойств(Объект, РеквизитыШапки, , "СвязанныйАкт, УведомлениеОРасхождении, РегистрационныйНомерСвязанногоАкта");
	
	Объект.Товары.Очистить();
	Объект.Марки.Очистить();
	
	Для Каждого СтрокаТовар Из ДанныеПоТоварам Цикл
		
		Если Объект.ТипАкта = ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исправленный") Тогда
			//Для Акта-корректировки данные по маркам приходят в полном объеме из Уведомления о расхождении
			НоваяСтрока = Объект.Марки.Добавить();
			НоваяСтрока.Номенклатура 	   = СтрокаТовар.Номенклатура;
			НоваяСтрока.КоличествоИсточник = СтрокаТовар.КоличествоИсточник;
			НоваяСтрока.КодМаркировки	   = СтрокаТовар.КодМаркировки;
			НоваяСтрока.ВидУпаковки		   = СтрокаТовар.ВидУпаковки;
			НоваяСтрока.Количество		   = СтрокаТовар.Количество;
			НоваяСтрока.GTIN			   = СтрокаТовар.GTIN;
			НоваяСтрока.EAN 			   = СтрокаТовар.EAN;
			НоваяСтрока.КодИдентификации   = СтрокаТовар.КодИдентификации;
		Иначе
			//Для первичного Акта данные заполняем из документа-основания
			НоваяСтрока = Объект.Товары.Добавить();
			НоваяСтрока.Номенклатура 	   = СтрокаТовар.Номенклатура;
			НоваяСтрока.КоличествоИсточник = СтрокаТовар.КоличествоИсточник;
		КонецЕсли;
	КонецЦикла;
	
	СформироватьДеревоКодовМаркировки();

КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДокументаОснованияНаСервере() Экспорт
	
	Возврат ИнтеграцияИСМПТКПереопределяемый.ПолучитьДанныеДокументаОснованияАППНаСервере(Объект.ДокументОснование);

КонецФункции

&НаСервере
Функция СформироватьДеревоКодовМаркировки() Экспорт
	
	//Добавим группы номенклатуры
	ПустаяСтруктураКМ = ПолучитьСтруктуруКодовМаркировки();
	ОчиститьДеревоКодовМаркировки();
	
	ТаблицаТоваров = Объект.Товары.Выгрузить();
	ТаблицаТоваров.Свернуть("Номенклатура, EAN", "КоличествоИсточник");
	
	Для Каждого ГруппаНом Из ТаблицаТоваров Цикл
		Если Не ЗначениеЗаполнено(ГруппаНом.Номенклатура) Тогда 
			ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(ГруппаНом.EAN);
			Если ТипЗнч(ДанныеНоменклатуры) = Тип("Структура") Тогда
				Номенклатура = ДанныеНоменклатуры.Номенклатура;
			Иначе
				Номенклатура = ДанныеНоменклатуры;
			КонецЕсли;
			ГруппаНом.Номенклатура = Номенклатура;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаТоваров.Свернуть("Номенклатура", "КоличествоИсточник");
	Для Каждого ГруппаНом Из ТаблицаТоваров Цикл
		ПустаяСтруктураКМ.КоличествоИсточник = ГруппаНом.КоличествоИсточник;
		Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий")
			ИЛИ (Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий")
				И ЗначениеЗаполнено(ГруппаНом.Номенклатура)) Тогда 
			ДобавитьКодМаркировкиВДерево(ГруппаНом.Номенклатура, ПустаяСтруктураКМ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из Объект.Марки Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.КодИдентификации) Тогда
			
			Номенклатура = ИнтеграцияИСМПТКПереопределяемый.ПолучитьПустуюСсылкуНоменклатура();
			Если СтрокаТЧ.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда
				//Для групповых поиск по регистру не выполняем, т.к. EAN в стркое относится к вложенному товару, а номенклатура для групповых 
				//должна заполняться по верхнему уровню, т.е. по реквизиту GTINВерхнегоУровня.
				EANВерхнегоУровня  = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодEANИзGTIN(СтрокаТЧ.GTINВерхнегоУровня);
				ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(EANВерхнегоУровня);
			Иначе
				ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(СтрокаТЧ.EAN);
			КонецЕсли;
			
			Если ТипЗнч(ДанныеНоменклатуры) = Тип("Структура") Тогда
				Номенклатура = ДанныеНоменклатуры.Номенклатура;
			Иначе
				Номенклатура = ДанныеНоменклатуры;
			КонецЕсли;
			
			//Если Номенклатура не нашлась в ТЧ Товары и в регистре Штрихкоды, 
			//возможно, она была назначена в форме рдактирования транспортных упаковок без регистрации ШК.
			//Проверяем этот случай по данным строки 
			Если НЕ ЗначениеЗаполнено(Номенклатура) И ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
				Номенклатура = СтрокаТЧ.Номенклатура;
			КонецЕсли;
			
			ДобавитьКодМаркировкиВДерево(Номенклатура, СтрокаТЧ);
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ДеревоМаркированнойПродукции.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьКнопокИзмененияСтатуса()
	
	СответвствиеСтатусов = ИнтеграцияИСМПТК.РазрешенныеДействияПоСтатусамАкта(Объект.Направление, Объект.ВидОперации);
		
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументАктПриемаПередачиИСМПТКОтозвать",    "Видимость", СответвствиеСтатусов[ИнтеграцияИСМПТККлиентСервер.ДействиеОтзыв()][Объект.Статус]);
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументАктПриемаПередачиИСМПТКПодтвердить", "Видимость", СответвствиеСтатусов[ИнтеграцияИСМПТККлиентСервер.ДействиеПодтверждение()][Объект.Статус]);
	ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументАктПриемаПередачиИСМПТКОтклонить",   "Видимость", СответвствиеСтатусов[ИнтеграцияИСМПТККлиентСервер.ДействиеОтклонение()][Объект.Статус]);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКоманднуюПанельДокумента()
	
	НеОтправлен = Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик");
	Если Объект.Направление = Перечисления.НаправленияДокументовИСМПТК.Входящий Тогда
		
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументАктПриемаПередачиИСМПТКОтправить", "Видимость", Ложь);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаОбщаяКомандаПроверить", "Видимость", Ложь);
				
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументОснованиеСоздать", "Видимость", Истина);
		
	ИначеЕсли Объект.Направление = Перечисления.НаправленияДокументовИСМПТК.Исходящий Тогда
		
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументАктПриемаПередачиИСМПТКОтправить", "Доступность", НеОтправлен);
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаОбщаяКомандаПроверить", "Доступность", НеОтправлен);
				
		ИнтерфейсИСМПТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаДокументОснованиеСоздать", "Видимость", Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияЗаписиУОРНаСервере()

	ЭтаФорма.ПредставлениеУведомлениеОРасхождении = Неопределено;
	
	Если ИнтеграцияИСМПТК.ПолучитьДанныеУОР(ЭтаФорма) Тогда 
		ИнтеграцияИСМПТК.ЗаполнитьТекстПроУОР(ЭтаФорма);
	КонецЕсли;

	ЭтотОбъект.Прочитать();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьДеревоКодовМаркировки() 
	
	ЭлементыДерева = ДеревоМаркированнойПродукции.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКоличествоКодовМаркировки(КодИд, Количество) 
	
	СтрокаКИ = Объект.Марки.НайтиСтроки(Новый Структура("КодИдентификации", КодИд));
	Если Не СтрокаКИ.Количество() = 0 Тогда 
		СтрокаКИ[0].Количество = Количество;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьКодыМаркировкиИзФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если Не ВыбранныеФайлы = Неопределено Тогда
		
		НовыйТекстовыйДокумент = Новый ТекстовыйДокумент;
		
		Для Каждого СтрокаТЧ Из Объект.Марки Цикл
			КодДляЗаписи = ?(ЗначениеЗаполнено(СтрокаТЧ.КодМаркировки), 
									РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.Base64ВШтрихкод(СтрокаТЧ.КодМаркировки), СтрокаТЧ.КодИдентификации);
			НовыйТекстовыйДокумент.ДобавитьСтроку(КодДляЗаписи);
		КонецЦикла;
		
		НовыйТекстовыйДокумент.Записать(ВыбранныеФайлы[0]);
				
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьДокументПоОснованию(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьАктПоДокументуОснованию();
		Модифицированность = Истина;
		УправлениеФормой();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьНаличиеSSCCВДокументе(КодИдентификации)
	
	ТаблицаМарок = Объект.Марки.Выгрузить();
	Возврат Не РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.ВыполнитьПроверкуТранспортногоКодаПоДокументу(КодИдентификации, ТаблицаМарок); 
	
КонецФункции

&НаКлиенте
Процедура ОповеститьОПроблемеЗапросаВалидностиКМ() Экспорт
	
	//Если по какой-то причине не удалось выполнить получение Токена, то и запрос на проверку статуса КМ не выполнялся,
	//об этом нужно предупредить и отметить непроверенные коды.
	Если ТокенАвторизацииВрем = Неопределено Тогда 
		СообщитьОНевозможностиВыполнитьПроверкуСтатусаКМ("Фоновый");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапретитьРедактированиеПолей()
	
	Если Не Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик") Тогда
		//Документ отправлен на сервер, запрещаем редактирование данных, уже переданных в ИС МПТ
		Элементы.РегистрационныйНомерСвязанногоАкта.ТолькоПросмотр = Истина;
				
		Элементы.ВыпискаБумажногоАкта.ТолькоПросмотр = Истина;
		Элементы.РегНомерПервичногоДокумента.ТолькоПросмотр = Истина;
		Элементы.ДатаПервичногоДокумента.ТолькоПросмотр = Истина;
		
		Элементы.ПоставщикИдентификатор.ТолькоПросмотр = Истина;
		Элементы.ПоставщикНаименование.ТолькоПросмотр = Истина;
		
		Элементы.ПолучательИдентификатор.ТолькоПросмотр = Истина;
		Элементы.ПолучательНаименование.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовДерева

&НаКлиентеНаСервереБезКонтекста
Функция НайтиПоИдентификатору(ДанныеФормыДерево, Идентификатор)
	
	// Сейчас есть проблема в веб-клиенте: метод НайтиПоИдентификатору, может вернуть ДанныеФормыДерево
	
	Если Идентификатор = -1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеФормыЭлементДерева = ДанныеФормыДерево.НайтиПоИдентификатору(Идентификатор);
	
	Если ТипЗнч(ДанныеФормыЭлементДерева) = Тип("ДанныеФормыЭлементДерева") Тогда
		Возврат ДанныеФормыЭлементДерева;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ДеревоМаркировкиПередУдалением(Элемент, Отказ)
	
	Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий") 
		ИЛИ (Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") 
			И (Объект.ТипАкта = ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исправленный") 
				ИЛИ	(Объект.ТипАкта = ПредопределенноеЗначение("Перечисление.ВидыДокументаИСМПТК.Исходный") 
					И НЕ Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыДокументовИСМПТК.Черновик")))) Тогда
			
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ТекстОшибки = "";
	УдаляемыеКодыМаркировки = Новый Массив();
		
	Для Каждого ИдентификаторСтроки Из Элемент.ВыделенныеСтроки Цикл
		
		УдаляемыйЭлемент = НайтиПоИдентификатору(ДеревоМаркированнойПродукции, ИдентификаторСтроки);
		РодительУдаляемогоЭлемента = УдаляемыйЭлемент.ПолучитьРодителя();
		
		Если Не РодительУдаляемогоЭлемента = Неопределено Тогда
			РодительУдаляемогоЭлемента.КоличествоПодобранно = РодительУдаляемогоЭлемента.КоличествоПодобранно -1;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(УдаляемыйЭлемент.КодИдентификации) Тогда
			НайденныеСтроки = Объект.Марки.НайтиСтроки(Новый Структура("КодИдентификации", УдаляемыйЭлемент.КодИдентификации));
			Если НайденныеСтроки.Количество() = 1 Тогда
				Объект.Марки.Удалить(НайденныеСтроки[0]);
			КонецЕсли;
		Иначе 
			НайденныеСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", УдаляемыйЭлемент.Номенклатура));
			Если НайденныеСтроки.Количество() = 1 Тогда
				Объект.Товары.Удалить(НайденныеСтроки[0]);
			КонецЕсли;
			
			ВложенныеЭлементы = УдаляемыйЭлемент.ПолучитьЭлементы();
			Для Каждого ВложенныйЭлемент Из ВложенныеЭлементы Цикл 
				НайденныеСтроки = Объект.Марки.НайтиСтроки(Новый Структура("КодИдентификации", ВложенныйЭлемент.КодИдентификации));
				Если НайденныеСтроки.Количество() = 1 Тогда
					Объект.Марки.Удалить(НайденныеСтроки[0]);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМаркированнойПродукцииКоличествоПодобранноПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоМаркированнойПродукции.ТекущиеДанные;
	
	ЭлементРодитель = ТекущиеДанные.ПолучитьРодителя();
	Если Не ЭлементРодитель = Неопределено Тогда 
		КоличествоПодобранных = 0;
		ВложенныеЭлементы = ЭлементРодитель.ПолучитьЭлементы();
		Для Каждого Вложенный Из ВложенныеЭлементы Цикл
			КоличествоПодобранных = КоличествоПодобранных + Вложенный.КоличествоПодобранно;
		КонецЦикла;
		ЭлементРодитель.КоличествоПодобранно = КоличествоПодобранных;
	КонецЕсли; 
	
	ИзменитьКоличествоКодовМаркировки(ТекущиеДанные.КодИдентификации, ТекущиеДанные.КоличествоПодобранно);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКодыМаркировкиИзФайла(Команда)
	
	ОчиститьСообщения();
	Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() 
		И Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Включена опция автоматической проверки состояния кодов маркировки по данным сервера. Для получения токена сеанса требуется указать Организацию!'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если Не Объект.Марки.Количество() = 0 Тогда
		ЗагрузитьКодыМаркировкиИзФайлаПослеВопроса = Новый ОписаниеОповещения("ЗагрузитьКодыМаркировкиИзФайлаПослеВопроса", ЭтаФорма);
		ТекстВопроса = НСтр("ru = 'Выполнить предварительную очистку данных в таблице?'");
		ПоказатьВопрос(ЗагрузитьКодыМаркировкиИзФайлаПослеВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ИнтеграцияИСМПТККлиент.ЗагрузитьКодыМаркировкиИзФайла(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКодыМаркировкиИзФайлаПослеВопроса(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.Марки.Очистить();
		СформироватьДеревоКодовМаркировки(); //для очистки дерева
	КонецЕсли;
	ИнтеграцияИСМПТККлиент.ЗагрузитьКодыМаркировкиИзФайла(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьКодыМаркировкиВФайл(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.Заголовок = "Выбор файла";
	ДиалогВыбораФайла.Фильтр = "CSV (Comma-Separated Values)(*.csv)|*.csv";
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ВыгрузитьКодыМаркировкиИзФайлаЗавершение", ЭтотОбъект);
	ДиалогВыбораФайла.Показать(Оповещение);
		
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМаркированнойПродукцииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьВсеДервео(Команда)
	
	СвернутьРазвернутьДеревоКодов(ДеревоМаркированнойПродукции.ПолучитьЭлементы(), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьВсеДервео(Команда)
	
	СвернутьРазвернутьДеревоКодов(ДеревоМаркированнойПродукции.ПолучитьЭлементы(), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьДеревоКодов(ДервероЭлементов, Развернуть)
	
	Для Каждого Элемент Из ДервероЭлементов Цикл
		
		ВложенныеЭлементы = Элемент.ПолучитьЭлементы();
		
        Если ВложенныеЭлементы.Количество() > 0 Тогда
			
			СвернутьРазвернутьДеревоКодов(ВложенныеЭлементы, Развернуть);

            Если Развернуть = Истина Тогда
                Элементы.ДеревоМаркированнойПродукции.Развернуть(Элемент.ПолучитьИдентификатор());
            Иначе
                Элементы.ДеревоМаркированнойПродукции.Свернуть(Элемент.ПолучитьИдентификатор());
			КонецЕсли;
			
        КонецЕсли;
    КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеВExcel(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.Заголовок = "Выбор файла";
	ДиалогВыбораФайла.Фильтр = НСтр("ru='Все поддерживаемые форматы файлов(*.xls;*.xlsx;)|*.xls;*.xlsx;|Книга Excel 97 (*.xls)|*.xls|Книга Excel 2007 (*.xlsx)|*.xlsx'");
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("СоздатьФайлXLS", ЭтотОбъект);
	ДиалогВыбораФайла.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьФайлXLS(ВыбранныеФайлы, ДополнительныеПараметрым) Экспорт
	
	Если Не ВыбранныеФайлы = Неопределено Тогда
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		Итератор = 1;
		Для Каждого СтрокаТЧ Из Объект.Марки Цикл
			
			Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Входящий")
				Или Не ЗначениеЗаполнено(СтрокаТЧ.КодМаркировки) Тогда 
				КодДляЗаписи = СтрокаТЧ.КодИдентификации;
			Иначе 
				КодДляЗаписи = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.Base64ВШтрихкод(СтрокаТЧ.КодМаркировки);
			КонецЕсли;
			
			ТабличныйДокумент.Область("R" + Формат(Итератор,"ЧГ=0") + "C1").Текст = КодДляЗаписи;
			Итератор = Итератор + 1;
		КонецЦикла;
		
		ТабличныйДокумент.Записать(ВыбранныеФайлы[0], ТипФайлаТабличногоДокумента.XLS97);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроцедуруФоновоВыполнено(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(нСтр("ru='Операция отменена.'", "ru"));
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		
		// обрабатываем результат
		Модифицированность = Истина;
		
		Если ОбработатьРезультатЗагрузкиКодов(Результат.АдресРезультата) Тогда 
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(нСтр("ru='Загрузка кодов маркировки выполнена!'", "ru"));
			
			Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() Тогда 
				
				ТокенАвторизацииВрем = Неопределено;
				ЗавершениеПроверитьСостояниеКодовНаСервере = Новый ОписаниеОповещения("ЗавершениеПроверитьСостояниеКодовНаСервере", ЭтаФорма);
				ПолучитьКлючАвторизации(ЗавершениеПроверитьСостояниеКодовНаСервере);
				
				//Если по какой-то причине не удалось выполнить получение Токена, то и запрос на проверку статуса КМ не выполнялся,
				//об этом нужно предупредить и отметить непроверенные коды.
				Если ТокенАвторизацииВрем = Неопределено Тогда 
					СообщитьОНевозможностиВыполнитьПроверкуСтатусаКМ("Фоновый");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатЗагрузкиКодов(АдресРезультата)
	
	РезультатЗадания = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если РезультатЗадания.Свойство("МассивОшибок") И Не РезультатЗадания.МассивОшибок.Количество() = 0 Тогда
		Для Каждого ТекстОшибки Из РезультатЗадания.МассивОшибок Цикл 
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
		КонецЦикла;
	КонецЕсли;
	
	Если РезультатЗадания.Успешно И НЕ РезультатЗадания.ТаблицаПолученныхКодов.Количество() = 0 Тогда
		Для Каждого Строка Из РезультатЗадания.ТаблицаПолученныхКодов Цикл
			НайденныеСтроки = Объект.Марки.НайтиСтроки(Новый Структура("КодИдентификации", Строка.КодИдентификации));
			Если НайденныеСтроки.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(Объект.Марки.Добавить(), Строка);
			КонецЕсли;
		КонецЦикла;
		ИнтеграцияИСМПТК.ПроверитьНаличиеУпаковокПоКоторымНеПолучилосьВыполнитьЗапросСостава(ЭтаФорма);
		СформироватьДеревоКодовМаркировки();
		Возврат Истина;
	Иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти 

#Область Штрихкодирование

&НаКлиенте
Процедура ОбработатьКодМаркировки(ИсходныеДанные, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ИсходныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИсходныеДанные.Свойство("ФормаПоискаНоменклатуры_Успешно") Тогда
		//Обработчик после того, как была вызвана форма проверки и подбора номенклатуры (для регистрации ШК)
		Если ИсходныеДанные.ФормаПоискаНоменклатуры_Успешно Тогда
			ДобавитьНовыйКод(ИсходныеДанные.СтруктураКодовМаркировки);
			ТекстУведомления = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияЗарегистрированНовыйШтрихкод(); 
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстУведомления);
		Иначе
			ТекстОшибки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияОтказДобавленияКодаНеОпределенаНоменклатура();
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
			Возврат;
		КонецЕсли;
		
	Иначе
		
		//Для передачи на сервер КМ, в составе которого есть символы GS (29), требуется дополнительное кодирование
		РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ЗакодироватьШтрихкодДанныхBase64(ИсходныеДанные);
		ИсходныеДанные.Вставить("ФорматBase64", Истина);	
		СтруктураКодовМаркировки = ИнтеграцияИСМПТКВызовСервера.ПолучитьКодыМаркировки(ИсходныеДанные);   
		
		//Если удалось разобрать код, вернется структура. Если нет - текст сообщения об ошибке.
		Если ТипЗнч(СтруктураКодовМаркировки) = Тип("Строка") Тогда
			РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСерверПереопределяемый.СообщитьПользователю(СтруктураКодовМаркировки);
			Возврат;
		КонецЕсли;
		
		ОтказатьВДобавлении = Ложь;
		Если Не НайтиПоКодуИдентификации(СтруктураКодовМаркировки.КодИдентификации) = Неопределено Тогда 
			
			ОтказатьВДобавлении = Истина;
			
		ИначеЕсли РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.ПроверитьЗначениеКонстанты("ПоддержкаДвойногоФорматаТранспортныхКодовИСМПТК")
			И СтруктураКодовМаркировки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Логистическая") Тогда
			
			//В этом случае в таблице документа может находиться уже преобразованный SSCC, соответствующий обрабатываемому в данный момент штрихкоду,
			//т.е. нужна специфическая проверка на дублирование данных кода
			ОтказатьВДобавлении = ПроверитьНаличиеSSCCВДокументе(СтруктураКодовМаркировки.КодИдентификации);
			
		КонецЕсли;
		
		Если ОтказатьВДобавлении Тогда
			ПараметрыОткрытия = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного();
			ПараметрыОткрытия.Штрихкод    = ИсходныеДанные.Штрихкод;
			ПараметрыОткрытия.ТекстОшибки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияКодМаркировкиУказанВДокументе();
			ОткрытьФорму("Обработка.ОбменИСМПТК.Форма.ИнформацияОНевозможностиДобавленияОтсканированногоИСМПТ", ПараметрыОткрытия, ЭтаФорма);
			Возврат;
		КонецЕсли;
		
		Если Не СтруктураКодовМаркировки = Неопределено Тогда
			Если Не СтруктураКодовМаркировки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда 
				//Проверяем, включено ли автоматическое получение данных с сервера по групповым упаковкам
				Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоОпределениеЛогистическихКодовИСМПТК() Тогда
					ДобавитьНовыйЛогистическийКод(СтруктураКодовМаркировки); //Заполняем автоматически с сервера
				Иначе
					//Даем возможность указать данные вручную
					ПараметрыОткрытияФормы = Новый Структура("Номенклатура, СтруктураКодовМаркировки, Организация", ИнтеграцияИСМПТКПереопределяемый.ПолучитьПустуюСсылкуНоменклатура(), СтруктураКодовМаркировки, Объект.Организация);
					ОткрытьФорму("Обработка.ОбменИСМПТК.Форма.ФормаУточненияДанныхИС", ПараметрыОткрытияФормы, ЭтотОбъект,,,, Новый ОписаниеОповещения("ЗавершитьДобавлениеКодаМаркировки", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				КонецЕсли;
			Иначе
				//Для потребительских заполняем напрямую
				ДобавитьНовыйКод(СтруктураКодовМаркировки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиПоКодуИдентификации(КодИдентификации)
	
	СтруктураПоиска = Новый Структура("КодИдентификации", КодИдентификации);
	
	НайденныеСтроки = Объект.Марки.НайтиСтроки(СтруктураПоиска);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		Возврат НайденныеСтроки;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьНовыйКод(СтруктураКодовМаркировки)
	
	ДанныеНоменклатуры = ПолучитьНоменклатуруПоШтрихкоду(СтруктураКодовМаркировки.EAN);
	Если ТипЗнч(ДанныеНоменклатуры) = Тип("Структура") Тогда
		Номенклатура = ДанныеНоменклатуры.Номенклатура;
	Иначе
		Номенклатура = ДанныеНоменклатуры;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда 
		
		ДанныеДляСопоставленияНоменклатуры = Новый Структура(); //передается в параметрах открытия формы подбора
		Операция = "СопоставлениеНоменклатуры";
		
		ИсходныеДанные = Новый Структура("Количество, Штрихкод", 1, СтруктураКодовМаркировки.КодИдентификации);
		ДанныеДляСопоставленияНоменклатуры.Вставить("ИсходныеДанные", ИсходныеДанные); //содержит структуру "Количество, Штрихкод", где штрихкод это отсканированный КМ
		
		ШтрихкодыКСопоставлению = Новый Массив();
		ДанныеШтрихкода = Новый Структура("Количество, Штрихкод", 1, СтруктураКодовМаркировки.EAN);
		ШтрихкодыКСопоставлению.Добавить(ДанныеШтрихкода);
		ДанныеДляСопоставленияНоменклатуры.Вставить("ШтрихкодыКСопоставлению", ШтрихкодыКСопоставлению); //содержит массив, где каждый элемент - структура "Количество, Штрихкод", где штрихкод - это EAN из КМ, который необходимо проверить по регистру Штрихкодов
				
		ПараметрыОткрытияФормы = Новый Структура("ДанныеДляСопоставленияНоменклатуры, Операция, НеизвестныеШтрихкоды, СтруктураКодовМаркировки, Направление", ДанныеДляСопоставленияНоменклатуры, Операция, ШтрихкодыКСопоставлению, СтруктураКодовМаркировки, Объект.Направление);
		ОбработкаОповещенияПодборНоменклатуры = Новый ОписаниеОповещения("ОбработатьКодМаркировки", ЭтотОбъект);
		РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму("Обработка.РозничноеВыбытиеМаркированнойПродукцииИСМПТК.Форма.ПоискНоменклатурыПоШтрихкоду", ПараметрыОткрытияФормы, ЭтотОбъект,,,, ОбработкаОповещенияПодборНоменклатуры, РежимОткрытия);
				
	ИначеЕсли Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", Номенклатура)).Количество() = 0
		И Объект.Марки.НайтиСтроки(Новый Структура("Номенклатура", Номенклатура)).Количество() = 0
		И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		ТекстВопроса = НСтр("ru = 'Указанный код маркировки принадлежит номенклатуре «" + Номенклатура + "», отсутствующей в выбранном документе-основании.
							|Добавить данный код в Акт?'");
		
		ИзменитьДокументОснованиеЗавершение = Новый ОписаниеОповещения("ДобавитьНовуюНоменклатуру", ЭтаФорма, Новый Структура("СтруктураКодовМаркировки, Номенклатура", СтруктураКодовМаркировки, Номенклатура));
		ПоказатьВопрос(ИзменитьДокументОснованиеЗавершение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		
		Возврат;
		
	Иначе
		
		СтруктураКодовМаркировки.Вставить("Номенклатура", Номенклатура);
		ЗавершитьДобавлениеКодаМаркировки(СтруктураКодовМаркировки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНовыйЛогистическийКод(СтруктураКодовМаркировки)
	
	ЗавершениеПолучитьДанныеПоГрупповомуКодуМаркировки = Новый ОписаниеОповещения("ЗавершениеПолучитьДанныеПоГрупповомуКодуМаркировки", ЭтаФорма, СтруктураКодовМаркировки);
	ПолучитьКлючАвторизации(ЗавершениеПолучитьДанныеПоГрупповомуКодуМаркировки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПолучитьДанныеПоГрупповомуКодуМаркировки(РезультатВыполнения, Параметры) Экспорт
	
	ИнтеграцияИСМПТККлиент.ЗавершениеПолучитьДанныеПоГрупповомуКодуМаркировки_Общая(ЭтаФорма, РезультатВыполнения, Параметры);
		
КонецПроцедуры

&НаСервере
функция ПолучитьНоменклатуруПоШтрихкоду(EAN)
	
	Возврат ИнтеграцияИСМПТК.ПолучитьНоменклатуруПоШтрихкоду(EAN, ЭтаФорма);
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьДобавлениеКодаМаркировки(СтруктураКодовМаркировки = Неопределено, ПараметрКоманды = Неопределено) Экспорт
	
	Если Не СтруктураКодовМаркировки = Неопределено Тогда 
		
		Если ЗначениеЗаполнено(СтруктураКодовМаркировки.Номенклатура) 
			И Не ЗначениеЗаполнено(СтруктураКодовМаркировки.ВидПродукцииИС) Тогда
			
			ВидПродукцииИС = ИнтеграцияИСМПТКПереопределяемый.ПолучитьВидПродукцииПоНоменклатуре(СтруктураКодовМаркировки.Номенклатура);
			СтруктураКодовМаркировки.ВидПродукцииИС = ВидПродукцииИС;
		КонецЕсли;
		
		ДобавитьКодМаркировкиВДерево(СтруктураКодовМаркировки.Номенклатура, СтруктураКодовМаркировки);
		
		НоваяСтрока = Объект.Марки.Добавить();
		НоваяСтрока.КодМаркировки 		= СтруктураКодовМаркировки.КодМаркировки;
		НоваяСтрока.КодИдентификации 	= СтруктураКодовМаркировки.КодИдентификации;
		НоваяСтрока.GTIN 				= СтруктураКодовМаркировки.GTIN;
		Если СтруктураКодовМаркировки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда 
			НоваяСтрока.GTINВерхнегоУровня 	= СтруктураКодовМаркировки.GTINВерхнегоУровня;
		КонецЕсли;
		НоваяСтрока.EAN 				= СтруктураКодовМаркировки.EAN;
		НоваяСтрока.Номенклатура 		= СтруктураКодовМаркировки.Номенклатура;
		НоваяСтрока.Количество 			= СтруктураКодовМаркировки.Количество;
		НоваяСтрока.ВидУпаковки			= СтруктураКодовМаркировки.ВидУпаковки;
		НоваяСтрока.ВидПродукцииИС		= СтруктураКодовМаркировки.ВидПродукцииИС;
				
		Для Каждого Строка ИЗ ДеревоМаркированнойПродукции.ПолучитьЭлементы() Цикл
			Элементы.ДеревоМаркированнойПродукции.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
		КонецЦикла;
		
		//Проверяем состояние кода маркировки и выводим предупреждение, если с кодом что-то не так
		Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() Тогда
			ДополнительныеПараметры = Новый Структура("КодИдентификации", СтруктураКодовМаркировки.КодИдентификации);
			ТокенАвторизацииВрем = Неопределено;
			//отложено: вывод сообщения с результатом запроса статуса
			ЗавершениеПроверитьСостояниеКМПриДобавленииНаСервере = Новый ОписаниеОповещения("ЗавершениеПроверитьСостояниеКМПриДобавленииНаСервере", ЭтаФорма, ДополнительныеПараметры);
			//предварительный запрос токена
			ПолучитьКлючАвторизации(ЗавершениеПроверитьСостояниеКМПриДобавленииНаСервере);
			
			//Если по какой-то причине не удалось выполнить получение Токена, то и запрос на проверку статуса КМ не выполнялся,
			//об этом нужно предупредить и отметить непроверенные коды.
			Если ТокенАвторизацииВрем = Неопределено Тогда 
				СообщитьОНевозможностиВыполнитьПроверкуСтатусаКМ("Фоновый");
			КонецЕсли;			
			
		КонецЕсли;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьКодМаркировкиВДерево(Номенклатура, СтруктураКодовМаркировки) Экспорт
	
	НайденнаяСтрока = НайтиЗначениеЭлементаДереваПоНоменклатуре(Номенклатура, ДеревоМаркированнойПродукции);
	
	Если Не НайденнаяСтрока = Неопределено Тогда
		
		Если СтруктураКодовМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.Потребительская Тогда 
			КоличествоПодобранно = 1;
		Иначе 
			КоличествоПодобранно = СтруктураКодовМаркировки.Количество;
		КонецЕсли;
		
		НоваяСтрокаКод = НайденнаяСтрока.ПолучитьЭлементы().Добавить();
		
		//Поддержка двойного формата кодов SSCC
		ВыполнялосьПреобразованиеКМ = ?(Параметры.Свойство("ВыполнялосьПреобразованиеКМ"), Параметры.ВыполнялосьПреобразованиеКМ, Ложь);
		КодИдентификации 			= ?(Параметры.Свойство("КодИдентификации"), Параметры.КодИдентификации, СтруктураКодовМаркировки.КодИдентификации); 
		Если ВыполнялосьПреобразованиеКМ Тогда
			НоваяСтрокаКод.КодМаркировки    = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодВBase64(КодИдентификации);
			НоваяСтрокаКод.КодИдентификации = КодИдентификации;
		Иначе
			НоваяСтрокаКод.КодМаркировки    = СтруктураКодовМаркировки.КодМаркировки;
			НоваяСтрокаКод.КодИдентификации = СтруктураКодовМаркировки.КодИдентификации;
		КонецЕсли;
		//////
		
		НоваяСтрокаКод.GTIN 			 	 = СтруктураКодовМаркировки.GTIN;
		Если СтруктураКодовМаркировки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда 
			НоваяСтрокаКод.GTINВерхнегоУровня = СтруктураКодовМаркировки.GTINВерхнегоУровня;
		КонецЕсли;
		
		НоваяСтрокаКод.Количество			 = 0;
		НоваяСтрокаКод.КоличествоПодобранно	 = КоличествоПодобранно;
		
		НайденнаяСтрока.КоличествоПодобранно = НайденнаяСтрока.КоличествоПодобранно + НоваяСтрокаКод.КоличествоПодобранно;
		НоваяСтрокаКод.ВидУпаковки 			 = СтруктураКодовМаркировки.ВидУпаковки;
		НоваяСтрокаКод.ВидПродукцииИС		 = СтруктураКодовМаркировки.ВидПродукцииИС;
					
	Иначе
		
		ЭлементыДерева = ДеревоМаркированнойПродукции.ПолучитьЭлементы();
		
		НоваяСтрокаНоменклатура = ЭлементыДерева.Добавить();
		НоваяСтрокаНоменклатура.Номенклатура = Номенклатура;
		
		Если ЗначениеЗаполнено(СтруктураКодовМаркировки.КодИдентификации) Тогда 
			НоваяСтрокаКод = НоваяСтрокаНоменклатура.ПолучитьЭлементы().Добавить();
			НоваяСтрокаКод.КодМаркировки 	  = СтруктураКодовМаркировки.КодМаркировки;
			НоваяСтрокаКод.КодИдентификации   = СтруктураКодовМаркировки.КодИдентификации;
			НоваяСтрокаКод.GTIN 			  = СтруктураКодовМаркировки.GTIN;
			Если СтруктураКодовМаркировки.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая") Тогда 
				НоваяСтрокаКод.GTINВерхнегоУровня = СтруктураКодовМаркировки.GTINВерхнегоУровня;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтруктураКодовМаркировки.Количество) Тогда 
				НоваяСтрокаКод.КоличествоПодобранно	= ?(СтруктураКодовМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.Потребительская, 1, 0);
			Иначе 
				НоваяСтрокаКод.КоличествоПодобранно	= СтруктураКодовМаркировки.Количество;
			КонецЕсли;
			
			НоваяСтрокаНоменклатура.КоличествоПодобранно = НоваяСтрокаКод.КоличествоПодобранно;
			
			НоваяСтрокаКод.ВидУпаковки 	  = СтруктураКодовМаркировки.ВидУпаковки;
			НоваяСтрокаКод.ВидПродукцииИС = СтруктураКодовМаркировки.ВидПродукцииИС;
				
		КонецЕсли;
		
		НоваяСтрокаНоменклатура.ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.ПустаяСсылка();
		
		Если СтруктураКодовМаркировки.Свойство("КоличествоИсточник") Тогда 
			НоваяСтрокаНоменклатура.Количество	= СтруктураКодовМаркировки.КоличествоИсточник;
		Иначе 
			НоваяСтрокаКод.Количество = 0;
		КонецЕсли;
		
		ЕденицаИзмерения = ИнтеграцияИСМПТКПереопределяемый.ПолучитьБазовуюЕденицуИзмерения(Номенклатура);
		НоваяСтрокаНоменклатура.ЕдиницаИзмерения = ЕденицаИзмерения;
		
		ИнтерфейсИСМПТККлиентСервер.УстановитьИндексКартинкиТипаУпаковки(НоваяСтрокаНоменклатура);
		
	КонецЕсли;
	
	Если Не НоваяСтрокаКод = Неопределено Тогда
		ИнтерфейсИСМПТККлиентСервер.УстановитьИндексКартинкиТипаУпаковки(НоваяСтрокаКод);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиЗначениеЭлементаДереваПоНоменклатуре(Номенклатура, Дерево)
	
	НайденныйЭлемент = Неопределено;
	
	ЭлементыВетвиДерева = Дерево.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыВетвиДерева Цикл
		Если ЭлементДерева.Номенклатура = Номенклатура Тогда
			НайденныйЭлемент = ЭлементДерева;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденныйЭлемент;
	
КонецФункции

&НаСервере
Функция ПолучитьСтруктуруКодовМаркировки()
	
	СтруктураКодовМаркировки = Новый Структура;
	СтруктураКодовМаркировки.Вставить("КодМаркировки", "");
	СтруктураКодовМаркировки.Вставить("КодИдентификации", "");
	СтруктураКодовМаркировки.Вставить("GTIN", "");
	СтруктураКодовМаркировки.Вставить("КоличествоИсточник", 0);
	СтруктураКодовМаркировки.Вставить("Количество", 0);
	СтруктураКодовМаркировки.Вставить("ВидУпаковки", Перечисления.ВидыУпаковокИСМПТК.ПустаяСсылка());
	
	Возврат СтруктураКодовМаркировки;
	
КонецФункции

&НаСервере
Функция ЗагрузитьКодыМаркировки(ИсходныеДанные, ПараметрыСканирования = Неопределено)
	
	НастройкиРазбораКодаМаркировки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйПовтИсп.НастройкиРазбораКодаМаркировки();    
	
	Если Не ТипЗнч(ИсходныеДанные) = Тип("Массив") Тогда
		ДанныеДляРазбора = Новый Массив;
		ДанныеДляРазбора.Добавить(ИсходныеДанные);
	Иначе 
		ДанныеДляРазбора = ИсходныеДанные;
	КонецЕсли;
	
	Для Каждого КодМаркировкиBase64 Из ДанныеДляРазбора Цикл
		
		ДанныеРазбора = РазборИОбработкаКодовМаркировкиИСМПТКСлужебный.РазобратьКодМаркировки(КодМаркировкиBase64, НастройкиРазбораКодаМаркировки.ДоступныеВидыПродукции, , НастройкиРазбораКодаМаркировки);
		
		Если ДанныеРазбора = Неопределено Тогда 
			ТекстОшибки = НСтр("ru = 'Не удалось распознать код маркировки.'");
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
			Возврат Неопределено;
		КонецЕсли;
		
		GTINВерхнегоУровня = "";
		GTIN = "";
		
		СтрокаКодыМаркировки = Объект.Марки.Добавить();
		
		Если ДанныеРазбора.ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.Групповая
			Или ДанныеРазбора.ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.Потребительская Тогда 
			
			GTINВерхнегоУровня = ДанныеРазбора.СоставКодаМаркировки.GTIN;
			Если ДанныеРазбора.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда
				GTIN = ДанныеРазбора.СоставКодаМаркировки.GTIN; //Для потребительского кода оба реквизита GTIN равны.
			Иначе
				GTIN = ""; //Для групповой упаковки GTIN (вложенного товара) при загрузке не знаем, поэтому оставляем пустым
			КонецЕсли;
			
			ГрупповаяУпаковка = ДанныеРазбора.ВидУпаковки = Перечисления.ВидыУпаковокИСМПТК.Групповая;
			
			СтрокаКодыМаркировки.КодМаркировки 		= КодМаркировкиBase64.ШтрихКод;
			СтрокаКодыМаркировки.GTIN 				= GTIN;
			СтрокаКодыМаркировки.GTINВерхнегоУровня = GTINВерхнегоУровня;
			СтрокаКодыМаркировки.EAN 				= ДанныеРазбора.СоставКодаМаркировки.EAN;
			СтрокаКодыМаркировки.КодИдентификации	= ДанныеРазбора.НормализованныйКодМаркировки;
			СтрокаКодыМаркировки.ВидУпаковки		= ДанныеРазбора.ВидУпаковки;
			СтрокаКодыМаркировки.ВидПродукцииИС		= ДанныеРазбора.ВидыПродукции[0];
			СтрокаКодыМаркировки.Количество			= 1;
			
		Иначе
			
			//Для транспортной (логистической) упаковки не можем сразу определить GTIN, нужен запрос данных вложенных позиций с сервера.
			//GTINВерхнегоУровня у транспортной упаковки всегда пустой, т.к. в коде SSCC не содержится GTIN.
			GTINВерхнегоУровня = "";
			GTIN = "";
			
			СтрокаКодыМаркировки.КодМаркировки 	= КодМаркировкиBase64.ШтрихКод;
			Если ДанныеРазбора.СоставКодаМаркировки.Свойство("SSCC") Тогда
				СтрокаКодыМаркировки.GTIN 				= GTIN;
				СтрокаКодыМаркировки.GTINВерхнегоУровня = GTINВерхнегоУровня;
				СтрокаКодыМаркировки.EAN 				= ДанныеРазбора.СоставКодаМаркировки.SSCC;
			Иначе 
				СтрокаКодыМаркировки.GTIN 				= GTIN;
				СтрокаКодыМаркировки.GTINВерхнегоУровня = GTINВерхнегоУровня;
				СтрокаКодыМаркировки.EAN 				= ДанныеРазбора.СоставКодаМаркировки.EAN;
			КонецЕсли;
			СтрокаКодыМаркировки.КодИдентификации 	= ДанныеРазбора.НормализованныйКодМаркировки;
			СтрокаКодыМаркировки.ВидУпаковки		= Перечисления.ВидыУпаковокИСМПТК.Логистическая;
			СтрокаКодыМаркировки.ВидПродукцииИС		= ДанныеРазбора.ВидыПродукции[0];
			СтрокаКодыМаркировки.Количество			= 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

#КонецОбласти

#Область РаботаСТСД

#Область ЗагрузкаИзТСД

&НаКлиенте
Процедура ЗагрузитьИзТСДПослеАвторизации(РезультатАвторизации, Штрихкоды) Экспорт
	
	Подключаемый_ПолученыДанныеИзТСД(Штрихкоды, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолученыДанныеИзТСД(Штрихкоды, АвторизацияЗапрашивалась = Неопределено) Экспорт
	
	Если Штрихкоды.Количество() = 0 Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'В полученных данных не содержится информации о считанных штриховых кодах'"));
		Возврат;
	КонецЕсли;
	
	РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.ОповеститьОНачалеОбработкиДанныхТСД();
	ПараметрыСканирования = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.ПараметрыСканирования(ЭтотОбъект);
	РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.ПреобразоватьШтрихкодыТСДВBase64(Штрихкоды);
	ЗагрузкаДанныхТСД = ОбработатьПолученныеДанныеТСДНаСервере(Штрихкоды, ПараметрыСканирования, Неопределено);
	
	ОбработатьПолученныеДанныеТСД();
	
КонецПроцедуры

&НаСервере
Функция ОбработатьПолученныеДанныеТСДНаСервере(ШтрихкодыТСД, ПараметрыСканирования, КэшированныеЗначения)
	
	Результат = ИнтеграцияИСМПТК.ОбработатьПолученныеДанныеТСДВДокументе(ЭтотОбъект, ШтрихкодыТСД, ПараметрыСканирования);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПолученныеДанныеТСД()
	
	Если ЗагрузкаДанныхТСД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивКодовМаркировки = Новый Массив;
	
	Для НомерСтроки = 0 По ЗагрузкаДанныхТСД.Всего - 1 Цикл
		Штрихкод = ЗагрузкаДанныхТСД.ШтрихкодыТСД[НомерСтроки];
		ДанныеШтрихкода = Новый Структура("ШтрихКод, ФорматBase64, Количество", Штрихкод.Штрихкод, Ложь, 1);
		РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ЗакодироватьШтрихкодДанныхBase64(ДанныеШтрихкода);
		Если Не ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПроверкаФорматаТранспортногоКодаПройденаУспешно(ДанныеШтрихкода, Истина) Тогда
			Продолжить;
		КонецЕсли;
		МассивКодовМаркировки.Добавить(ДанныеШтрихкода);
	КонецЦикла;
	
	СтруктураПараметров = Новый Структура("ФормаОбъекта, МассивКодовМаркировки", ЭтотОбъект, МассивКодовМаркировки);
	
	Токен = Неопределено;
	Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоОпределениеЛогистическихКодовИСМПТК() Тогда 
		Токен = ИнтеграцияИСМПТККлиент.ПолучитьТокенАвторизации(Объект.Организация);
		ИнтеграцияИСМПТККлиент.ЗагрузитьКодыМаркировкиИзФайлаЗавершение(Токен, СтруктураПараметров);
	Иначе
		ИнтеграцияИСМПТККлиент.ЗагрузитьКодыМаркировкиИзФайлаЗавершение(Неопределено, СтруктураПараметров);
	КонецЕсли;
	
	Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() Тогда 
		
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
			ТекстОшибки = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияТребуетсяУказатьОрганизациюПриЗапросеДанныхУпаковокССервера();
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
			Возврат;
		КонецЕсли;
		
		//После заполнения ТЧ проверяем коды еще одним запросом
		Если Токен = Неопределено Тогда 
			Токен = ИнтеграцияИСМПТККлиент.ПолучитьТокенАвторизации(Объект.Организация);
		КонецЕсли;
		ЗапроситьДанныеОСостоянииКодовСписком(Токен);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область МобильноеПриложение

&НаКлиенте
Процедура МобильноеПриложениеЗагрузитьТовары(Команда)
	
	ОчиститьСообщения();
	Если ИнтеграцияИСМПТКВызовСервера.ИспользоватьАвтоПроверкаВалидностиКодаИСМПТК() 
		И Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Включена опция автоматической проверки состояния кодов маркировки по данным сервера. Для получения токена сеанса требуется указать Организацию!'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеВыбораЭлемента = Новый ОписаниеОповещения("МобильноеПриложениеЗагрузитьТоварыОбработкаВыбора", ЭтотОбъект);
	Список = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйВызовСервера.МобильноеПриложениеЗагрузитьТоварыНаСервере();
	Список.ПоказатьОтметкуЭлементов(ОповещениеПослеВыбораЭлемента, "Выберите документ для загрузки");
	
КонецПроцедуры

&НаКлиенте
Процедура МобильноеПриложениеЗагрузитьТоварыОбработкаВыбора(Элемент, Параметры) Экспорт
	
	РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиент.МобильноеПриложениеЗагрузитьТоварыОбработкаВыбораОбщая(ЭтотОбъект, Элемент, Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область ВыводИзОборота

&НаСервере
Функция ПолучитьУВИО() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =  "ВЫБРАТЬ
	|	УведомлениеОВыводеИзОборотаИСМПТК.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.УведомлениеОВыводеИзОборотаИСМПТК КАК УведомлениеОВыводеИзОборотаИСМПТК
	|ГДЕ
	|	УведомлениеОВыводеИзОборотаИСМПТК.ДокументОснование = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
	Возврат ПредопределенноеЗначение("Документ.УведомлениеОВыводеИзОборотаИСМПТК.ПустаяСсылка");
	
КонецФункции

#КонецОбласти

#Область КомандыРедактированияСтроки

#Область ПотребительскиеИГрупповыеКМ

&НаКлиенте
Процедура ПодобратьНоменклатуруИЗарегистрироватьGTIN(Команда)
	
	//1. Просим записать документ
	//2. Перебираем ТЧ Марки:
	//   отбираем все строки с НЕ транспортными кодами, у которых заполнены GTINВерхнегоУровня, но не заполнена номенклатура.
	// Это будут потребительские коды, у которых не удалось опознать номенклатуру,
	// а также групповые КМ, у которых не определилась номенклатура верхнего уровня, т.е. по коду товара самой групповой упаковки (блока).
	//3. Открываем форму обработки ПоискНоменклатурыПоШтрихкоду с передачей параметра Операция = "РегистрацияШтрихкодовСписком"
	//4. В форме назначается номенклатура, при закрытии должна списком регистрироваться информация по ШК.
	//5. В обработчике события закрытия формы обработки дозаполнеям ТЧ Марок, модифицируем документ, перестраиваем дерево кодов.
	
	Если Объект.Марки.Количество() = 0 Тогда
		ТексСообщения = НСтр("ru = 'В документе отсутствуют данные по кодам маркировки!'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТексСообщения);
		Возврат;
	КонецЕсли;
		
	//Проверяем, записан ли документ
	ТекстВопроса = "";
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Параметры.Ключ) ИЛИ ЭтаФорма.Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Перед началом подбора номенклатуры и регистрации штрихкодов необходимо записать документ. Продолжить?'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение   = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПодобратьНоменклатуруИЗарегистрироватьGTIN", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		
	Иначе
		
		//Переходим к отбору штрихкодов, требующих регистрации
		ПродолжитьПодборНоменклатурыИРегистрацииШтрихкодов();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПодобратьНоменклатуруИЗарегистрироватьGTIN(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Попытка
			// выполняем запись документа
			ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись);
			Если ЭтаФорма.Записать(ПараметрыЗаписи) Тогда
				//Переходим к отбору штрихкодов, требующих регистрации
				ПродолжитьПодборНоменклатурыИРегистрацииШтрихкодов();
			КонецЕсли;
			
		Исключение
			
			Информация = ИнформацияОбОшибке();
			ТекстСообщения = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияНеУдалосьЗаписатьДокументМаркировки();
			ТекстСообщения = ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, КраткоеПредставлениеОшибки(Информация));
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, , , "Форма.Объект");
			
		КонецПопытки;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПодборНоменклатурыИРегистрацииШтрихкодов()
	
	//Отбираем из ТЧ Марки все строки, у которых заполнен GTIN, но не заполнена номенклатура:
	//для таких позиций необходимо сформировать список штрихкодов к регистрации с группировкой по GTIN.
	ШтрихкодыКСопоставлению = ПодготовитьДанныеДляРегистрацииНоменклатуры();
	
	//Открываем форму подбора и регистрации с передачей неопознанных штрихкодов
	ДанныеДляСопоставленияНоменклатуры = Новый Структура(); //передается в параметрах открытия формы подбора
	Операция = "РегистрацияШтрихкодовСписком";
			
	ДанныеДляСопоставленияНоменклатуры.Вставить("ШтрихкодыКСопоставлению", ШтрихкодыКСопоставлению); //содержит массив, где каждый элемент - структура "Количество, Штрихкод", где штрихкод - это EAN из строка ТЧ Марки, который необходимо зарегистрировать
	
	ПараметрыОткрытияФормы = Новый Структура("ДанныеДляСопоставленияНоменклатуры, Операция, НеизвестныеШтрихкоды, СтруктураКодовМаркировки, Направление", ДанныеДляСопоставленияНоменклатуры, Операция, ШтрихкодыКСопоставлению, Неопределено, Объект.Направление);
	ОбработкаОповещенияПодборНоменклатуры = Новый ОписаниеОповещения("ПодборНоменклатурыИРегистрацииШтрихкодовПослеЗакрытияФормы", ЭтотОбъект);
	РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Обработка.РозничноеВыбытиеМаркированнойПродукцииИСМПТК.Форма.ПоискНоменклатурыПоШтрихкоду", ПараметрыОткрытияФормы, ЭтотОбъект,,,, ОбработкаОповещенияПодборНоменклатуры, РежимОткрытия);
		
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеДляРегистрацииНоменклатуры()
	
	ТаблицаДляПроверки = Объект.Марки.Выгрузить(, "GTINВерхнегоУровня, Номенклатура, ВидУпаковки");
	ТаблицаДляПроверки.Свернуть("GTINВерхнегоУровня, Номенклатура, ВидУпаковки");
	
	ШтрихкодыКСопоставлению = Новый Массив();
	Для Каждого ЭлементМассива Из ТаблицаДляПроверки Цикл
		Если ЭлементМассива.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая")
			Или ЭлементМассива.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Потребительская") Тогда
			//Нужно проверить: если это входящий АПП и в момент получения некоторые ШК товаров из этого документа
			//не были зар-ны в базе, в ТЧ Марки поле Номенклатура будет пустым даже после регистрации этих ШК.
			//Поэтому дополнительно проверяем по регистру.
			ЕАН = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодEANИзGTIN(ЭлементМассива.GTINВерхнегоУровня);
			ДанныеНоменклатуры = ИнтеграцияИСМПТКПереопределяемый.ПолучитьНоменклатуруПоШтрихкоду(ЕАН);
			Если ЗначениеЗаполнено(ДанныеНоменклатуры.Номенклатура) Тогда 
				//Этот ШК не нужно регистрировать
				Продолжить;
			КонецЕсли;			
			ДанныеШтрихкода = Новый Структура("Количество, Штрихкод", 1, РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.EANПоGTIN(ЭлементМассива.GTINВерхнегоУровня));
			ШтрихкодыКСопоставлению.Добавить(ДанныеШтрихкода);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ШтрихкодыКСопоставлению;
	
КонецФункции

&НаКлиенте
Процедура ПодборНоменклатурыИРегистрацииШтрихкодовПослеЗакрытияФормы(ИсходныеДанные, ДополнительныеПараметры = Неопределено) Экспорт

	Если Не ИсходныеДанные.Свойство("ШтрихкодыНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ИсходныеДанные.ШтрихкодыНоменклатуры.Количество() = 0 Тогда
		Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
			Если ИсходныеДанные.ОтложенныеТовары.Количество() = 0 Тогда 
				//Все ШК были успешно отработаны, заполняем полностью
				Для Каждого НовыйШтрикход Из ИсходныеДанные.ШтрихкодыНоменклатуры Цикл
					Номенклатура = НовыйШтрикход.Номенклатура;
					Штрихкод 	 = НовыйШтрикход.Штрихкод;
					
					ОбработатьСтрокиТаблицыПослеПодбораНаСервере(Номенклатура, Штрихкод);
					Модифицированность = Истина;
				КонецЦикла;
			Иначе
				//Некоторые товары (КМ) не были подобраны, их заполнять не нужно. Выполняем проверки и исключаем такие ШК
				ШтрихкодыДляИсключения = Новый Массив();
				Для Каждого ИсключаемыйШК Из ИсходныеДанные.ОтложенныеТовары Цикл
					ШтрихкодыДляИсключения.Добавить(ИсключаемыйШК.Штрихкод);
				КонецЦикла;

				Для Каждого НовыйШтрикход Из ИсходныеДанные.ШтрихкодыНоменклатуры Цикл
					Номенклатура = НовыйШтрикход.Номенклатура;
					Штрихкод 	 = НовыйШтрикход.Штрихкод;
					Если ШтрихкодыДляИсключения.Найти(Штрихкод) = Неопределено Тогда
						ОбработатьСтрокиТаблицыПослеПодбораНаСервере(Номенклатура, Штрихкод);
						Модифицированность = Истина;	
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Иначе
			ЭтаФорма.Прочитать();
		КонецЕсли;
	КонецЕсли;
	
	СформироватьДеревоКодовМаркировки();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокиТаблицыПослеПодбораНаСервере(Номенклатура, Штрихкод)
	
	GTINВерхнегоУровня = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.GTINПоШтрихкодуEAN(Штрихкод);
	НайденныеСтрокиТаблицы = Объект.Марки.НайтиСтроки(Новый Структура("GTINВерхнегоУровня", GTINВерхнегоУровня));
	
	Для Каждого Строка Из НайденныеСтрокиТаблицы Цикл
		Строка.Номенклатура = Номенклатура;
	КонецЦикла;
  	
КонецПроцедуры

#КонецОбласти

#Область ТранспортныеКМ

&НаКлиенте
Процедура РедактироватьСтрокиТранспортныеКоды(Команда)
	
	//1. Просим записать документ
	//2. Перебираем ТЧ Марки:
	//   отбираем все строки с транспортными и групповыми кодами.
	//3. Открываем форму обработки ПоискНоменклатурыПоШтрихкоду с передачей параметра Операция = "РаботаСТранспортнымиКодами"
	//4. В форме можно указать номенклатуру, а также GTIN. 
	//5. В обработчике события закрытия формы обработки дозаполнеям ТЧ Марок, модифицируем документ, перестраиваем дерево кодов.
	
	Если Объект.Марки.Количество() = 0 Тогда
		ТексСообщения = НСтр("ru = 'В документе отсутствуют данные по кодам маркировки!'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТексСообщения);
		Возврат;
	КонецЕсли;
	
	//Проверяем, записан ли документ
	ТекстВопроса = "";
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Параметры.Ключ) ИЛИ ЭтаФорма.Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Перед началом подбора номенклатуры и регистрации штрихкодов необходимо записать документ. Продолжить?'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение   = Новый ОписаниеОповещения("ПослеЗакрытияВопросаРедактироватьСтрокиТранспортныеКоды", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		
	Иначе
		
		//Переходим к отбору упаковок
		ПродолжитьРедактироватьСтрокиТранспортныеКоды();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаРедактироватьСтрокиТранспортныеКоды(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Попытка
			// выполняем запись документа
			ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись);
			Если ЭтаФорма.Записать(ПараметрыЗаписи) Тогда
				//Переходим к отбору упаковок
				ПродолжитьРедактироватьСтрокиТранспортныеКоды();
			КонецЕсли;
			
		Исключение
			
			Информация = ИнформацияОбОшибке();
			ТекстСообщения = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияНеУдалосьЗаписатьДокументМаркировки();
			ТекстСообщения = ИнтеграцияИСМПТККлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, КраткоеПредставлениеОшибки(Информация));
			ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения, , , "Форма.Объект");
			
		КонецПопытки;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьРедактироватьСтрокиТранспортныеКоды()
	
	//Отбираем из ТЧ Марки все строки, у которых не заполнен GTIN, либо заполнен некорректно, либо заполнен, но не заполнена номенклатура:
	//для таких позиций необходимо сформировать список штрихкодов к указанию данных вручную.
	ШтрихкодыКСопоставлению = ПодготовитьДанныеДляРедактированияСтрокТранспортныхУпаковок();
	
	//Открываем форму подбора и регистрации с передачей неопознанных штрихкодов
	ДанныеДляСопоставленияНоменклатуры = Новый Структура(); //передается в параметрах открытия формы подбора
	Операция = "РаботаСТранспортнымиКодами";
			
	ДанныеДляСопоставленияНоменклатуры.Вставить("ШтрихкодыКСопоставлению", ШтрихкодыКСопоставлению); 
	
	ПараметрыОткрытияФормы = Новый Структура("ДанныеДляСопоставленияНоменклатуры, Операция, НеизвестныеШтрихкоды, СтруктураКодовМаркировки, Направление", ДанныеДляСопоставленияНоменклатуры, Операция, ШтрихкодыКСопоставлению, Неопределено,  Объект.Направление);
	ОбработкаОповещенияПодборНоменклатуры = Новый ОписаниеОповещения("РедактированиеСтрокТранспортныхКодовПослеЗакрытияФормы", ЭтотОбъект);
	РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Обработка.РозничноеВыбытиеМаркированнойПродукцииИСМПТК.Форма.ПоискНоменклатурыПоШтрихкоду", ПараметрыОткрытияФормы, ЭтотОбъект,,,, ОбработкаОповещенияПодборНоменклатуры, РежимОткрытия);
		
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеДляРедактированияСтрокТранспортныхУпаковок()
	
	ШтрихкодыКСопоставлению = Новый Массив();
	
	//1. Обрабатываем транспортные коды
	ШтрихкодыКСопоставлениюТранспортные = Новый Массив();	
	ПараметрыОтбора = Новый Структура("ВидУпаковки", ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Логистическая"));
	ТаблицаУпаковок = Объект.Марки.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого ЭлементМассива Из ТаблицаУпаковок Цикл
		
		ДанныеУпаковки = Новый Структура();
		ДанныеУпаковки.Вставить("Штрихкод", 	ЭлементМассива.КодИдентификации); 
		ДанныеУпаковки.Вставить("Номенклатура", ЭлементМассива.Номенклатура);
		ДанныеУпаковки.Вставить("GTIN", 	    ЭлементМассива.GTIN);
		ДанныеУпаковки.Вставить("Количество",   ЭлементМассива.Количество);
		ДанныеУпаковки.Вставить("ВидПродукции", ЭлементМассива.ВидПродукцииИС);
		ДанныеУпаковки.Вставить("ВидУпаковки",  ЭлементМассива.ВидУпаковки);
		
		ДанныеНоменклатурыХарактеристика = ИнтеграцияИСМПТКПереопределяемый.ПолучитьСведенияОХарактеристикеНоменклатуры(ЭлементМассива.Номенклатура, ЭлементМассива.EAN);
		Если НЕ ДанныеНоменклатурыХарактеристика = Неопределено Тогда
			ДанныеУпаковки.Вставить("Характеристика", ДанныеНоменклатурыХарактеристика.Характеристика);
		Иначе
			ДанныеУпаковки.Вставить("Характеристика", "");
		КонецЕсли;
			
		ШтрихкодыКСопоставлениюТранспортные.Добавить(ДанныеУпаковки);
		
	КонецЦикла;
	
	//2. Обрабатываем групповые коды
	ШтрихкодыКСопоставлениюГрупповые = Новый Массив();	
	ПараметрыОтбора = Новый Структура("ВидУпаковки", ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Групповая"));
	ТаблицаУпаковок = Объект.Марки.НайтиСтроки(ПараметрыОтбора);
		
	Для Каждого ЭлементМассива Из ТаблицаУпаковок Цикл
		
		ДанныеУпаковки = Новый Структура();
		ДанныеУпаковки.Вставить("Штрихкод", 	ЭлементМассива.КодИдентификации); 
		ДанныеУпаковки.Вставить("Номенклатура", Неопределено);  //Для групповых упаковок Номенклатура в данном случае не важна, т.к. она относится к верхнему уровню, а сейчас работаем с нижним (вложенный товар)
		ДанныеУпаковки.Вставить("GTIN", 	    ЭлементМассива.GTIN);
		ДанныеУпаковки.Вставить("Количество",   ЭлементМассива.Количество);
		ДанныеУпаковки.Вставить("ВидПродукции", ЭлементМассива.ВидПродукцииИС);
		ДанныеУпаковки.Вставить("Характеристика", "");
		ДанныеУпаковки.Вставить("ВидУпаковки",  ЭлементМассива.ВидУпаковки);
					
		ШтрихкодыКСопоставлениюГрупповые.Добавить(ДанныеУпаковки);
		
	КонецЦикла;
	
	//3.Формируем общие данные
	ШтрихкодыКСопоставлению.Добавить(ШтрихкодыКСопоставлениюТранспортные);
	ШтрихкодыКСопоставлению.Добавить(ШтрихкодыКСопоставлениюГрупповые);
	
	Возврат ШтрихкодыКСопоставлению;
	
КонецФункции

&НаКлиенте
Процедура РедактированиеСтрокТранспортныхКодовПослеЗакрытияФормы(ИсходныеДанные, ДополнительныеПараметры = Неопределено) Экспорт

	Если Не ИсходныеДанные.Свойство("ШтрихкодыНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ИсходныеДанные.ШтрихкодыНоменклатуры.Количество() = 0 Тогда
		Если Объект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияДокументовИСМПТК.Исходящий") Тогда
			ЗаполнитьТаблицуМарок(ИсходныеДанные.ШтрихкодыНоменклатуры);
			Модифицированность = Истина;
		Иначе
			ЭтаФорма.Прочитать();
		КонецЕсли;
	КонецЕсли;
	
	СформироватьДеревоКодовМаркировки();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуМарок(МассивДанныхУпаковок)
	
	Для Каждого Элемент Из МассивДанныхУпаковок Цикл
		
		КодИдентификации = Элемент.КодИдентификации;
		ДанныеУпаковки   = Элемент.ДанныеУпаковки;
		
		ИзменилсяИдентификаторКода = ?(Элемент.Свойство("ИзменилсяИдентификаторКода"), Элемент.ИзменилсяИдентификаторКода, Ложь);
		ПреобразованныйКод = КодИдентификации; //этот код будет использован для перезаполнения данных в таблице документа
		Если ИзменилсяИдентификаторКода Тогда
			//Было выполнено преобразование формата транспортного кода SSCC (18 <> 20 символов)
			//Возвращаем исходное значение, чтобы найти нужную строку в таблице документа
			Если СтрДлина(КодИдентификации) = 18 Тогда
				КодИдентификации = "00" + КодИдентификации;
			ИначеЕсли СтрДлина(КодИдентификации) = 20 Тогда
				КодИдентификации = Сред(КодИдентификации, 3);
			КонецЕсли;
		КонецЕсли;
				
		НайденныеСтрокиТаблицы = Объект.Марки.НайтиСтроки(Новый Структура("КодИдентификации", КодИдентификации));
		Для Каждого Строка Из НайденныеСтрокиТаблицы Цикл
			Если Строка.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИСМПТК.Логистическая") Тогда
				ЗаполнитьЗначенияСвойств(Строка, ДанныеУпаковки);
				Если ИзменилсяИдентификаторКода Тогда
					Строка.КодИдентификации = ПреобразованныйКод;
					Строка.КодМаркировки	= РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ШтрихкодВBase64(ПреобразованныйКод);
				КонецЕсли;
			Иначе
				ЗаполнитьЗначенияСвойств(Строка, ДанныеУпаковки,, "Номенклатура"); //Для групповой Ном-ра заполняется по верхнему уровню, а здесь обрабатывается нижний (вложенные товары)
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроверкаСостоянияКодаНаСервере

&НаКлиенте
Процедура ПроверитьСостояниеКодовНаСервере(Команда)
	
	//Проверяем данные таблицы
	Если Объект.Марки.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'В табличной части Марки отсутствуют коды маркировки.'"); 
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстОшибки = НСтр("ru = 'Не заполнена Организация, получение Токена для запроса невозможно.'"); 
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ТокенАвторизацииВрем = Неопределено;
	ЗавершениеПроверитьСостояниеКодовНаСервере = Новый ОписаниеОповещения("ЗавершениеПроверитьСостояниеКодовНаСервере", ЭтаФорма);
	ПолучитьКлючАвторизации(ЗавершениеПроверитьСостояниеКодовНаСервере);
	
	//Если по какой-то причине не удалось выполнить получение Токена, то и запрос на проверку статуса КМ не выполнялся,
	//об этом нужно предупредить и отметить непроверенные коды.
	Если ТокенАвторизацииВрем = Неопределено Тогда 
		СообщитьОНевозможностиВыполнитьПроверкуСтатусаКМ("Ручной");
	КонецЕсли;
	
КонецПроцедуры

//Выполнить запрос состояния КМ с сервера списком.
//Используется из команд ручной проверки статусов, загрузки из файла, ТСД и Мобильного приложения.
&НаСервере
Процедура ЗавершениеПроверитьСостояниеКодовНаСервере(РезультатВыполнения, Параметры) Экспорт
			
	ТокенАвторизацииВрем = ?(ЗначениеЗаполнено(РезультатВыполнения), РезультатВыполнения, Неопределено);
	ЗапроситьДанныеОСостоянииКодовСписком(ТокенАвторизацииВрем);
	
КонецПроцедуры

&НаСервере
Процедура ЗапроситьДанныеОСостоянииКодовСписком(ТокенАвторизации = Неопределено)
	
	МассивКМ = Новый Массив();
	Для Каждого СтрокаМарка Из Объект.Марки Цикл
		МассивКМ.Добавить(СтрокаМарка.КодИдентификации);	
	КонецЦикла;
	
	ОтветСервераСостояниеКМ = ИнтерфейсИСМПТК.ЗапроситьДанныеОСостоянииКМ(МассивКМ, Объект.Организация, ТокенАвторизации);
	ОбработатьОтветСервераПоСостояниюКМ(МассивКМ, ОтветСервераСостояниеКМ);	
	
КонецПроцедуры

//Выполнить запрос состояния конкретного КМ при его добавлении.
//
&НаСервере
Процедура ЗавершениеПроверитьСостояниеКМПриДобавленииНаСервере(РезультатВыполнения, Параметры) Экспорт
	
	ТокенАвторизацииВрем = ?(ЗначениеЗаполнено(РезультатВыполнения), РезультатВыполнения, Неопределено);
		
	МассивКМ = Новый Массив();
	МассивКМ.Добавить(Параметры.КодИдентификации);
	
	ОтветСервераСостояниеКМ = ИнтерфейсИСМПТК.ЗапроситьДанныеОСостоянииКМ(МассивКМ, Объект.Организация, ТокенАвторизацииВрем);
	ОбработатьОтветСервераПоСостояниюКМ(МассивКМ, ОтветСервераСостояниеКМ);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьОтветСервераПоСостояниюКМ(МассивКМ, ОтветСервераСостояниеКМ)
	
	НовоеСоответствиеСтатусов = Неопределено;
	ИнтеграцияИСМПТК.ОбработатьОтветСервераПоСостояниюКМ_Общая(ЭтаФорма, МассивКМ, ОтветСервераСостояниеКМ, "АктПриемаПередачи", НовоеСоответствиеСтатусов);
	
	//Заполняем статус проверки в дереве по данным запроса сервера
	ЭлементыВетвиДерева = ДеревоМаркированнойПродукции.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыВетвиДерева Цикл
		Если Не ЗначениеЗаполнено(ЭлементДерева.КодИдентификации) Тогда
			Для Каждого ЭлементНижнийУровень Из ЭлементДерева.ПолучитьЭлементы() Цикл
				Если Не ЭлементНижнийУровень.Ошибочный Тогда //Значение по умолчанию - ложь. Если стоит Истина, значит код уже проверен - сохранеям этот признак.
					ЭлементНижнийУровень.Ошибочный = ?(НовоеСоответствиеСтатусов.Получить(ЭлементНижнийУровень.КодИдентификации) = Неопределено, Ложь, НовоеСоответствиеСтатусов.Получить(ЭлементНижнийУровень.КодИдентификации));
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если Не ЭлементДерева.Ошибочный Тогда 
				ЭлементДерева.Ошибочный = ?(НовоеСоответствиеСтатусов.Получить(ЭлементДерева.КодИдентификации) = Неопределено, Ложь, НовоеСоответствиеСтатусов.Получить(ЭлементДерева.КодИдентификации));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
			
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОНевозможностиВыполнитьПроверкуСтатусаКМ(РежимЗапроса) Экспорт

	Если РежимЗапроса = "Ручной" Тогда
		ТекстСообщения = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияНевозможноВыполнитьПроверкуСтатусаКМ_Вручную();
	Иначе	
		ТекстСообщения = РазборИОбработкаКодовМаркировкиИСМПТКСлужебныйКлиентСервер.ТекстСообщенияНевозможноВыполнитьПроверкуСтатусаКМ_ВФоне();
	КонецЕсли;
	ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти