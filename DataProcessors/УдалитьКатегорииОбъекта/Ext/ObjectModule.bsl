#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Процедура заполняет табличную часть обработки категориями объекта.
// При заполнении используются значения реквизитов обработки: 
// ОбъектОтбораКатегорий - объект, категории которого отбираются.
// НазначениеКатегорий - значение реквизита, по которому отбораются категории.
//
// Параметры:
//  Нет.
//
Процедура ПрочитатьЗаполнитьКатегорииОбъекта() Экспорт

	КатегорииОбъекта.Загрузить(ПрочитатьТаблицуКатегорийОбъекта());

КонецПроцедуры

// Процедура проверяет, должна ли новая категория попасть в табличную часть обработки, 
// если Да, то добавляет категорию в табличную часть КатегорииОбъекта.
//
// Параметры:
//  Категория - добавляемая категория.
//
Процедура ПроверитьДобавитьНовуюКатегорию(Категория) Экспорт

	Если ТипЗнч(НазначениеКатегорий) = Тип("СписокЗначений") Тогда
		Если НазначениеКатегорий.НайтиПоЗначению(Категория.НазначениеКатегории) = Неопределено Тогда
			Возврат;
		КонецЕсли;

	ИначеЕсли Категория.НазначениеКатегории <> НазначениеКатегорий Тогда
		Возврат;
	КонецЕсли;

	// Определение позиции категории в табличной части.

	Для Индекс = 0 По КатегорииОбъекта.Количество() - 1 Цикл
		Если Категория.Наименование < КатегорииОбъекта[Индекс].Категория.Наименование Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;

	// Вставка категории с соответствующую позицию.

	НоваяСтрока = КатегорииОбъекта.Вставить(Индекс);

	НоваяСтрока.ИндексКартинки  = ?(Категория.ПометкаУдаления, 4, 3);
	НоваяСтрока.Категория       = Категория;
	НоваяСтрока.Принадлежность  = Истина;
	
КонецПроцедуры

// Функция записывеет категории объекта в информационную базу.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Истина - если категории объекта были записаны, или их не требуется записывать
//  Ложь   - если категории объекта не удалось записать.
//
Функция ЗаписатьКатегорииОбъекта() Экспорт

	Если ОбщегоНазначенияБК.ПроверитьПравоДоступа(Метаданные.РегистрыСведений.УдалитьКатегорииОбъектов, "Изменение", Истина) Тогда
		
		НаборЗаписейКатегорииОбъекта = РегистрыСведений.УдалитьКатегорииОбъектов.СоздатьНаборЗаписей();

		Для Каждого Строка Из КатегорииОбъекта Цикл
			Если Строка.Принадлежность Тогда
				Запись = НаборЗаписейКатегорииОбъекта.Добавить();

				Запись.Объект    = ОбъектОтбораКатегорий;
				Запись.Категория = Строка.Категория;
			КонецЕсли;
		КонецЦикла;

		НаборЗаписейКатегорииОбъекта.Отбор.Объект.Установить(ОбъектОтбораКатегорий);

		Попытка
			НаборЗаписейКатегорииОбъекта.Записать();
		Исключение
			ВызватьИсключение "Не удалось записать категории объекта:" + Символы.ПС + ОписаниеОшибки();
		КонецПопытки;

		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПрочитатьТаблицуКатегорийОбъекта()
	
	Запрос = Новый Запрос();

	Запрос.УстановитьПараметр("НазначениеКатегорий",     НазначениеКатегорий);
	Запрос.УстановитьПараметр("ОбъектОтбораКатегорий",   ОбъектОтбораКатегорий);

	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА КатегорииОбъектов.ПометкаУдаления
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ИндексКартинки,
	|	КатегорииОбъектов.Ссылка                                     КАК Категория,
	|	КатегорииОбъектов.Наименование                               КАК Наименование,
	|
	|	ВЫБОР КОГДА
	|		РегистрСведений.УдалитьКатегорииОбъектов.Объект ЕСТЬ НЕ NULL
	|	ТОГДА
	|		Истина
	|	ИНАЧЕ
	|		Ложь
	|	КОНЕЦ                                                        КАК Принадлежность
	|
	|ИЗ
	|// Отбираются категории, предназначенные для заданного типа объектов.
	|	(
	|	ВЫБРАТЬ 
	|		Справочник.УдалитьКатегорииОбъектов.Ссылка          КАК Ссылка,
	|		Справочник.УдалитьКатегорииОбъектов.Наименование    КАК Наименование,
	|		Справочник.УдалитьКатегорииОбъектов.ПометкаУдаления КАК ПометкаУдаления
	|
	|	ИЗ
	|		Справочник.УдалитьКатегорииОбъектов
	|
	|	ГДЕ
	|		Справочник.УдалитьКатегорииОбъектов.НазначениеКатегории В ( &НазначениеКатегорий )
	|
	|	)                                                            КАК КатегорииОбъектов
	|
	|ЛЕВОЕ ВНЕШНЕЕ СОЕДИНЕНИЕ
	|// Присоединяются категории, назначенные для заданного объекта.
	|	РегистрСведений.УдалитьКатегорииОбъектов
	|ПО
	|	РегистрСведений.УдалитьКатегорииОбъектов.Категория = КатегорииОбъектов.Ссылка
	|	И
	|	РегистрСведений.УдалитьКатегорииОбъектов.Объект = &ОбъектОтбораКатегорий
	|
	|УПОРЯДОЧИТЬ ПО
	|	КатегорииОбъектов.Наименование
	|";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецЕсли