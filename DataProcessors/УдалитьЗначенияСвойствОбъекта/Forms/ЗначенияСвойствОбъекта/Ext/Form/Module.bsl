&НаКлиенте
Перем ВыполняетсяЗакрытие;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.ВладелецСвойств) Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр(
		"ru = 'Данная обработка является служебной.
		|Открытие формы данной обработки возможно только из форм других объектов.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Объект.ОбъектОтбораЗначений = Параметры.ВладелецСвойств;
	Объект.ОбъектОтбораНазначений = Параметры.ВладелецСвойств;
	Объект.НазначениеСвойств = ОбщегоНазначенияБК.ПолучитьСписокНазначенийСвойствКатегорийОбъектовПоСсылке(Объект.ОбъектОтбораЗначений);
	
	ПеречитатьНаСервере();

	ТекстЗаголовка = НСтр("ru = 'Свойства: %1'");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка, Параметры.ВладелецСвойств);
	ЭтаФорма.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	ОбработкаЗаписиНовогоНаСервере(НовыйОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаЗаписиНовогоНаСервере(НовыйОбъект)
	
	Если ТипЗнч(НовыйОбъект) = Тип("ПланВидовХарактеристикСсылка.УдалитьСвойстваОбъектов") Тогда
		
		ОбъектОбработка = РеквизитФормыВЗначение("Объект");	
		ОбъектОбработка.ПроверитьДобавитьНовоеСвойство(НовыйОбъект);
		ЗначениеВРеквизитФормы(ОбъектОбработка, "Объект");
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ВыполняетсяЗакрытие Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		
		ТекстВопроса = НСтр("ru = 'Измененные значения свойств не были записаны. Сохранить изменения?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ЗаписатьНаСервере();
		
		Если НЕ Модифицированность Тогда
			ВыполняетсяЗакрытие = Истина;
			Закрыть();
		КонецЕсли;
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ СвойстваИЗначения

&НаКлиенте
Процедура СвойстваИЗначенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Элемент.ПодчиненныеЭлементы.СвойстваИЗначенияЗначение.ОграничениеТипа = ТипЗначения(Элемент.ТекущиеДанные.Свойство);

КонецПроцедуры

&НаКлиенте
Процедура СвойстваИЗначенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ЗначенияЗаполнения = Новый Структура;
	
	Если Копирование И Элементы.СвойстваИЗначения.ТекущиеДанные <> Неопределено Тогда
		
		РеквизитыСвойства = РеквизитыСвойства(Элемент.ТекущиеДанные.Свойство);
		ЗначенияЗаполнения.Вставить("НазначениеСвойства", РеквизитыСвойства.НазначениеСвойства);
		ЗначенияЗаполнения.Вставить("Наименование", РеквизитыСвойства.Наименование);
		ЗначенияЗаполнения.Вставить("ТипЗначения", РеквизитыСвойства.ТипЗначения);
		
	Иначе
	
		Если ТипЗнч(Объект.НазначениеСвойств) = Тип("СписокЗначений") Тогда
			Если Объект.НазначениеСвойств.Количество() <> 0 Тогда
				ЗначенияЗаполнения.Вставить("НазначениеСвойства", Объект.НазначениеСвойств[0].Значение);	
			КонецЕсли;
		Иначе
			ЗначенияЗаполнения.Вставить("НазначениеСвойства", Объект.НазначениеСвойств);	
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("ПланВидовХарактеристик.УдалитьСвойстваОбъектов.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваИЗначенияПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.СвойстваИЗначения.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда 
		
		ПометкаУдаления = ИнвертироватьПометкуУдаленияСвойства(ТекущиеДанные.Свойство);
		
		Если ПометкаУдаления Тогда
			ТекущиеДанные.ИндексКартинки = 4;	
		Иначе
			ТекущиеДанные.ИндексКартинки = 3;	
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	ЗаписатьНаСервере();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Перечитать(Команда)
	
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Измененные значения свойств не были записаны. Продолжить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПеречитатьЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОкОтмена);
	Иначе
		ПеречитатьНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		ПеречитатьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	ЗаписатьНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаписатьНаСервере()
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");	
	
	Если ОбъектОбработка.ЗаписатьЗначенияСвойств() Тогда
		Модифицированность = Ложь;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ОбъектОбработка, "Объект");

КонецПроцедуры

&НаСервере
Процедура ПеречитатьНаСервере()
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");	
	ОбъектОбработка.ПрочитатьЗаполнитьСвойстваИЗначения();
	ЗначениеВРеквизитФормы(ОбъектОбработка, "Объект");
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнвертироватьПометкуУдаленияСвойства(Свойство)
	
	ОбъектСвойство = Свойство.ПолучитьОбъект();
	
	Попытка
		ОбъектСвойство.УстановитьПометкуУдаления(НЕ ОбъектСвойство.ПометкаУдаления, Истина);
	Исключение
		ВызватьИсключение "Не удалось изменить пометку удаления свойства:" + Символы.ПС + ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ОбъектСвойство.ПометкаУдаления;
	
КонецФункции

&НаСервереБезКонтекста
Функция РеквизитыСвойства(Свойство)
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("НазначениеСвойства", Свойство.НазначениеСвойства);
	Реквизиты.Вставить("Наименование", Свойство.Наименование);
	Реквизиты.Вставить("ТипЗначения", Свойство.ТипЗначения);
	
	Возврат Реквизиты;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТипЗначения(Свойство)
	
	Возврат Свойство.ТипЗначения;
	
КонецФункции

ВыполняетсяЗакрытие = Ложь;