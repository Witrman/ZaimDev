
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("НомерЛицевогоСчета") Тогда
		НомерЛицевогоСчета = Параметры.НомерЛицевогоСчета;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заказать(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ТокенАвторизации = ИнтеграцияИСМПТККлиент.ПолучитьТокенАвторизации(Организация);
	
	Если ТокенАвторизации = Неопределено Тогда
		
		ТекстСообщенияТокенНеПолучен = НСтр("ru='Операция прервана: не получены данные ЭЦП.'");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщенияТокенНеПолучен);
		Возврат;
		
	КонецЕсли;
	
	ЗаказатьНаСервере(ТокенАвторизации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаказатьНаСервере(ТокенАвторизации)
	
	ОбработкаОбменИСМПТ = ИнтеграцияИСМПТКПовтИсп.ОбработкаОбменИСМПТ();
	
	ПараметрыСчета = Новый Структура;
	ПараметрыСчета.Вставить("НомерЛицевогоСчета", НомерЛицевогоСчета);
	ПараметрыСчета.Вставить("АдресПочты", АдресПочты);
	ПараметрыСчета.Вставить("Сумма", Сумма);
	
	РезультатЗапроса = ОбработкаОбменИСМПТ.СоздатьСчетНаОплату(ПараметрыСчета, ТокенАвторизации);
	Если Не ЗначениеЗаполнено(РезультатЗапроса.ТекстОшибки) Тогда
		ТекстСообщенияУспешно = нСтр("ru='Запрос успешно обработан. Счет будет отправлен в ближайшее время'", "ru");
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщенияУспешно);
	Иначе
		ИнтеграцияИСМПТККлиентСерверПереопределяемый.СообщитьПользователю(РезультатЗапроса.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
