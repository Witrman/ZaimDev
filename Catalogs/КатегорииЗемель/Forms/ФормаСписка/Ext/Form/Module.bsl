
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.ФормаВыбораИзКлассификатора") И  ВыбранноеЗначение <> Неопределено Тогда
		ОбработатьПодборКлассификатора(ВыбранноеЗначение);
		Элементы.Список.Обновить();
	КонецЕсли;    

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура Подбор(Команда)
	
	ТекущиеДанные =  Элементы.Список.ТекущиеДанные;
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		  , Истина);
	ПараметрыФормы.Вставить("ИмяМакета"			  , "КодыКатегорийЗемель");
	ПараметрыФормы.Вставить("ИмяСекции"			  ,	"Классификатор");
	ПараметрыФормы.Вставить("ПолучатьПолныеДанные", Истина);
	ПараметрыФормы.Вставить("ТекущийКодСтроки"	  , Неопределено);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораИзКлассификатора", ПараметрыФормы, ЭтаФорма, Истина,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов


////////////////////////////////////////////////////////////////////////////////
//СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция ПроверитьНаличиеКатегорииВСправочнике(КодПоКлассификатору)
	
	ЕстьКатегория = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК ЕстьКатегория
	|ИЗ
	|	Справочник.КатегорииЗемель КАК КатегорииЗемель
	|ГДЕ
	|	КатегорииЗемель.КодПоКлассификатору = &КодПоКлассификатору";
	
	Запрос.УстановитьПараметр("КодПоКлассификатору", КодПоКлассификатору);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Если Выборка.Количество() = 0 Тогда 
		ЕстьКатегория = Ложь;
	Иначе 
		ЕстьКатегория = Истина;
	КонецЕсли;
	
	Возврат ЕстьКатегория;
КонецФункции 

&НаСервереБезКонтекста
Функция ОбработатьПодборКлассификатора(ЗначениеВыбора)
	
	Если ТипЗнч(ЗначениеВыбора) <> Тип("Структура") Тогда
		КодСтроки      = ЗначениеВыбора;
		Наименование   = ЗначениеВыбора;
		ПолноеНаименование = "";
	Иначе
		Наименование		= ЗначениеВыбора.КодСтроки;
		КодСтроки           = ЗначениеВыбора.КодСтроки;
	    ПолноеНаименование  = ЗначениеВыбора.Наименование;
	КонецЕсли;
	
	ЕстьКатегория = ПроверитьНаличиеКатегорииВСправочнике(КодСтроки);																									
	Если ЕстьКатегория Тогда 
		ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Категория с кодом <%1> уже указана в справочнике!'"),КодСтроки));
	Иначе					
		НовыйЭлемент = Справочники.КатегорииЗемель.СоздатьЭлемент();
		НовыйЭлемент.КодПоКлассификатору = КодСтроки;
		НовыйЭлемент.ПолноеНаименование  = ПолноеНаименование;			
		НовыйЭлемент.Наименование        = Наименование;
		Попытка
			НовыйЭлемент.Записать(); 			
		Исключение
			ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось записать элемент с наименованием %1'"), Наименование));
		КонецПопытки 
		
	КонецЕсли;				
			
КонецФункции 