#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура вызывается перед записью элемента справочника.
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЭтоГруппа И НЕ ЗначениеЗаполнено(Банк) Тогда
		ТекстСообщения = НСтр("ru='Счет № """ + СокрЛП(Наименование) + """ : не указан банк карт-счета.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	
	
	Если НЕ ЭтоГруппа И НЕ ЗначениеЗаполнено(НомерСчета) Тогда
		ТекстСообщения = НСтр("ru='Счет № """ + СокрЛП(Наименование) + """ : не указан номер карт-счета.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	
	
	Если НЕ ЭтоГруппа И СтрДлина(НомерСчета) < 20 Тогда
		ТекстСообщения = НСтр("ru='Счет № """ + СокрЛП(Наименование) + """ : длина номера счета менее 20 знаков. Рекомендуется проверить правильность введенного номера.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;	
	
	Если НЕ Отказ И НЕ ЭтоНовый() И НЕ ЭтоГруппа Тогда
		ПроверитьВозможностьИзмененияДанных(Отказ);	
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура проверяет возможность изменения данных 
Процедура ПроверитьВозможностьИзмененияДанных(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КартСчетаФизЛиц.ФизЛицо,
	               |	КартСчетаФизЛиц.Банк
	               |ИЗ
	               |	РегистрСведений.КартСчетаФизЛиц КАК КартСчетаФизЛиц
	               |ГДЕ
	               |	КартСчетаФизЛиц.НомерКартСчета = &Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	ИзменилосьФизЛицо = Ложь;
	ИзменилсяБанк = Ложь;
	Пока Выборка.Следующий() Цикл
		Если Выборка.ФизЛицо <> Владелец Тогда
			ИзменилосьФизЛицо = Истина;
		КонецЕсли;
		Если Выборка.Банк <> Банк Тогда
			ИзменилсяБанк = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ИзменилосьФизЛицо Тогда
		ТекстСообщения = НСтр("ru='Счет № """ + СокрЛП(Наименование) + """ : нельзя изменить владельца карт-счета, данный счет используется в регистре сведений ""Карт-счета физлиц"".'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
	Если ИзменилсяБанк Тогда
		ТекстСообщения = НСтр("ru='Счет № """ + СокрЛП(Наименование) + """ : нельзя изменить банк карт-счета, данный счет используется в регистре сведений ""Карт-счета физлиц"".'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
		
КонецПроцедуры

#КонецЕсли
