////////////////////////////////////////////////////////////////////////////////
// Описание формы
// Параметры фомры:
//  ИмяМакета - Строка - передается имя макета.
//  ИмяСекции - Строка - передается имя секции макета.
//  ПолучатьПолныеДанные - Булево - признак возврата полных данных. Если Истина то возвращается структура "Код" и "Наименование" если Ложь то возвращается только "Код".
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КоличествоГрупп = 2;
	
	Если НЕ ЗначениеЗаполнено(Параметры.ИмяМакета) ИЛИ НЕ ЗначениеЗаполнено(Параметры.ИмяСекции) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ЗаголовокФормы = "";
	
	Если Параметры.Свойство("Заголовок", ЗаголовокФормы) И ЗначениеЗаполнено(ЗаголовокФормы) Тогда
		ЭтаФорма.АвтоЗаголовок = Ложь;
		ЭтаФорма.Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	ИмяМакета 		 = Параметры.ИмяМакета;
	ИмяСекции 		 = Параметры.ИмяСекции;
	ТекущийКодСтроки = Параметры.ТекущийКодСтроки;
	
	ПолучатьПолныеДанные = Параметры.ПолучатьПолныеДанные;
	
	Дерево = РеквизитФормыВЗначение("Таблица",Тип("ДеревоЗначений"));
	Дерево.Строки.Очистить();
	
	Макет = РеквизитФормыВЗначение("ОтчетОбъект").ПолучитьМакет("КодыВидов");
	
	Если ТипЗнч(Макет) = Тип("ТабличныйДокумент") Тогда
		
		ТекстШапки = Макет.ПолучитьОбласть("Шапка|Наименование").ТекущаяОбласть.Текст;
		Элементы.ДекорацияНадписьЗаголовка.Заголовок = СокрЛП(ТекстШапки);
		
		Область = Макет.Области[Параметры.ИмяСекции];
		
		Для Ном = Область.Верх По Область.Низ Цикл
			Если НЕ СокрЛП(Макет.Область(Ном, 5).Текст) = "" Тогда
				Если НЕ СокрЛП(Макет.Область(Ном, 1).Текст) = "" Тогда
					НоваяСтрока = Дерево.Строки.Добавить();
					НоваяСтрока.КодСтроки    = СокрЛП(Макет.Область(Ном, 1).Текст);
					НоваяСтрока.Наименование = СокрЛП(Макет.Область(Ном, 5).Текст);
					НоваяСтрока.Группа = 0;
				ИначеЕсли НЕ СокрЛП(Макет.Область(Ном, 2).Текст) = "" Тогда
					Подстрока = НоваяСтрока.Строки.Добавить();
					Подстрока.КодСтроки    = СокрЛП(Макет.Область(Ном, 2).Текст);
					Подстрока.Наименование = СокрЛП(Макет.Область(Ном, 5).Текст);
					Подстрока.Группа = 1;
				ИначеЕсли НЕ СокрЛП(Макет.Область(Ном, 3).Текст) = "" Тогда
					ПодстрокаПодстроки = Подстрока.Строки.Добавить();
					ПодстрокаПодстроки.КодСтроки    = СокрЛП(Макет.Область(Ном, 3).Текст);
					ПодстрокаПодстроки.Наименование = СокрЛП(Макет.Область(Ном, 5).Текст);
					ПодстрокаПодстроки.Группа = 2;
				ИначеЕсли НЕ СокрЛП(Макет.Область(Ном, 4).Текст) = "" Тогда
					ПодстрокаПодстрокиСтроки = ПодстрокаПодстроки.Строки.Добавить();
					ПодстрокаПодстрокиСтроки.КодСтроки    = СокрЛП(Макет.Область(Ном, 4).Текст);
					ПодстрокаПодстрокиСтроки.Наименование = СокрЛП(Макет.Область(Ном, 5).Текст);
					ПодстрокаПодстрокиСтроки.Группа = 3;
					Группировать = Истина; 
				КонецЕсли;
				КоличествоГрупп = 3;
				
			ИначеЕсли НЕ СокрЛП(Макет.Область(Ном, 4).Текст) = "" Тогда
				Если НЕ СокрЛП(Макет.Область(Ном, 1).Текст) = "" Тогда
					НоваяСтрока = Дерево.Строки.Добавить();
					НоваяСтрока.КодСтроки    = СокрЛП(Макет.Область(Ном, 1).Текст);
					НоваяСтрока.Наименование = СокрЛП(Макет.Область(Ном, 4).Текст);
					НоваяСтрока.Группа = 0;
				ИначеЕсли НЕ СокрЛП(Макет.Область(Ном, 2).Текст) = "" Тогда
					Подстрока = НоваяСтрока.Строки.Добавить();
					Подстрока.КодСтроки    = СокрЛП(Макет.Область(Ном, 2).Текст);
					Подстрока.Наименование = СокрЛП(Макет.Область(Ном, 4).Текст);
					Подстрока.Группа = 1;
				ИначеЕсли НЕ СокрЛП(Макет.Область(Ном, 3).Текст) = "" Тогда
					ПодстрокаПодстроки = Подстрока.Строки.Добавить();
					ПодстрокаПодстроки.КодСтроки    = СокрЛП(Макет.Область(Ном, 3).Текст);
					ПодстрокаПодстроки.Наименование = СокрЛП(Макет.Область(Ном, 4).Текст);
					ПодстрокаПодстроки.Группа = 2;
					Группировать = Истина;
				КонецЕсли;
				КоличествоГрупп = 2;
			ИначеЕсли НЕ СокрЛП(Макет.Область(Ном, 3).Текст) = "" Тогда 
				НоваяСтрока = Дерево.Строки.Добавить();
				НоваяСтрока.КодСтроки    = СокрЛП(Макет.Область(Ном, 1).Текст);
				НоваяСтрока.Наименование = СокрЛП(Макет.Область(Ном, 2).Текст);
				НоваяСтрока.Группа = 1;
				ОбластьПодстроки = Макет.Области[Параметры.ИмяСекции + "_" + СтрЗаменить(НоваяСтрока.КодСтроки,".","_")];
				Для НомПодСекции = ОбластьПодстроки.Верх По ОбластьПодстроки.Низ Цикл
					Подстрока = НоваяСтрока.Строки.Добавить();
					Подстрока.КодСтроки    = СокрЛП(Макет.Область(НомПодСекции, 1).Текст);
					Подстрока.Наименование = СокрЛП(Макет.Область(НомПодСекции, 2).Текст);
					Подстрока.КодПодстроки = НоваяСтрока.КодСтроки + "." + СокрЛП(Макет.Область(НомПодСекции, 1).Текст);
					Подстрока.Группа = 0;
				КонецЦикла;	
			Иначе	
				Строка = Дерево.Строки.Добавить();
				Строка.КодСтроки    = СокрЛП(Макет.Область(Ном, 1).Текст);
				Строка.Наименование = СокрЛП(Макет.Область(Ном, 2).Текст);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
     
   Если ЭтаФорма.ИмяСекции = "КатегорииФизическихЛиц2021" ИЛИ ЭтаФорма.ИмяСекции = "КатегорииФизическихЛиц2022" ИЛИ ЭтаФорма.ИмяСекции = "КатегорииФизическихЛиц2023" Тогда
       
       Если Параметры.СтруктураДляПоиска.Свойство("Код") Тогда
           
           Если ТипЗнч(Параметры.СтруктураДляПоиска.Код) = Тип("СписокЗначений") Тогда
               
               Для Каждого СтрокаСписка Из Параметры.СтруктураДляПоиска.Код Цикл
                   
                   // сохраним текущие отмеченные структурные единицы
                   Если Дерево.Строки.Количество() <> 0 Тогда
                       СохраненныйСписок = Новый СписокЗначений;
                       
                       СтруктураПоиска = Новый Структура();
                       СтруктураПоиска.Вставить("КодСтроки", СтрокаСписка.Значение);
                       
                       НайденныеСтроки = Дерево.Строки.НайтиСтроки(СтруктураПоиска, Истина);
                       Если НайденныеСтроки.Количество() > 0 Тогда
                           СтрокаДерева = НайденныеСтроки[0];
                           СтрокаДерева.ОтметкаЗаполнения = 1;                          
                       КонецЕсли;
                   КонецЕсли;
               КонецЦикла;
           КонецЕсли;
       КонецЕсли;
       Элементы.ГруппаПроизвольноеЗначение.Видимость = Ложь;
   КонецЕсли; 

    ЗначениеВРеквизитФормы(Дерево,"Таблица");

	Если ТекущийКодСтроки <> Неопределено И ТекущийКодСтроки <> "" Тогда
		ТекущийКодСтроки = ?(Прав(СокрЛП(ТекущийКодСтроки),1) = ".", Лев(ТекущийКодСтроки, СтрДлина(СокрЛП(ТекущийКодСтроки)) -1), СокрЛП(ТекущийКодСтроки));	
		
		МассивНайденныхСтрок = Новый Массив;
		НайтиСтрокиВДанныхФормыДерево(Таблица.ПолучитьЭлементы(), "КодСтроки", ТекущийКодСтроки, МассивНайденныхСтрок);
		Если МассивНайденныхСтрок.Количество() <> 0 Тогда
			Элементы.Таблица.ТекущаяСтрока = МассивНайденныхСтрок[0].ПолучитьИдентификатор();
        Иначе
            ПроизвольноеЗначение = ТекущийКодСтроки;
            ИспользуетсяПроизвольноеЗначение = ЗначениеЗаполнено(ПроизвольноеЗначение);
        КонецЕсли;
        
    КонецЕсли;
    	
КонецПроцедуры

&НаСервере
Функция НайтиСтрокиВДанныхФормыДерево(ЭлементыДанныхФормыДерево, ИмяКолонки, ИскомоеЗначение, МассивНайденныхСтрок)
	
	Для Каждого ЭлементДерева Из ЭлементыДанныхФормыДерево Цикл
		
		ЗначенияСовпадают = Истина;
		
		Если ЭлементДерева[ИмяКолонки] <> ИскомоеЗначение  Тогда
			ЗначенияСовпадают = Ложь;
		КонецЕсли;
		
		Если ЗначенияСовпадают Тогда
			МассивНайденныхСтрок.Добавить(ЭлементДерева);
		КонецЕсли;
		
		НайтиСтрокиВДанныхФормыДерево(ЭлементДерева.ПолучитьЭлементы(), "КодПодстроки", ИскомоеЗначение, МассивНайденныхСтрок);
	КонецЦикла;
	
	Возврат МассивНайденныхСтрок;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПроизвольноеЗначениеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ПроизвольноеЗначение) И НЕ ИспользуетсяПроизвольноеЗначение Тогда
		ИспользуетсяПроизвольноеЗначение = Истина;
	ИначеЕсли НЕ ЗначениеЗаполнено(ПроизвольноеЗначение) И ИспользуетсяПроизвольноеЗначение Тогда
		ИспользуетсяПроизвольноеЗначение = Ложь;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ "ТАБЛИЦА"

&НаКлиенте
Процедура ТаблицаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначениеВозврата = Неопределено;
    
    Если ЭтаФорма.ИмяСекции = "КатегорииФизическихЛиц2021" ИЛИ ЭтаФорма.ИмяСекции = "КатегорииФизическихЛиц2022" ИЛИ ЭтаФорма.ИмяСекции = "КатегорииФизическихЛиц2023" Тогда
        
        СписокОтмеченныхКодовСтрок = Новый СписокЗначений;
        
        СписокВыбранныхЭлементов(Таблица, СписокОтмеченныхКодовСтрок);
        
        ОповеститьОВыборе(СписокОтмеченныхКодовСтрок);
        
        Возврат;
        
    КонецЕсли; 

	Если НЕ ПустаяСтрока(Элемент.ТекущиеДанные.КодПодстроки) Тогда
		КодСтроки = Элемент.ТекущиеДанные.КодПодстроки;
		ИмяКолонки = "КодПодстроки";
	Иначе
		КодСтроки = Элемент.ТекущиеДанные.КодСтроки;
		ИмяКолонки = "КодСтроки";
 	КонецЕсли;
	
	Выбор(КодСтроки, ЗначениеВозврата, ИмяКолонки);
	
	Если ЗначениеВозврата = Неопределено Тогда 
		Возврат
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеВозврата) = Тип("Структура")
			И ЗначениеВозврата.Свойство("Развернуть") Тогда
		СтрокаДерева = Элементы.Таблица.ТекущаяСтрока;
		
		Если Элементы.Таблица.Развернут(СтрокаДерева) Тогда
			Элементы.Таблица.Свернуть(СтрокаДерева);
		Иначе	
			Элементы.Таблица.Развернуть(СтрокаДерева, Истина);
		КонецЕсли;	
		Возврат
	КонецЕсли;
	
	ОповеститьОВыборе(ЗначениеВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОтметкаЗаполненияПриИзменении(Элемент)
    
    СтрокаТабличнойЧасти = Элементы.Таблица.ТекущиеДанные;
    Если СтрокаТабличнойЧасти.ОтметкаЗаполнения = Ложь Тогда
        СтрокаТабличнойЧасти.Сумма = 0;     
    КонецЕсли;
    ИтогиНаСервере();
    
КонецПроцедуры

&НаСервере
Процедура ИтогиНаСервере()
    
	Дерево = РеквизитФормыВЗначение("Таблица",Тип("ДеревоЗначений"));
    
    СуммаПоСтроке = 0;
    Для каждого СтрокаТаблицы Из Дерево.Строки Цикл
    
    	СуммаПоСтроке = СуммаПоСтроке + СтрокаТаблицы.Сумма;
    
    КонецЦикла; 
    
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммаПриИзменении(Элемент)
    
    ИтогиНаСервере();
    
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбранныхЭлементов(РодительскаяСтрока, СписокВыбранных)

	Для Каждого СтрокаДерева Из РодительскаяСтрока.ПолучитьЭлементы() Цикл
		
		Если СтрокаДерева.ОтметкаЗаполнения = 1 Тогда
			СписокВыбранных.Добавить(СтрокаДерева.КодСтроки);	
		КонецЕсли;
		
		Если СтрокаДерева.ПолучитьЭлементы().Количество() > 0 Тогда
			СписокВыбранныхЭлементов(СтрокаДерева, СписокВыбранных);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура МассивВыбранныхЭлементов(РодительскаяСтрока, СписокВыбранных)

	Для Каждого СтрокаДерева Из РодительскаяСтрока.ПолучитьЭлементы() Цикл
		
		СписокВыбранных.Добавить(СтрокаДерева);	
		
		Если СтрокаДерева.ПолучитьЭлементы().Количество() > 0 Тогда
			МассивВыбранныхЭлементов(СтрокаДерева, СписокВыбранных);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Выбрать(Команда)
	
	ЗначениеВозврата = Неопределено;
    
    Если ЭтаФорма.ИмяСекции = "КатегорииФизическихЛиц2021" ИЛИ ЭтаФорма.ИмяСекции = "КатегорииФизическихЛиц2022" ИЛИ ЭтаФорма.ИмяСекции = "КатегорииФизическихЛиц2023" Тогда
        
        СписокОтмеченныхКодовСтрок = Новый СписокЗначений;
        
        СписокВыбранныхЭлементов(Таблица, СписокОтмеченныхКодовСтрок);
        
        ОповеститьОВыборе(СписокОтмеченныхКодовСтрок);
        
        Возврат;
        
    КонецЕсли; 

	Если НЕ ПустаяСтрока(Элементы.Таблица.ТекущиеДанные.КодПодстроки) Тогда
		КодСтроки = Элементы.Таблица.ТекущиеДанные.КодПодстроки;
		ИмяКолонки = "КодПодстроки";
	Иначе
		КодСтроки = Элементы.Таблица.ТекущиеДанные.КодСтроки;
		ИмяКолонки = "КодСтроки";
 	КонецЕсли;
	
	Выбор(КодСтроки, ЗначениеВозврата, ИмяКолонки);
	
	Если ЗначениеВозврата = Неопределено Тогда 
		Возврат
	КонецЕсли;
	
    ЗначениеВозврата.КодСтроки = ?(ИспользуетсяПроизвольноеЗначение, ПроизвольноеЗначение, ЗначениеВозврата.КодСтроки);
	
	ОповеститьОВыборе(ЗначениеВозврата);
    
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура Выбор(КодСтроки, ЗначениеВозврата, ИмяКолонки)
	
	Дерево = РеквизитФормыВЗначение("Таблица");
	
	ВыбраннаяСтрока = Дерево.Строки.Найти(КодСтроки, "КодСтроки", Истина);
	
	Если Группировать = Истина Тогда
		Если Не ВыбраннаяСтрока.Родитель = Неопределено И ВыбраннаяСтрока.Группа = КоличествоГрупп Тогда
			Если ПолучатьПолныеДанные Тогда
				ЗначениеВозврата = Новый Структура("КодСтроки, Наименование", ВыбраннаяСтрока.КодСтроки, ВыбраннаяСтрока.Наименование);
			Иначе
				ЗначениеВозврата = ВыбраннаяСтрока.КодСтроки;
			КонецЕсли;
		Иначе
			Возврат;
		КонецЕсли;
	Иначе	
		СтандартнаяОбработка = Ложь;
		Если ПолучатьПолныеДанные Тогда
			ЗначениеВозврата = Новый Структура("КодСтроки, Наименование", ВыбраннаяСтрока.КодСтроки, ВыбраннаяСтрока.Наименование);
		Иначе
			ЗначениеВозврата = ВыбраннаяСтрока.КодСтроки;
		КонецЕсли;
    КонецЕсли;
	
	Если ВозвращатьСтруктуруДополнительныхПараметров Тогда
		ЗначениеВозврата = Новый Структура("ЗначениеВыбора, Команда, СтруктураДопПараметров",ЗначениеВозврата, "ПодборИзКлассификатора", СтруктураДополнительныхПараметров);
	КонецЕсли;
	
КонецПроцедуры
