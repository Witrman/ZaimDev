////////////////////////////////////////////////////////////////////////////////
// Описание формы
// Параметры фомры:
//  Организация - СправочникСсылка.Организации - Налогоплательщик, представляющий отчет
//  мДатаНачалаПериодаОтчета, мДатаКонцаПериодаОтчета - Дата - отчетный период формирования отчета
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СформироватьСтруктуруРеквизитовФормы();
	
	мВыбраннаяФорма = "ФормаОтчета2020Кв1";
	КодФормы 		= "ФормаОтчета";
	               		
	РегламентированнаяОтчетность.ЗаполнитьСтруктуруФормРегОтчета(ЭтотОбъект);	
			
	СтруктураРеквизитовФормы.СписокПечатаемыхЛистов = Новый СписокЗначений;	
	СтруктураРеквизитовФормы.мПечатныеформы         = Новый СписокЗначений;	
	
	ЗаполнитьПараметрыФормы(ЭтотОбъект, Параметры);	
	
	СтруктураРеквизитовФормы.мПоддержкаРаботыСоСтруктурнымиПодразделениями = РегламентированнаяОтчетностьПереопределяемый.ПоддержкаРаботыСоСтруктурнымиПодразделениями(); // перевести в переопределяемый модуль 

	// Общие механизмы
	ЗаполнитьСведенияОбОтчетномПериоде();
		
	Инициализация(Параметры.БезОткрытияФормы);
	
	УстановитьФорматВывода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;	
	КонецЕсли;
	
	Если Модифицированность Тогда 
		Оповещение = Новый ОписаниеОповещения("ПриЗакрытииЗавершение", ЭтотОбъект, Новый Структура);				
		Оповещение.ДополнительныеПараметры.Вставить("КодФормы", КодФормы);	
		ТекстВопроса = НСТР("ru = 'Сохранить данные отчета?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);	
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПредставлениеСпискаСтруктурныхЕдиницНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСДиалогамиКлиент.НачалоВыбораСпискаСтруктурныхЕдиниц(мСписокСтруктурныхЕдиниц, ЭтаФорма, СтандартнаяОбработка, "", Налогоплательщик,,, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСпискаСтруктурныхЕдиниц(Результат, Параметры) Экспорт

	Если Результат <> Неопределено Тогда		
		мСписокСтруктурныхЕдиниц = Результат;		
		ПредставлениеСпискаСтруктурныхЕдиниц = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(мСписокСтруктурныхЕдиниц); 		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	ЕдиницаИзмеренияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТочностьЕдиницыИзмеренияПриИзменении(Элемент)
	
	ТочностьЕдиницыИзмеренияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ПараметрыФормы	= Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",            Истина);
	ПараметрыФормы.Вставить("РежимВыбора",                   Истина);
	ПараметрыФормы.Вставить("ОтборОрганизация",              Налогоплательщик);
	
	Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	
	ОткрытьФорму("Справочник.СотрудникиОрганизаций.Форма.ФормаСписка", ПараметрыФормы, Элемент,,,,, Режим);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
		
	Если ЗначениеЗаполнено(Сотрудник) Тогда
	    СотрудникПриИзмененииНаСервере();
	Иначе
		ФизЛицо = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение)
		И ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		
		Если НЕ ПроверитьСоответствиеСотрудникаОрганизацииНаСервере(ВыбранноеЗначение) Тогда
	        ТекстСообщения = НСтр("ru = 'Организация отчета не соответствует организации, указанной в карточке сотрудника!'");            
	        ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);                
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПодписиПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ДатаПодписи) Тогда
		ДатаПодписиПриИзмененииНаСервере();
		Модифированность = Истина;
	КонецЕсли;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ "Отчеты"

&НаКлиенте
Процедура ФормаОтчетаПолеТабличногоДокументаСтраница1ПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	Модифицированность = Истина;
	ФормаОтчетаРасчетНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Налогоплательщик) Тогда
		Сообщить("Не выбрана организация!");
		Возврат;
	КонецЕсли;
	
	Если мСписокСтруктурныхЕдиниц.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбраны структурные единицы!'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФизЛицо) ИЛИ НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран сотрудник!'"));
		Возврат;
	КонецЕсли;
	
	Перезаполнить = Истина; // Для выполнения запроса на пересчет данных к пользователю
	
	СтрокаФормы = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(мСписокФормБезИерархии, Новый Структура("КодФормы", КодФормы));
	
	Если СтрокаФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Перезаполнить Тогда
		ПризнакАвтоЗаполнения =  РегламентированнаяОтчетностьКлиент.ОпределитьПризнакАвтоЗаполненияФормы(СтрокаФормы);
		Если Не ПризнакАвтоЗаполнения Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если Перезаполнить Тогда
		
		Если ВсегоСтраниц = 0 Тогда
				
			НачатьЗамерВремени();

			ЗаполнитьАвтоНаСервере();
			
		Иначе
				
			Если СтрокаФормы.ПолучитьЭлементы().Количество() > 0 Тогда
				ТекстВопроса = "Перезаполнить данные формы и ее дополнительных форм?";
			Иначе
				ТекстВопроса = "Перезаполнить данные формы?";
			КонецЕсли;
			
			Оповещение = Новый ОписаниеОповещения("ЗаполнитьАвтоЗавершение", ЭтотОбъект, Новый Структура);				
			Оповещение.ДополнительныеПараметры.Вставить("КодФормы", КодФормы);
			Оповещение.ДополнительныеПараметры.Вставить("Перезаполнить", Перезаполнить);
			
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);		
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФорму(Команда)
	
	РегламентированнаяОтчетностьКлиент.ОчиститьРеглОтчет(ЭтотОбъект, "ОчиститьФорму");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвтоЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;	 	
	
	НачатьЗамерВремени();
	
	ЗаполнитьАвтоНаСервере();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланк(Команда)
	
	Печать(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечататьСразу(Команда)
	
	Печать(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка(Команда)
	
	ОткрытьФормуНастройкиСтраниц();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяФорма(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	ПараметрыФормы.Вставить("мСохраненныйДок",          СтруктураРеквизитовФормы.мСохраненныйДок);	
	ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета",  СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	ПараметрыФормы.Вставить("мПериодичность",           СтруктураРеквизитовФормы.мПериодичность);
	ПараметрыФормы.Вставить("Организация",              Налогоплательщик);
	ПараметрыФормы.Вставить("мВыбраннаяФорма",          мВыбраннаяФорма);
	ПараметрыФормы.Вставить("мСписокСтруктурныхЕдиниц", мСписокСтруктурныхЕдиниц);	
	
	ОткрытьФорму(СтрЗаменить(ЭтаФорма.ИмяФормы, мВыбраннаяФорма, "") + "ОсновнаяФорма", ПараметрыФормы, ЭтотОбъект, Новый УникальныйИдентификатор);	

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область ОценкаПроизводительности

&НаКлиенте
Функция НачатьЗамерВремени()
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ОценкаПроизводительности") Тогда
		
		КлючеваяОперация = "Регламентированный отчет ""справка о заработной плате"" (заполнение)";
		ОбщегоНазначенияКлиент.ОбщийМодуль("ОценкаПроизводительностиКлиент").НачатьЗамерВремени(Истина, КлючеваяОперация);
		
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеТаймера()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОценкаПроизводительности") Тогда 
		
		Возврат ОбщегоНазначения.ОбщийМодуль("ОценкаПроизводительности").НачатьЗамерВремени();
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьЗамер(ВремяНачала)
	
	Если ВремяНачала <> Неопределено 
		И ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОценкаПроизводительности") Тогда
		
		КлючеваяОперация = "Регламентированный отчет ""справка о заработной плате"" (заполнение)";
		ОбщегоНазначения.ОбщийМодуль("ОценкаПроизводительности").ЗакончитьЗамерВремени(КлючеваяОперация, ВремяНачала);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СохранитьДанные() Экспорт
	
	СохранитьДанныеНаСервере();
	Оповестить("Позиционирование в списке отчетов", СтруктураРеквизитовФормы.мСохраненныйДок);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиВДокумент(СписокСохранения)
	
	// очищаем текущие данные
	ТекТабличныйДокумент = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];
	
	РегламентированнаяОтчетность.ОчиститьДанныеРегОтчета(ЭтотОбъект, КодФормы, Ложь, Ложь);

	Отчет = РеквизитФормыВЗначение("ОтчетОбъект");
	Макет = Отчет.ПолучитьМакет("МакетФормаОтчета2020Кв1");

	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаЛиста = Макет.ПолучитьОбласть("ШапкаЛиста");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	МассивОбластей = Новый Массив;
	МассивОбластей.Добавить(ОбластьСтрока);
	МассивОбластей.Добавить(ОбластьПодвал);
	
	ВсегоСтраниц = 0;
	Верх = 1;
	
	// Шапка отчета
	ТекТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТекТабличныйДокумент.Вывести(ОбластьШапка);
	
	// Строки отчета
	
	// Определим максимальный номер строки
	МаксНомерСтроки = 0;
	Для Каждого КлючЗначение Из СписокСохранения Цикл
		Позиция = Найти(КлючЗначение.Ключ, "_");
		Если Позиция <> 0 Тогда
			ПрефиксИмени = Лев(КлючЗначение.Ключ, Позиция);
			
			Если (ПрефиксИмени = "Период_") 
				 Или (ПрефиксИмени = "Заработок_") 
				 Или (ПрефиксИмени = "ОПВ_") 				 
				 Или (ПрефиксИмени = "ИПН_") 
				 Или (ПрефиксИмени = "ВОСМС_") Тогда
				
				МаксНомерСтроки = Макс(МаксНомерСтроки, Число(Сред(КлючЗначение.Ключ, Позиция + 1)));
			КонецЕсли;		
		КонецЕсли;
	КонецЦикла;
	
	Для НомерСтроки = 1 По МаксНомерСтроки Цикл
		
		// Номера областей устанавливаем в соответствии с текущим номером
		Для Каждого ИспользуемаяОбласть Из ОбластьСтрока.Области Цикл
			ИспользуемаяОбласть.Имя = СтрЗаменить(ИспользуемаяОбласть.Имя, "_" + (НомерСтроки - 1), "_" + НомерСтроки);
		КонецЦикла;
	
		ТекТабличныйДокумент.Вывести(ОбластьСтрока);		
		
		// проверим умещаются ли строки на странице
		Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТекТабличныйДокумент, МассивОбластей) Тогда
			ВсегоСтраниц = ВсегоСтраниц + 1;
			ТекТабличныйДокумент.Область(Верх, , ТекТабличныйДокумент.ВысотаТаблицы).Имя = "Страница" + Формат(ВсегоСтраниц, "ЧГ=0");
			Верх = ТекТабличныйДокумент.ВысотаТаблицы + 1;
			ТекТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТекТабличныйДокумент.Вывести(ОбластьШапкаЛиста);
		КонецЕсли;
		
	КонецЦикла;	
	
	// Выводим подвал
	ТекТабличныйДокумент.Вывести(ОбластьПодвал);
	
	// имя для последней страницы
	ВсегоСтраниц = ВсегоСтраниц + 1;
	ТекТабличныйДокумент.Область(Верх,, ТекТабличныйДокумент.ВысотаТаблицы).Имя = "Страница" + Формат(ВсегоСтраниц, "ЧГ=0");

	Если СписокСохранения <> Неопределено И ТипЗнч(СписокСохранения) = Тип("Структура") Тогда
		// Переписываем показатели из структуры сохранения в области табличного документа
		Для Каждого ЗаполняемаяОбласть Из ТекТабличныйДокумент.Области Цикл
			Значение = 0;
			Если СписокСохранения.Свойство(ЗаполняемаяОбласть.Имя, Значение) Тогда
				ЗаполняемаяОбласть.Значение = Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРеглОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Форма = ДополнительныеПараметры.Форма;	
		СтрокаФормы = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(Форма.мСписокФормБезИерархии, Новый Структура("КодФормы", Форма.КодФормы));
		Вложенность = СтрокаФормы.ПолучитьЭлементы().Количество() > 0;	
		
		ОчиститьРеглОтчетЗавершениеНаСервере(СтрокаФормы.КодФормы, СтрокаФормы.Многострочность, Вложенность)	
	КонецЕсли;
	
КонецПроцедуры 	

&НаСервере
Процедура ОчиститьРеглОтчетЗавершениеНаСервере(КодФормы, ПризнакМногострочности, Вложенность)
	
	РегламентированнаяОтчетность.ОчиститьДанныеРегОтчета(ЭтотОбъект, КодФормы, ПризнакМногострочности, Вложенность);
	ВсегоСтраниц = 0;
	
КонецПроцедуры

&НаСервере
// Процедура очистки формы
//
// параметр: ТекИмяФормы - имя очищаемой формы (=КодФормы)
//           ОчиститьВсе - признак вида очистки формы              
//
Процедура Очистить(ТекИмяФормы, ОчиститьВсе = Ложь) Экспорт
	
	СписокПоказателейНеПодлежащихОчистке = Новый СписокЗначений;
	
	Если НЕ ОчиститьВсе Тогда
		СписокПоказателейНеПодлежащихОчистке.Добавить("ОргНазв");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ОргНазв1");
	    СписокПоказателейНеПодлежащихОчистке.Добавить("РеквизитыОрганизации");
		СписокПоказателейНеПодлежащихОчистке.Добавить("Номер");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ДолжностьРуководителя");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ФИОРуководителя");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ФИОГлБухгалтера");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ДолжностьИсполнителя");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ФИОИсполнителя");
		СписокПоказателейНеПодлежащихОчистке.Добавить("МестоПредоставления");
	КонецЕсли;

	ТекТабличныйДокумент = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];
	
	Для Каждого Показатель Из СписокПоказателейНеПодлежащихОчистке Цикл
		Знч = ПолучитьЗначениеОбласти(ТекТабличныйДокумент, Показатель.Значение, Неопределено);
		Если Знч <> Неопределено Тогда
			мСтруктураСохраняемыхПоказателей.Вставить(Показатель.Значение, Знч);
		КонецЕсли;
	КонецЦикла;

	ТекТабличныйДокумент.Очистить();
	
	ВсегоСтраниц = 0;
	
КонецПроцедуры // Очистить()

&НаКлиенте
Процедура ОбновитьПараметрыФормыНаКлиенте(Параметры) Экспорт
	
	ЗаполнитьПараметрыФормы(ЭтотОбъект, Параметры);	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПараметрыФормы(ТекФорма, Параметры)
	
	ЗаполнитьЗначенияСвойств(ТекФорма.СтруктураРеквизитовФормы, Параметры);
	
	ТекФорма.Налогоплательщик = Параметры.Организация;
	ТекФорма.мСписокСтруктурныхЕдиниц = Параметры.мСписокСтруктурныхЕдиниц;
	ТекФорма.ПредставлениеСпискаСтруктурныхЕдиниц = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ТекФорма.мСписокСтруктурныхЕдиниц);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяГлавнойФормы(ВыбраннаяФорма) Экспорт
	
	Возврат СтрДлина(ВыбраннаяФорма)-7;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры инициализации отчета и структуры его данных

&НаСервере
Процедура СформироватьСтруктуруРеквизитовФормы()
	
	СтруктураРеквизитовФормы = Новый Структура;
	СтруктураРеквизитовФормы.Вставить("Организация");
	СтруктураРеквизитовФормы.Вставить("СписокСтруктурныхЕдиниц");
	СтруктураРеквизитовФормы.Вставить("ЕдиницаИзмерения");
	СтруктураРеквизитовФормы.Вставить("ТочностьЕдиницыИзмерения");			
	СтруктураРеквизитовФормы.Вставить("мВыбраннаяФорма");
	СтруктураРеквизитовФормы.Вставить("мДатаКонцаПериодаОтчета");
	СтруктураРеквизитовФормы.Вставить("мДатаНачалаПериодаОтчета");		
	СтруктураРеквизитовФормы.Вставить("мПериодичность");
	СтруктураРеквизитовФормы.Вставить("СписокПечатаемыхЛистов");
	СтруктураРеквизитовФормы.Вставить("мПечатныеФормы");		
	СтруктураРеквизитовФормы.Вставить("мРежимПечати");
	СтруктураРеквизитовФормы.Вставить("мСкопированаФорма");
	СтруктураРеквизитовФормы.Вставить("мСоставПоказателей");
	СтруктураРеквизитовФормы.Вставить("мСохраненныйДок");
	СтруктураРеквизитовФормы.Вставить("мСтрокаФормата");
	СтруктураРеквизитовФормы.Вставить("мСчетчикСтраниц");		
	СтруктураРеквизитовФормы.Вставить("ФлажокОтклАвтоРасчет", Ложь);
	СтруктураРеквизитовФормы.Вставить("СтрПериодОтчета");	
	СтруктураРеквизитовФормы.Вставить("НужноРассчитатьОтчетНаСервере", Ложь);			
	СтруктураРеквизитовФормы.Вставить("СформироватьФормуОтчетаАвтоматически");
	СтруктураРеквизитовФормы.Вставить("мБезОткрытияФормы", Параметры.БезОткрытияФормы);	
	СтруктураРеквизитовФормы.Вставить("мПериодичность", Параметры.мПериодичность);		
	СтруктураРеквизитовФормы.Вставить("НалоговыйКомитет", Неопределено);			
	СтруктураРеквизитовФормы.Вставить("мГод");	
	СтруктураРеквизитовФормы.Вставить("мКвартал");	
	СтруктураРеквизитовФормы.Вставить("мМесяц");	
	// индивидуальные параметры формы
	СтруктураРеквизитовФормы.Вставить("мПоддержкаРаботыСоСтруктурнымиПодразделениями");
	СтруктураРеквизитовФормы.Вставить("Сотрудник");
	СтруктураРеквизитовФормы.Вставить("ФизЛицо");
	СтруктураРеквизитовФормы.Вставить("ДатаПодписи");
	
КонецПроцедуры // СформироватьСтруктуруРеквизитовФормы

&НаСервере
Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	
	Перем ИсходноеКоличествоСтрокГруппы;
	
	СтруктураРеквизитовФормы.мБезОткрытияФормы = БезОткрытияФормы;			
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		
		Если СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда // документ скопирован
			ВосстановитьСохраненныеДанные();
		Иначе // новый документ
			ЕдиницаИзмерения         = Перечисления.ПорядкиОкругленияОтчетности.Окр1;
			ТочностьЕдиницыИзмерения = 2;
			ЯзыкФормирования 		 = "ru";
			ДатаПодписи              = ТекущаяДатаСеанса();
			
			Если ЗначениеЗаполнено(Налогоплательщик) Тогда
				
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("ГоловнаяОрганизацияДляЗП", ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(Налогоплательщик));
				
				Запрос.Текст = "
				|ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	Организации.Ссылка КАК СтруктурнаяЕдиница,
				|	Организации.Наименование КАК Наименование
				|ИЗ
				|	Справочник.Организации КАК Организации
				|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаПоПерсоналуОрганизаций КАК УчетнаяПолитикаПоПерсоналуОрганизаций
				|		ПО Организации.Ссылка = УчетнаяПолитикаПоПерсоналуОрганизаций.Организация
				|ГДЕ
				|	ВЫБОР
				|		КОГДА Организации.Ссылка.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
				|				ИЛИ (НЕ УчетнаяПолитикаПоПерсоналуОрганизаций.ВестиУчетПоГоловнойОрганизации)
				|			ТОГДА Организации.Ссылка
				|		ИНАЧЕ Организации.Ссылка.ГоловнаяОрганизация
				|	КОНЕЦ = &ГоловнаяОрганизацияДляЗП";
				
				мСписокСтруктурныхЕдиниц.Очистить();
				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					НовыйЭлемент 				= мСписокСтруктурныхЕдиниц.Добавить();
					НовыйЭлемент.Значение 		= Выборка.СтруктурнаяЕдиница;
					НовыйЭлемент.Представление 	= Выборка.Наименование;
				КонецЦикла;
				
				// заполним представление списка структурных единиц
				ПредставлениеСпискаСтруктурныхЕдиниц = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(мСписокСтруктурныхЕдиниц);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Модифицированность = Истина;
		
	Иначе
		ВосстановитьСохраненныеДанные();	
		
		Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда
			СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	//Обход ситуации с повторным открытием основной формы, из-за ТД
	МассивРеквизитовФормы = ЭтотОбъект.ПолучитьРеквизиты();
	Для Счетчик = 0 По МассивРеквизитовФормы.Количество()-1 Цикл		
		РеквизитФормы = МассивРеквизитовФормы[Счетчик];
		Если РеквизитФормы.ТипЗначения.СодержитТип(Тип("ТабличныйДокумент")) Тогда
			ЭтотОбъект[РеквизитФормы.Имя] = ЭтотОбъект[РеквизитФормы.Имя];	
		КонецЕсли;
	КонецЦикла;	  	
	
	мСтруктураЗначений = Новый Структура;
	мСтруктураСохраняемыхПоказателей = Новый Структура;
	
	Если СтруктураРеквизитовФормы.Свойство("СформироватьФормуОтчетаАвтоматически") И СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически <> Неопределено Тогда		
		ЗначениеТаймера = ЗначениеТаймера();
		
		ЗаполнитьАвтоНаСервере();
		
		ЗаписатьЗамер(ЗначениеТаймера);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОтчетномПериоде() Экспорт

	СтрПериодОтчета = ПредставлениеПериода(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета, СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ФП = истина");
	
	Если СтруктураРеквизитовФормы.мПериодичность = Перечисления.Периодичность.Квартал Тогда
		СтруктураРеквизитовФормы.мКвартал = Лев(СтрПериодОтчета, 1);
		СтруктураРеквизитовФормы.мМесяц = Неопределено;
	Иначе
		СтруктураРеквизитовФормы.мМесяц = Месяц(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
		СтруктураРеквизитовФормы.мКвартал = Неопределено;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мГод = Лев(Прав(СтрПериодОтчета, 7), 4);
		
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// Сохранение и восстановление данных

&НаСервере
Процедура СохранитьДанныеНаСервере()
	
	Перем ТаблицаСтраниц;

	ДатаНачалаЗамераВремени = ОценкаПроизводительности.НачатьЗамерВремени();
	
	Если Не ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок) ИЛИ (СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено) Тогда
		// создаем новый документ вида РегламентированныйОтчет
		ДокументРеглОтчет = Документы.РегламентированныйОтчет.СоздатьДокумент();
				
		ДокументРеглОтчет.Дата = ТекущаяДатаСеанса();
		ДокументРеглОтчет.УстановитьВремя();
		
	Иначе
		ДокументРеглОтчет = СтруктураРеквизитовФормы.мСохраненныйДок.ПолучитьОбъект();
	КонецЕсли;
	
	ИмяТекТабличногоПоля = "ФормаОтчетаПолеТабличногоДокументаСтраница1";
	ТекТабличноеПоле     = ЭтотОбъект[ИмяТекТабличногоПоля];

	// Сформируем комментарий, если не указан
	Если НЕ ЗначениеЗаполнено(Комментарий) Тогда
		
		Комментарий = ФизЛицо.Наименование;
		
		МестоПредоставления = ПолучитьЗначениеОбласти(ТекТабличноеПоле, "МестоПредоставления", "");
		Если ЗначениеЗаполнено(МестоПредоставления) Тогда
			Комментарий = Комментарий + " (" + МестоПредоставления + ")";
		КонецЕсли;
			
	КонецЕсли;

	// установим текущие значения реквизитов документа
	МетаданныеОтчета = РеквизитФормыВЗначение("ОтчетОбъект"). Метаданные();
	ДокументРеглОтчет.ИсточникОтчета           = МетаданныеОтчета.Имя;
	ДокументРеглОтчет.НаименованиеОтчета       = МетаданныеОтчета.Формы.ОсновнаяФорма.Представление();
	ДокументРеглОтчет.ДатаНачала               = СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета;
	ДокументРеглОтчет.ДатаОкончания            = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	ДокументРеглОтчет.Периодичность            = СтруктураРеквизитовФормы.мПериодичность;  
	ДокументРеглОтчет.ДатаПодписи              = ДатаПодписи;
	ДокументРеглОтчет.ВыбраннаяФорма           = мВыбраннаяФорма;
	ДокументРеглОтчет.Организация              = Налогоплательщик;
	ДокументРеглОтчет.ЕдиницаИзмерения         = ЕдиницаИзмерения;
	ДокументРеглОтчет.ТочностьЕдиницыИзмерения = ТочностьЕдиницыИзмерения;
	ДокументРеглОтчет.ВидОтчетности            = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	ДокументРеглОтчет.Комментарий              = Комментарий;
		
	// обработка номера документа для возможности автонумерации
	ПрефиксДок 	= ДокументРеглОтчет.ПолучитьПрефиксДокументаОтчета();
	НомерДок 	= СокрЛП(ПолучитьЗначениеОбласти(ТекТабличноеПоле, "Номер", ""));
	Если ЗначениеЗаполнено(НомерДок) Тогда	
		НомерДок              = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(НомерДок, ДокументРеглОтчет.Метаданные().ДлинаНомера - СтрДлина(ПрефиксДок), "0", "Слева");
		ДокументРеглОтчет.Номер = ПрефиксДок + НомерДок;
	Иначе
		ДокументРеглОтчет.УстановитьНовыйНомер(ПрефиксДок);
			
		// выведем номер в табличный документ
		НомерДок = ДокументРеглОтчет.Номер;
		Пока СтрДлина(НомерДок) > 0 И Найти("123456789", Лев(НомерДок, 1)) = 0 Цикл
			НомерДок = Сред(НомерДок, 2);
		КонецЦикла;
		УстановитьЗначениеОбласти(ТекТабличноеПоле, "Номер", НомерДок);
	КонецЕсли;

	// формируем данные редактируемых ячеек таблицы
	ПоказателиОтчета = Новый Структура();

	ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ТекТабличноеПоле);
	ПоказателиОтчета.Вставить(ИмяТекТабличногоПоля, ПоказателиТекущегоЛиста);

	// формируем список сохранения
	СписокСохранения = Новый Структура();

	// вставляем данные редактируемых ячеек таблицы	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	СписокСохранения.Вставить("СписокСтруктурныхЕдиниц", мСписокСтруктурныхЕдиниц);	
	
	// вставляем версию формы
	СписокСохранения.Вставить("ВерсияФормы", "01/01/2020");

	// ФизЛицо
	СписокСохранения.Вставить("ФизЛицо",   ФизЛицо);
	СписокСохранения.Вставить("Сотрудник", Сотрудник);

	// Сохраняем документ
	Если ДокументРеглОтчет <> Неопределено Тогда
						
		ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
		ДокументРеглОтчет.ДанныеОтчета = ХранилищеДанных;
		
		СохранитьОшибка = 1;
		
		// записываем документ, хранящий данные отчета
		Попытка
			ДокументРеглОтчет.Записать();			
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Не удается сохранить регламентированный отчет! " + ОписаниеОшибки());			
			СохранитьОшибка = 0;
		КонецПопытки;
		
		Если СохранитьОшибка = 1 Тогда
			// всё прошло без ошибок
			Модифицированность = Ложь;
		КонецЕсли;		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мСохраненныйДок = ДокументРеглОтчет.Ссылка;
	
	ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Записан документ %1 %2'"), ДокументРеглОтчет.Ссылка, ДокументРеглОтчет.Ссылка.Комментарий));	
	
	КлючеваяОперация = "Документ ""регламентированный отчет справка о заработной плате"" (запись)'";
	ОценкаПроизводительности.ЗакончитьЗамерВремени(КлючеваяОперация, ДатаНачалаЗамераВремени);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьСохраненныеДанные()
	
	Перем ПоказателиОтчета;
	
	мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок;

	// восстанавливаем реквизиты отчета
	Налогоплательщик         = мСохраненныйДок.Организация;
	ДатаПодписи              = мСохраненныйДок.ДатаПодписи;
	ЕдиницаИзмерения         = мСохраненныйДок.ЕдиницаИзмерения;
	ТочностьЕдиницыИзмерения = мСохраненныйДок.ТочностьЕдиницыИзмерения;
	Комментарий              = мСохраненныйДок.Комментарий;
	Периодичность			 = мСохраненныйДок.Периодичность;

	// восстанавливаем сохраненные данные отчета
	СписокСохранения = мСохраненныйДок.ДанныеОтчета.Получить();

	// восстанавливаем ФизЛицо
	СписокСохранения.Свойство("ФизЛицо", ФизЛицо);
	
	Если СписокСохранения.Свойство("Сотрудник") Тогда
		Сотрудник = СписокСохранения.Сотрудник;		
	Иначе
		// если заполнено ФизЛицо, то определим сотрудника для данного физ.лица
		Если ЗначениеЗаполнено(ФизЛицо) Тогда			
				
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	РаботникиОрганизации.Сотрудник,
			|	РаботникиОрганизации.Организация,
			|	РаботникиОрганизации.Период,
			|	РаботникиОрганизации.ПричинаИзмененияСостояния
			|ИЗ
			|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(
			|		&парамДатаАктуальности, 
			|		Организация = &парамГоловнаяОрганизация
			|		И Сотрудник.ФизЛицо = &парамФизЛицо
			|		И Сотрудник.ВидЗанятости <> &парамВнутреннееСовместительство) КАК РаботникиОрганизации
			|УПОРЯДОЧИТЬ ПО
			|	РаботникиОрганизации.Период УБЫВ, // берем ближайщее к дате отчета назначение
			|	ВЫБОР
			|		КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния = &парамУволен
			|			ТОГДА 2
			|		ИНАЧЕ 1
			|	КОНЕЦ
			|";
			
			ГоловнаяОрганизация = ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(Налогоплательщик);
			Запрос.УстановитьПараметр("парамГоловнаяОрганизация",        ГоловнаяОрганизация);
			Запрос.УстановитьПараметр("парамФизЛицо",                    ФизЛицо);
			Запрос.УстановитьПараметр("парамВнутреннееСовместительство", Перечисления.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство);
			Запрос.УстановитьПараметр("парамУволен",                     Перечисления.ПричиныИзмененияСостояния.Увольнение);
			Запрос.УстановитьПараметр("парамДатаАктуальности",           ТекущаяДата());
                          			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Сотрудник = Выборка.Сотрудник;
			Иначе
				// среди работников организаций такое физлицо не числится, т.е. какой-то получатель разовых выплат
				// сотрудник будет пустым
			КонецЕсли;	      			
			
		КонецЕсли;		
	КонецЕсли;

	// восстановим сохраненные данные редактируемых ячеек
	СписокСохранения.Свойство( "ПоказателиОтчета", ПоказателиОтчета);
	
	Если ТипЗнч(ПоказателиОтчета) = Тип("Структура") И ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРасчет") Тогда
		ВывестиВДокумент(ПоказателиОтчета.ПолеТабличногоДокументаРасчет);
	ИначеЕсли ТипЗнч(ПоказателиОтчета) = Тип("Структура") И ПоказателиОтчета.Свойство("ФормаОтчетаПолеТабличногоДокументаСтраница1") Тогда
		ВывестиВДокумент(ПоказателиОтчета.ФормаОтчетаПолеТабличногоДокументаСтраница1);
	КонецЕсли;

	Если СписокСохранения.Свойство("СписокСтруктурныхЕдиниц") Тогда
		мСписокСтруктурныхЕдиниц = СписокСохранения.СписокСтруктурныхЕдиниц;
	Иначе
	    мСписокСтруктурныхЕдиниц.Очистить();
	    мСписокСтруктурныхЕдиниц.Добавить(Налогоплательщик);
	КонецЕсли;
	ПредставлениеСпискаСтруктурныхЕдиниц = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(мСписокСтруктурныхЕдиниц);

	УстановитьФорматВывода();
	Модифицированность = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьДанныеНаСервере();
		Оповестить("Позиционирование в списке отчетов", СтруктураРеквизитовФормы.мСохраненныйДок);
	КонецЕсли;
	
КонецПроцедуры   

////////////////////////////////////////////////////////////////////////////////
// Печать

&НаКлиенте
Процедура Печать(ВидПечати, НеИзФормыОтчета = Ложь) Экспорт
	
	Если ВсегоСтраниц = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Нет данных для печати'"));
		Возврат;
	КонецЕсли;
    
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1. Формируется печатная форма...'"), ЭтаФорма.Заголовок),,, БиблиотекаКартинок.Печать);
	
	Если НЕ ПечатьНаСервере(ВидПечати) Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтаФорма, ВидПечати,, СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере(ВидПечати)
            
    Перем СтруктураГруппы;
	
    Если НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
        ТекстСообщения = НСтр("ru = 'Перед формированием печатных форм необходимо определить в системе принтер и задать его в качестве используемого по умолчанию!'");            
        ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);                
        Возврат Ложь;
    КонецЕсли;
            
	СтруктураРеквизитовФормы.СписокПечатаемыхЛистов.Очистить();
	СтруктураРеквизитовФормы.мПечатныеФормы.Очистить();
    СтруктураРеквизитовФормы.мРежимПечати = Истина;
    
    Если СтруктураРеквизитовФормы.НужноРассчитатьОтчетНаСервере Тогда
        ФормаОтчетаРасчетНаСервере();
    КонецЕсли; 

	ТекТабличноеПоле = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];
	ИдентификаторТекФормы = Новый УникальныйИдентификатор();
	НаименованиеДляЗаписи = "";
	Стр = 1;

	Пока Стр < ВсегоСтраниц + 1  Цикл
		
		ТабДок = Новый ТабличныйДокумент;
		ИмяОбласти = "Страница" + СокрЛП(Стр);
		ТекОбласть = ТекТабличноеПоле.ПолучитьОбласть(ИмяОбласти);
		ТабДок.Вывести(ТекОбласть);
		ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		ТабДок.ЧерноБелаяПечать   = Истина;
		ТабДок.АвтоМасштаб        = Истина;
		
		Если Стр = 1 Тогда 
			НаименованиеДляЗаписи = НСтр("ru = 'Справка о ЗП. '") + ТабДок.Области.ПериодС.Текст + НСтр("ru = ' по '") + ТабДок.Области.ПериодПО.Текст;
		КонецЕсли;	
		
		ИмяЛиста = НСтр("ru = 'Страница № '") + Строка(Стр);
		Стр = Стр + 1;
		
		Значение = Новый Массив;
		Значение.Добавить(ТабДок);
		Значение.Добавить(ИдентификаторТекФормы);
		Значение.Добавить(НаименованиеДляЗаписи);
		
		СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(Значение, ИмяЛиста);
			
	КонецЦикла;
		
	Для Каждого Эл Из СтруктураРеквизитовФормы.мПечатныеФормы Цикл
				
		Значение = Новый Массив;
		Значение.Добавить(ПоместитьВоВременноеХранилище(Эл.Значение[0], УникальныйИдентификатор));
		Значение.Добавить(Эл.Значение[1]);
		Значение.Добавить(Эл.Значение[2]);
		
		СтруктураРеквизитовФормы.СписокПечатаемыхЛистов.Добавить(Значение, Эл.Представление);			
		
	КонецЦикла;
	
	СтруктураРеквизитовФормы.мПечатныеФормы.Очистить();
        
    Возврат Истина;
    
КонецФункции

&НаСервере
Процедура ЕдиницаИзмеренияПриИзмененииНаСервере()
	
	ПроверитьТочность();
	УстановитьФорматВывода();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ТочностьЕдиницыИзмеренияПриИзмененииНаСервере()
	
	ПроверитьТочность();
	УстановитьФорматВывода();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиСтраниц()
	
// Запоминаем текущий раздел, установленный в дереве разделов отчета.
	Если НЕ Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		ТекущийРаздел = Элементы.РазделыОтчета.ТекущиеДанные.Страницы;		
	КонецЕсли;
	
	Для Каждого ТекФорма ИЗ мСписокФормБезИерархии.ПолучитьЭлементы() Цикл		
			
		ТекущаяСтрокаСписка = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(СписокФормДерева, Новый Структура("КодФормы",ТекФорма.КодФормы));
			
		Если  НЕ ТекущаяСтрокаСписка = Неопределено Тогда
			ТекущаяСтрокаСписка.Выгружать               = ТекФорма.Выгружать;
			ТекущаяСтрокаСписка.ПоказатьСтраницу        = ТекФорма.ПоказатьСтраницу;			
			ТекущаяСтрокаСписка.АвтополучениеИтогов     = ТекФорма.АвтополучениеИтогов;  
			ТекущаяСтрокаСписка.АвтоЗаполнение          = ТекФорма.АвтоЗаполнение;  
		КонецЕсли;		   
		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("СписокФормДерева",СписокФормДерева);
	
	ДопПараметры = Новый Структура("ТекущийРаздел",ТекущийРаздел);
	
	ОповещениеПослеЗакрытияФормы = Новый ОписаниеОповещения("ПослеЗакрытияФормыНастройкиСтраниц",ЭтотОбъект,ДопПараметры);
	
	ОткрытьФорму("Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.НастройкиОтчета",ПараметрыФормы, ЭтаФорма,,,,ОповещениеПослеЗакрытияФормы,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыНастройкиСтраниц(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КопироватьДанныеФормы(РезультатЗакрытия, СписокФормДерева);
	
	Модифицированность = Истина;
	
	//обновим мСписокФормБезИерархии
	Для Каждого СтрокаФормы ИЗ мСписокФормБезИерархии.ПолучитьЭлементы() Цикл		
			
		ТекущаяСтрокаСписка = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(СписокФормДерева, Новый Структура("КодФормы",СтрокаФормы.КодФормы));
			
		Если  НЕ ТекущаяСтрокаСписка = Неопределено Тогда
			СтрокаФормы.Выгружать               = ТекущаяСтрокаСписка.Выгружать;
			СтрокаФормы.ПоказатьСтраницу        = ТекущаяСтрокаСписка.ПоказатьСтраницу;			
			СтрокаФормы.АвтополучениеИтогов     = ТекущаяСтрокаСписка.АвтополучениеИтогов;  
			СтрокаФормы.АвтоЗаполнение          = ТекущаяСтрокаСписка.АвтоЗаполнение;  
		КонецЕсли;			
	КонецЦикла; 	

	Если ДополнительныеПараметры.Свойство("ТекущийРаздел") И НЕ ДополнительныеПараметры.ТекущийРаздел = Неопределено Тогда  		
		// Пытаемся найти ранее запомненный раздел.
		НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(РазделыОтчета, Новый Структура("СписокФормДереваСтраницы",ДополнительныеПараметры.ТекущийРаздел));
		// В случае успешного поиска, устанавливаем курсор на найденный раздел.
		Если НЕ НайденнаяСтрока = Неопределено Тогда
			Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
		КонецЕсли;			
	КонецЕсли;  	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочие

&НаСервере
// Процедура управляет представлением суммовых (денежных) показателей
// табличного документа при изменении размерности.
//
Процедура УстановитьФорматВывода()
	
	Перем СтруктураГруппы;
	Перем РазмерностьПрописью;
	
	Если ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1 Тогда
		мДелитель = 1;
		РазмерностьПрописью = НСтр("ru = ""тенге""; kz = ""теңге""", "ru");				
		мСтрокаФормата = "ЧЦ = 15; ЧДЦ = " + ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 0; ЧРГ= ";
		
	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000 Тогда
		мДелитель = 1000;
		РазмерностьПрописью = НСтр("ru = ""тысяч тенге""; kz = ""мың теңге""", "ru");				
		мСтрокаФормата = "ЧЦ = 15; ЧДЦ = " + ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 3; ЧРГ= ";
		
	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000000 Тогда
		мДелитель = 1000000;
		РазмерностьПрописью = НСтр("ru = ""млн. тенге""; kz = ""млн. теңге""", "ru");				
		мСтрокаФормата = "ЧЦ = 15; ЧДЦ = " + ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 6; ЧРГ= ";
		
	КонецЕсли;
	
	ТекТабличныйДокумент = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];

	Для Каждого ОбластьТаблицы Из ТекТабличныйДокумент.Области Цикл
		// по областям каждой страницы
		Если Не ОбластьТаблицы.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбластьТаблицы.СодержитЗначение Тогда
			Если СокрЛП(ОбластьТаблицы.ТипЗначения) = "Число" Тогда
				ОбластьТаблицы.Формат = мСтрокаФормата;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	
	УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ЕдиницаИзмерения", РазмерностьПрописью);
	
КонецПроцедуры // УстановитьФорматВывода()

&НаКлиенте
Процедура ФормаОтчетаРасчет()

	ФормаОтчетаРасчетНаСервере();	
			    	                	
КонецПроцедуры   //ФормаРасчет()

&НаСервере
Процедура ФормаОтчетаРасчетНаСервере()
	
	ИтогоЗаработок = 0;
	ИтогоОПВ = 0;
	ИтогоИПН = 0;
	ИтогоВОСМС = 0;
	
	ТекТабличныйДокумент = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];
	СканируемыеОбласти   = ТекТабличныйДокумент.Области;
	
	Для Каждого Область Из СканируемыеОбласти Цикл
		Если Найти(Область.Имя, "Заработок_") <> 0 Тогда
			
			НомерСтроки = Сред(Область.Имя, 11);
			Заработок 	= Область.Значение;
			ОПВ			= ПолучитьЗначениеОбласти(ТекТабличныйДокумент, "ОПВ_" + НомерСтроки, 0);
			ИПН			= ПолучитьЗначениеОбласти(ТекТабличныйДокумент, "ИПН_" + НомерСтроки, 0);
			ВОСМС		= ПолучитьЗначениеОбласти(ТекТабличныйДокумент, "ВОСМС_" + НомерСтроки, 0);
			
			УстановитьЗначениеОбласти(ТекТабличныйДокумент, "СуммаКВыплате_" + НомерСтроки, Заработок - ОПВ - ИПН - ВОСМС);
		
			ИтогоЗаработок 	= ИтогоЗаработок + Заработок;
			ИтогоОПВ 		= ИтогоОПВ + ОПВ;
			ИтогоИПН 		= ИтогоИПН + ИПН;
			ИтогоВОСМС 		= ИтогоВОСМС + ВОСМС;
			
		КонецЕсли;
	КонецЦикла;
	
	// Итоговые строки
	УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоЗаработок", ИтогоЗаработок);
	УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоОПВ", ИтогоОПВ);
	УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоИПН", ИтогоИПН);
	УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоВОСМС", ИтогоВОСМС);
	УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоСуммаКВыплате", ИтогоЗаработок - ИтогоОПВ - ИтогоИПН - ИтогоВОСМС);

	//РегламентированнаяОтчетность.ОбновитьТаблицуДанныхОбычнойФормы(ЭтотОбъект, КодФормы, ТаблицаДанных);
КонецПроцедуры

&НаСервере
Функция ФормаРассчитатьДанныеФормы(ВыдаватьСообщения = Ложь)
	
	РасчетНаРегистрах = Метаданные.РегистрыРасчета.Найти("ОсновныеНачисленияРаботниковОрганизаций") <> Неопределено;
	
	// Расчет вычисляемых параметров
	ГоловнаяОрганизация = ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(Налогоплательщик);
	
	// Создание запроса и установка всех необходимых параметров
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("парамОрганизация",		   		   Налогоплательщик);
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация",		   ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамФизЛицо", 					   ФизЛицо);
	Запрос.УстановитьПараметр("парамПриход",					   ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("парамВидСтроки",			  		   Перечисления.РасчетыСБюджетомФондамиВидСтроки.Исчисление);
	Запрос.УстановитьПараметр("парамВидПлатежа",	 			   Перечисления.ВидыПлатежейВБюджетИФонды.Налог);
	Запрос.УстановитьПараметр("парамВнутреннееСовместительство",   Перечисления.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство);
	Запрос.УстановитьПараметр("парамДатаАктуальности",			   ДатаПодписи);
	Запрос.УстановитьПараметр("парамОтветЛицоРуководитель",		   Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Запрос.УстановитьПараметр("парамОтветЛицоГлБух",			   Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	Запрос.УстановитьПараметр("парамТекущийПользователь",		   Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("парамПериодС",					   НачалоМесяца(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("парамПериодПо",					   КонецМесяца(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("парамПустаяОрганизация",			   Справочники.Организации.ПустаяСсылка());
	Запрос.УстановитьПараметр("парамУволен",					   Перечисления.ПричиныИзмененияСостояния.Увольнение);
	Запрос.УстановитьПараметр("парамСписокСтруктурныхЕдиниц",	   мСписокСтруктурныхЕдиниц);
	
	// Контактная информация
	Запрос.УстановитьПараметр("парамТипИнформацииАдрес" , 		   Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("парамТипИнформацииТелефон" , 	   Перечисления.ТипыКонтактнойИнформации.Телефон);
	Если Налогоплательщик.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		Запрос.УстановитьПараметр("парамОрганизацияКИ",            Налогоплательщик.ИндивидуальныйПредприниматель);
		Запрос.УстановитьПараметр("парамВидАдресаЮридический" ,    Справочники.ВидыКонтактнойИнформации.ЮрАдресФизЛица);
		Запрос.УстановитьПараметр("парамВидТелефонОрганизации" ,   Справочники.ВидыКонтактнойИнформации.ТелефонФизЛица);
	Иначе
		Запрос.УстановитьПараметр("парамОрганизацияКИ",            Налогоплательщик);
		Запрос.УстановитьПараметр("парамВидАдресаЮридический" ,    Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
		Запрос.УстановитьПараметр("парамВидТелефонОрганизации" ,   Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	КонецЕсли;

	// ---------------------------------------------------------------------------
	// Тексты запросов
	//

	//-----------------------------------------------------------------------------
	// ВЫБОРКА СВЕДЕНИЙ О ФИЗЛИЦЕ 
	// 	
	
	ДанныеОФизЛицеТекст = 
	"ВЫБРАТЬ 
	|	ЕСТЬNULL(ФИОФизЛица.Фамилия + "" "" + ФИОФизЛица.Имя + "" "" + ФИОФизЛица.Отчество, ДанныеОФизЛице.Наименование) КАК ФИОРаботника,
	|	ДанныеОФизЛице.РНН КАК РННРаботника,
	|	ДанныеОФизЛице.ИдентификационныйКодЛичности КАК ИИНРаботника,
	|	РаботникиОрганизации.Сотрудник.Код КАК ТабельныйНомер,
	|	РаботникиОрганизации.ПодразделениеОрганизации.Наименование КАК ПодразделениеОрганизации,
	|	РаботникиОрганизации.Должность.Наименование КАК Должность
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ДанныеОФизЛице
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|					&парамДатаАктуальности, 
	|					ФизЛицо = &парамФизЛицо) КАК ФИОФизЛица
	|		ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|							МАКСИМУМ(ВЫБОР
	|										КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния = &парамУволен
	|											ТОГДА ДОБАВИТЬКДАТЕ(РаботникиОрганизации.Период, ДЕНЬ, -1)
	|										ИНАЧЕ РаботникиОрганизации.Период
	|									 КОНЕЦ) КАК Период
	|						ИЗ
	|							РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|									&парамДатаАктуальности, 
	|									Сотрудник.ФизЛицо = &парамФизЛицо И
	|									Организация = &парамГоловнаяОрганизация И
	|									(Сотрудник.ВидЗанятости <> &парамВнутреннееСовместительство)) КАК РаботникиОрганизации
	|					) КАК РаботникиОрганизацииСрезПоследних
	|		ПО ИСТИНА
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ПО РаботникиОрганизацииСрезПоследних.Период = ВЫБОР
	|														КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния = &парамУволен
	|															ТОГДА ДОБАВИТЬКДАТЕ(РаботникиОрганизации.Период, ДЕНЬ, -1)
	|														ИНАЧЕ РаботникиОрганизации.Период
	|													  КОНЕЦ
	|			И РаботникиОрганизации.Сотрудник.ФизЛицо = &парамФизЛицо
	|			И РаботникиОрганизации.Организация = &парамГоловнаяОрганизация
	|			И (РаботникиОрганизации.Сотрудник.ВидЗанятости <> &парамВнутреннееСовместительство)
	|
	|ГДЕ
	|	ДанныеОФизЛице.Ссылка = &парамФизЛицо
	|";

	Если РасчетНаРегистрах Тогда
	
		ДоходыТекст = "
		|ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(ОсновныеНачисления.ПериодРегистрации, МЕСЯЦ) КАК Период,
		|		ОсновныеНачисления.Результат КАК Заработок
		|ИЗ
		|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисления
		|ГДЕ
		|		ОсновныеНачисления.ФизЛицо = &парамФизЛицо
		|		И ОсновныеНачисления.ОбособленноеПодразделение В (&парамСписокСтруктурныхЕдиниц)
		|		И ОсновныеНачисления.ПериодРегистрации МЕЖДУ &парамПериодС И &парамПериодПо
		|		И НЕ ОсновныеНачисления.ВидРасчета.ЯвляетсяПрочимДоходом
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(ДополнительныеНачисления.ПериодРегистрации, МЕСЯЦ) КАК Период,
		|		ДополнительныеНачисления.Результат КАК Заработок
		|ИЗ
		|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисления
		|ГДЕ
		|		ДополнительныеНачисления.ФизЛицо = &парамФизЛицо
		|		И ДополнительныеНачисления.ОбособленноеПодразделение В (&парамСписокСтруктурныхЕдиниц)
		|		И ДополнительныеНачисления.ПериодРегистрации МЕЖДУ &парамПериодС И &парамПериодПо
		|		И НЕ ДополнительныеНачисления.ВидРасчета.ЯвляетсяПрочимДоходом
		|";
		
	Иначе

		ДоходыТекст = "
		|ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(ОсновныеНачисления.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК Период,
		|		ОсновныеНачисления.Результат КАК Заработок
		|ИЗ
		|		Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК ОсновныеНачисления
		|ГДЕ
		|		ОсновныеНачисления.Ссылка.Проведен
		|		И ОсновныеНачисления.ФизЛицо = &парамФизЛицо
		|		И ОсновныеНачисления.Ссылка.Организация В (&парамСписокСтруктурныхЕдиниц)
		|		И ОсновныеНачисления.Ссылка.ПериодРегистрации МЕЖДУ &парамПериодС И &парамПериодПо
		|";
	
	КонецЕсли;
	

	ДоходыИНалогиТекст = 
	"ВЫБРАТЬ
	|	ДоходыИНалоги.Период,
	|	СУММА(ДоходыИНалоги.Заработок) КАК Заработок,
	|	СУММА(ДоходыИНалоги.ОПВ) КАК ОПВ,
	|	СУММА(ДоходыИНалоги.ИПН) КАК ИПН,
	|	СУММА(ДоходыИНалоги.ВОСМС) КАК ВОСМС
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		Доходы.Период КАК Период,
	|		Доходы.Заработок КАК Заработок,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВ,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ИПН,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ВОСМС
	|	ИЗ
	|		(" + ДоходыТекст + ") КАК Доходы
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ОПВРасчетыСФондами.Период, МЕСЯЦ) КАК Период,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК Заработок,
	|		ОПВРасчетыСФондами.Взнос КАК ОПВ,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ИПН,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ВОСМС
	|	ИЗ
	|		РегистрНакопления.ОПВРасчетыСФондами КАК ОПВРасчетыСФондами
	|	ГДЕ
	|		ОПВРасчетыСФондами.ФизЛицо = &парамФизЛицо
	|		И ОПВРасчетыСФондами.Организация В (&парамСписокСтруктурныхЕдиниц)
	|		И ОПВРасчетыСФондами.ВидДвижения = &парамПриход
	|		И ОПВРасчетыСФондами.ВидПлатежа = &парамВидПлатежа
	|		И ОПВРасчетыСФондами.ВидСтроки = &парамВидСтроки
	|		И ОПВРасчетыСФондами.Период МЕЖДУ &парамПериодС И &парамПериодПо
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ИПНРасчетыСБюджетом.Период, МЕСЯЦ) КАК Период,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК Заработок,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВ,
	|		ИПНРасчетыСБюджетом.Налог КАК ИПН,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ВОСМС
	|	ИЗ
	|		РегистрНакопления.ИПНРасчетыСБюджетом КАК ИПНРасчетыСБюджетом
	|	ГДЕ
	|		ИПНРасчетыСБюджетом.ФизЛицо = &парамФизЛицо
	|		И ИПНРасчетыСБюджетом.Организация В (&парамСписокСтруктурныхЕдиниц)
	|		И ИПНРасчетыСБюджетом.ВидДвижения = &парамПриход
	|		И ИПНРасчетыСБюджетом.ВидСтроки = &парамВидСтроки
	|		И ИПНРасчетыСБюджетом.Период МЕЖДУ &парамПериодС И &парамПериодПо
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ВОСМСРасчетыСФондами.Период, МЕСЯЦ) КАК Период,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК Заработок,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВ,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ИПН,
	|		ВОСМСРасчетыСФондами.Взнос КАК ВОСМС
	|	ИЗ
	|		РегистрНакопления.ВОСМСРасчетыСФондами КАК ВОСМСРасчетыСФондами
	|	ГДЕ
	|		ВОСМСРасчетыСФондами.ФизЛицо = &парамФизЛицо
	|		И ВОСМСРасчетыСФондами.Организация В (&парамСписокСтруктурныхЕдиниц)
	|		И ВОСМСРасчетыСФондами.ВидДвижения = &парамПриход
	|		И ВОСМСРасчетыСФондами.ВидСтроки = &парамВидСтроки
	|		И ВОСМСРасчетыСФондами.Период МЕЖДУ &парамПериодС И &парамПериодПо 
	|   
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ЕПКомпоненты.Период, МЕСЯЦ) КАК Период,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК Заработок,
	|		ЕПКомпоненты.СуммаОПВ КАК ОПВ,
	|		ЕПКомпоненты.СуммаИПН КАК ИПН,
	|		ЕПКомпоненты.СуммаВОСМС КАК ВОСМС
	|	ИЗ
	|		РегистрНакопления.ЕПКомпоненты КАК ЕПКомпоненты
	|	ГДЕ
	|		ЕПКомпоненты.ФизЛицо = &парамФизЛицо
	|		И ЕПКомпоненты.Организация В(&парамСписокСтруктурныхЕдиниц)
	|		И ЕПКомпоненты.ВидПлатежа = &парамВидПлатежа
	|		И ЕПКомпоненты.Период МЕЖДУ &парамПериодС И &парамПериодПо
	|
	|	) КАК ДоходыИНалоги
    |
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыИНалоги.Период
	|";
	
	// Определим прочие рассчитываемые графы
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоходыИНалоги.Период,
	|	ДанныеОФизЛице.ФИОРаботника,
	|	ДанныеОФизЛице.РННРаботника,
	|	ДанныеОФизЛице.ИИНРаботника,
	|	ДанныеОФизЛице.ТабельныйНомер,
	|	ДанныеОФизЛице.ПодразделениеОрганизации,
	|	ДанныеОФизЛице.Должность,
	|	ВЫРАЗИТЬ(ДанныеОбОрганизации.НаименованиеПолное КАК СТРОКА(1000)) КАК НазваниеОрганизации,
	|	ДанныеОбОрганизации.РНН КАК РННОрганизации,	
	|	ДанныеОбОрганизации.ИдентификационныйНомер КАК БИНОрганизации,	
	|	ВЫРАЗИТЬ(КонтактнаяИнформацияЮрАдрес.Представление КАК СТРОКА(1000)) КАК ЮрАдресОрганизации,
	|	ВЫРАЗИТЬ(КонтактнаяИнформацияТелефон.Представление КАК СТРОКА(1000)) КАК ТелефонОрганизации,
	|	ВЫБОР 
	|		КОГДА ФИОФизЛицРуководитель.ФизЛицо ЕСТЬ NULL ТОГДА ОтветственныеЛицаРуководитель.ФизическоеЛицо.Наименование 
	|		ИНАЧЕ ФИОФизЛицРуководитель.Фамилия + 	ВЫБОР 
	|													КОГДА ФИОФизЛицРуководитель.Имя <> """" 
	|														ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицРуководитель.Имя, 1, 1) + ""."" 
	|													ИНАЧЕ """" 
	|												КОНЕЦ + 
	|												ВЫБОР 
	|													КОГДА ФИОФизЛицРуководитель.Отчество <> """" 
	|														ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицРуководитель.Отчество, 1, 1) + ""."" 
	|													ИНАЧЕ """" 
	|												КОНЕЦ 
	|	КОНЕЦ КАК ФИОРуководителя,
	|	ОтветственныеЛицаРуководитель.Должность.Наименование КАК ДолжностьРуководителя,
	|	ВЫБОР 
	|		КОГДА ФИОФизЛицГлБух.ФизЛицо ЕСТЬ NULL ТОГДА ОтветственныеЛицаГлБух.ФизическоеЛицо.Наименование 
	|		ИНАЧЕ ФИОФизЛицГлБух.Фамилия + 	ВЫБОР 
	|											КОГДА ФИОФизЛицГлБух.Имя <> """" 
	|												ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицГлБух.Имя, 1, 1) + ""."" 
	|											ИНАЧЕ """" 
	|										КОНЕЦ + 
	|										ВЫБОР 
	|											КОГДА ФИОФизЛицГлБух.Отчество <> """" 
	|												ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицГлБух.Отчество, 1, 1) + ""."" 
	|											ИНАЧЕ """" 
	|										КОНЕЦ 
	|	КОНЕЦ КАК ФИОГлБухгалтера,
	|	ВЫБОР
	|		КОГДА ОтветственныеЛицаГлБух.Должность.Ссылка ЕСТЬ NULL
	|			ТОГДА ""Главный бухгалтер""
	|		ИНАЧЕ ОтветственныеЛицаГлБух.Должность.Наименование
	|	КОНЕЦ КАК ДолжностьГлБухгалтера,
	|	ВЫБОР 
	|		КОГДА ФИОФизЛицИсполнитель.ФизЛицо ЕСТЬ NULL 
	|			ТОГДА 	ВЫБОР 
	|						КОГДА Пользователи.ФизЛицо.Ссылка ЕСТЬ NULL ТОГДА Пользователи.Наименование 
	|						ИНАЧЕ Пользователи.ФизЛицо.Наименование 
	|					КОНЕЦ 
	|			ИНАЧЕ ФИОФизЛицИсполнитель.Фамилия + 	ВЫБОР 
	|														КОГДА ФИОФизЛицИсполнитель.Имя <> """" 
	|															ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицИсполнитель.Имя, 1, 1) + ""."" 
	|														ИНАЧЕ """" 
	|													КОНЕЦ + 
	|													ВЫБОР 
	|														КОГДА ФИОФизЛицИсполнитель.Отчество <> """" 
	|															ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицИсполнитель.Отчество, 1, 1) + ""."" 
	|														ИНАЧЕ """" 
	|													КОНЕЦ 
	|	КОНЕЦ КАК ФИОИсполнителя,
	|	СведенияОбИсполнителе.Должность.Наименование КАК ДолжностьИсполнителя,
	|	ДоходыИНалоги.Заработок,
	|	ДоходыИНалоги.ОПВ,
	|	ДоходыИНалоги.ИПН,
	|	ДоходыИНалоги.ВОСМС
	|
	|ИЗ
	|	(" + ДоходыИНалогиТекст + ") КАК ДоходыИНалоги
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (" + ДанныеОФизЛицеТекст + ") КАК ДанныеОФизЛице
	|		ПО (ИСТИНА)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ДанныеОбОрганизации
	|		ПО ДанныеОбОрганизации.Ссылка = &парамОрганизация 
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформацияЮрАдрес
	|		ПО КонтактнаяИнформацияЮрАдрес.Объект = &парамОрганизацияКИ 
	|		   И КонтактнаяИнформацияЮрАдрес.Тип = &парамТипИнформацииАдрес 
	|		   И КонтактнаяИнформацияЮрАдрес.Вид = &парамВидАдресаЮридический
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформацияТелефон
	|		ПО КонтактнаяИнформацияТелефон.Объект = &парамОрганизацияКИ 
	|		   И КонтактнаяИнформацияТелефон.Тип = &парамТипИнформацииТелефон 
	|		   И КонтактнаяИнформацияТелефон.Вид = &парамВидТелефонОрганизации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|							&парамДатаАктуальности, 
	|							СтруктурнаяЕдиница = &парамОрганизация И 
	|							ОтветственноеЛицо = &парамОтветЛицоРуководитель) КАК ОтветственныеЛицаРуководитель
	|		ПО (ИСТИНА)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&парамДатаАктуальности) КАК ФИОФизЛицРуководитель
	|		ПО ОтветственныеЛицаРуководитель.ФизическоеЛицо = ФИОФизЛицРуководитель.ФизЛицо
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|							&парамДатаАктуальности, 
	|							СтруктурнаяЕдиница = &парамОрганизация И 
	|							ОтветственноеЛицо = &парамОтветЛицоГлБух) КАК ОтветственныеЛицаГлБух
	|		ПО (ИСТИНА)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&парамДатаАктуальности) КАК ФИОФизЛицГлБух
	|		ПО ОтветственныеЛицаГлБух.ФизическоеЛицо = ФИОФизЛицГлБух.ФизЛицо
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО Пользователи.Ссылка = &парамТекущийПользователь
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&парамДатаАктуальности) КАК ФИОФизЛицИсполнитель
	|		ПО Пользователи.ФизЛицо = ФИОФизЛицИсполнитель.ФизЛицо	
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|							РаботникиОрганизации.Сотрудник.ФизЛицо КАК ФизЛицо,
	|							МАКСИМУМ(ВЫБОР
	|										КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния = &парамУволен
	|											ТОГДА ДОБАВИТЬКДАТЕ(РаботникиОрганизации.Период, ДЕНЬ, -1)
	|										ИНАЧЕ РаботникиОрганизации.Период
	|									КОНЕЦ) КАК Период
	|						ИЗ
	|							РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|									&парамДатаАктуальности,
	|									Организация = &парамГоловнаяОрганизация И
	|									(Сотрудник.ВидЗанятости <> &парамВнутреннееСовместительство)) КАК РаботникиОрганизации
	|						СГРУППИРОВАТЬ ПО
	|							РаботникиОрганизации.Сотрудник.ФизЛицо) КАК СведенияОбИсполнителеСрезПоследних
	|		ПО Пользователи.ФизЛицо = СведенияОбИсполнителеСрезПоследних.ФизЛицо
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК СведенияОбИсполнителе
	|		ПО СведенияОбИсполнителеСрезПоследних.ФизЛицо = СведенияОбИсполнителе.Сотрудник.ФизЛицо
	|			И СведенияОбИсполнителеСрезПоследних.Период = ВЫБОР
	|															КОГДА СведенияОбИсполнителе.ПричинаИзмененияСостояния = &парамУволен
	|																ТОГДА ДОБАВИТЬКДАТЕ(СведенияОбИсполнителе.Период, ДЕНЬ, -1)
	|															ИНАЧЕ СведенияОбИсполнителе.Период
	|														  КОНЕЦ
	|			И СведенияОбИсполнителе.Организация = &парамГоловнаяОрганизация
	|			И (СведенияОбИсполнителе.Сотрудник.ВидЗанятости <> &парамВнутреннееСовместительство)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоходыИНалоги.Период
	|";
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
		     	
	Возврат Результат;
	
КонецФункции // ФормаРассчитатьДанныеФормы()

&НаСервере
Процедура ЗаполнитьАвтоНаСервере()
		
	Выборка = ФормаРассчитатьДанныеФормы().Выбрать();
   
	// Сохраняем данные в структуру данные в поле табличного документа
	СписокСохранения = Новый Структура();
	
	НомерСтроки 		= 0;
	ИтогоЗаработок 		= 0;
	ИтогоОПВ 			= 0;
	ИтогоИПН			= 0;
	ИтогоВОСМС			= 0;

	ДатаПрекращенияВыводаРНН = Константы.ДатаПрекращенияВыводаРННВПервичныхДокументах.Получить();
	ВыводитьРНН = НЕ ЗначениеЗаполнено(ДатаПрекращенияВыводаРНН) ИЛИ ДатаПодписи < ДатаПрекращенияВыводаРНН;
	
	Пока Выборка.Следующий() Цикл
		НомерСтроки = НомерСтроки + 1;
		
		// Если первая строка, то заполняем реквизиты шапки и подвала
		Если НомерСтроки = 1 Тогда

    		// период отчета
    		СписокСохранения.Вставить("ПериодС",  СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
    		СписокСохранения.Вставить("ПериодПо", СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	
			// Реквизиты организации
			СписокСохранения.Вставить("ОргНазв",  СокрЛП(Выборка.НазваниеОрганизации));
			СписокСохранения.Вставить("ОргНазв1", СокрЛП(Выборка.НазваниеОрганизации));
			
			РеквизитыОрганизации = "";
			Если ЗначениеЗаполнено(Выборка.ЮрАдресОрганизации) Тогда
				РеквизитыОрганизации = СокрЛП(Выборка.ЮрАдресОрганизации);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.ТелефонОрганизации) Тогда
				Если РеквизитыОрганизации <> "" Тогда
					РеквизитыОрганизации = РеквизитыОрганизации + ", ";
				КонецЕсли;
				РеквизитыОрганизации = РеквизитыОрганизации + "тел. " + СокрЛП(Выборка.ТелефонОрганизации);
			КонецЕсли;
			
			СписокСохранения.Вставить("РеквизитыОрганизации", РеквизитыОрганизации);
			СписокСохранения.Вставить("ДатаПодписи",          ДатаПодписи);

			Если ВыводитьРНН Тогда
				СписокСохранения.Вставить("РНН_БИНОрганизации",      Выборка.РННОрганизации);
				СписокСохранения.Вставить("ТекстРНН_БИНОрганизации", "РНН налогоплательщика:");
			Иначе
				СписокСохранения.Вставить("РНН_БИНОрганизации",      Выборка.БИНОрганизации);
				СписокСохранения.Вставить("ТекстРНН_БИНОрганизации", "БИН налогоплательщика:");
			КонецЕсли;
			
			// Реквизиты физлица
			СведенияОРаботнике = Выборка.ФИОРаботника;
			Если ЗначениеЗаполнено(Выборка.ТабельныйНомер) Тогда
				СведенияОРаботнике = СведенияОРаботнике + ", таб. № " + Выборка.ТабельныйНомер;
			КонецЕсли;
			
			Если ВыводитьРНН Тогда
				Если ЗначениеЗаполнено(Выборка.РННРаботника) Тогда
					СведенияОРаботнике = СведенияОРаботнике + ", РНН " + Выборка.РННРаботника;
				КонецЕсли;
			Иначе
				Если ЗначениеЗаполнено(Выборка.ИИНРаботника) Тогда
					СведенияОРаботнике = СведенияОРаботнике + ", ИИН " + Выборка.ИИНРаботника;
				КонецЕсли;
			КонецЕсли;
			
			СписокСохранения.Вставить("ФИОРаботника", СведенияОРаботнике); 
			
			СведенияОРаботнике = "";
			Если ЗначениеЗаполнено(Выборка.ПодразделениеОрганизации) Тогда
				Если СведенияОРаботнике <> "" Тогда
					СведенияОРаботнике = СведенияОРаботнике + ", ";
				КонецЕсли;
				
				СведенияОРаботнике = СведенияОРаботнике + Выборка.ПодразделениеОрганизации;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.Должность) Тогда
				Если СведенияОРаботнике <> "" Тогда
					СведенияОРаботнике = СведенияОРаботнике + ", ";
				КонецЕсли;
				
				СведенияОРаботнике = СведенияОРаботнике + Выборка.Должность;
			КонецЕсли;
			СписокСохранения.Вставить("СведенияОРаботнике", СведенияОРаботнике);
			
			// Руководитель организации
			СписокСохранения.Вставить("ФИОРуководителя",       Выборка.ФИОРуководителя);
			СписокСохранения.Вставить("ДолжностьРуководителя", Выборка.ДолжностьРуководителя);

			// Главный бухгалтер
			СписокСохранения.Вставить("ФИОГлБухгалтера",       Выборка.ФИОГлБухгалтера);
			СписокСохранения.Вставить("ДолжностьГлБухгалтера", Выборка.ДолжностьГлБухгалтера);
			
			// Непосредственный исполнитель
			СписокСохранения.Вставить("ФИОИсполнителя",        Выборка.ФИОИсполнителя);
			СписокСохранения.Вставить("ДолжностьИсполнителя",  Выборка.ДолжностьИсполнителя);
		КонецЕсли;

		СписокСохранения.Вставить("Период_" + НомерСтроки, Выборка.Период);
		
		// Заполняем колонки отчета
		ИтогоЗаработок = ИтогоЗаработок + Выборка.Заработок;
		СписокСохранения.Вставить("Заработок_" + НомерСтроки, Выборка.Заработок);
		
		ИтогоОПВ = ИтогоОПВ + Выборка.ОПВ;
		СписокСохранения.Вставить("ОПВ_" + НомерСтроки, Выборка.ОПВ);
		
		ИтогоИПН = ИтогоИПН + Выборка.ИПН;
		СписокСохранения.Вставить("ИПН_" + НомерСтроки, Выборка.ИПН);
		
		ИтогоВОСМС = ИтогоВОСМС + Выборка.ВОСМС;
		СписокСохранения.Вставить("ВОСМС_" + НомерСтроки, Выборка.ВОСМС);
		
		СписокСохранения.Вставить("СуммаКВыплате_" + НомерСтроки, Выборка.Заработок - Выборка.ОПВ - Выборка.ИПН - Выборка.ВОСМС);
	КонецЦикла;	
	
	// Итоги отчета
	СписокСохранения.Вставить("ИтогоЗаработок", ИтогоЗаработок);
	СписокСохранения.Вставить("ИтогоОПВ", ИтогоОПВ);
	СписокСохранения.Вставить("ИтогоИПН", ИтогоИПН);
	СписокСохранения.Вставить("ИтогоВОСМС", ИтогоВОСМС);
	СписокСохранения.Вставить("ИтогоСуммаКВыплате", ИтогоЗаработок - ИтогоОПВ - ИтогоИПН - ИтогоВОСМС);
	
	ВывестиВДокумент(СписокСохранения);
	
	ТекТабличныйДокумент = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];
	Если ТипЗнч(мСтруктураСохраняемыхПоказателей) = Тип("Структура") Тогда
		Для Каждого Показатель Из мСтруктураСохраняемыхПоказателей Цикл
			Если ЗначениеЗаполнено(Показатель.Значение) Тогда
				УстановитьЗначениеОбласти(ТекТабличныйДокумент, Показатель.Ключ, Показатель.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	УстановитьФорматВывода();
	Комментарий = "";

КонецПроцедуры

&НаСервере
Функция ПроверитьСоответствиеСотрудникаОрганизацииНаСервере(ВыбранныйСотрудник)
	
	ГоловнаяОрганизацияОтчета    = ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(Налогоплательщик);
	ВыбраннаяГоловнаяОрганизация = ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(ВыбранныйСотрудник.ТекущаяСтруктурнаяЕдиница);
	
	Возврат ГоловнаяОрганизацияОтчета = ВыбраннаяГоловнаяОрганизация;
	
КонецФункции

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	
	ФизЛицо	= Сотрудник.ФизЛицо;

КонецПроцедуры

&НаСервере
Процедура ДатаПодписиПриИзмененииНаСервере()
	
	УстановитьЗначениеОбласти(ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"], "ДатаПодписи", ДатаПодписи);
	Модифированность = Истина;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьЗначениеОбласти(Приемник, ИмяОбласти, Значение)

	Результат = Ложь;
	Если ТипЗнч(Приемник) = Тип("Структура") Тогда
		
		Приемник.Вставить(ИмяОбласти, Значение);
		Результат = Истина;
	
	ИначеЕсли ТипЗнч(Приемник) = Тип("ТабличныйДокумент") Тогда
		
		ИскомаяОбласть = Приемник.Области.Найти(ИмяОбласти);
		Если ИскомаяОбласть <> Неопределено Тогда
			Если ИскомаяОбласть.СодержитЗначение Тогда
				ИскомаяОбласть.Значение = Значение;
			Иначе
				ИскомаяОбласть.Текст = Строка(Значение);
			КонецЕсли;
			Результат = Истина;
		КонецЕсли;
				
	КонецЕсли;

    Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЗначениеОбласти(Источник, ИмяОбласти, ЗначениеПоУмолчанию = Неопределено) 

	Результат = ЗначениеПоУмолчанию;
	Если ТипЗнч(Источник) = Тип("Структура") Тогда
		
		Источник.Свойство(ИмяОбласти, Результат);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ТабличныйДокумент") Тогда
		
		ИскомаяОбласть = Источник.Области.Найти(ИмяОбласти);
		
		Если ИскомаяОбласть <> Неопределено Тогда
			
			Если ИскомаяОбласть.СодержитЗначение Тогда
				Результат = ИскомаяОбласть.Значение;
			Иначе
				Результат = ИскомаяОбласть.Текст;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаСервере
// Процедура контролирует корректность установленной точности указания
// суммовых (денежных) показателей при выборе единицы измерения.
//
Процедура ПроверитьТочность()
	
	Если ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1 Тогда

		Если ТочностьЕдиницыИзмерения > 2 Тогда
	        ТекстСообщения = НСтр("ru = 'При выводе сумм в тенге точность не может превышать 2 знака'");            
	        ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);                
			ТочностьЕдиницыИзмерения = 2;
		КонецЕсли;

	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000 Тогда

		Если ТочностьЕдиницыИзмерения > 3 Тогда
	        ТекстСообщения = НСтр("ru = 'При выводе сумм в тысячах тенге точность не может превышать 3 знака'");            
	        ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);                
			ТочностьЕдиницыИзмерения = 3;
		КонецЕсли;

	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000000 Тогда

		Если ТочностьЕдиницыИзмерения > 6 Тогда
	        ТекстСообщения = НСтр("ru = 'При выводе сумм в миллионах тенге точность не может превышать 6 знаков'");            
	        ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);                
			ТочностьЕдиницыИзмерения = 6;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ПроверитьТочность()

&НаСервере
Функция СобратьДанныеТекущегоТаблПоля(ТабличноеПоле)

	// Собираем в список значений имена вычисляемых показателей
	//
	//
	СтруктураДанныхПоля = Новый Структура;

	Для Инд = 0 По ТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ТабличноеПоле.Области[Инд];

		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;

		Если НЕ(ТекущаяОбласть.СодержитЗначение) Тогда
			Продолжить;
		КонецЕсли;

		ИмяПоказателя      = ТекущаяОбласть.Имя;
		ЗначениеПоказателя = ТекущаяОбласть.Значение;

		СтруктураДанныхПоля.Вставить(ИмяПоказателя, ЗначениеПоказателя);

	КонецЦикла;

	Возврат СтруктураДанныхПоля;

КонецФункции // СобратьДанныеТекущегоТаблПоля()

