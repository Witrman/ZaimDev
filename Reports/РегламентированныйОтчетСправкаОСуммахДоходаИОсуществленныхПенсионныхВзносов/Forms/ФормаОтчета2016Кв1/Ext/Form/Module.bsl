////////////////////////////////////////////////////////////////////////////////
// Описание формы
// Параметры фомры:
//  Организация - СправочникСсылка.Организации - Налогоплательщик, представляющий отчет
//  мДатаНачалаПериодаОтчета, мДатаКонцаПериодаОтчета - Дата - отчетный период формирования отчета
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СформироватьСтруктуруРеквизитовФормы();
	
	мВыбраннаяФорма = "ФормаОтчета2016Кв1";
	КодФормы 		= "ФормаОтчета";
	               		
	РегламентированнаяОтчетность.ЗаполнитьСтруктуруФормРегОтчета(ЭтотОбъект);	
			
	СтруктураРеквизитовФормы.СписокПечатаемыхЛистов = Новый СписокЗначений;	
	СтруктураРеквизитовФормы.мПечатныеформы         = Новый СписокЗначений;	
	
	ЗаполнитьПараметрыФормы(ЭтотОбъект, Параметры);	
	
	СтруктураРеквизитовФормы.мПоддержкаРаботыСоСтруктурнымиПодразделениями = РегламентированнаяОтчетностьПереопределяемый.ПоддержкаРаботыСоСтруктурнымиПодразделениями(); // перевести в переопределяемый модуль 

	// Общие механизмы
	ЗаполнитьСведенияОбОтчетномПериоде();
		
	Инициализация(Параметры.БезОткрытияФормы);
	
	УправлениеПометкамиКнопокКоманднойПанели();
	
	УстановитьФорматВывода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;	
	КонецЕсли;
	
	Если Модифицированность Тогда 
		Оповещение = Новый ОписаниеОповещения("ПриЗакрытииЗавершение", ЭтотОбъект, Новый Структура);				
		Оповещение.ДополнительныеПараметры.Вставить("КодФормы", КодФормы);	
		ТекстВопроса = НСТР("ru = 'Сохранить данные отчета?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);	
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПредставлениеСпискаСтруктурныхЕдиницНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСДиалогамиКлиент.НачалоВыбораСпискаСтруктурныхЕдиниц(мСписокСтруктурныхЕдиниц, ЭтаФорма, СтандартнаяОбработка, "", Налогоплательщик,,, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСпискаСтруктурныхЕдиниц(Результат, Параметры) Экспорт

	Если Результат <> Неопределено Тогда		
		мСписокСтруктурныхЕдиниц = Результат;		
		ПредставлениеСпискаСтруктурныхЕдиниц = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(мСписокСтруктурныхЕдиниц); 		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	ЕдиницаИзмеренияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТочностьЕдиницыИзмеренияПриИзменении(Элемент)
	
	ТочностьЕдиницыИзмеренияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ПараметрыФормы	= Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",            Истина);
	ПараметрыФормы.Вставить("РежимВыбора",                   Истина);
	ПараметрыФормы.Вставить("ОтборОрганизация",              Налогоплательщик);
	
	Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	
	ОткрытьФорму("Справочник.СотрудникиОрганизаций.Форма.ФормаСписка", ПараметрыФормы, Элемент,,,,, Режим);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
		
	Если ЗначениеЗаполнено(Сотрудник) Тогда
	    СотрудникПриИзмененииНаСервере();
	Иначе
		ФизЛицо = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение)
		И ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СотрудникиОрганизаций") Тогда
		
		Если НЕ ПроверитьСоответствиеСотрудникаОрганизацииНаСервере(ВыбранноеЗначение) Тогда
	        ТекстСообщения = НСтр("ru = 'Организация отчета не соответствует организации, указанной в карточке сотрудника!'");            
	        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);                
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПодписиПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ДатаПодписи) Тогда
		ДатаПодписиПриИзмененииНаСервере();
		Модифированность = Истина;
	КонецЕсли;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ "Отчеты"

&НаКлиенте
Процедура ФормаОтчетаПолеТабличногоДокументаСтраница1ПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	Модифицированность = Истина;
	ФормаОтчетаРасчетНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Налогоплательщик) Тогда
		Сообщить("Не выбрана организация!");
		Возврат;
	КонецЕсли;
	
	Если мСписокСтруктурныхЕдиниц.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбраны структурные единицы!'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФизЛицо) ИЛИ НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран сотрудник!'"));
		Возврат;
	КонецЕсли;
	
	Перезаполнить = Истина; // Для выполнения запроса на пересчет данных к пользователю
	
	СтрокаФормы = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(мСписокФормБезИерархии, Новый Структура("КодФормы", КодФормы));
	
	Если СтрокаФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Перезаполнить Тогда
		ПризнакАвтоЗаполнения =  РегламентированнаяОтчетностьКлиент.ОпределитьПризнакАвтоЗаполненияФормы(СтрокаФормы);
		Если Не ПризнакАвтоЗаполнения Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если Перезаполнить Тогда
		
		Если ВсегоСтраниц = 0 Тогда
			
			ЗаполнитьАвтоНаСервере();
			
		Иначе
				
			Если СтрокаФормы.ПолучитьЭлементы().Количество() > 0 Тогда
				ТекстВопроса = "Перезаполнить данные формы и ее дополнительных форм?";
			Иначе
				ТекстВопроса = "Перезаполнить данные формы?";
			КонецЕсли;
			
			Оповещение = Новый ОписаниеОповещения("ЗаполнитьАвтоЗавершение", ЭтотОбъект, Новый Структура);				
			Оповещение.ДополнительныеПараметры.Вставить("КодФормы", КодФормы);
			Оповещение.ДополнительныеПараметры.Вставить("Перезаполнить", Перезаполнить);
			
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);		
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФорму(Команда)
	
	РегламентированнаяОтчетностьКлиент.ОчиститьРеглОтчет(ЭтотОбъект, "ОчиститьФорму");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвтоЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;	 	
	
	ЗаполнитьАвтоНаСервере();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланк(Команда)
	
	Печать(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечататьСразу(Команда)
	
	Печать(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура Настройка(Команда)
	
	ОткрытьФормуНастройкиСтраниц();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяФорма(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	ПараметрыФормы.Вставить("мСохраненныйДок",          СтруктураРеквизитовФормы.мСохраненныйДок);	
	ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета",  СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	ПараметрыФормы.Вставить("мПериодичность",           СтруктураРеквизитовФормы.мПериодичность);
	ПараметрыФормы.Вставить("Организация",              Налогоплательщик);
	ПараметрыФормы.Вставить("мВыбраннаяФорма",          мВыбраннаяФорма);
	ПараметрыФормы.Вставить("мСписокСтруктурныхЕдиниц", мСписокСтруктурныхЕдиниц);	
	
	ОткрытьФорму(СтрЗаменить(ЭтаФорма.ИмяФормы, мВыбраннаяФорма, "") + "ОсновнаяФорма", ПараметрыФормы, ЭтотОбъект, Новый УникальныйИдентификатор);	

КонецПроцедуры

&НаКлиенте
Процедура Заголовок (Команда)
	
	Если НЕ ЗначениеЗаполнено(Налогоплательщик) Тогда
		Сообщить("Не выбрана организация!");
		Возврат;
	КонецЕсли;
	
	Если мСписокСтруктурныхЕдиниц.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбраны структурные единицы!'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФизЛицо) ИЛИ НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран сотрудник!'"));
		Возврат;
	КонецЕсли;
	
	Перезаполнить = Истина; // Для выполнения запроса на пересчет данных к пользователю
	
	СтрокаФормы = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(мСписокФормБезИерархии, Новый Структура("КодФормы", КодФормы));
	
	Если СтрокаФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Перезаполнить Тогда
		ПризнакАвтоЗаполнения =  РегламентированнаяОтчетностьКлиент.ОпределитьПризнакАвтоЗаполненияФормы(СтрокаФормы);
		Если Не ПризнакАвтоЗаполнения Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	// Перезаполнять заголовок можно только у "чистого" отчета
	Если ВсегоСтраниц = 0 Тогда
		Если Перезаполнить Тогда
			
			ЗаполнитьАвтоНаСервере();
			
		Иначе
				
			Если СтрокаФормы.ПолучитьЭлементы().Количество() > 0 Тогда
				ТекстВопроса = "Перезаполнить данные формы и ее дополнительных форм?";
			Иначе
				ТекстВопроса = "Перезаполнить данные формы?";
			КонецЕсли;
			
			Оповещение = Новый ОписаниеОповещения("ЗаполнитьАвтоЗавершение", ЭтотОбъект, Новый Структура);				
			Оповещение.ДополнительныеПараметры.Вставить("КодФормы", КодФормы);
			Оповещение.ДополнительныеПараметры.Вставить("Перезаполнить", Перезаполнить);
			
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);		
			
		КонецЕсли;
	КонецЕсли;

	ОтчетОбъект.ПоказыватьЗаголовок = Команда.Имя;
	ВыводЗаголовка(ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"]);
	
	УправлениеПометкамиКнопокКоманднойПанели();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура СохранитьДанные() Экспорт
	
	СохранитьДанныеНаСервере();
	Оповестить("Позиционирование в списке отчетов", СтруктураРеквизитовФормы.мСохраненныйДок);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиВДокумент(СписокСохранения)
	
	// очищаем текущие данные
	ТекТабличныйДокумент = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];
	
	РегламентированнаяОтчетность.ОчиститьДанныеРегОтчета(ЭтотОбъект, КодФормы, Ложь, Ложь);

	Отчет = РеквизитФормыВЗначение("ОтчетОбъект");
	Макет = Отчет.ПолучитьМакет("МакетФормаОтчета2016Кв1");
	
	ОбластьЗаголовок 			= Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовокПриложение2	= Макет.ПолучитьОбласть("ЗаголовокПриложение2");
	ОбластьШапка      			= Макет.ПолучитьОбласть("Шапка");
	ОбластьРеквизиты  			= Макет.ПолучитьОбласть(?(ВыводитьРНН, "РеквизитыРНН", "РеквизитыИИН_БИН"));
	ОбластьШапкаЛиста 			= Макет.ПолучитьОбласть("ШапкаЛиста");
	ОбластьСтрока     			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтогПоГоду 			= Макет.ПолучитьОбласть("ИтогПоГоду");
	ОбластьПодвал     			= Макет.ПолучитьОбласть("Подвал");
	
	МассивОбластей = Новый Массив;
	МассивОбластей.Добавить(ОбластьСтрока);
	МассивОбластей.Добавить(ОбластьПодвал);
	
	ВсегоСтраниц = 0;
	Верх = 1;
	
	// Шапка отчета
	ТекТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТекТабличныйДокумент.Вывести(ОбластьЗаголовок);	
	ТекТабличныйДокумент.Вывести(ОбластьЗаголовокПриложение2);	
	ТекТабличныйДокумент.Вывести(ОбластьШапка);
	ТекТабличныйДокумент.Вывести(ОбластьРеквизиты);
	ТекТабличныйДокумент.Вывести(ОбластьШапкаЛиста);
	
	// Строки отчета
	ТекущийГод = 0;
	ТекущийНомерСтроки = 0;
	
	мДатаНачалаПериодаОтчета = НачалоМесяца(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета); // СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета;
	мДатаКонцаПериодаОтчета  = КонецМесяца(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета); // СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	
	Для Каждого ЭлементГод Из мМассивОтчетныхГодов Цикл
		
		Год = ЭлементГод.Значение;
		
		СтрГод = Формат(Год, "ЧН=; ЧГ=");
		СтрТекущийГод = Формат(ТекущийГод, "ЧН=; ЧГ=");
		
		Для Каждого ИспользуемаяОбласть Из ОбластьИтогПоГоду.Области Цикл
			ИспользуемаяОбласть.Имя = СтрЗаменить(ИспользуемаяОбласть.Имя, "_" + СтрТекущийГод, "_" + СтрГод);
		КонецЦикла;
		
		Для НомерСтроки = 1 По 12 Цикл
			НаличиеДанныхПоПериоду = Ложь;
			// Номера областей устанавливаем в соответствии с текущим годом и номером
			
			Для Каждого ИспользуемаяОбласть Из ОбластьСтрока.Области Цикл
				ИспользуемаяОбласть.Имя = СтрЗаменить(ИспользуемаяОбласть.Имя, "_" + СтрТекущийГод + "_" + (ТекущийНомерСтроки), "_" + СтрГод + "_" + НомерСтроки);
				Если СписокСохранения.Свойство(ИспользуемаяОбласть.Имя) Тогда
					НаличиеДанныхПоПериоду = Истина;
				КонецЕсли;
				
			КонецЦикла;
			
			// Выводим строки с данными или пустые строки по необходимым периодам
			Если НЕ НаличиеДанныхПоПериоду Тогда
				Если Дата(Год, НомерСтроки, 1) >= мДатаНачалаПериодаОтчета 
					И Дата(Год, НомерСтроки, 1) <= мДатаКонцаПериодаОтчета Тогда
					СписокСохранения.Вставить("Период_" + СтрГод + "_" + НомерСтроки, Дата(Год, НомерСтроки, 1));
					ТекТабличныйДокумент.Вывести(ОбластьСтрока);
				КонецЕсли;
			Иначе 
				ТекТабличныйДокумент.Вывести(ОбластьСтрока);
			КонецЕсли;
			
			ТекущийГод = Год;
			СтрТекущийГод = Формат(ТекущийГод, "ЧН=; ЧГ=");
			ТекущийНомерСтроки = НомерСтроки;
						
			// проверим умещаются ли строки на странице
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТекТабличныйДокумент, МассивОбластей) Тогда
				ВсегоСтраниц = ВсегоСтраниц + 1;
				ТекТабличныйДокумент.Область(Верх,, ТекТабличныйДокумент.ВысотаТаблицы).Имя = "Страница" + Формат(ВсегоСтраниц, "ЧГ=0");
				Верх = ТекТабличныйДокумент.ВысотаТаблицы + 1;
				ТекТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТекТабличныйДокумент.Вывести(ОбластьШапкаЛиста);
			КонецЕсли;
			
		КонецЦикла;
		ТекТабличныйДокумент.Вывести(ОбластьИтогПоГоду);
	КонецЦикла;
	
	// Выводим подвал
	ТекТабличныйДокумент.Вывести(ОбластьПодвал);
	
	// имя для последней страницы
	ВсегоСтраниц = ВсегоСтраниц + 1;
	ТекТабличныйДокумент.Область(Верх,, ТекТабличныйДокумент.ВысотаТаблицы).Имя = "Страница" + Формат(ВсегоСтраниц, "ЧГ=0");

	Если СписокСохранения <> Неопределено И ТипЗнч(СписокСохранения) = Тип("Структура") Тогда
		// Переписываем показатели из структуры сохранения в области табличного документа
		Для Каждого ЗаполняемаяОбласть Из ТекТабличныйДокумент.Области Цикл
			Значение = 0;
			Если СписокСохранения.Свойство(ЗаполняемаяОбласть.Имя, Значение) Тогда
				ЗаполняемаяОбласть.Значение = Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ВыводЗаголовка(ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"]);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРеглОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Форма = ДополнительныеПараметры.Форма;	
		СтрокаФормы = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(Форма.мСписокФормБезИерархии, Новый Структура("КодФормы", Форма.КодФормы));
		Вложенность = СтрокаФормы.ПолучитьЭлементы().Количество() > 0;	
		
		ОчиститьРеглОтчетЗавершениеНаСервере(СтрокаФормы.КодФормы, СтрокаФормы.Многострочность, Вложенность)	
	КонецЕсли;
	
КонецПроцедуры 	

&НаСервере
Процедура ОчиститьРеглОтчетЗавершениеНаСервере(КодФормы, ПризнакМногострочности, Вложенность)
	
	РегламентированнаяОтчетность.ОчиститьДанныеРегОтчета(ЭтотОбъект, КодФормы, ПризнакМногострочности, Вложенность);
	ВсегоСтраниц = 0;
	
КонецПроцедуры

&НаСервере
// Процедура очистки формы
//
// параметр: ТекИмяФормы - имя очищаемой формы (=КодФормы)
//           ОчиститьВсе - признак вида очистки формы              
//
Процедура Очистить(ТекИмяФормы, ОчиститьВсе = Ложь) Экспорт
	
	СписокПоказателейНеПодлежащихОчистке = Новый СписокЗначений;
	
	Если НЕ ОчиститьВсе Тогда
		СписокПоказателейНеПодлежащихОчистке.Добавить("ОргНазв");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ОргНазв1");
	    СписокПоказателейНеПодлежащихОчистке.Добавить("РеквизитыОрганизации");
		СписокПоказателейНеПодлежащихОчистке.Добавить("Номер");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ДолжностьРуководителя");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ФИОРуководителя");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ФИОГлБухгалтера");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ДолжностьИсполнителя");
		СписокПоказателейНеПодлежащихОчистке.Добавить("ФИОИсполнителя");
		СписокПоказателейНеПодлежащихОчистке.Добавить("МестоПредоставления");
	КонецЕсли;

	ТекТабличныйДокумент = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];
	
	Для Каждого Показатель Из СписокПоказателейНеПодлежащихОчистке Цикл
		Знч = ПолучитьЗначениеОбласти(ТекТабличныйДокумент, Показатель.Значение, Неопределено);
		Если Знч <> Неопределено Тогда
			мСтруктураСохраняемыхПоказателей.Вставить(Показатель.Значение, Знч);
		КонецЕсли;
	КонецЦикла;

	ТекТабличныйДокумент.Очистить();
	
	ВсегоСтраниц = 0;
	
КонецПроцедуры // Очистить()

&НаКлиенте
Процедура ОбновитьПараметрыФормыНаКлиенте(Параметры) Экспорт
	
	ЗаполнитьПараметрыФормы(ЭтотОбъект, Параметры);	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПараметрыФормы(ТекФорма, Параметры)
	
	ЗаполнитьЗначенияСвойств(ТекФорма.СтруктураРеквизитовФормы, Параметры);
	
	ТекФорма.Налогоплательщик = Параметры.Организация;
	ТекФорма.мСписокСтруктурныхЕдиниц = Параметры.мСписокСтруктурныхЕдиниц;
	ТекФорма.ПредставлениеСпискаСтруктурныхЕдиниц = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ТекФорма.мСписокСтруктурныхЕдиниц);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяГлавнойФормы(ВыбраннаяФорма) Экспорт
	
	Возврат СтрДлина(ВыбраннаяФорма)-7;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры инициализации отчета и структуры его данных

&НаСервере
Процедура СформироватьСтруктуруРеквизитовФормы()
	
	СтруктураРеквизитовФормы = Новый Структура;
	СтруктураРеквизитовФормы.Вставить("Организация");
	СтруктураРеквизитовФормы.Вставить("СписокСтруктурныхЕдиниц");
	СтруктураРеквизитовФормы.Вставить("ЕдиницаИзмерения");
	СтруктураРеквизитовФормы.Вставить("ТочностьЕдиницыИзмерения");			
	СтруктураРеквизитовФормы.Вставить("мВыбраннаяФорма");
	СтруктураРеквизитовФормы.Вставить("мДатаКонцаПериодаОтчета");
	СтруктураРеквизитовФормы.Вставить("мДатаНачалаПериодаОтчета");		
	СтруктураРеквизитовФормы.Вставить("мПериодичность");
	СтруктураРеквизитовФормы.Вставить("СписокПечатаемыхЛистов");
	СтруктураРеквизитовФормы.Вставить("мПечатныеФормы");		
	СтруктураРеквизитовФормы.Вставить("мРежимПечати");
	СтруктураРеквизитовФормы.Вставить("мСкопированаФорма");
	СтруктураРеквизитовФормы.Вставить("мСоставПоказателей");
	СтруктураРеквизитовФормы.Вставить("мСохраненныйДок");
	СтруктураРеквизитовФормы.Вставить("мСтрокаФормата");
	СтруктураРеквизитовФормы.Вставить("мСчетчикСтраниц");		
	СтруктураРеквизитовФормы.Вставить("ФлажокОтклАвтоРасчет", Ложь);
	СтруктураРеквизитовФормы.Вставить("СтрПериодОтчета");	
	СтруктураРеквизитовФормы.Вставить("НужноРассчитатьОтчетНаСервере", Ложь);			
	СтруктураРеквизитовФормы.Вставить("СформироватьФормуОтчетаАвтоматически");
	СтруктураРеквизитовФормы.Вставить("мБезОткрытияФормы", Параметры.БезОткрытияФормы);	
	СтруктураРеквизитовФормы.Вставить("мПериодичность", Параметры.мПериодичность);		
	СтруктураРеквизитовФормы.Вставить("НалоговыйКомитет", Неопределено);			
	СтруктураРеквизитовФормы.Вставить("мГод");	
	СтруктураРеквизитовФормы.Вставить("мКвартал");	
	СтруктураРеквизитовФормы.Вставить("мМесяц");	
	// индивидуальные параметры формы
	СтруктураРеквизитовФормы.Вставить("мПоддержкаРаботыСоСтруктурнымиПодразделениями");
	СтруктураРеквизитовФормы.Вставить("ВзносыПоПериодуИсчисления");
	СтруктураРеквизитовФормы.Вставить("Сотрудник");
	СтруктураРеквизитовФормы.Вставить("ФизЛицо");
	СтруктураРеквизитовФормы.Вставить("ДатаПодписи");
	
КонецПроцедуры // СформироватьСтруктуруРеквизитовФормы

&НаСервере
Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	
	Перем ИсходноеКоличествоСтрокГруппы;
	
	СтруктураРеквизитовФормы.мБезОткрытияФормы = БезОткрытияФормы;			
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	
	мСтруктураЗначений = Новый Структура;
	мСтруктураСохраняемыхПоказателей = Новый Структура;
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		
		Если СтруктураРеквизитовФормы.мСкопированаФорма <> Неопределено Тогда // документ скопирован
			ВосстановитьСохраненныеДанные();
		Иначе // новый документ
			ЕдиницаИзмерения          = Перечисления.ПорядкиОкругленияОтчетности.Окр1;
			ТочностьЕдиницыИзмерения  = 2;
			ЯзыкФормирования 		  = "ru";
			ДатаПодписи               = ТекущаяДатаСеанса();
			ВзносыПоПериодуИсчисления = 0;
			ОтчетОбъект.ПоказыватьЗаголовок = "Приложение3";
		КонецЕсли;
		
		Модифицированность = Истина;
		
	Иначе
		ВосстановитьСохраненныеДанные();	
		
		Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда
			СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	//Обход ситуации с повторным открытием основной формы, из-за ТД
	МассивРеквизитовФормы = ЭтотОбъект.ПолучитьРеквизиты();
	Для Счетчик = 0 По МассивРеквизитовФормы.Количество()-1 Цикл		
		РеквизитФормы = МассивРеквизитовФормы[Счетчик];
		Если РеквизитФормы.ТипЗначения.СодержитТип(Тип("ТабличныйДокумент")) Тогда
			ЭтотОбъект[РеквизитФормы.Имя] = ЭтотОбъект[РеквизитФормы.Имя];	
		КонецЕсли;
	КонецЦикла;	  	
	
	Если СтруктураРеквизитовФормы.Свойство("СформироватьФормуОтчетаАвтоматически") И СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически <> Неопределено Тогда		
		ЗаполнитьАвтоНаСервере();		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОтчетномПериоде() Экспорт

	СтрПериодОтчета = ПредставлениеПериода(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета, СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ФП = истина");
	
	Если СтруктураРеквизитовФормы.мПериодичность = Перечисления.Периодичность.Квартал Тогда
		СтруктураРеквизитовФормы.мКвартал = Лев(СтрПериодОтчета, 1);
		СтруктураРеквизитовФормы.мМесяц = Неопределено;
	Иначе
		СтруктураРеквизитовФормы.мМесяц = Месяц(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
		СтруктураРеквизитовФормы.мКвартал = Неопределено;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мГод = Лев(Прав(СтрПериодОтчета, 7), 4);
		
КонецПроцедуры 


////////////////////////////////////////////////////////////////////////////////
// Сохранение и восстановление данных

&НаСервере
Процедура СохранитьДанныеНаСервере()
	
	Перем ТаблицаСтраниц;

	Если Не ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСохраненныйДок) ИЛИ (СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено) Тогда
		// создаем новый документ вида РегламентированныйОтчет
		ДокументРеглОтчет = Документы.РегламентированныйОтчет.СоздатьДокумент();
				
		ДокументРеглОтчет.Дата = ТекущаяДатаСеанса();
		ДокументРеглОтчет.УстановитьВремя();
		
	Иначе
		ДокументРеглОтчет = СтруктураРеквизитовФормы.мСохраненныйДок.ПолучитьОбъект();
	КонецЕсли;
	
	ИмяТекТабличногоПоля = "ФормаОтчетаПолеТабличногоДокументаСтраница1";
	ТекТабличноеПоле     = ЭтотОбъект[ИмяТекТабличногоПоля];

	// Сформируем комментарий, если не указан
	Если НЕ ЗначениеЗаполнено(Комментарий) Тогда
		Комментарий = ФизЛицо.Наименование;
	КонецЕсли;

	// установим текущие значения реквизитов документа
	МетаданныеОтчета = РеквизитФормыВЗначение("ОтчетОбъект"). Метаданные();
	ДокументРеглОтчет.ИсточникОтчета           = МетаданныеОтчета.Имя;
	ДокументРеглОтчет.НаименованиеОтчета       = МетаданныеОтчета.Формы.ОсновнаяФорма.Представление();
	ДокументРеглОтчет.ДатаНачала               = СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета;
	ДокументРеглОтчет.ДатаОкончания            = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	ДокументРеглОтчет.Периодичность            = СтруктураРеквизитовФормы.мПериодичность;  
	ДокументРеглОтчет.ДатаПодписи              = ДатаПодписи;
	ДокументРеглОтчет.ВыбраннаяФорма           = мВыбраннаяФорма;
	ДокументРеглОтчет.Организация              = Налогоплательщик;
	ДокументРеглОтчет.ЕдиницаИзмерения         = ЕдиницаИзмерения;
	ДокументРеглОтчет.ТочностьЕдиницыИзмерения = ТочностьЕдиницыИзмерения;
	ДокументРеглОтчет.ВидОтчетности            = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	ДокументРеглОтчет.Комментарий              = Комментарий;
		
	// обработка номера документа для возможности автонумерации
	ПрефиксДок 	= ДокументРеглОтчет.ПолучитьПрефиксДокументаОтчета();
	НомерДок 	= СокрЛП(ПолучитьЗначениеОбласти(ТекТабличноеПоле, "Номер", ""));
	Если ЗначениеЗаполнено(НомерДок) Тогда	
		НомерДок              = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(НомерДок, ДокументРеглОтчет.Метаданные().ДлинаНомера - СтрДлина(ПрефиксДок), "0", 0);
		ДокументРеглОтчет.Номер = ПрефиксДок + НомерДок;
	Иначе
		ДокументРеглОтчет.УстановитьНовыйНомер(ПрефиксДок);
	КонецЕсли;
		
	// выведем номер в табличный документ
	НомерДок = ДокументРеглОтчет.Номер;
	Пока СтрДлина(НомерДок) > 0 И Найти("123456789", Лев(НомерДок, 1)) = 0 Цикл
		НомерДок = Сред(НомерДок, 2);
	КонецЦикла;
	УстановитьЗначениеОбласти(ТекТабличноеПоле, "Номер", НомерДок);

	// формируем данные редактируемых ячеек таблицы
	ПоказателиОтчета = Новый Структура();

	ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ТекТабличноеПоле);
	ПоказателиОтчета.Вставить(ИмяТекТабличногоПоля, ПоказателиТекущегоЛиста);

	// формируем список сохранения
	СписокСохранения = Новый Структура();

	// вставляем данные редактируемых ячеек таблицы	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	СписокСохранения.Вставить("СписокСтруктурныхЕдиниц", мСписокСтруктурныхЕдиниц);	
	СписокСохранения.Вставить("ПоказыватьЗаголовок", ОтчетОбъект.ПоказыватьЗаголовок);
	
	// вствляем версию формы
	СписокСохранения.Вставить("ВерсияФормы", "01/10/2016");

	// ФизЛицо
	СписокСохранения.Вставить("ФизЛицо",   ФизЛицо);
	СписокСохранения.Вставить("Сотрудник", Сотрудник);
	
	СписокСохранения.Вставить("ВзносыПоПериодуИсчисления", ВзносыПоПериодуИсчисления);

	// Сохраняем документ
	Если ДокументРеглОтчет <> Неопределено Тогда
						
		ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
		ДокументРеглОтчет.ДанныеОтчета = ХранилищеДанных;
		
		СохранитьОшибка = 1;
		
		// записываем документ, хранящий данные отчета
		Попытка
			ДокументРеглОтчет.Записать();			
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удается сохранить регламентированный отчет! " + ОписаниеОшибки());			
			СохранитьОшибка = 0;
		КонецПопытки;
		
		Если СохранитьОшибка = 1 Тогда
			// всё прошло без ошибок
			Модифицированность = Ложь;
		КонецЕсли;		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мСохраненныйДок = ДокументРеглОтчет.Ссылка;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Записан документ %1 %2'"), ДокументРеглОтчет.Ссылка, ДокументРеглОтчет.Ссылка.Комментарий));	
		
КонецПроцедуры

&НаСервере
Процедура ВосстановитьСохраненныеДанные()
	
	Перем ПоказателиОтчета;
	
	мСохраненныйДок = СтруктураРеквизитовФормы.мСохраненныйДок;

	// восстанавливаем реквизиты отчета
	Налогоплательщик         = мСохраненныйДок.Организация;
	ДатаПодписи              = мСохраненныйДок.ДатаПодписи;
	ЕдиницаИзмерения         = мСохраненныйДок.ЕдиницаИзмерения;
	ТочностьЕдиницыИзмерения = мСохраненныйДок.ТочностьЕдиницыИзмерения;
	Комментарий              = мСохраненныйДок.Комментарий;
	Периодичность			 = мСохраненныйДок.Периодичность;
	
	ДатаПрекращенияВыводаРНН = Константы.ДатаПрекращенияВыводаРННВПервичныхДокументах.Получить();
	ВыводитьРНН = НЕ ЗначениеЗаполнено(ДатаПрекращенияВыводаРНН) ИЛИ ДатаПодписи < ДатаПрекращенияВыводаРНН;

	// восстанавливаем сохраненные данные отчета
	СписокСохранения = мСохраненныйДок.ДанныеОтчета.Получить();

	// восстанавливаем ФизЛицо
	СписокСохранения.Свойство("ФизЛицо", ФизЛицо);
	
	Если СписокСохранения.Свойство("Сотрудник") Тогда
		Сотрудник = СписокСохранения.Сотрудник;		
	Иначе
		// если заполнено ФизЛицо, то определим сотрудника для данного физ.лица
		Если ЗначениеЗаполнено(ФизЛицо) Тогда			
				
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	РаботникиОрганизации.Сотрудник,
			|	РаботникиОрганизации.Организация,
			|	РаботникиОрганизации.Период,
			|	РаботникиОрганизации.ПричинаИзмененияСостояния
			|ИЗ
			|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(
			|		&парамДатаАктуальности, 
			|		Организация = &парамГоловнаяОрганизация
			|		И Сотрудник.ФизЛицо = &парамФизЛицо
			|		И Сотрудник.ВидЗанятости <> &парамВнутреннееСовместительство) КАК РаботникиОрганизации
			|УПОРЯДОЧИТЬ ПО
			|	РаботникиОрганизации.Период УБЫВ, // берем ближайщее к дате отчета назначение
			|	ВЫБОР
			|		КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния = &парамУволен
			|			ТОГДА 2
			|		ИНАЧЕ 1
			|	КОНЕЦ
			|";
			
			ГоловнаяОрганизация = ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(Налогоплательщик);
			Запрос.УстановитьПараметр("парамГоловнаяОрганизация",        ГоловнаяОрганизация);
			Запрос.УстановитьПараметр("парамФизЛицо",                    ФизЛицо);
			Запрос.УстановитьПараметр("парамВнутреннееСовместительство", Перечисления.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство);
			Запрос.УстановитьПараметр("парамУволен",                     Перечисления.ПричиныИзмененияСостояния.Увольнение);
			Запрос.УстановитьПараметр("парамДатаАктуальности",           ТекущаяДата());
                          			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Сотрудник = Выборка.Сотрудник;
			Иначе
				// среди работников организаций такое физлицо не числится, т.е. какой-то получатель разовых выплат
				// сотрудник будет пустым
			КонецЕсли;	      			
			
		КонецЕсли;		
	КонецЕсли;
	
	СписокСохранения.Свойство("ВзносыПоПериодуИсчисления", ВзносыПоПериодуИсчисления);
	
	СформироватьМассивОтчетныхГодов();

	// восстановим сохраненные данные редактируемых ячеек
	СписокСохранения.Свойство( "ПоказателиОтчета", ПоказателиОтчета);
	
	Если СписокСохранения.Свойство("ПоказыватьЗаголовок") Тогда
		ОтчетОбъект.ПоказыватьЗаголовок = СписокСохранения.ПоказыватьЗаголовок;
	Иначе
	    ОтчетОбъект.ПоказыватьЗаголовок = "БезЗаголовка";
	КонецЕсли;
	
	Если ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРасчет") Тогда
		ВывестиВДокумент(ПоказателиОтчета.ПолеТабличногоДокументаРасчет);
	ИначеЕсли ПоказателиОтчета.Свойство("ФормаОтчетаПолеТабличногоДокументаСтраница1") Тогда
		ВывестиВДокумент(ПоказателиОтчета.ФормаОтчетаПолеТабличногоДокументаСтраница1);
	КонецЕсли;
	
	Если СписокСохранения.Свойство("СписокСтруктурныхЕдиниц") Тогда
		мСписокСтруктурныхЕдиниц = СписокСохранения.СписокСтруктурныхЕдиниц;
	Иначе
	    мСписокСтруктурныхЕдиниц.Очистить();
	    мСписокСтруктурныхЕдиниц.Добавить(Налогоплательщик);
	КонецЕсли;
	ПредставлениеСпискаСтруктурныхЕдиниц = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(мСписокСтруктурныхЕдиниц);

	УстановитьФорматВывода();
	Модифицированность = Ложь;
	
	ВыводЗаголовка(ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"]);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьДанныеНаСервере();
		Оповестить("Позиционирование в списке отчетов", СтруктураРеквизитовФормы.мСохраненныйДок);
	КонецЕсли;
	
КонецПроцедуры   

////////////////////////////////////////////////////////////////////////////////
// Печать

&НаКлиенте
Процедура Печать(ВидПечати, НеИзФормыОтчета = Ложь) Экспорт
	
	Если ВсегоСтраниц = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Нет данных для печати'"));
		Возврат;
	КонецЕсли;
    
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1. Формируется печатная форма...'"), ЭтаФорма.Заголовок),,, БиблиотекаКартинок.Печать);
	
	Если НЕ ПечатьНаСервере(ВидПечати) Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтаФорма, ВидПечати,, СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);
	
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере(ВидПечати)
            
    Перем СтруктураГруппы; 
    
    Если НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
        ТекстСообщения = НСтр("ru = 'Перед формированием печатных форм необходимо определить в системе принтер и задать его в качестве используемого по умолчанию!'");            
        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);                
        Возврат Ложь;
    КонецЕсли;
            
	СтруктураРеквизитовФормы.СписокПечатаемыхЛистов.Очистить();
	СтруктураРеквизитовФормы.мПечатныеФормы.Очистить();
    СтруктураРеквизитовФормы.мРежимПечати = Истина;
    
    Если СтруктураРеквизитовФормы.НужноРассчитатьОтчетНаСервере Тогда
        ФормаОтчетаРасчетНаСервере();
    КонецЕсли; 

	ТекТабличноеПоле = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];
	ИдентификаторТекФормы = Новый УникальныйИдентификатор();
	НаименованиеДляЗаписи = "";
	Стр = 1;

	Пока Стр < ВсегоСтраниц + 1  Цикл
		
		ТабДок = Новый ТабличныйДокумент;
		ИмяОбласти = "Страница" + СокрЛП(Стр);
		ТекОбласть = ТекТабличноеПоле.ПолучитьОбласть(ИмяОбласти);
		ТабДок.Вывести(ТекОбласть);
		ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		ТабДок.ЧерноБелаяПечать   = Истина;
		ТабДок.АвтоМасштаб        = Истина;
		
		ВыводЗаголовка(ТабДок);
		
		Если Стр = 1 Тогда 
			НаименованиеДляЗаписи = НСтр("ru = 'Справка о суммах доходов и ОПВ. '") + ТабДок.Области.ПериодС.Текст + НСтр("ru = ' по '") + ТабДок.Области.ПериодПО.Текст;
		КонецЕсли;	
		
		ИмяЛиста = НСтр("ru = 'Страница № '") + Строка(Стр);
		Стр = Стр + 1;
		
		Значение = Новый Массив;
		Значение.Добавить(ТабДок);
		Значение.Добавить(ИдентификаторТекФормы);
		Значение.Добавить(НаименованиеДляЗаписи);
		
		СтруктураРеквизитовФормы.мПечатныеФормы.Добавить(Значение, ИмяЛиста);
			
	КонецЦикла;
		
	Для Каждого Эл Из СтруктураРеквизитовФормы.мПечатныеФормы Цикл
				
		Значение = Новый Массив;
		Значение.Добавить(ПоместитьВоВременноеХранилище(Эл.Значение[0], УникальныйИдентификатор));
		Значение.Добавить(Эл.Значение[1]);
		Значение.Добавить(Эл.Значение[2]);
		
		СтруктураРеквизитовФормы.СписокПечатаемыхЛистов.Добавить(Значение, Эл.Представление);			
		
	КонецЦикла;
	
	СтруктураРеквизитовФормы.мПечатныеФормы.Очистить();
        
    Возврат Истина;
    
КонецФункции


&НаСервере
Процедура ЕдиницаИзмеренияПриИзмененииНаСервере()
	
	ПроверитьТочность();
	УстановитьФорматВывода();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ТочностьЕдиницыИзмеренияПриИзмененииНаСервере()
	
	ПроверитьТочность();
	УстановитьФорматВывода();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиСтраниц()
	
// Запоминаем текущий раздел, установленный в дереве разделов отчета.
	Если НЕ Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		ТекущийРаздел = Элементы.РазделыОтчета.ТекущиеДанные.Страницы;		
	КонецЕсли;
	
	Для Каждого ТекФорма ИЗ мСписокФормБезИерархии.ПолучитьЭлементы() Цикл		
			
		ТекущаяСтрокаСписка = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(СписокФормДерева, Новый Структура("КодФормы",ТекФорма.КодФормы));
			
		Если  НЕ ТекущаяСтрокаСписка = Неопределено Тогда
			ТекущаяСтрокаСписка.Выгружать               = ТекФорма.Выгружать;
			ТекущаяСтрокаСписка.ПоказатьСтраницу        = ТекФорма.ПоказатьСтраницу;			
			ТекущаяСтрокаСписка.АвтополучениеИтогов     = ТекФорма.АвтополучениеИтогов;  
			ТекущаяСтрокаСписка.АвтоЗаполнение          = ТекФорма.АвтоЗаполнение;  
		КонецЕсли;		   
		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("СписокФормДерева",СписокФормДерева);
	
	ДопПараметры = Новый Структура("ТекущийРаздел",ТекущийРаздел);
	
	ОповещениеПослеЗакрытияФормы = Новый ОписаниеОповещения("ПослеЗакрытияФормыНастройкиСтраниц",ЭтотОбъект,ДопПараметры);
	
	ОткрытьФорму("Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.НастройкиОтчета",ПараметрыФормы, ЭтаФорма,,,,ОповещениеПослеЗакрытияФормы,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыНастройкиСтраниц(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КопироватьДанныеФормы(РезультатЗакрытия, СписокФормДерева);
	
	Модифицированность = Истина;
	
	//обновим мСписокФормБезИерархии
	Для Каждого СтрокаФормы ИЗ мСписокФормБезИерархии.ПолучитьЭлементы() Цикл		
			
		ТекущаяСтрокаСписка = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(СписокФормДерева, Новый Структура("КодФормы",СтрокаФормы.КодФормы));
			
		Если  НЕ ТекущаяСтрокаСписка = Неопределено Тогда
			СтрокаФормы.Выгружать               = ТекущаяСтрокаСписка.Выгружать;
			СтрокаФормы.ПоказатьСтраницу        = ТекущаяСтрокаСписка.ПоказатьСтраницу;			
			СтрокаФормы.АвтополучениеИтогов     = ТекущаяСтрокаСписка.АвтополучениеИтогов;  
			СтрокаФормы.АвтоЗаполнение          = ТекущаяСтрокаСписка.АвтоЗаполнение;  
		КонецЕсли;			
	КонецЦикла; 	

	Если ДополнительныеПараметры.Свойство("ТекущийРаздел") И НЕ ДополнительныеПараметры.ТекущийРаздел = Неопределено Тогда  		
		// Пытаемся найти ранее запомненный раздел.
		НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиСтрокуДерева(РазделыОтчета, Новый Структура("СписокФормДереваСтраницы",ДополнительныеПараметры.ТекущийРаздел));
		// В случае успешного поиска, устанавливаем курсор на найденный раздел.
		Если НЕ НайденнаяСтрока = Неопределено Тогда
			Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
		КонецЕсли;			
	КонецЕсли;  	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочие

&НаСервере
// Процедура управляет представлением суммовых (денежных) показателей
// табличного документа при изменении размерности.
//
Процедура УстановитьФорматВывода()
	
	Перем СтруктураГруппы;
	Перем РазмерностьПрописью;
	
	Если ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1 Тогда
		мДелитель = 1;
		РазмерностьПрописью = НСтр("ru = ""тенге""; kz = ""теңге""", "ru");				
		мСтрокаФормата = "ЧЦ = 15; ЧДЦ = " + ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 0; ЧРГ= ";
		
	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000 Тогда
		мДелитель = 1000;
		РазмерностьПрописью = НСтр("ru = ""тысяч тенге""; kz = ""мың теңге""", "ru");				
		мСтрокаФормата = "ЧЦ = 15; ЧДЦ = " + ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 3; ЧРГ= ";
		
	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000000 Тогда
		мДелитель = 1000000;
		РазмерностьПрописью = НСтр("ru = ""млн. тенге""; kz = ""млн. теңге""", "ru");				
		мСтрокаФормата = "ЧЦ = 15; ЧДЦ = " + ТочностьЕдиницыИзмерения + "; ЧРД=,; ЧН=-; ЧС = 6; ЧРГ= ";
		
	КонецЕсли;
	
	ТекТабличныйДокумент = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];

	Для Каждого ОбластьТаблицы Из ТекТабличныйДокумент.Области Цикл
		// по областям каждой страницы
		Если Не ОбластьТаблицы.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбластьТаблицы.СодержитЗначение Тогда
			Если СокрЛП(ОбластьТаблицы.ТипЗначения) = "Число" Тогда
				ОбластьТаблицы.Формат = мСтрокаФормата;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	
	УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ЕдиницаИзмерения", РазмерностьПрописью);
	
КонецПроцедуры // УстановитьФорматВывода()

&НаКлиенте
Процедура ФормаОтчетаРасчет()

	ФормаОтчетаРасчетНаСервере();	
			    	                	
КонецПроцедуры   //ФормаРасчет()

&НаСервере
Процедура ФормаОтчетаРасчетНаСервере()
	
	ТекТабличныйДокумент = ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"];
	СканируемыеОбласти   = ТекТабличныйДокумент.Области;
	
	ИтогоСумма = 0;
	
	Для Каждого Год Из мМассивОтчетныхГодов Цикл
		
		СтрГод = Формат(Год.Значение, "ЧГ=; ЧН=");
		ИтогоСуммаЗаработка 	= 0;
        ИтогоОПВНачислено 		= 0;
		ИтогоОПВПеречислено 	= 0;
        ИтогоОПВПени			= 0;
		
		Для Каждого Область Из СканируемыеОбласти Цикл
			
			// сумма заработка
			Если Найти(Область.Имя, "СуммаЗаработка_" + СтрГод + "_") <> 0 Тогда
				СуммаЗаработка 		= Область.Значение;
				ИтогоСуммаЗаработка	= ИтогоСуммаЗаработка + СуммаЗаработка;
			КонецЕсли;
			
			// ОПВ начислено
			Если Найти(Область.Имя, "ОПВНачислено_" + СтрГод + "_") <> 0 Тогда
				ОПВНачислено 		= Область.Значение;
				ИтогоОПВНачислено	= ИтогоОПВНачислено + ОПВНачислено;
			КонецЕсли;
			
			// ОПВ перечислено
			Если Найти(Область.Имя, "ОПВПеречислено_" + СтрГод + "_") <> 0 Тогда
				ОПВПеречислено 		= Область.Значение;
				ИтогоОПВПеречислено	= ИтогоОПВПеречислено + ОПВПеречислено;
			КонецЕсли;
			
			// ОПВ пени
			Если Найти(Область.Имя, "ОПВПени_" + СтрГод + "_") <> 0 Тогда
				ОПВПени 		= Область.Значение;
				ИтогоОПВПени	= ИтогоОПВПени + ОПВПени;
			КонецЕсли;

		КонецЦикла;
		
		УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоСуммаЗаработка_" + СтрГод, ИтогоСуммаЗаработка);
		УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоОПВНачислено_"   + СтрГод, ИтогоОПВНачислено);
		УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоОПВПеречислено_" + СтрГод, ИтогоОПВПеречислено);
        УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоОПВПени_"        + СтрГод, ИтогоОПВПени);
		
		ИтогоСумма = ИтогоСумма + ИтогоСуммаЗаработка;
		
	КонецЦикла;
	
	УстановитьЗначениеОбласти(ТекТабличныйДокумент, "ИтогоСумма", ОбщегоНазначенияБКВызовСервера.СформироватьСуммуПрописью(ИтогоСумма, Константы.ВалютаРегламентированногоУчета.Получить()));
	
КонецПроцедуры

&НаСервере
Функция ФормаРассчитатьДанныеФормы(ВыдаватьСообщения = Ложь)
	
	РасчетНаРегистрах = Метаданные.РегистрыРасчета.Найти("ОсновныеНачисленияРаботниковОрганизаций") <> Неопределено;
	
	// Создание запроса и установка всех необходимых параметров
	Запрос = Новый Запрос;
	Если РасчетНаРегистрах Тогда
		Запрос.УстановитьПараметр("парамГоловнаяОрганизация", ОбщегоНазначенияБК.ГоловнаяОрганизация(Налогоплательщик));
	Иначе
		Запрос.УстановитьПараметр("парамГоловнаяОрганизация", ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(Налогоплательщик));
	КонецЕсли;
	Запрос.УстановитьПараметр("парамОрганизация",             Налогоплательщик);
	Запрос.УстановитьПараметр("парамСписокСтруктурныхЕдиниц", мСписокСтруктурныхЕдиниц);
	Запрос.УстановитьПараметр("парамФизЛицо",                 ФизЛицо);
	Запрос.УстановитьПараметр("парамДатаАктуальности",        ДатаПодписи);
	Запрос.УстановитьПараметр("парамПериодС",                 НачалоМесяца(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("парамПериодПо",                КонецМесяца(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета));	
	
	// ---------------------------------------------------------------------------
	// Тексты запросов
	//

	//-----------------------------------------------------------------------------
	// ВЫБОРКА СВЕДЕНИЙ О ФИЗЛИЦЕ 
	// 	
	
	ТекстЗапроса = 
	"ВЫБРАТЬ 
	|	ЕСТЬNULL(ФИОФизЛица.Фамилия + "" "" + ФИОФизЛица.Имя + "" "" + ФИОФизЛица.Отчество, ДанныеОФизЛице.Наименование) КАК ФИОРаботника,
	|	ДанныеОФизЛице.РНН КАК РННРаботника,
	|	ДанныеОФизЛице.ИдентификационныйКодЛичности КАК ИИНРаботника,
	|	ДанныеОФизЛице.СИК КАК СИКРаботника,
	|	РаботникиОрганизации.Сотрудник.Код КАК ТабельныйНомер,
	|	РаботникиОрганизации.ПодразделениеОрганизации.Наименование КАК ПодразделениеОрганизации,
	|	РаботникиОрганизации.Должность.Наименование КАК Должность
	|ПОМЕСТИТЬ ВТ_ДанныеОФизЛице
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ДанныеОФизЛице
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|					&парамДатаАктуальности, 
	|					ФизЛицо = &парамФизЛицо) КАК ФИОФизЛица
	|		ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|							МАКСИМУМ(ВЫБОР
	|										КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|											ТОГДА ДОБАВИТЬКДАТЕ(РаботникиОрганизации.Период, ДЕНЬ, -1)
	|										ИНАЧЕ РаботникиОрганизации.Период
	|									 КОНЕЦ) КАК Период
	|						ИЗ
	|							РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|									&парамДатаАктуальности, 
	|									Сотрудник.ФизЛицо = &парамФизЛицо И
	|									Организация = &парамГоловнаяОрганизация И
	|									(Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство))) КАК РаботникиОрганизации
	|					) КАК РаботникиОрганизацииСрезПоследних
	|		ПО ИСТИНА
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ПО РаботникиОрганизацииСрезПоследних.Период = ВЫБОР
	|														КОГДА РаботникиОрганизации.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|															ТОГДА ДОБАВИТЬКДАТЕ(РаботникиОрганизации.Период, ДЕНЬ, -1)
	|														ИНАЧЕ РаботникиОрганизации.Период
	|													  КОНЕЦ
	|			И РаботникиОрганизации.Сотрудник.ФизЛицо = &парамФизЛицо
	|			И РаботникиОрганизации.Организация = &парамГоловнаяОрганизация
	|			И (РаботникиОрганизации.Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство))
	|
	|ГДЕ
	|	ДанныеОФизЛице.Ссылка = &парамФизЛицо
	|;
	|
	|/////////////////////////////////////////
	|ВЫБРАТЬ 
	| 	ПлатежноеПоручениеИсходящееПеречислениеПенсионныхВзносов.Документ КАК Ведомость,
	| 	ПлатежноеПоручениеИсходящееПеречислениеПенсионныхВзносов.Ссылка КАК Регистратор
	|ПОМЕСТИТЬ ВТ_ППИ
	|ИЗ
	| 	РегистрНакопления.ОПВРасчетыСФондами.Обороты(
	|												,
	|												,
	|												Регистратор,
	|												ФизЛицо = &парамФизЛицо 
	|													И Организация В (&парамСписокСтруктурныхЕдиниц) 
	|													И (МесяцНалоговогоПериода МЕЖДУ &парамПериодС И &парамПериодПо)) КАК ОПВРасчетыСФондамиОбороты
	|  		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручениеИсходящее.ПеречислениеПенсионныхВзносов КАК ПлатежноеПоручениеИсходящееПеречислениеПенсионныхВзносов
	|  		ПО ОПВРасчетыСФондамиОбороты.Регистратор = ПлатежноеПоручениеИсходящееПеречислениеПенсионныхВзносов.Ссылка
	|
	|;
	|
	|/////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлатежноеПоручениеВходящееВозвратПенсионныхВзносов.Документ КАК Ведомость,
	|	ПлатежноеПоручениеВходящееВозвратПенсионныхВзносов.Ссылка КАК Регистратор
	|ПОМЕСТИТЬ ВТ_ППВ
	|ИЗ
	|	РегистрНакопления.ОПВРасчетыСФондами.Обороты(
	|												,
	|												,
	|												Регистратор,
	|												ФизЛицо = &парамФизЛицо 
	|													И Организация В (&парамСписокСтруктурныхЕдиниц) 
	|													И (МесяцНалоговогоПериода МЕЖДУ &парамПериодС И &парамПериодПо)) КАК ОПВРасчетыСФондамиОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручениеВходящее.ВозвратПенсионныхВзносов КАК ПлатежноеПоручениеВходящееВозвратПенсионныхВзносов
	|		ПО ОПВРасчетыСФондамиОбороты.Регистратор = ПлатежноеПоручениеВходящееВозвратПенсионныхВзносов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансовыйОтчетПеречислениеПенсионныхВзносов.Документ КАК Ведомость,
	|	АвансовыйОтчетПеречислениеПенсионныхВзносов.Ссылка КАК Регистратор
	|ПОМЕСТИТЬ ВТ_АО
	|ИЗ
	|	РегистрНакопления.ОПВРасчетыСФондами.Обороты(
	|												,
	|												,
	|												Регистратор,
	|												ФизЛицо = &парамФизЛицо
	|													И Организация В (&парамСписокСтруктурныхЕдиниц)
	|													И (МесяцНалоговогоПериода МЕЖДУ &парамПериодС И &парамПериодПо)) КАК ОПВРасчетыСФондамиОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет.ПеречислениеПенсионныхВзносов КАК АвансовыйОтчетПеречислениеПенсионныхВзносов
	|		ПО ОПВРасчетыСФондамиОбороты.Регистратор = АвансовыйОтчетПеречислениеПенсионныхВзносов.Ссылка
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Запрос_Регистратор.РегистраторНомер,
	|	Запрос_Регистратор.РегистраторДата,
	|	Запрос_Регистратор.Регистратор
	|ПОМЕСТИТЬ ВТ_Регистратор
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ППИ.Регистратор.Номер КАК РегистраторНомер,
	|		ВТ_ППИ.Регистратор.Дата КАК РегистраторДата,
	|		ВТ_ППИ.Регистратор КАК Регистратор
	|	ИЗ
	|		ВТ_ППИ КАК ВТ_ППИ
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ППВ КАК ВТ_ППВ
	|			ПО ВТ_ППИ.Ведомость = ВТ_ППВ.Ведомость.ДокументОснование
	|	ГДЕ
	|		ВТ_ППВ.Ведомость ЕСТЬ NULL 
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_АО.Регистратор.Номер,
	|		ВТ_АО.Регистратор.Дата,
	|		ВТ_АО.Регистратор
	|	ИЗ
	|		ВТ_АО КАК ВТ_АО) КАК Запрос_Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Год(ДоходыИВзносы.Период) КАК Год,
	|	ДоходыИВзносы.Период КАК Период,	
	|	СУММА(ДоходыИВзносы.СуммаЗаработка) КАК СуммаЗаработка,
	|	СУММА(ДоходыИВзносы.ОПВНачислено) КАК ОПВНачислено,
	|	СУММА(ДоходыИВзносы.ОПВПеречислено) КАК ОПВПеречислено,
	|	СУММА(ДоходыиВзносы.ОПВПени) КАК ОПВПени,
	|	ДоходыИВзносы.ДатаПеречисления КАК ДатаПеречисления,
	|	ДоходыИВзносы.НомерПоручения КАК НомерПоручения,
	|	ДоходыИВзносы.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода
	|ПОМЕСТИТЬ ВТ_ДоходыИВзносы
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ОПВСведенияОДоходах.Период, МЕСЯЦ) КАК Период,
	|		ВЫБОР
	|			КОГДА ОПВСведенияОДоходах.СпособНалогообложения <> ЗНАЧЕНИЕ(Справочник.СпособыНалогообложенияДоходов.НеОблагаетсяЦеликом)
	|				ТОГДА ОПВСведенияОДоходах.СуммаДохода - ОПВСведенияОДоходах.СуммаВычета
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаЗаработка,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВНачислено,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВПеречислено,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВПени,
	|		NULL КАК ДатаПеречисления,
	|		NULL КАК НомерПоручения,
	|		NULL КАК МесяцНалоговогоПериода
	|	ИЗ
	|		РегистрНакопления.ОПВСведенияОДоходах КАК ОПВСведенияОДоходах
	|	ГДЕ
	|		ОПВСведенияОДоходах.ФизЛицо = &парамФизЛицо
	|		И ОПВСведенияОДоходах.Организация В(&парамСписокСтруктурныхЕдиниц)
	|		И ОПВСведенияОДоходах.Период МЕЖДУ &парамПериодС И &парамПериодПо
	|		И ОПВСведенияОДоходах.СпособНалогообложения <> ЗНАЧЕНИЕ(Справочник.СпособыНалогообложенияДоходов.НеОблагаетсяЦеликом)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ОПВРасчетыСФондами.Период, МЕСЯЦ) КАК Период,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СуммаЗаработка,
	|		ОПВРасчетыСФондами.Взнос КАК ОПВНачислено,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВПеречислено,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВПени,
	|       NULL КАК ДатаПеречисления,
	|		NULL КАК НомерПоручения,
	|		NULL КАК МесяцНалоговогоПериода
	|	ИЗ
	|		РегистрНакопления.ОПВРасчетыСФондами КАК ОПВРасчетыСФондами
	|	ГДЕ
	|		ОПВРасчетыСФондами.ФизЛицо = &парамФизЛицо
	|		И ОПВРасчетыСФондами.Организация В (&парамСписокСтруктурныхЕдиниц)
	|		И ОПВРасчетыСФондами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
	|		И ОПВРасчетыСФондами.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.РасчетыСБюджетомФондамиВидСтроки.Исчисление)
	|		И ОПВРасчетыСФондами.Период МЕЖДУ &парамПериодС И &парамПериодПо
	|                                               
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		" + ?(ВзносыПоПериодуИсчисления = 1, 
			"НАЧАЛОПЕРИОДА(ОПВРасчетыСФондами.МесяцНалоговогоПериода, МЕСЯЦ)", 
			"НАЧАЛОПЕРИОДА(ОПВРасчетыСФондами.Период, МЕСЯЦ)") + " КАК Период,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СуммаЗаработка,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВНачислено,
	|		ОПВРасчетыСФондами.Взнос КАК ОПВПеречислено,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВПени,
	|		ВТ_Регистратор.РегистраторДата КАК ДатаПеречисления,
	|		ВТ_Регистратор.РегистраторНомер КАК НомерПоручения,
	|		ОПВРасчетыСФондами.МесяцНалоговогоПериода
	|	ИЗ
	|		РегистрНакопления.ОПВРасчетыСФондами КАК ОПВРасчетыСФондами
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистратор КАК ВТ_Регистратор
	|			ПО ОПВРасчетыСФондами.Регистратор = ВТ_Регистратор.Регистратор
	|	ГДЕ
	|		ОПВРасчетыСФондами.ФизЛицо = &парамФизЛицо
	|		И ОПВРасчетыСФондами.Организация В (&парамСписокСтруктурныхЕдиниц)
	|		И (ОПВРасчетыСФондами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ОПВРасчетыСФондами.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.РасчетыСБюджетомФондамиВидСтроки.Перечисление)
	|		   ИЛИ ОПВРасчетыСФондами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			   И ОПВРасчетыСФондами.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.РасчетыСБюджетомФондамиВидСтроки.Возврат))	
	|      " + ?(ВзносыПоПериодуИсчисления,
		    "И ОПВРасчетыСФондами.МесяцНалоговогоПериода МЕЖДУ &парамПериодС И &парамПериодПо", 
			"И ОПВРасчетыСФондами.Период МЕЖДУ &парамПериодС И &парамПериодПо") + "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ОПВРасчетыСФондами.Период, МЕСЯЦ) КАК Период,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СуммаЗаработка,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВНачислено,
	|		ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК ОПВПеречислено,
	|		ОПВРасчетыСФондами.Взнос КАК ОПВПени,
	|       NULL КАК ДатаПеречисления,
	|		NULL КАК НомерПоручения,
	|		NULL КАК МесяцНалоговогоПериода
	|	ИЗ
	|		РегистрНакопления.ОПВРасчетыСФондами КАК ОПВРасчетыСФондами
	|	ГДЕ
	|		ОПВРасчетыСФондами.ФизЛицо = &парамФизЛицо
	|		И ОПВРасчетыСФондами.Организация В (&парамСписокСтруктурныхЕдиниц)
	|		И ОПВРасчетыСФондами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (ОПВРасчетыСФондами.ВидПлатежа = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.ПениСам)
	|			ИЛИ ОПВРасчетыСФондами.ВидПлатежа = ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.ПениАкт))
	|		И ОПВРасчетыСФондами.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.РасчетыСБюджетомФондамиВидСтроки.Исчисление)
	|		И ОПВРасчетыСФондами.Период МЕЖДУ &парамПериодС И &парамПериодПо)КАК ДоходыИВзносы
	|
	| СГРУППИРОВАТЬ ПО
	|	Год(ДоходыИВзносы.Период),
	|	ДоходыИВзносы.Период,	
	|	ДоходыИВзносы.ДатаПеречисления,
	|	ДоходыИВзносы.НомерПоручения,
	|	ДоходыИВзносы.МесяцНалоговогоПериода
	|;
	|
	|/////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоходыИВзносы.Год,
	|	ДоходыИВзносы.Период,
	|	ДанныеОФизЛице.ТабельныйНомер,
	|	ДанныеОФизЛице.ФИОРаботника,
	|	ДанныеОФизЛице.РННРаботника,
	|	ДанныеОФизЛице.СИКРаботника,
	|	ДанныеОФизЛице.ИИНРаботника,
	|	ВЫРАЗИТЬ(ДанныеОбОрганизации.НаименованиеПолное КАК СТРОКА(1000)) КАК НаименованиеОрганизации,
	|	ДанныеОбОрганизации.РНН КАК РННОрганизации,
	|	ДанныеОбОрганизации.ИдентификационныйНомер КАК БИНОрганизации,
	|	ВЫРАЗИТЬ(КонтактнаяИнформацияЮрАдрес.Представление КАК СТРОКА(1000)) КАК ЮрАдресОрганизации,
	|	ВЫБОР
	|		КОГДА ФИОФизЛицРуководитель.ФизЛицо ЕСТЬ NULL 
	|			ТОГДА ОтветственныеЛицаРуководитель.ФизическоеЛицо.Наименование
	|		ИНАЧЕ ФИОФизЛицРуководитель.Фамилия + ВЫБОР
	|				КОГДА ФИОФизЛицРуководитель.Имя <> """"
	|					ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицРуководитель.Имя, 1, 1) + "".""
	|				ИНАЧЕ """"
	|			КОНЕЦ + ВЫБОР
	|				КОГДА ФИОФизЛицРуководитель.Отчество <> """"
	|					ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицРуководитель.Отчество, 1, 1) + "".""
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ КАК ФИОРуководителя,
	|	ОтветственныеЛицаРуководитель.Должность.Наименование КАК ДолжностьРуководителя,
	|	ВЫБОР
	|		КОГДА ФИОФизЛицГлБух.ФизЛицо ЕСТЬ NULL 
	|			ТОГДА ОтветственныеЛицаГлБух.ФизическоеЛицо.Наименование
	|		ИНАЧЕ ФИОФизЛицГлБух.Фамилия + ВЫБОР
	|				КОГДА ФИОФизЛицГлБух.Имя <> """"
	|					ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицГлБух.Имя, 1, 1) + "".""
	|				ИНАЧЕ """"
	|			КОНЕЦ + ВЫБОР
	|				КОГДА ФИОФизЛицГлБух.Отчество <> """"
	|					ТОГДА "" "" + ПОДСТРОКА(ФИОФизЛицГлБух.Отчество, 1, 1) + "".""
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ КАК ФИОГлБухгалтера,
	|	ВЫБОР
	|		КОГДА ОтветственныеЛицаГлБух.Должность.Ссылка ЕСТЬ NULL 
	|			ТОГДА ""Главный бухгалтер""
	|		ИНАЧЕ ОтветственныеЛицаГлБух.Должность.Наименование
	|	КОНЕЦ КАК ДолжностьГлБухгалтера,
	|	ДоходыИВзносы.СуммаЗаработка,
	|	ДоходыИВзносы.ОПВНачислено,
	|	ДоходыИВзносы.ОПВПеречислено,
	|	ДоходыИВзносы.ОПВПени,
	|	ДоходыИВзносы.ДатаПеречисления,
	|	ДоходыИВзносы.НомерПоручения,
	|	ДоходыИВзносы.МесяцНалоговогоПериода
	|ИЗ
	|	ВТ_ДоходыИВзносы КАК ДоходыИВзносы
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеОФизЛице КАК ДанныеОФизЛице
	|		ПО (ИСТИНА)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ДанныеОбОрганизации
	|		ПО (ДанныеОбОрганизации.Ссылка = &парамОрганизация)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформацияЮрАдрес
	|		ПО (КонтактнаяИнформацияЮрАдрес.Объект = &парамОрганизация)
	|			И (КонтактнаяИнформацияЮрАдрес.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации))
	|			И (КонтактнаяИнформацияЮрАдрес.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|				&парамДатаАктуальности,
	|				СтруктурнаяЕдиница = &парамОрганизация
	|					И ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)) КАК ОтветственныеЛицаРуководитель
	|		ПО (ИСТИНА)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&парамДатаАктуальности, ) КАК ФИОФизЛицРуководитель
	|		ПО (ОтветственныеЛицаРуководитель.ФизическоеЛицо = ФИОФизЛицРуководитель.ФизЛицо)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|				&парамДатаАктуальности,
	|				СтруктурнаяЕдиница = &парамОрганизация
	|					И ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)) КАК ОтветственныеЛицаГлБух
	|		ПО (ИСТИНА)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&парамДатаАктуальности, ) КАК ФИОФизЛицГлБух
	|		ПО (ОтветственныеЛицаГлБух.ФизическоеЛицо = ФИОФизЛицГлБух.ФизЛицо)
	| 
	| УПОРЯДОЧИТЬ ПО 
	| ДоходыИВзносы.Год, 
	| ДоходыИВзносы.Период";
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
		     	
	Возврат Результат;
	
КонецФункции // ФормаРассчитатьДанныеФормы()

&НаСервере
Процедура ЗаполнитьАвтоНаСервере()
		
	Выборка = ФормаРассчитатьДанныеФормы().Выбрать();
   
	// Сохраняем данные в структуру данные в поле табличного документа
	СписокСохранения = Новый Структура();
	
	НомерСтроки 			= 0;
	СтрНомерСтроки 			= Формат(НомерСтроки, "ЧГ=");
	ИтогоСумма				= 0;
	СуммаЗаработка 			= 0;
	ОПВНачислено 			= 0;
	ОПВПеречислено 			= 0;
	ОПВПени 				= 0;
	РеквизитыДокументов 	= "";
	ИтогоСуммаЗаработка 	= 0;
	ИтогоОПВНачислено		= 0;
	ИтогоОПВПеречислено		= 0;
	ИтогоОПВПени			= 0;
	ТекущийПериод 			= Дата(1,1,1);
	ТекущийГод 				= 0;

	ДатаПрекращенияВыводаРНН = Константы.ДатаПрекращенияВыводаРННВПервичныхДокументах.Получить();
	ВыводитьРНН = НЕ ЗначениеЗаполнено(ДатаПрекращенияВыводаРНН) ИЛИ ДатаПодписи < ДатаПрекращенияВыводаРНН;
	
	мДатаНачалаПериодаОтчета = СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета;
	мДатаКонцаПериодаОтчета  = СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Период <> ТекущийПериод Тогда
						
			// Если первая строка, то заполняем реквизиты шапки и подвала
			Если НомерСтроки = 0 и ТекущийГод = 0 Тогда
				
				// период отчета
				СписокСохранения.Вставить("ПериодС",  мДатаНачалаПериодаОтчета);
				СписокСохранения.Вставить("ПериодПо", мДатаКонцаПериодаОтчета);
				
				// Реквизиты организации
				РеквизитыОрганизации = СокрЛП(Выборка.НаименованиеОрганизации);
				Если ЗначениеЗаполнено(Выборка.ЮрАдресОрганизации) Тогда
					РеквизитыОрганизации = РеквизитыОрганизации + ?(ЗначениеЗаполнено(РеквизитыОрганизации), "; ", "") + СокрЛП(Выборка.ЮрАдресОрганизации);
				КонецЕсли;
				
				Если ВыводитьРНН Тогда
					Если ЗначениеЗаполнено(Выборка.РННОрганизации) Тогда 
						РеквизитыОрганизации = РеквизитыОрганизации + ?(ЗначениеЗаполнено(РеквизитыОрганизации), "; ", "") + "РНН: " + Выборка.РННОрганизации;
					КонецЕсли;
					СписокСохранения.Вставить("РеквизитыОрганизации",   РеквизитыОрганизации);
				Иначе 
					Если ЗначениеЗаполнено(Выборка.БИНОрганизации) Тогда 
						РеквизитыОрганизации = РеквизитыОрганизации + ?(ЗначениеЗаполнено(РеквизитыОрганизации), "; ", "") + "БИН: " + Выборка.БИНОрганизации;
					КонецЕсли;
					СписокСохранения.Вставить("РеквизитыОрганизацииБИН", РеквизитыОрганизации);
				КонецЕсли;	
		
				СписокСохранения.Вставить("ДатаПодписи", ДатаПодписи);
				
				// Реквизиты физлица
				ФИОРаботника = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Выборка.ФИОРаботника, " ",, Истина);; 
				СписокСохранения.Вставить("Фамилия",  ?(ФИОРаботника.Количество() > 0, ФИОРаботника[0], ""));
				СписокСохранения.Вставить("Имя",      ?(ФИОРаботника.Количество() > 1, ФИОРаботника[1], ""));
				СписокСохранения.Вставить("Отчество", ?(ФИОРаботника.Количество() > 2, ФИОРаботника[2], ""));
				
				Если ВыводитьРНН Тогда
					СписокСохранения.Вставить("РННРаботника", Выборка.РННРаботника); 
					СписокСохранения.Вставить("СИКРаботника", Выборка.СИКРаботника); 
				Иначе 
					СписокСохранения.Вставить("ИИНРаботника", Выборка.ИИНРаботника); 
				КонецЕсли;
				
				СписокСохранения.Вставить("ТабельныйНомер" + ?(ВыводитьРНН, "", "ИИН"), Выборка.ТабельныйНомер); 
				
				// Руководитель организации
				СписокСохранения.Вставить("ФИОРуководителя",       Выборка.ФИОРуководителя);
				СписокСохранения.Вставить("ДолжностьРуководителя", Выборка.ДолжностьРуководителя);
				
				// Главный бухгалтер
				СписокСохранения.Вставить("ФИОГлБухгалтера",       Выборка.ФИОГлБухгалтера);
				СписокСохранения.Вставить("ДолжностьГлБухгалтера", Выборка.ДолжностьГлБухгалтера);
				
				ТекущийГод = Выборка.Год;
				
			КонецЕсли;
			
			Если НомерСтроки > 0 Тогда
				
				ИтогоСуммаЗаработка = ИтогоСуммаЗаработка + СуммаЗаработка;
				ИтогоОПВНачислено	= ИтогоОПВНачислено + ОПВНачислено;
				ИтогоОПВПеречислено	= ИтогоОПВПеречислено + ОПВПеречислено;
				ИтогоОПВПени		= ИтогоОПВПени + ОПВПени;
				
				СтрГод = Формат(ТекущийГод, "ЧГ=");
				
				СписокСохранения.Вставить("Период_"              + СтрГод + "_" + СтрНомерСтроки, ТекущийПериод);
				СписокСохранения.Вставить("СуммаЗаработка_"      + СтрГод + "_" + СтрНомерСтроки, СуммаЗаработка);
				СписокСохранения.Вставить("ОПВНачислено_"        + СтрГод + "_" + СтрНомерСтроки, ОПВНачислено);
				СписокСохранения.Вставить("ОПВПеречислено_"      + СтрГод + "_" + СтрНомерСтроки, ОПВПеречислено);
				СписокСохранения.Вставить("ОПВПени_"             + СтрГод + "_" + СтрНомерСтроки, ОПВПени);
				СписокСохранения.Вставить("РеквизитыДокументов_" + СтрГод + "_" + СтрНомерСтроки, РеквизитыДокументов);
				
				Если Выборка.Год <> ТекущийГод Тогда
					
					СписокСохранения.Вставить("ИтогоПериод_"         + СтрГод, НСтр("ru = 'Итого за '") + СтрГод +  НСтр("ru = ' год'"));
					СписокСохранения.Вставить("ИтогоСуммаЗаработка_" + СтрГод, ИтогоСуммаЗаработка);
					СписокСохранения.Вставить("ИтогоОПВНачислено_"   + СтрГод, ИтогоОПВНачислено);
					СписокСохранения.Вставить("ИтогоОПВПеречислено_" + СтрГод, ИтогоОПВПеречислено);
					СписокСохранения.Вставить("ИтогоОПВПени_"        + СтрГод, ИтогоОПВПени);
					
					ИтогоСумма = ИтогоСумма + ИтогоСуммаЗаработка;
					
					// Обнулим итоговые даные
					ИтогоСуммаЗаработка 	= 0;
					ИтогоОПВНачислено		= 0;
					ИтогоОПВПеречислено		= 0;
					ИтогоОПВПени			= 0;
					ТекущийГод 				= Выборка.Год;
					НомерСтроки 			= 0;
					
				КонецЕсли;
				
			КонецЕсли;
			
			СуммаЗаработка = ?(Выборка.СуммаЗаработка <> NULL, Выборка.СуммаЗаработка, 0);
			ОПВНачислено = ?(Выборка.ОПВНачислено <> NULL, Выборка.ОПВНачислено, 0);
			ОПВПеречислено = ?(Выборка.ОПВПеречислено <> NULL, Выборка.ОПВПеречислено, 0);
			ОПВПени = ?(Выборка.ОПВПени <> NULL, Выборка.ОПВПени, 0);
			
			РеквизитыТекущегоДокумента = ?(Выборка.ДатаПеречисления <> NULL, Формат(Выборка.ДатаПеречисления, "ДФ=dd.MM.yyyy"), "");
			РеквизитыТекущегоДокумента = РеквизитыТекущегоДокумента + ?(РеквизитыТекущегоДокумента <> "", ", ", "") + ?(Выборка.НомерПоручения <> NULL, Выборка.НомерПоручения, "");
			РеквизитыТекущегоДокумента = РеквизитыТекущегоДокумента + ?(РеквизитыТекущегоДокумента <> "", ", ", "") + ?(Выборка.МесяцНалоговогоПериода <> NULL, Формат(Выборка.МесяцНалоговогоПериода, "ДФ='ММММ гггг'"), "");
			
			РеквизитыДокументов = РеквизитыТекущегоДокумента; 
			
			ТекущийПериод = Выборка.Период;
			
			НомерСтроки = Месяц(Выборка.Период);
			СтрНомерСтроки = Формат(НомерСтроки, "ЧГ=");	
			
		Иначе 
			
			СуммаЗаработка = СуммаЗаработка + ?(Выборка.СуммаЗаработка <> NULL, Выборка.СуммаЗаработка, 0);
			ОПВНачислено = ОПВНачислено + ?(Выборка.ОПВНачислено <> NULL, Выборка.ОПВНачислено, 0);
			ОПВПеречислено = ОПВПеречислено + ?(Выборка.ОПВПеречислено <> NULL, Выборка.ОПВПеречислено, 0);
			ОПВПени = ОПВПени + ?(Выборка.ОПВПени <> NULL, Выборка.ОПВПени, 0);
			
			РеквизитыТекущегоДокумента = ?(Выборка.ДатаПеречисления <> NULL, Формат(Выборка.ДатаПеречисления, "ДФ=dd.MM.yyyy"), "");
			РеквизитыТекущегоДокумента = РеквизитыТекущегоДокумента + ?(РеквизитыТекущегоДокумента <> "", ", ", "") + ?(Выборка.НомерПоручения <> NULL, Выборка.НомерПоручения, "");
			РеквизитыТекущегоДокумента = РеквизитыТекущегоДокумента + ?(РеквизитыТекущегоДокумента <> "", ", ", "") + ?(Выборка.МесяцНалоговогоПериода <> NULL, Формат(Выборка.МесяцНалоговогоПериода, "ДФ='ММММ гггг'"), "");
			
			РеквизитыДокументов = РеквизитыДокументов + ?(РеквизитыДокументов <> "" И РеквизитыТекущегоДокумента <> "", "; ", "") + РеквизитыТекущегоДокумента; 
			
		КонецЕсли;
	КонецЦикла;	
	
	// Установим последние значения
	
	ИтогоСуммаЗаработка = ИтогоСуммаЗаработка + СуммаЗаработка;
	ИтогоОПВНачислено	= ИтогоОПВНачислено + ОПВНачислено;
	ИтогоОПВПеречислено	= ИтогоОПВПеречислено + ОПВПеречислено;
	ИтогоОПВПени		= ИтогоОПВПени + ОПВПени;
	
	ИтогоСумма = ИтогоСумма + ИтогоСуммаЗаработка;
	
	СтрГод = Формат(ТекущийГод, "ЧГ=");
	
	СписокСохранения.Вставить("Период_"              + СтрГод + "_" + СтрНомерСтроки, ТекущийПериод);
	СписокСохранения.Вставить("СуммаЗаработка_"      + СтрГод + "_" + СтрНомерСтроки, СуммаЗаработка);
	СписокСохранения.Вставить("ОПВНачислено_"        + СтрГод + "_" + СтрНомерСтроки, ОПВНачислено);
	СписокСохранения.Вставить("ОПВПеречислено_"      + СтрГод + "_" + СтрНомерСтроки, ОПВПеречислено);
	СписокСохранения.Вставить("ОПВПени_"             + СтрГод + "_" + СтрНомерСтроки, ОПВПени);
	СписокСохранения.Вставить("РеквизитыДокументов_" + СтрГод + "_" + СтрНомерСтроки, РеквизитыДокументов);
	
	СписокСохранения.Вставить("ИтогоПериод_"         + СтрГод, НСтр("ru = 'Итого за '") + СтрГод + НСтр("ru = ' год'"));
	СписокСохранения.Вставить("ИтогоСуммаЗаработка_" + СтрГод, ИтогоСуммаЗаработка);
	СписокСохранения.Вставить("ИтогоОПВНачислено_"   + СтрГод, ИтогоОПВНачислено);
	СписокСохранения.Вставить("ИтогоОПВПеречислено_" + СтрГод, ИтогоОПВПеречислено);
	СписокСохранения.Вставить("ИтогоОПВПени_"        + СтрГод, ИтогоОПВПени);
		
	// Итоги отчета
	СписокСохранения.Вставить("ИтогоСумма", Формат(ИтогоСумма, "ЧЦ=15; ЧДЦ=2; ЧРД=,") + " (" + ОбщегоНазначенияБКВызовСервера.СформироватьСуммуПрописью(ИтогоСумма, Константы.ВалютаРегламентированногоУчета.Получить()) + ")");

	Если Год(мДатаКонцаПериодаОтчета) > Год(мДатаНачалаПериодаОтчета) Тогда
		КоличествоЛет = Год(мДатаКонцаПериодаОтчета) - Год(мДатаНачалаПериодаОтчета);
		КоличествоМесяцев = (Месяц(КонецГода(мДатаНачалаПериодаОтчета)) - Месяц(мДатаНачалаПериодаОтчета) + 1) +
						 ?(КоличествоЛет > 1,КоличествоЛет-1,0)*12 + 
						 (Месяц(мДатаКонцаПериодаОтчета) - Месяц(НачалоГода(мДатаКонцаПериодаОтчета)) + 1);
	Иначе
		КоличествоМесяцев = Месяц(мДатаКонцаПериодаОтчета) - Месяц(мДатаНачалаПериодаОтчета) + 1;
	КонецЕсли;
	
	СписокСохранения.Вставить("КоличествоМесяцев", КоличествоМесяцев);
	
	// Получим массив с числовым значением годов
	мМассивОтчетныхГодов.Очистить();
	СформироватьМассивОтчетныхГодов();
	
	ВывестиВДокумент(СписокСохранения);
	
	Модифицированность = Истина;
	УстановитьФорматВывода();
	Комментарий = "";

КонецПроцедуры

&НаСервере
// Формирует список отчетных годов, необходимых для формирования отчета
//
Процедура СформироватьМассивОтчетныхГодов()
	
	ТекущийПериод  = НачалоГода(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	Пока ТекущийПериод <= КонецГода(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета) Цикл 
		мМассивОтчетныхГодов.Добавить(Год(ТекущийПериод));
		ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 12);		
	КонецЦикла;  	
	
КонецПроцедуры

&НаСервере
Функция ПроверитьСоответствиеСотрудникаОрганизацииНаСервере(ВыбранныйСотрудник)
	
	ГоловнаяОрганизацияОтчета    = ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(Налогоплательщик);
	ВыбраннаяГоловнаяОрганизация = ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(ВыбранныйСотрудник.ТекущаяСтруктурнаяЕдиница);
	
	Возврат ГоловнаяОрганизацияОтчета = ВыбраннаяГоловнаяОрганизация;
	
КонецФункции

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	
	ФизЛицо	= Сотрудник.ФизЛицо;

КонецПроцедуры

&НаСервере
Процедура ДатаПодписиПриИзмененииНаСервере()
	
	УстановитьЗначениеОбласти(ЭтотОбъект["ФормаОтчетаПолеТабличногоДокументаСтраница1"], "ДатаПодписи", ДатаПодписи);
	Модифированность = Истина;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьЗначениеОбласти(Приемник, ИмяОбласти, Значение)

	Результат = Ложь;
	Если ТипЗнч(Приемник) = Тип("Структура") Тогда
		
		Приемник.Вставить(ИмяОбласти, Значение);
		Результат = Истина;
	
	ИначеЕсли ТипЗнч(Приемник) = Тип("ТабличныйДокумент") Тогда
		
		ИскомаяОбласть = Приемник.Области.Найти(ИмяОбласти);
		Если ИскомаяОбласть <> Неопределено Тогда
			Если ИскомаяОбласть.СодержитЗначение Тогда
				ИскомаяОбласть.Значение = Значение;
			Иначе
				ИскомаяОбласть.Текст = Строка(Значение);
			КонецЕсли;
			Результат = Истина;
		КонецЕсли;
				
	КонецЕсли;

    Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЗначениеОбласти(Источник, ИмяОбласти, ЗначениеПоУмолчанию = Неопределено) 

	Результат = ЗначениеПоУмолчанию;
	Если ТипЗнч(Источник) = Тип("Структура") Тогда
		
		Источник.Свойство(ИмяОбласти, Результат);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ТабличныйДокумент") Тогда
		
		ИскомаяОбласть = Источник.Области.Найти(ИмяОбласти);
		
		Если ИскомаяОбласть <> Неопределено Тогда
			
			Если ИскомаяОбласть.СодержитЗначение Тогда
				Результат = ИскомаяОбласть.Значение;
			Иначе
				Результат = ИскомаяОбласть.Текст;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаСервере
// Процедура контролирует корректность установленной точности указания
// суммовых (денежных) показателей при выборе единицы измерения.
//
Процедура ПроверитьТочность()
	
	Если ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1 Тогда

		Если ТочностьЕдиницыИзмерения > 2 Тогда
	        ТекстСообщения = НСтр("ru = 'При выводе сумм в тенге точность не может превышать 2 знака'");            
	        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);                
			ТочностьЕдиницыИзмерения = 2;
		КонецЕсли;

	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000 Тогда

		Если ТочностьЕдиницыИзмерения > 3 Тогда
	        ТекстСообщения = НСтр("ru = 'При выводе сумм в тысячах тенге точность не может превышать 3 знака'");            
	        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);                
			ТочностьЕдиницыИзмерения = 3;
		КонецЕсли;

	ИначеЕсли ЕдиницаИзмерения = Перечисления.ПорядкиОкругленияОтчетности.Окр1000000 Тогда

		Если ТочностьЕдиницыИзмерения > 6 Тогда
	        ТекстСообщения = НСтр("ru = 'При выводе сумм в миллионах тенге точность не может превышать 6 знаков'");            
	        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);                
			ТочностьЕдиницыИзмерения = 6;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ПроверитьТочность()

&НаСервере
Функция СобратьДанныеТекущегоТаблПоля(ТабличноеПоле)

	// Собираем в список значений имена вычисляемых показателей
	//
	//
	СтруктураДанныхПоля = Новый Структура;

	Для Инд = 0 По ТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ТабличноеПоле.Области[Инд];

		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;

		Если НЕ(ТекущаяОбласть.СодержитЗначение) Тогда
			Продолжить;
		КонецЕсли;

		ИмяПоказателя      = ТекущаяОбласть.Имя;
		ЗначениеПоказателя = ТекущаяОбласть.Значение;

		СтруктураДанныхПоля.Вставить(ИмяПоказателя, ЗначениеПоказателя);

	КонецЦикла;

	Возврат СтруктураДанныхПоля;

КонецФункции // СобратьДанныеТекущегоТаблПоля()

&НаСервере
Процедура ВыводЗаголовка(ТабДок)

	ПоказыватьЗаголовокПриложение3 = Ложь;
	ПоказыватьЗаголовокПриложение2 = Ложь;
	
	Если ОтчетОбъект.ПоказыватьЗаголовок = "Приложение3" Тогда
		ПоказыватьЗаголовокПриложение3 = Истина;		
	КонецЕсли;	
	
	Если ОтчетОбъект.ПоказыватьЗаголовок = "Приложение2" Тогда
		ПоказыватьЗаголовокПриложение2 = Истина;		
	КонецЕсли;	
	
	Если ТабДок.Области.Найти("Заголовок") <> Неопределено Тогда
		ТабДок.Область("Заголовок").Видимость = ПоказыватьЗаголовокПриложение3;
	КонецЕсли;
	
	Если ТабДок.Области.Найти("ЗаголовокПриложение2") <> Неопределено Тогда
		ТабДок.Область("ЗаголовокПриложение2").Видимость = ПоказыватьЗаголовокПриложение2;
	КонецЕсли;   
	
КонецПроцедуры

&НаСервере
Процедура УправлениеПометкамиКнопокКоманднойПанели()
	
	Если ЗначениеЗаполнено(ОтчетОбъект.ПоказыватьЗаголовок) Тогда
		Для Счетчик = 1 по Элементы.ФормаГруппаЗаголовок.ПодчиненныеЭлементы.Количество() Цикл
			Если Элементы.ФормаГруппаЗаголовок.ПодчиненныеЭлементы.Получить(Счетчик - 1).Имя = "Форма" + ОтчетОбъект.ПоказыватьЗаголовок Тогда
				Элементы.ФормаГруппаЗаголовок.ПодчиненныеЭлементы.Получить(Счетчик - 1).Пометка = Истина;	
			Иначе       
				Элементы.ФормаГруппаЗаголовок.ПодчиненныеЭлементы.Получить(Счетчик - 1).Пометка = Ложь;	
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;		
	
КонецПроцедуры 
