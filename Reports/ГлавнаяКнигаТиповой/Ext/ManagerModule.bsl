#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура СформироватьОтчет(Знач ПараметрыОтчета, АдресХранилища) Экспорт
	
	Результат = Новый ТабличныйДокумент;
	
	СформироватьГлавнуюКнигу(Результат, ПараметрыОтчета);
	
	//ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	ПоместитьВоВременноеХранилище(Новый Структура("Результат", Результат), АдресХранилища);
	ПоместитьВоВременноеХранилище(ПараметрыОтчета, ПараметрыОтчета.ДанныеРасшифровки);
	
КонецПроцедуры

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Возврат Новый Структура;
							
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура СформироватьГлавнуюКнигу(ДокументРезультат, ПараметрыОтчета)
	
	ПредставленияПериодичности = Новый Соответствие;
	ПредставленияПериодичности.Вставить(0, "");
	ПредставленияПериодичности.Вставить(9, "Месяц");
	ПредставленияПериодичности.Вставить(10, "Квартал");
	ПредставленияПериодичности.Вставить(11, "Полугодие");
	ПредставленияПериодичности.Вставить(12, "Год");
	
	ПараметрыОтчета.Вставить("ПредставленияПериодичности", ПредставленияПериодичности);
	
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ВыводитьЗаголовок = Истина;
	Если ПараметрыОтчета.Свойство("ВыводитьЗаголовок") Тогда
		ВыводитьЗаголовок = ПараметрыОтчета.ВыводитьЗаголовок;
	КонецЕсли;
	
	ПараметрыОтчета.Вставить("МассивШиринКолонок", Новый Массив);
	
	// выполним общий запрос
	Результат = ВыполнитьОбщийЗапрос(ПараметрыОтчета);
	
	Макет = ПолучитьМакет("Макет");

	Если ВыводитьЗаголовок Тогда
		// Вывод заголовка отчета
		ОбластьЗаголовка = СформироватьЗаголовок(ПараметрыОтчета);
		ВысотаЗаголовка  = ОбластьЗаголовка.ВысотаТаблицы;
		ДокументРезультат.Вывести(ОбластьЗаголовка, 1);
	КонецЕсли;
	
	// Получим форматную строку для формирования наименований периодов
	ФорматПериода = ПолучитьСтрокуФорматаПериода(ПараметрыОтчета.Периодичность);

	СтруктураПараметров = СформироватьСтруктуруПараметров(ДокументРезультат, Макет, ФорматПериода);
	
	ВыводитьРазвернутоеСальдо = Ложь;
	Для Каждого СтрокаТаблицы Из ПараметрыОтчета.РазвернутоеСальдо Цикл
		Если СтрокаТаблицы.Использование И ЗначениеЗаполнено(СтрокаТаблицы.Счет) Тогда
			ВыводитьРазвернутоеСальдо = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	//
	// Вывод отчета
	//
	ВыборкаПоСчетам = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Счет");
	Пока ВыборкаПоСчетам.Следующий() Цикл
		
		// если разбиваем по листам, то каждый счет печатается в своем документе
		Если ПараметрыОтчета.РазбитьПоЛистам Тогда
			ДокументОтдельногоСчета = Новый ТабличныйДокумент;
			СтруктураПараметров.Вставить("ДокументРезультат", ДокументОтдельногоСчета);
		КонецЕсли;
		
		СтруктураПараметров.Вставить("Счет",              ВыборкаПоСчетам.Счет);
		СтруктураПараметров.Вставить("СчетПредставление", ВыборкаПоСчетам.СчетПредставление);
		
		Если ВыводитьРазвернутоеСальдо Тогда
			СтрокаРазвернутогоСальдо = ПараметрыОтчета.РазвернутоеСальдо.Найти(ВыборкаПоСчетам.Счет,"Счет");
			СтруктураПараметров.Вставить("РазвернутоеСальдо"       , СтрокаРазвернутогоСальдо <> Неопределено);
			СтруктураПараметров.Вставить("СтрокаРазвернутогоСальдо", СтрокаРазвернутогоСальдо);
		Иначе
			СтруктураПараметров.Вставить("РазвернутоеСальдо"       , Ложь);
			СтруктураПараметров.Вставить("СтрокаРазвернутогоСальдо", Неопределено);
		КонецЕсли;
		
		ВывестиСчет(ПараметрыОтчета, ВыборкаПоСчетам, СтруктураПараметров, ВыводитьЗаголовок);
		
		// разобьем на страницы, если это необходимо
		Если ПараметрыОтчета.РазбитьПоЛистам Тогда
			
			Если ДокументОтдельногоСчета.ВысотаТаблицы <> 0 Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			Иначе
				Продолжить;
			КонецЕсли;
			
			ТабДок = Новый ТабличныйДокумент;
			ТабДок.ОриентацияСтраницы = ДокументРезультат.ОриентацияСтраницы;

			Начало = ДокументОтдельногоСчета.ПолучитьОбласть(, 1,, 1);
			Хвост  = ДокументОтдельногоСчета.ПолучитьОбласть(, 2,, ДокументОтдельногоСчета.ШиринаТаблицы);
			
			МассивТаблиц = Новый Массив;
			МассивТаблиц.Добавить(Хвост);
			
			ТабДок.Вывести(Начало);
			
			// считаем что по высоте таблица счета всегда умещается на странице
			Если ТабДок.ПроверитьПрисоединение(МассивТаблиц) Тогда
				// если табличка по одному счету помещается на один лист, то сразу выводим ее
				ПараметрыОтчета.Вставить("МассивШиринКолонок", Новый Массив);
				
				Ширина1 = ДокументРезультат.ШиринаТаблицы;
				Ширина2 = ДокументОтдельногоСчета.ШиринаТаблицы;
				Для к = Ширина1 + 1 По Ширина2 Цикл
					ПараметрыОтчета.МассивШиринКолонок.Добавить(ДокументОтдельногоСчета.Область(, к,, к).ШиринаКолонки);
				КонецЦикла;
				
				ДокументРезультат.Вывести(ДокументОтдельногоСчета);
				
				Для к = 0 По ПараметрыОтчета.МассивШиринКолонок.Количество() - 1 Цикл
					ДокументРезультат.Область(, Ширина1 + 1 + к,, Ширина1 + 1 + к).ШиринаКолонки = ПараметрыОтчета.МассивШиринКолонок[к];
				КонецЦикла;
				
			Иначе
				// а если табличка по одному счету не помещается на один лист, то...
				
				НачальноеСмещение = Начало.ШиринаТаблицы + 1;
				н = НачальноеСмещение;
				
				ШиринаТаблицы = ДокументОтдельногоСчета.ШиринаТаблицы;
				Пока н <= ШиринаТаблицы Цикл
					
					МассивКолонок = Новый Массив;
					
					Для н = НачальноеСмещение По ШиринаТаблицы + 1 Цикл
						
						Если н = ШиринаТаблицы + 1 Тогда
							Прервать;
						КонецЕсли;
						
						МассивКолонок.Добавить(ДокументОтдельногоСчета.ПолучитьОбласть(, н,, н));
						
						Если Не ТабДок.ПроверитьПрисоединение(МассивКолонок) Тогда
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					ПоследнийЭлемент = МассивКолонок.Количество() - ?(н <= ШиринаТаблицы, 2, 1);
					
					Для к=0 По ПоследнийЭлемент Цикл
						ТабДок.Присоединить(МассивКолонок[0]);
						МассивКолонок.Удалить(0);
					КонецЦикла;
					
					ПараметрыОтчета.Вставить("МассивШиринКолонок", Новый Массив);
					
					Ширина1 = ДокументРезультат.ШиринаТаблицы;
					Ширина2 = ТабДок.ШиринаТаблицы;
					Для к = Ширина1 + 1 По Ширина2 Цикл
						ПараметрыОтчета.МассивШиринКолонок.Добавить(ТабДок.Область(, к,, к).ШиринаКолонки);
					КонецЦикла;
					
					ДокументРезультат.Вывести(ТабДок);
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					
					Для к = 0 По ПараметрыОтчета.МассивШиринКолонок.Количество() - 1 Цикл
						ДокументРезультат.Область(, Ширина1 + 1 + к,, Ширина1 + 1 + к).ШиринаКолонки = ПараметрыОтчета.МассивШиринКолонок[к];
					КонецЦикла;
					
					ТабДок.Очистить();
					ТабДок.Вывести(Начало);
					// добавим надписи периодов
					ТабДок.Присоединить(ДокументОтдельногоСчета.ПолучитьОбласть(, 2,, 2));
					
					НачальноеСмещение = н;
					
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	//	
	//	Отчет выведен
	//	
	
	// Заполним общую расшифровку:
	СтруктураНастроекОтчета = Новый Структура;

	СтруктураНастроекОтчета.Вставить("СписокОрганизаций", ПараметрыОтчета.СписокСтруктурныхЕдиниц);
	СтруктураНастроекОтчета.Вставить("ДатаНач", ПараметрыОтчета.НачалоПериода);
	СтруктураНастроекОтчета.Вставить("ДатаКон", ПараметрыОтчета.КонецПериода);
	СтруктураНастроекОтчета.Вставить("ПоказыватьЗаголовок", ВыводитьЗаголовок);

	ДокументРезультат.Область(1, 1).Расшифровка = СтруктураНастроекОтчета;
	
		// Присвоим имя для сохранения параметров печати табличного документа
	ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ГлавнаяКнигаТиповой";
	
	БухгалтерскиеОтчетыВызовСервера.ВыводПодписейОтчета(ПараметрыОтчета, ДокументРезультат);

	// Первую колонку не печатаем //первую колонку печатаем, в нее попадают подписи
	ДокументРезультат.ОбластьПечати = ДокументРезультат.Область(1, 1, ДокументРезультат.ВысотаТаблицы + 1, ДокументРезультат.ШиринаТаблицы);
	
	//УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ДокументРезультат, ЗаголовокОтчета(), Строка(глТекущийПользователь));
	
КонецПроцедуры

Функция ВыполнитьОбщийЗапрос(ПараметрыОтчета)
	
	ДатаНачала    = ПараметрыОтчета.НачалоПериода;
	ДатаКонца     = ПараметрыОтчета.КонецПериода;
	Периодичность = ПараметрыОтчета.Периодичность;
	
	Если Периодичность = 0 Тогда
		ДатаНачала = НачалоДня(ДатаНачала);
		ДатаКонца  = ?(ДатаКонца <> '00010101000000', КонецДня(ДатаКонца), ДатаКонца);
	ИначеЕсли Периодичность = 9 Тогда
		ДатаНачала = НачалоМесяца(ДатаНачала);
		ДатаКонца  = ?(ДатаКонца <> '00010101000000', КонецДня(КонецМесяца(ДатаКонца)), ДатаКонца);
	ИначеЕсли Периодичность = 10 Тогда
		ДатаНачала = НачалоКвартала(ДатаНачала);
		ДатаКонца  = ?(ДатаКонца <> '00010101000000', КонецДня(КонецКвартала(ДатаКонца)), ДатаКонца);
	ИначеЕсли Периодичность = 11 Тогда
		СерединаГода = ДобавитьМесяц(НачалоГода(ДатаНачала), 7);
		Если ДатаНачала <= СерединаГода Тогда
			ДатаНачала = НачалоГода(ДатаНачала);
		Иначе
			ДатаНачала = СерединаГода;
		КонецЕсли;
		СерединаГода = КонецМесяца(ДобавитьМесяц(НачалоГода(ДатаКонца), 6));
		Если ДатаКонца <= СерединаГода Тогда
			ДатаКонца = ?(ДатаКонца <> '00010101000000', СерединаГода, ДатаКонца);
		Иначе
			ДатаКонца = ?(ДатаКонца <> '00010101000000', КонецДня(КонецГода(ДатаКонца)), ДатаКонца);
		КонецЕсли;
	ИначеЕсли Периодичность = 12 Тогда
		ДатаНачала = НачалоГода(ДатаНачала);
		ДатаКонца  = ?(ДатаКонца <> '00010101000000', КонецДня(КонецГода(ДатаКонца)), ДатаКонца);
	КонецЕсли; 

	Запрос = Новый Запрос;
	
	// установим параметры
	Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыОтчета.СписокСтруктурныхЕдиниц);
	
	Запрос.УстановитьПараметр("ДатаНач", ДатаНачала);
	
	Если ДатаНачала = '00010101000000' Тогда
		Запрос.УстановитьПараметр("ДатаНач", ДатаНачала + 1);
	КонецЕсли;

	
	Если ДатаКонца <> '00010101000000' Тогда
		Запрос.УстановитьПараметр("ДатаКон", КонецДня(ДатаКонца));
	Иначе
		Запрос.УстановитьПараметр("ДатаКон", ДатаКонца);
	КонецЕсли;
	
	// сформируем текст запроса
	Запрос.Текст = ПолучитьТекстОбщегоЗапроса(ПараметрыОтчета);
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция ПолучитьТекстОбщегоЗапроса(ПараметрыОтчета)

	ПоПериодам = ЗначениеЗаполнено(ПараметрыОтчета.Периодичность);
	ИмяПланаСчетов = "Типовой";
	
	СтрокаОграниченийПоРеквизитам = "";
	ДополнитьСтрокуОграниченийПоРеквизитам(СтрокаОграниченийПоРеквизитам, "СписокОрганизаций", ПараметрыОтчета.СписокСтруктурныхЕдиниц, "Организация");
	
	Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Счета.Ссылка КАК Счет,
	|	Счета.Наименование КАК СчетНаименование,
	|	Счета.Родитель КАК СчетРодитель,
	|	Счета.Валютный КАК СчетВалютный,
	|	ПРЕДСТАВЛЕНИЕ(Счета.Ссылка) КАК СчетПредставление,"
	+ ?(ПоПериодам, "Обороты.Период КАК Период,", "") + "
	|	КорСчета.Ссылка КАК КорСчет,
	|	ПРЕДСТАВЛЕНИЕ(КорСчета.Ссылка) КАК КорСчетПредставление,
	|	Обороты.СуммаОборотДт КАК СуммаОборотДт,
	|	Обороты.СуммаОборотКт КАК СуммаОборотКт,
	|	КорСчета.Порядок КАК КорСчетПорядок,
	|	Счета.Порядок КАК СчетПорядок,
	|	NULL КАК СуммаНачДт,
	|	NULL КАК СуммаНачКт
	|ИЗ
	|	РегистрБухгалтерии." + ИмяПланаСчетов + ".Обороты(&ДатаНач, &ДатаКон, " + ПараметрыОтчета.ПредставленияПериодичности[ПараметрыОтчета.Периодичность] + ", , , " + СтрокаОграниченийПоРеквизитам + ", , ) КАК Обороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов." + ИмяПланаСчетов + " КАК Счета
	|			ПО Обороты.Счет = Счета.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов." + ИмяПланаСчетов + " КАК КорСчета
	|			ПО Обороты.КорСчет = КорСчета.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Счета.Ссылка,
	|	Счета.Наименование,
	|	Счета.Родитель,
	|	Счета.Валютный,
	|	ПРЕДСТАВЛЕНИЕ(Счета.Ссылка),"
	+ ?(ПоПериодам, "NULL КАК Период,", "") + "
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	Счета.Порядок,
	|	Остатки.СуммаОстатокДт,
	|	Остатки.СуммаОстатокКт
	|ИЗ
	|	РегистрБухгалтерии." + ИмяПланаСчетов + ".Остатки(&ДатаНач, , , " + СтрокаОграниченийПоРеквизитам + ") КАК Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов." + ИмяПланаСчетов + " КАК Счета
	|			ПО Остатки.Счет = Счета.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетПорядок,"
	+ ?(ПоПериодам, "Период,", "") + "
	|	КорСчетПорядок
	|ИТОГИ
	|	СУММА(СуммаОборотДт),
	|	СУММА(СуммаОборотКт),
	|	СУММА(СуммаНачДт),
	|	СУММА(СуммаНачКт)
	|ПО
	|	Счет" + ?(ПараметрыОтчета.ПоГруппамСчетов, " ИЕРАРХИЯ,", ",")
	+ ?(ПоПериодам, "Период"+?(ПараметрыОтчета.ВсеПериоды, " ПЕРИОДАМИ(" + ПараметрыОтчета.ПредставленияПериодичности[ПараметрыОтчета.Периодичность] + ",&ДатаНач,&ДатаКон)", "") + ",", "") + "
	|	КорСчет" + ?(ПараметрыОтчета.ПоГруппамКорСчетов, " ИЕРАРХИЯ", "");
	
	Возврат Текст;
	
КонецФункции // ПолучитьТекстОбщегоЗапроса()

Процедура ДополнитьСтрокуОграниченийПоРеквизитам(СтрокаОграничения, Знач ИмяОграниченияПоРеквизиту, Знач ОграничениеПоРеквизиту, ИмяРеквизита = "")
	
	Если НЕ ЗначениеЗаполнено(ОграничениеПоРеквизиту) Тогда
		Возврат;
	КонецЕсли;
	
	Если (ИмяОграниченияПоРеквизиту = "ВидУчета") ИЛИ (ТипЗнч(ОграничениеПоРеквизиту) = Тип("СписокЗначений")) Тогда
		СтрокаНовогоОграничения = ?(ПустаяСтрока(ИмяРеквизита), ИмяОграниченияПоРеквизиту, ИмяРеквизита) + " В Иерархии (&" + ИмяОграниченияПоРеквизиту + ") ";
	Иначе		
		СтрокаНовогоОграничения = ИмяОграниченияПоРеквизиту + " = &" + ИмяОграниченияПоРеквизиту + " ";
	КонецЕсли;
		
	СтрокаОграничения = ОбъединитьОграничения(СтрокаОграничения, СтрокаНовогоОграничения);
    
КонецПроцедуры

Функция СформироватьЗаголовок(ПараметрыОтчета)

	ОписаниеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
	Макет = ПолучитьМакет("Макет");

	ЗаголовокОтчета  = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок = Макет.Область("Заголовок");

	// После удаления областей нужно установить свойства ПоВыделеннымКолонкам
	Для Индекс = 1 По ЗаголовокОтчета.ВысотаТаблицы - 1 Цикл
		Макет.Область(ОбластьЗаголовок.Верх + Индекс, 2, ОбластьЗаголовок.Верх + Индекс, 2).РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
		Макет.Область(ОбластьЗаголовок.Верх + Индекс, 2, ОбластьЗаголовок.Верх + Индекс, ОбластьЗаголовок.Право).ПоВыделеннымКолонкам = Истина;
	КонецЦикла;

	ЗаголовокОтчета = Макет.ПолучитьОбласть("Заголовок");

	НазваниеОрганизации = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ПараметрыОтчета.СписокСтруктурныхЕдиниц);

	ЗаголовокОтчета.Параметры.НазваниеОрганизации = НазваниеОрганизации;
	ЗаголовокОтчета.Параметры.ОписаниеПериода     = ОписаниеПериода;

	ТекстПроСписокПоказателей = НСтр("ru = 'Выводимые данные: сумма'");
	ТекстПроИтоги = "";
	ТекстПроИтоги = Сред(ТекстПроИтоги, 3);

	Если Не ПустаяСтрока(ТекстПроИтоги) Тогда
		ЗаголовокОтчета.Параметры.ТекстПроИтоги = НСтр("ru = 'Детализация по '") + ТекстПроИтоги;
	КонецЕсли;
	
	ЗаголовокОтчета.Параметры.Заголовок = НСтр("ru = 'Главная книга'");
	
	Возврат ЗаголовокОтчета;

КонецФункции

Функция ПолучитьСтрокуФорматаПериода(Периодичность)
	
	ФорматПериода = "";
	Если ЗначениеЗаполнено(Периодичность) Тогда
		Если Периодичность = 12 Тогда
			ФорматПериода = "Л=ru; ДФ = ""гггг """"г.""""""";
		ИначеЕсли Периодичность = 11 Тогда
			ФорматПериода = "Л=ru; ДФ = """"""Полугодие с"""" дд.ММ.гггг """"""";
		ИначеЕсли Периодичность = 10 Тогда
			ФорматПериода = "Л=ru; ДФ = ""к"""" квартал"""" гггг """"г.""""""";
		ИначеЕсли Периодичность = 9 Тогда
			ФорматПериода = "Л=ru; ДФ = ""ММММ гггг """"г.""""""";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФорматПериода;
	
КонецФункции

Функция СформироватьСтруктуруПараметров(ДокументРезультат, Макет, ФорматПериода)

	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ДокументРезультат",ДокументРезультат);
	СтруктураПараметров.Вставить("Макет",Макет);
	
	// Период
	СтруктураПараметров.Вставить("ОбластьСтрокаПериод", Макет.ПолучитьОбласть("Строка|Период"));
	СтруктураПараметров.ОбластьСтрокаПериод.Область(1, 2).Формат = ФорматПериода;
	
	// Кон сальдо
	СтруктураПараметров.Вставить("ОбластьСтрокаСальдоКонДт", Макет.ПолучитьОбласть("Строка|СальдоКонДт"));
	СтруктураПараметров.Вставить("ОбластьСтрокаСальдоКонКт", Макет.ПолучитьОбласть("Строка|СальдоКонКт"));
	
	// Оборот 
	СтруктураПараметров.Вставить("ОбластьСтрокаОборотДт", Макет.ПолучитьОбласть("Строка|ОборотДт"));
	СтруктураПараметров.Вставить("ОбластьСтрокаОборотКт", Макет.ПолучитьОбласть("Строка|ОборотКт"));
	СтруктураПараметров.Вставить("ОбластьСтрокаОборотДтКорСчет", Макет.ПолучитьОбласть("Строка|ОборотДтКорСчет"));
	
	// по субконто
	// Период
	СтруктураПараметров.Вставить("ОбластьСтрокаСубконтоПериод", Макет.ПолучитьОбласть("СтрокаСубконто|Период"));
	
	// Кон сальдо
	СтруктураПараметров.Вставить("ОбластьСтрокаСубконтоСальдоКонДт", Макет.ПолучитьОбласть("СтрокаСубконто|СальдоКонДт"));
	СтруктураПараметров.Вставить("ОбластьСтрокаСубконтоСальдоКонКт", Макет.ПолучитьОбласть("СтрокаСубконто|СальдоКонКт"));
	
	// Оборот 
	СтруктураПараметров.Вставить("ОбластьСтрокаСубконтоОборотДт", Макет.ПолучитьОбласть("СтрокаСубконто|ОборотДт"));
	СтруктураПараметров.Вставить("ОбластьСтрокаСубконтоОборотКт", Макет.ПолучитьОбласть("СтрокаСубконто|ОборотКт"));
	СтруктураПараметров.Вставить("ОбластьСтрокаСубконтоОборотДтКорСчет", Макет.ПолучитьОбласть("СтрокаСубконто|ОборотДтКорСчет"));
	
	// по валютам
	// Период
	СтруктураПараметров.Вставить("ОбластьСтрокаВалютаПериод", Макет.ПолучитьОбласть("СтрокаВалюта|Период"));
	
	// Кон сальдо
	СтруктураПараметров.Вставить("ОбластьСтрокаВалютаСальдоКонДт", Макет.ПолучитьОбласть("СтрокаВалюта|СальдоКонДт"));
	СтруктураПараметров.Вставить("ОбластьСтрокаВалютаСальдоКонКт", Макет.ПолучитьОбласть("СтрокаВалюта|СальдоКонКт"));
	
	// Оборот 
	СтруктураПараметров.Вставить("ОбластьСтрокаВалютаОборотДт", Макет.ПолучитьОбласть("СтрокаВалюта|ОборотДт"));
	СтруктураПараметров.Вставить("ОбластьСтрокаВалютаОборотКт", Макет.ПолучитьОбласть("СтрокаВалюта|ОборотКт"));
	СтруктураПараметров.Вставить("ОбластьСтрокаВалютаОборотДтКорСчет", Макет.ПолучитьОбласть("СтрокаВалюта|ОборотДтКорСчет"));
	
	Возврат СтруктураПараметров;
	
КонецФункции // СформироватьСтруктуруПараметров()

// Вывод таблички для отдельного счета
//
Процедура ВывестиСчет(ПараметрыОтчета, ОбщийИтог, СтруктураПараметров, ПоказыватьЗаголовок)
	
	Если ПропуститьСчет(ОбщийИтог.СчетРодитель, ПараметрыОтчета.ПоСубсчетам) Тогда
		Возврат;
	КонецЕсли;
	
	ВысотаУжеВыведеннойТаблицы = СтруктураПараметров.ДокументРезультат.ВысотаТаблицы;
	ШиринаВыводимогоРаздела = 0;
	
	БазовыйУровеньВыборки = ОбщийИтог.Уровень() + 1;
	
	// Вывод кор счетов с дебетовыми оборотами
	Выборка = ОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "КорСчет", "Все");
	
	СписокДт = Новый СписокЗначений;
	СтруктураПараметров.Вставить("СписокДт", СписокДт);
	
	СписокКт = Новый СписокЗначений;
	СтруктураПараметров.Вставить("СписокКт", СписокКт);

	СтруктураПараметров.ДокументРезультат.НачатьАвтогруппировкуСтрок();
	
	ВывестиШапкуТаблицы(ПараметрыОтчета, СтруктураПараметров, Выборка, ШиринаВыводимогоРаздела);
	
	// Соответствия хранят счета, по которым были обороты
	ЗаполнитьТаблицуРазвернутыхОстатков(ПараметрыОтчета, СтруктураПараметров);
		
	Если ЗначениеЗаполнено(ПараметрыОтчета.Периодичность) Тогда
		ВывестиПериоды(ПараметрыОтчета, ОбщийИтог, СтруктураПараметров, Новый Соответствие, БазовыйУровеньВыборки);
	КонецЕсли;
	
	// Итого
	ВывестиПодвалТаблицы(ПараметрыОтчета, ОбщийИтог, СтруктураПараметров, Выборка);
	
	СтруктураПараметров.ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();
	
	// Обведение таблицы отчета линией, как в области границы
	ОбластьИтогПериод = СтруктураПараметров.Макет.ПолучитьОбласть("Итог|Период");
	
	ТолстаяЛиния = ОбластьИтогПериод.Область(1, 2).ГраницаСверху;
	
	СтруктураПараметров.ДокументРезультат.Область(ВысотаУжеВыведеннойТаблицы + 3, 2, СтруктураПараметров.ДокументРезультат.ВысотаТаблицы, ШиринаВыводимогоРаздела).Обвести(ТолстаяЛиния, ТолстаяЛиния, ТолстаяЛиния, ТолстаяЛиния);
	
	ШиринаТаблицы = СтруктураПараметров.ДокументРезультат.ШиринаТаблицы;
	
	// Восстановление ширин колонок
	Если ТипЗнч(ПараметрыОтчета.МассивШиринКолонок) = Тип("Массив") Тогда
		Если ПараметрыОтчета.МассивШиринКолонок.Количество() = ШиринаТаблицы Тогда
			Инд = 0;
			Для Каждого Элемент Из ПараметрыОтчета.МассивШиринКолонок Цикл
				СтруктураПараметров.ДокументРезультат.Область( , 1 + Инд, , 1 + Инд).ШиринаКолонки = Элемент;
				Инд = Инд + 1;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Заполним общую расшифровку:
	СтруктураНастроекОтчета = Новый Структура;
	
	СтруктураНастроекОтчета.Вставить("ДатаНач", ПараметрыОтчета.НачалоПериода);
	СтруктураНастроекОтчета.Вставить("ДатаКон", ПараметрыОтчета.КонецПериода);
	СтруктураНастроекОтчета.Вставить("СписокОрганизаций"  , ПараметрыОтчета.СписокСтруктурныхЕдиниц);
	СтруктураНастроекОтчета.Вставить("ПоказыватьЗаголовок", ПоказыватьЗаголовок);
	
	СтруктураПараметров.ДокументРезультат.Область(1,1).Расшифровка = СтруктураНастроекОтчета;
	
КонецПроцедуры

Функция ПропуститьСчет(Сч, ПоСубсчетам)

	Результат = Ложь;
	
	Если Сч = NULL Тогда
		Результат = Истина;
	Иначе
		Если Не Сч.Пустая() И НЕ ПоСубсчетам Тогда
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции // ПропуститьСчет()

Процедура ВывестиШапкуТаблицы(ПараметрыОтчета, СтруктураПараметров, Выборка, ШиринаВыводимогоРаздела)
	
	ИмяСтроки = "ЗаголовокТаблицы";
	
	НачалоЗаголовка = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|Период");
	ШиринаВыводимогоРаздела = ШиринаВыводимогоРаздела + НачалоЗаголовка.ШиринаТаблицы;
	
	Выборка.Следующий();
	
	НачалоЗаголовка.Параметры.ОписаниеСчета = НСтр("ru = 'Главная книга. Счет '") + Выборка.СчетПредставление + " """ + Выборка.СчетНаименование + """";
	Если ЗначениеЗаполнено(ПараметрыОтчета.Периодичность) Тогда
		НачалоЗаголовка.Параметры.Заголовок = НСтр("ru = 'Период'");
	КонецЕсли;
	
	Выборка.Сбросить();
	
	СтруктураПараметров.ДокументРезультат.Вывести(НачалоЗаголовка,0);
	
	ВерхнийУровень = 1000;
	Обл = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|ОборотДтКорсчет");
	Пока Выборка.Следующий() Цикл
		
		// Это итог по строке остатка
		Если Выборка.КорСчет = NULL Тогда
			Продолжить;
		КонецЕсли;
		
		// Выводим только верхний уровень
		Если НЕ ПараметрыОтчета.ПоСубсчетамКорСчетов И ВерхнийУровень < Выборка.Уровень() Тогда		
			Продолжить;
		Иначе
			ВерхнийУровень = Выборка.Уровень();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтчета.Периодичность) Тогда
			ВыборкаПериод = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Все");
			Пока ВыборкаПериод.Следующий() Цикл
				Если ПривестиКЧислу(ВыборкаПериод.СуммаОборотДт) <> 0 Тогда
					СтруктураПараметров.СписокДт.Добавить(Выборка.КорСчет);
					
					Обл.Параметры.Заполнить(Выборка);
					СтруктураПараметров.ДокументРезультат.Присоединить(Обл, Выборка.Уровень() + 1 );
					ШиринаВыводимогоРаздела = ШиринаВыводимогоРаздела + Обл.ШиринаТаблицы;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			Если ПривестиКЧислу(Выборка.СуммаОборотДт) <> 0 Тогда				
				СтруктураПараметров.СписокДт.Добавить(Выборка.КорСчет);
				
				Обл.Параметры.Заполнить(Выборка);
				СтруктураПараметров.ДокументРезультат.Присоединить(Обл, Выборка.Уровень() + 1);
				ШиринаВыводимогоРаздела = ШиринаВыводимогоРаздела + Обл.ШиринаТаблицы;
			КонецЕсли;
		
		КонецЕсли;
	КонецЦикла;
	
	МакетОборотДт = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|ОборотДт");
	СтруктураПараметров.ДокументРезультат.Присоединить(МакетОборотДт, 0);
	ШиринаВыводимогоРаздела = ШиринаВыводимогоРаздела + МакетОборотДт.ШиринаТаблицы;
	
	МакетОборотКт = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|ОборотКт");
	СтруктураПараметров.ДокументРезультат.Присоединить(МакетОборотКт, 0);
	ШиринаВыводимогоРаздела = ШиринаВыводимогоРаздела + МакетОборотКт.ШиринаТаблицы;
	
	МакетСальдоКонДт = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|СальдоКонДт");
	СтруктураПараметров.ДокументРезультат.Присоединить(МакетСальдоКонДт, 0);
	ШиринаВыводимогоРаздела = ШиринаВыводимогоРаздела + МакетСальдоКонДт.ШиринаТаблицы;
	
	МакетСальдоКонКт = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|СальдоКонКт");
	СтруктураПараметров.ДокументРезультат.Присоединить(МакетСальдоКонКт, 0);
	ШиринаВыводимогоРаздела = ШиринаВыводимогоРаздела + МакетСальдоКонКт.ШиринаТаблицы;
	
КонецПроцедуры

Процедура ВывестиПодвалТаблицы(ПараметрыОтчета, ОбщийИтог, СтруктураПараметров, Выборка)

	// Итого по отчету
	ИмяСтроки = "Итог";
	
	ОблНачало = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|Период");
	
	Расшифровка = ПолучитьСоответствиеРасшифровки(ОбщийИтог, Новый Соответствие);
	Расшифровка.Вставить("Счет", СтруктураПараметров.Счет);
	Расшифровка.Вставить("СчетПредставление", СтруктураПараметров.СчетПредставление);
	ОблНачало.Параметры.Расшифровка = ПолучитьСписокРасшифровок(Расшифровка, Ложь);
	
	СтруктураПараметров.ДокументРезультат.Вывести(ОблНачало,0);
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Периодичность) Тогда
		ВыборкаИтогПоСчету = ОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Все");
		ВыборкаИтогПоСчету.Следующий();
	Иначе
		ВыборкаИтогПоСчету = ОбщийИтог;
	КонецЕсли;
	
	СуммаНачДт = 0;
	СуммаНачКт = 0;
	
	Если Не СтруктураПараметров.РазвернутоеСальдо Тогда
		
		СуммаНачДт = ПривестиКЧислу(ВыборкаИтогПоСчету.СуммаНачДт);
		СуммаНачКт = ПривестиКЧислу(ВыборкаИтогПоСчету.СуммаНачКт);
		
		// если это были начальные остатки, то надо двинуть выборку дальще
		Если ЗначениеЗаполнено(ПараметрыОтчета.Периодичность) Тогда
			Если ВыборкаИтогПоСчету.Период = NULL Тогда
				ВыборкаИтогПоСчету.Следующий();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// Вывод кор счетов с дебетовыми оборотами
	Выборка.Сбросить();
	
	ВерхнийУровень = 1000;
	Обл = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|ОборотДтКорсчет");
	Пока Выборка.Следующий() Цикл
		
		// Это итог по строке остатка
		Если Выборка.КорСчет = NULL Тогда
			Продолжить;
		КонецЕсли;
		
		// Выводим только верхний уровень
		Если НЕ ПараметрыОтчета.ПоСубсчетамКорСчетов И ВерхнийУровень < Выборка.Уровень() Тогда
			Продолжить;
		Иначе
			ВерхнийУровень = Выборка.Уровень();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтчета.Периодичность) Тогда
			
			ВыборкаПериод = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Все");
			
			Пока ВыборкаПериод.Следующий() Цикл
				Если ПривестиКЧислу(ВыборкаПериод.СуммаОборотДт) <> 0 Тогда
					Обл.Параметры.Заполнить(Выборка);
					СтруктураПараметров.ДокументРезультат.Присоединить(Обл,Выборка.Уровень());
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Если ПривестиКЧислу(Выборка.СуммаОборотДт) <> 0 Тогда
				Обл.Параметры.Заполнить(Выборка);
				СтруктураПараметров.ДокументРезультат.Присоединить(Обл,Выборка.Уровень());
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Обл = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|ОборотДт");
	Обл.Параметры.Заполнить(ОбщийИтог);
	СтруктураПараметров.ДокументРезультат.Присоединить(Обл);
	
	Обл = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|ОборотКт");
	Обл.Параметры.Заполнить(ОбщийИтог);
	СтруктураПараметров.ДокументРезультат.Присоединить(Обл);
	
	// Получим конечное сальдо:
	Если Не СтруктураПараметров.РазвернутоеСальдо Тогда
		
		СуммаКонДт = СуммаНачДт + ПривестиКЧислу(ОбщийИтог.СуммаОборотДт) - ПривестиКЧислу(ОбщийИтог.СуммаОборотКт);
		СуммаКонКт = СуммаНачКт;
		
	Иначе
		
		ДеревоОстатков = СтруктураПараметров["ДеревоОстатков"];
		КолвоСтрок     = ДеревоОстатков.Строки.Количество();
		
		Если КолвоСтрок <> 0 Тогда
			
			СтрокаОстатков = ДеревоОстатков.Строки[0];
			
			СуммаКонДт = СтрокаОстатков.СуммаРазвернутоКонДт;
			СуммаКонКт = СтрокаОстатков.СуммаРазвернутоКонКт;
			
		Иначе
			СуммаКонДт = 0;
			СуммаКонКт = 0;
		КонецЕсли;
	КонецЕсли;
	
	Если Не СтруктураПараметров.РазвернутоеСальдо Тогда
		
		Если СуммаКонДт > СуммаКонКт Тогда
			СуммаКонДт = СуммаКонДт-СуммаКонКт;
			СуммаКонКт = 0;
		Иначе
			СуммаКонКт = СуммаКонКт - СуммаКонДт;
			СуммаКонДт = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	Обл = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|СальдоКонДт");
	Обл.Параметры.СуммаКонДт = СуммаКонДт;
	СтруктураПараметров.ДокументРезультат.Присоединить(Обл);
	
	Обл = СтруктураПараметров.Макет.ПолучитьОбласть(ИмяСтроки + "|СальдоКонКт");
	Обл.Параметры.СуммаКонКт = СуммаКонКт;
	СтруктураПараметров.ДокументРезультат.Присоединить(Обл);

КонецПроцедуры

Процедура ВывестиПериоды(ПараметрыОтчета, Выборка, СтруктураПараметров, ОтборДляРасшифровки, БазовыйУровеньВыборки = 0)
	
	Если ПараметрыОтчета.ВсеПериоды Тогда
		ВыборкаПоПериодам = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Все");
	Иначе
		ВыборкаПоПериодам = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "Счет");
	КонецЕсли;
	
	СуммаНачДт = 0;
	СуммаНачКт = 0;
	КоличествоНачДт = 0;
	КоличествоНачКт = 0;
	
	ОблНачало          = СтруктураПараметров.ОбластьСтрокаПериод;
	ОблОборотДт        = СтруктураПараметров.ОбластьСтрокаОборотДт;
	ОблОборотКт        = СтруктураПараметров.ОбластьСтрокаОборотКт;
	ОблОборотДтКорСчет = СтруктураПараметров.ОбластьСтрокаОборотДтКорСчет;
	ОблСальдоКонДт     = СтруктураПараметров.ОбластьСтрокаСальдоКонДт;
	ОблСальдоКонКт     = СтруктураПараметров.ОбластьСтрокаСальдоКонКт;
	
	// Идем по остаткам, т.к. там есть начпериоды без оборотов
	Пока ВыборкаПоПериодам.Следующий() Цикл
		// сначала получим остатки
		Если ВыборкаПоПериодам.Период = NULL Тогда
			СуммаНачДт = ПривестиКЧислу(ВыборкаПоПериодам.СуммаНачДт);
			СуммаНачКт = ПривестиКЧислу(ВыборкаПоПериодам.СуммаНачКт);
			
			Если СуммаНачДт > СуммаНачКт Тогда
				СуммаНачДт = СуммаНачДт - СуммаНачКт;
				СуммаНачКт = 0;
			Иначе
				СуммаНачКт = СуммаНачКт - СуммаНачДт;
				СуммаНачДт = 0;
			КонецЕсли;
			
			// остатки получили, пойдем за оборотами
			//Продолжить;
		КонецЕсли;
		
		Уровень = ВыборкаПоПериодам.Уровень();
		
		// Вывод начального сальдо
		
		// Выводим начало строки:
		ОблНачало.Параметры.Заполнить(ВыборкаПоПериодам);
		ОблНачало.Область(1, 2, ОблНачало.ВысотаТаблицы, 2).Отступ = Уровень - БазовыйУровеньВыборки;
		
		// Расшифровка
		Расшифровка = ПолучитьСоответствиеРасшифровки(ВыборкаПоПериодам, ОтборДляРасшифровки);
		Расшифровка.Вставить("Счет"             , СтруктураПараметров.Счет);
		Расшифровка.Вставить("СчетПредставление", СтруктураПараметров.СчетПредставление);
		ОблНачало.Параметры.Расшифровка = ПолучитьСписокРасшифровок(Расшифровка, Ложь);
		
		СтруктураПараметров.ДокументРезультат.Вывести(ОблНачало, Уровень - БазовыйУровеньВыборки + 1);
		
		// Одна выборку будем использовать для вывода дебетовых и кредитовых оборотов
		ВыборкаОборотыПоКорСчетам = ВыборкаПоПериодам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "КорСчет");
		
		// Вывод вложенных итогов по кор счетам
		ВывестиКорСчета(ПараметрыОтчета, ВыборкаОборотыПоКорСчетам, СтруктураПараметров, "ДТ", "Сумма", ОблОборотДтКорСчет, Расшифровка);
		
		// Оборот Дт
		ОблОборотДт.Параметры.Заполнить(ВыборкаПоПериодам);
		РасшифровкаДт = СоздатьКопиюСоответствияСтруктуры(Расшифровка);
		РасшифровкаДт.Вставить("СчетДт", СтруктураПараметров.Счет);
		РасшифровкаДт.Удалить("СчетКт");
		ОблОборотДт.Параметры.Расшифровка = ПолучитьСписокРасшифровок(РасшифровкаДт);
		
		СтруктураПараметров.ДокументРезультат.Присоединить(ОблОборотДт);
		
		// Оборот Кт
		ОблОборотКт.Параметры.Заполнить(ВыборкаПоПериодам);
		РасшифровкаКт = СоздатьКопиюСоответствияСтруктуры(Расшифровка);
		РасшифровкаКт.Вставить("СчетКт", СтруктураПараметров.Счет);
		РасшифровкаКт.Удалить("СчетДт");
		ОблОборотКт.Параметры.Расшифровка = ПолучитьСписокРасшифровок(РасшифровкаКт);
		
		СтруктураПараметров.ДокументРезультат.Присоединить(ОблОборотКт);
		
		СуммаОборотДт = ПривестиКЧислу(ВыборкаПоПериодам.СуммаОборотДт);
		СуммаОборотКт = ПривестиКЧислу(ВыборкаПоПериодам.СуммаОборотКт);
		
		Если СтруктураПараметров.РазвернутоеСальдо Тогда
			
			СтрокаОстатков = ПолучитьСтрокуИзДереваОстатков(ПараметрыОтчета, СтруктураПараметров, ВыборкаПоПериодам.Период);
			
			Если СтрокаОстатков = Неопределено Тогда
				// не нашли остатков вообще
				СуммаКонДт = 0;
				СуммаКонКт = 0;
			Иначе
				Если ВыборкаПоПериодам.Период = NULL Тогда
					СуммаКонДт = ПривестиКЧислу(СтрокаОстатков.СуммаРазвернутоНачДт);
					СуммаКонКт = ПривестиКЧислу(СтрокаОстатков.СуммаРазвернутоНачКт);
				Иначе
					СуммаКонДт = ПривестиКЧислу(СтрокаОстатков.СуммаРазвернутоКонДт);
					СуммаКонКт = ПривестиКЧислу(СтрокаОстатков.СуммаРазвернутоКонКт);
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			// Вывод оборота и кон сальдо
			Если СуммаОборотДт <> 0
				или СуммаОборотКт <> 0 Тогда
				
				СуммаКонДт = СуммаНачДт + СуммаОборотДт - СуммаОборотКт;
				СуммаКонКт = СуммаНачКт;
			Иначе
				СуммаКонДт = СуммаНачДт;
				СуммаКонКт = СуммаНачКт;
			КонецЕсли;
			
			Если СуммаКонДт > СуммаКонКт Тогда
				СуммаКонДт = СуммаКонДт - СуммаКонКт;
				СуммаКонКт = 0;
			Иначе
				СуммаКонКт = СуммаКонКт - СуммаКонДт;
				СуммаКонДт = 0;
			КонецЕсли;
		КонецЕсли;
		
		// Вывод конечного сальдо
		ОблСальдоКонДт.Параметры.СуммаКонДт = СуммаКонДт;
		СтруктураПараметров.ДокументРезультат.Присоединить(ОблСальдоКонДт);
		
		ОблСальдоКонКт.Параметры.СуммаКонКт = СуммаКонКт;
		СтруктураПараметров.ДокументРезультат.Присоединить(ОблСальдоКонКт);

		// Нач.сальдо следующего периода равен кон.сальдо предыдущего
		СуммаНачДт = СуммаКонДт;
		СуммаНачКт = СуммаКонКт;

	КонецЦикла;

КонецПроцедуры

Процедура ВывестиКорСчета(ПараметрыОтчета, ВыборкаОборотыПоКорСчетам, СтруктураПараметров, ВидОборота, Ресурсы, ВыводимаяОбласть, РодительскаяРасшифровка = 0)
	
	СтРесурсы = Новый Структура(Ресурсы);
	
	Если ВидОборота = "ДТ" Тогда
		ТипПоля = "ОборотДт";
		СписокСчетов = СтруктураПараметров.СписокДт;
	Иначе
		Возврат;
	КонецЕсли;
	
	// Создадим структуру соотвтетствиия кор счетов структуре ресурство
	Соотв = Новый Соответствие;
	Для каждого Элемент Из СписокСчетов Цикл
		Соотв[Элемент.Значение] = Новый Структура(Ресурсы);
	КонецЦикла;
	
	ВерхнийУровень = 1000;
	_Счет = СтруктураПараметров.Счет;
	
	// Обойдем выборку по счетам, получим все ненулевые значения ресурсов и поставим их в соответсвие счетам
	Пока ВыборкаОборотыПоКорСчетам.Следующий() Цикл
		
		// Это итог по строке остатка
		Если ВыборкаОборотыПоКорСчетам.КорСчет = NULL Тогда
			Продолжить;
		КонецЕсли;
		
		// Выводим только верхний уровень
		Если НЕ ПараметрыОтчета.ПоСубсчетамКорСчетов И ВерхнийУровень < ВыборкаОборотыПоКорСчетам.Уровень() Тогда		
			Продолжить;
		Иначе
			ВерхнийУровень = ВыборкаОборотыПоКорСчетам.Уровень();
		КонецЕсли;
		
		СтЗначенияРесурсов = Новый Структура(Ресурсы);
		Вставлять = Ложь;
		
		Для Каждого Элемент Из СтРесурсы Цикл
			Если НЕ ПривестиКЧислу(ВыборкаОборотыПоКорСчетам[Элемент.Ключ + ТипПоля]) = 0 и ВыборкаОборотыПоКорСчетам[Элемент.Ключ + ТипПоля] <> NULL Тогда
				СтЗначенияРесурсов.Вставить(Элемент.Ключ, ВыборкаОборотыПоКорСчетам[Элемент.Ключ + ТипПоля]);
				Вставлять = Истина;
			КонецЕсли;
		КонецЦикла;
		
		// Полученную структуру вставляем в соответствие счетов
		Если Вставлять Тогда
			Соотв.Вставить(ВыборкаОборотыПоКорСчетам.КорСчет, СтЗначенияРесурсов);
		КонецЕсли;
		
	КонецЦикла;

	// Вывод ресурсов
	Для каждого ЭлементСчет Из СписокСчетов Цикл
		
		Для каждого Элемент Из Соотв[ЭлементСчет.Значение] Цикл
			ВыводимаяОбласть.Параметры[Элемент.Ключ + ТипПоля] = Элемент.Значение;
		КонецЦикла;

		Если ТипЗнч(РодительскаяРасшифровка) = Тип("Соответствие") Тогда
			Расшифровка = СоздатьКопиюСоответствияСтруктуры(РодительскаяРасшифровка);
			
			Расшифровка.Вставить("КорСчет", ЭлементСчет.Значение);
			
			Если ВидОборота = "ДТ" Тогда
				Расшифровка.Вставить("СчетДт", _Счет);
				Расшифровка.Вставить("СчетКт", ЭлементСчет.Значение);
			КонецЕсли;
			
			ВыводимаяОбласть.Параметры.Расшифровка = ПолучитьСписокРасшифровок(Расшифровка);
		КонецЕсли;
		
		СтруктураПараметров.ДокументРезультат.Присоединить(ВыводимаяОбласть);
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьТаблицуРазвернутыхОстатков(ПараметрыОтчета, СтруктураПараметров)

	Если СтруктураПараметров.РазвернутоеСальдо Тогда
		ТекстПолей  = "";
		ТекстИтогов = "";
		Если СтруктураПараметров.СтрокаРазвернутогоСальдо.ПоСубсчетам Тогда
			ТекстПолей  = ТекстПолей  + ", ОстаткиИОбороты.Счет";
			ТекстИтогов = ТекстИтогов + ", Счет ИЕРАРХИЯ";
		Иначе
			Если ЗначениеЗаполнено(СтруктураПараметров.СтрокаРазвернутогоСальдо.ПоСубконто) Тогда
				ПоСубконто = СтруктураПараметров.СтрокаРазвернутогоСальдо.ПоСубконто;
				ДанныеСчета = ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтруктураПараметров.Счет);
				СписокВидовСубконто = Новый СписокЗначений;
				КоличествоСубконто = СтрДлина(ПоСубконто) / 2;
				Для Индекс = 1 По КоличествоСубконто Цикл
					Если Сред(ПоСубконто, Индекс * 2 - 1, 1) = "+" Тогда
						ТекстПолей  = ТекстПолей + ", ОстаткиИОбороты.Субконто" + Сред(ПоСубконто, Индекс*2, 1);
						ТекстИтогов = ТекстИтогов + ", Субконто" + Сред(ПоСубконто, Индекс*2, 1);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаОграниченийПоРеквизитам = "";
		ДополнитьСтрокуОграниченийПоРеквизитам(СтрокаОграниченийПоРеквизитам, "СписокОрганизаций", ПараметрыОтчета.СписокСтруктурныхЕдиниц, "Организация");
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	" + ?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.Периодичность), "", "ОстаткиИОбороты.Период КАК Период,") + "
		|	ОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокДт КАК СуммаРазвернутоКонДт,
		|	ОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокКт КАК СуммаРазвернутоКонКт,
		|	ОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт КАК СуммаРазвернутоНачДт,
		|	ОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт КАК СуммаРазвернутоНачКт"
		+ ТекстПолей + "
		|ИЗ
		|	РегистрБухгалтерии.Типовой.ОстаткиИОбороты(&ДатаНач, &ДатаКон, " + ПараметрыОтчета.ПредставленияПериодичности[ПараметрыОтчета.Периодичность] + ", , Счет В ИЕРАРХИИ (&Счет), , " + СтрокаОграниченийПоРеквизитам + ") КАК ОстаткиИОбороты
		|
		|" + ?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.Периодичность), "", "УПОРЯДОЧИТЬ ПО
		|	Период") + "
		|ИТОГИ
		|	СУММА(СуммаРазвернутоКонДт),
		|	СУММА(СуммаРазвернутоКонКт),
		|	СУММА(СуммаРазвернутоНачДт),
		|	СУММА(СуммаРазвернутоНачКт)
		|ПО
		|	ОБЩИЕ" + ?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.Периодичность), "", ", Период ПЕРИОДАМИ(" + ПараметрыОтчета.ПредставленияПериодичности[ПараметрыОтчета.Периодичность] + ", , )")
		+ ТекстИтогов;
				
		Запрос = Новый Запрос(ТекстЗапроса);
		
		Запрос.УстановитьПараметр("ДатаНач", ПараметрыОтчета.НачалоПериода);
		
		Если ПараметрыОтчета.КонецПериода <> '00010101000000' Тогда
			Запрос.УстановитьПараметр("ДатаКон", КонецДня(ПараметрыОтчета.КонецПериода));
		Иначе
			Запрос.УстановитьПараметр("ДатаКон", ПараметрыОтчета.КонецПериода);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыОтчета.СписокСтруктурныхЕдиниц);
		Запрос.УстановитьПараметр("Счет", СтруктураПараметров.Счет);
		
		ДеревоОстатков = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		СтруктураПараметров.Вставить("ДеревоОстатков", ДеревоОстатков);
		
	КонецЕсли;

КонецПроцедуры

Функция ПривестиКЧислу(Значение)
	
	Если Значение = NULL Тогда
		Возврат 0;
	Иначе
		Возврат Значение;
	КонецЕсли;
	
КонецФункции

Функция СоздатьКопиюСоответствияСтруктуры(Знач СоответствиеИсточник)
	
	Если СоответствиеИсточник = Неопределено Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	КопияСоответствия = Новый (ТипЗнч(СоответствиеИсточник));
		
	Для Каждого ЭлементОтбора Из СоответствиеИсточник Цикл
				
		КопияСоответствия.Вставить(ЭлементОтбора.Ключ, ЭлементОтбора.Значение);
				
	КонецЦикла;
	
	Возврат КопияСоответствия;
	
КонецФункции

Функция ПолучитьСписокРасшифровок(ПараметрыРасшифровки, ОтчетПоПроводкам = Истина)
	
	Если ПараметрыРасшифровки <> Неопределено Тогда
		
		СписокРасшифровки = Новый СписокЗначений;
		
		СтрокаСчета = ПараметрыРасшифровки.Получить("СчетПредставление");
		Если Не ЗначениеЗаполнено(СтрокаСчета) Тогда
			СтрокаСчета = Строка(ПараметрыРасшифровки.Получить("Счет"));
		КонецЕсли;
		
		КопияРасшифровки = СоздатьКопиюСоответствияСтруктуры(ПараметрыРасшифровки);
		КопияРасшифровки.Вставить("ИмяОбъекта", "ОборотыСчетаТиповой");
		СписокРасшифровки.Добавить(КопияРасшифровки, "Обороты счета " + СтрокаСчета);
		
		Если ОтчетПоПроводкам Тогда
			КопияРасшифровки = СоздатьКопиюСоответствияСтруктуры(ПараметрыРасшифровки);
			КопияРасшифровки.Вставить("ИмяОбъекта", "ОтчетПоПроводкамТиповой");
			СписокРасшифровки.Добавить(КопияРасшифровки, "Отчет по проводкам " + СтрокаСчета);
		КонецЕсли;
		
	Иначе
		
		СписокРасшифровки = Неопределено;
		
	КонецЕсли;
	
	Возврат СписокРасшифровки;
	
КонецФункции // ПолчитьСписокРасшифровок()

Функция ПолучитьСтрокуИзДереваОстатков(ПараметрыОтчета, СтруктураПараметров, ДатаПоиска)
	
	ДеревоОстатков = СтруктураПараметров["ДеревоОстатков"];
	
	ДатаОстатков = ?(ДатаПоиска = NULL, ПараметрыОтчета.НачалоПериода, ДатаПоиска);
	
	СтрокаОстатков = ДеревоОстатков.Строки.Найти(ДатаОстатков, "Период", Ложь);
	
	// строки на данную дату не нашли. Будем искать строкие с более ранними датами
	Если СтрокаОстатков = Неопределено Тогда
		
		Для Каждого стр Из ДеревоОстатков.Строки Цикл
			
			Если стр.Период = NULL Тогда
				Продолжить;
			КонецЕсли;
			
			Если стр.Период <= ДатаОстатков Тогда
				СтрокаОстатков = стр;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СтрокаОстатков;
	
КонецФункции // ПолучитьСтрокуИзДереваОстатков()

Функция ПолучитьСоответствиеРасшифровки(Выборка, ОтборСубконто = Неопределено)

	ПараметрыРасшифровки = Новый Соответствие;
	
	ПараметрыРасшифровки.Вставить("Счет", Выборка.Счет);
	ПараметрыРасшифровки.Вставить("СчетПредставление", Выборка.СчетПредставление);

	Если Лев(Выборка.Группировка(), СтрДлина(Выборка.Группировка()) - 1) = "Субконто" Тогда

		Если ОтборСубконто <> Неопределено Тогда
			
			ОтборСубконто.Вставить(Выборка.Группировка(), Выборка[Выборка.Группировка()]);
			
			// Область должна содержать свою копию отбора по субконто
			ОтборРасшифровка = Новый Соответствие;
			
			Для каждого ЭлементОтбора Из ОтборСубконто Цикл
			
				ОтборРасшифровка.Вставить(ЭлементОтбора.Ключ, ЭлементОтбора.Значение);
			
			КонецЦикла; 
			
			ПараметрыРасшифровки.Вставить("Отбор", ОтборРасшифровка);
			
		КонецЕсли;

	ИначеЕсли Выборка.Группировка() = "Период" Тогда
		
		Если ОтборСубконто <> Неопределено Тогда
			
			ОтборСубконто.Вставить(Выборка.Группировка(), Выборка[Выборка.Группировка()]);
			
			// Область должна содержать свою копию отбора по субконто
			ОтборРасшифровка = Новый Соответствие;
			
			Для каждого ЭлементОтбора Из ОтборСубконто Цикл
			
				ОтборРасшифровка.Вставить(ЭлементОтбора.Ключ, ЭлементОтбора.Значение);
			
			КонецЦикла; 
			
			ПараметрыРасшифровки.Вставить("Отбор", ОтборРасшифровка);
			
		КонецЕсли;
		
	ИначеЕсли Выборка.Группировка() = "Валюта" Тогда
		
		Если ОтборСубконто <> Неопределено Тогда
			
			ОтборСубконто.Вставить(Выборка.Группировка(), Выборка[Выборка.Группировка()]);
			
			// Область должна содержать свою копию отбора по субконто
			ОтборРасшифровка = Новый Соответствие;
			
			Для каждого ЭлементОтбора Из ОтборСубконто Цикл
			
				ОтборРасшифровка.Вставить(ЭлементОтбора.Ключ, ЭлементОтбора.Значение);
			
			КонецЦикла; 
			
			ПараметрыРасшифровки.Вставить("Отбор", ОтборРасшифровка);
			
		КонецЕсли;
	Иначе
		
		ОтборРасшифровка = Новый Соответствие;
		ПараметрыРасшифровки.Вставить("Отбор", ОтборРасшифровка);
		
	КонецЕсли;

	Возврат ПараметрыРасшифровки;
	
КонецФункции // ПолучитьСоответствиеРасшифровки()

Функция ОбъединитьОграничения(Знач Ограничение1, Знач Ограничение2, Знач СтрокаОбъединенияОграничений = "И")
	
	Если ПустаяСтрока(Ограничение1) Тогда
		Возврат Ограничение2;
	КонецЕсли;
	
	Если ПустаяСтрока(Ограничение2) Тогда
		Возврат Ограничение1;
	КонецЕсли;
	
	СтрокаОграничения = Ограничение1 + " " + СтрокаОбъединенияОграничений + " " + Ограничение2;
	
	Возврат СтрокаОграничения;
	
КонецФункции


#КонецЕсли