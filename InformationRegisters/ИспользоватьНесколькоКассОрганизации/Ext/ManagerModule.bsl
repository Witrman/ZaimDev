#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

Функция УстановитьЗначенияФО() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	СУММА(ВЫБОР
	|			КОГДА Кассы.Ссылка ЕСТЬ NULL 
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Количество
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Кассы КАК Кассы
	|		ПО Организации.Ссылка = Кассы.Владелец
	|
	|СГРУППИРОВАТЬ ПО
	|	Организации.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(Выборка.Организация) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ИспользоватьНесколькоКассОрганизации.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Значение = Выборка.Организация;
		НаборЗаписей.Отбор.Организация.Использование = Истина;
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация  = Выборка.Организация;
		Запись.Используется = ?(Выборка.Количество < 2, Ложь, Истина);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		
	КонецЦикла;
	
КонецФункции

Функция ОтказИнтерактивногоИзменения(Знач Организация) Экспорт
	
	Возврат Справочники.Кассы.КоличествоКассОрганизации(Организация) <> 1;
	
КонецФункции

#КонецЕсли