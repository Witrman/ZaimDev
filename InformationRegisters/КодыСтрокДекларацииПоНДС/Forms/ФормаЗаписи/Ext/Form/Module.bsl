
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьНаименованиеКодаСтроки();
	мМакетКодовСтрок = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КодыСтрокНалоговыхДеклараций");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
		
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.ФормаВыбораИзКлассификатора") Тогда
		
		Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда 
			Запись.КодСтроки = ВыбранноеЗначение;
		Иначе 
			Запись.КодСтроки = ВыбранноеЗначение.КодСтроки;
			НаименованиеКодаСтроки = ВыбранноеЗначение.Наименование;
		КонецЕсли;
		
		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура КодСтрокиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИмяМакета"			  , "КодыСтрокНалоговыхДеклараций");
	ПараметрыФормы.Вставить("ИмяСекции"			  ,	"НДС_Реализация");
	ПараметрыФормы.Вставить("ПолучатьПолныеДанные", Истина);
	ТекущийКодСтроки = ?(НЕ ЗначениеЗаполнено(Запись.КодСтроки), Неопределено, СокрЛП(Запись.КодСтроки));
	ПараметрыФормы.Вставить("ТекущийКодСтроки"	  , ТекущийКодСтроки);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораИзКлассификатора", ПараметрыФормы, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КодСтрокиПриИзменении(Элемент)
	
	Запись.КодСтроки = РегламентированнаяОтчетностьКлиентСервер.ОбрезатьКодСтроки(Запись.КодСтроки);
	УстановитьНаименованиеКодаСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	 Запись.Период = НачалоКвартала(Запись.Период);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура УстановитьНаименованиеКодаСтроки()
	
	Если ПустаяСтрока(СтрЗаменить(Запись.КодСтроки, ".", "")) Тогда
		
		НаименованиеКодаСтроки = НСтр("ru = 'Код строки не указан.'");
		
	Иначе
		
		мМакетКодовСтрок = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КодыСтрокНалоговыхДеклараций");
		ОбластьСтрок = мМакетКодовСтрок.Области.Найти("НДС_Реализация");
		
		КодОсновнойСтроки = Лев(Запись.КодСтроки,10);
		ИмяОбластиМакета = "НДС_Реализация_" + СтрЗаменить(КодОсновнойСтроки, ".", "_");
		ОбластьДополнительныхСтрок = мМакетКодовСтрок.Области.Найти(ИмяОбластиМакета);
		
		Если ОбластьДополнительныхСтрок = Неопределено Тогда
			НаименованиеКодаСтроки = РегламентированнаяОтчетность.ПолучитьНаименованиеСтрокиКлассификатораПоКоду(мМакетКодовСтрок, ОбластьСтрок, Запись.КодСтроки);	
			
		Иначе	
			КодДополнительнойСтроки = Прав(Запись.КодСтроки, СтрДлина(Запись.КодСтроки) - 11);
			
			НаименованиеКодаСтроки = РегламентированнаяОтчетность.ПолучитьНаименованиеСтрокиКлассификатораПоКоду(мМакетКодовСтрок, ОбластьДополнительныхСтрок, КодДополнительнойСтроки);
		КонецЕсли;
		
		Если ПустаяСтрока(НаименованиеКодаСтроки) Тогда
			НаименованиеКодаСтроки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Строка с кодом ""%1"" не найдена.'"), СокрЛП(Запись.КодСтроки));
		Иначе
			НаименованиеКодаСтроки = НаименованиеКодаСтроки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры