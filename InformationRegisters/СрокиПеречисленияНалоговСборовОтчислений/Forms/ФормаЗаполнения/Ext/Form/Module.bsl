
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	ДатаНачала = НачалоМесяца(ДатаНачала);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	ДатаОкончания = КонецМесяца(ДатаОкончания);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <ИМЯ ТАБЛИЦЫ ФОРМЫ>

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура УстановитьФлаги(Команда)
	
	Для Каждого ЭлементКоллекции Из СписокОрганизаций Цикл
		ЭлементКоллекции.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлаги(Команда)
	
	Для Каждого ЭлементКоллекции Из СписокОрганизаций Цикл
		ЭлементКоллекции.Пометка = Ложь;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Инвертировать(Команда)
	
	Для Каждого ЭлементКоллекции Из СписокОрганизаций Цикл
		ЭлементКоллекции.Пометка = Не ЭлементКоллекции.Пометка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода", ДатаНачала, ДатаОкончания);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеРеквизиов(Отказ);
	
	Если Не Отказ Тогда
		
		СоздатьЗаписиРегистраНаСервере();	
	
		Оповестить("ОбновленыСрокиПеречисления", , ЭтаФорма);
	
		Закрыть();
		
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	СпособЗаполненияСледМес  = 1; 
	СпособЗаполненияСледКв 	 = 2;
	СпособЗаполненияОктИМарт = 3;

	ЗаполнитьСписокОрганизаций();
	
	// значения по умолчанию
	ВидНалога = Справочники.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносы;
	
	ДатаНачала 		= НачалоГода(ОбщегоНазначения.ТекущаяДатаПользователя());
	ДатаОкончания 	= КонецГода(ОбщегоНазначения.ТекущаяДатаПользователя());
	
	СпособЗаполнения = 1; //След месяц.;
	
	ДеньОкончания1 	 = 20;
	ДеньОкончания2   = 25;
	ДеньОкончания3_1 = 25;
	ДеньОкончания3_2 = 25;
	
    УстановитьПараметрыЗаполненияПоУмолчанию(ВидНалога, ПорядокОпределенияСрокаПеречисления);
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьСписокОрганизаций()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК Приоритет,
	|	ИСТИНА КАК Пометка,
	|	""Все организации"" КАК ОрганизацияНаименование, 
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2 КАК Приоритет,
	|	ИСТИНА КАК Пометка,
	|	Организации.Наименование КАК ОрганизацияНаименование,
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	ОрганизацияНаименование
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
	    НоваяСтрока = СписокОрганизаций.Добавить();
	    ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;

КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыЗаполненияПоУмолчанию(ВидНалога, ПорядокОпределенияСрокаПеречисления)

	Если ВидНалога = ПредопределенноеЗначение("Справочник.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносы") Тогда
		ПорядокОпределенияСрокаПеречисления = ПредопределенноеЗначение("Перечисление.ПорядокОпределенияСрокаПеречисления.ПоМесяцуВыплатыДоходов");
	Иначе
		ПорядокОпределенияСрокаПеречисления = ПредопределенноеЗначение("Перечисление.ПорядокОпределенияСрокаПеречисления.ПоМесяцуНалоговогоПериода");
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачала	  = НачалоМесяца(РезультатВыбора.НачалоПериода);
	ДатаОкончания = КонецМесяца(РезультатВыбора.КонецПериода);
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизиов(Отказ)
	
	// видНалога 
	Если НЕ ЗначениеЗаполнено(ВидНалога) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указан вид налога, сбора, отчисления!'"),, "ВидНалога", "Форма");
		Отказ = Истина;
	КонецЕсли;

	// ПорядокОпределенияСрокаПеречисления 
	Если НЕ ЗначениеЗаполнено(ПорядокОпределенияСрокаПеречисления) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указан порядок определения срока перечисления!'"),, "ПорядокОпределенияСрокаПеречисления", "Форма");
		Отказ = Истина;
	КонецЕсли;

	// проверим, чтобы были отмечены организации
	Если СписокОрганизаций.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не отмечена ни одна организация для заполнения!'"),, "СписокОрганизаций", "Форма");
		Отказ = Истина;
	КонецЕсли;
	
	// период заполнения
	Если НЕ ЗначениеЗаполнено(ДатаНачала) Или НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указан период заполнения!'"));
		Отказ = Истина;
	КонецЕсли;
	
	Если ДатаНачала > ДатаОкончания Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Дата начала периода заполнения не может быть больше даты окончания!'"),,"ДатаНачала","Форма");
		Отказ = Истина;
	КонецЕсли;
	
	Если СпособЗаполнения = 1 Тогда //След. месяц
		
		Если НЕ ЗначениеЗаполнено(ДеньОкончания1) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указано окончание срока - день следующего месяца!'"),,"ДеньОкончания1","Форма");
			Отказ = Истина;
		КонецЕсли;
		
	ИначеЕсли СпособЗаполнения = 2 Тогда //След. квартал 
		
		Если НЕ ЗначениеЗаполнено(ДеньОкончания2) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указано окончание срока - день месяца, следующего квартала!'"),,"ДеньОкончания2","Форма");
			Отказ = Истина;
		КонецЕсли;
		
	ИначеЕсли СпособЗаполнения = 3 Тогда //октябрь - март
		
		Если НЕ ЗначениеЗаполнено(ДеньОкончания3_1) Или НЕ ЗначениеЗаполнено(ДеньОкончания3_2) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указано окончание срока - числа октября и/или марта!'"));
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура СоздатьЗаписиРегистраНаСервере()

	ДлинаСуток = 86400;
	
	// Если стоят флажки на всех организациях, то создаем записи только по пустой (все) организации
	КоличествоОтмеченных = 0;
	ОбщееКоличество = 0;
	Для Каждого ЭлементКоллекции ИЗ СписокОрганизаций Цикл
		
		Если ЗначениеЗаполнено(ЭлементКоллекции.Организация) Тогда
			
			ОбщееКоличество = ОбщееКоличество + 1;
			Если ЭлементКоллекции.Пометка Тогда
				КоличествоОтмеченных = КоличествоОтмеченных + 1;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОбщееКоличество = КоличествоОтмеченных Тогда
		ПоВсемОрганизациям = ?(ОбщееКоличество = 1, Ложь, Истина);
	Иначе
		ПоВсемОрганизациям = Ложь;
	КонецЕсли;

	НаборЗаписей = РегистрыСведений.СрокиПеречисленияНалоговСборовОтчислений.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.ВидНалога.Использование 	= Истина;
	НаборЗаписей.Отбор.ВидНалога.ВидСравнения	= ВидСравнения.Равно;
	НаборЗаписей.Отбор.ВидНалога.Значение 		= ВидНалога;

	НаборЗаписей.Отбор.Организация.Использование= Истина;
	НаборЗаписей.Отбор.Организация.ВидСравнения	= ВидСравнения.Равно;
	
	НаборЗаписей.Отбор.Месяц.Использование 		= Истина;
	НаборЗаписей.Отбор.Месяц.ВидСравнения		= ВидСравнения.Равно;
	
	Для Каждого ЭлементКоллекции Из СписокОрганизаций Цикл
		
		МожноСоздавать = ЭлементКоллекции.Пометка;
		Если ПоВсемОрганизациям И ЗначениеЗаполнено(ЭлементКоллекции.Организация) Тогда
			МожноСоздавать = Ложь; // надо только по пустой организации, а это какая-то конкретная
		КонецЕсли;
		
		Если МожноСоздавать Тогда
		
			НаборЗаписей.Отбор.Организация.Значение = ЭлементКоллекции.Организация;
		
			ТекМесяц = НачалоМесяца(ДатаНачала);
			Пока ТекМесяц <= НачалоМесяца(ДатаОкончания) Цикл
			
				// пробегаемся по всем месяцам и определяем соответствующий срок
				НаборЗаписей.Отбор.Месяц.Значение = ТекМесяц;
				
				ЗаписьНабора = НаборЗаписей.Добавить();
				
				ЗаписьНабора.Месяц 			= ТекМесяц;
				ЗаписьНабора.ВидНалога		= ВидНалога;
				ЗаписьНабора.Организация 	= ЭлементКоллекции.Организация;
				
				ЗаписьНабора.ПорядокОпределенияСрокаПеречисления = ПорядокОпределенияСрокаПеречисления;				
				Если СпособЗаполнения = 1 Тогда //след. месяц
					ЗаписьНабора.СрокПеречисления = ДобавитьМесяц(ТекМесяц, 1) + (ДеньОкончания1 - 1) * ДлинаСуток;
				ИначеЕсли СпособЗаполнения = 2 Тогда //след. квартал
					ЗаписьНабора.СрокПеречисления = ДобавитьМесяц(НачалоКвартала(ТекМесяц), 3) + (ДеньОкончания2 - 1) * ДлинаСуток;
				Иначе
					Если Месяц(ТекМесяц) < 10 Тогда
						ЗаписьНабора.СрокПеречисления = Дата(Год(ТекМесяц), 10, 1) + (ДеньОкончания3_1 - 1) * ДлинаСуток;
					Иначе
						ЗаписьНабора.СрокПеречисления = Дата(Год(ТекМесяц) + 1, 3, 1) + (ДеньОкончания3_2 - 1) * ДлинаСуток;
					КонецЕсли;
				КонецЕсли;
				
				НаборЗаписей.Записать();
				НаборЗаписей.Очистить();
				
				ТекМесяц = ДобавитьМесяц(ТекМесяц, 1);
			
			КонецЦикла;			
		
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры
 