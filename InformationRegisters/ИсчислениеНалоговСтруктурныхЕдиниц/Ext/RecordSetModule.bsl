
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого ЗаписьНабора Из ЭтотОбъект Цикл
		// подразделение, указываемое в регистре должно иметь признак Структурного подразделения
		Если ТипЗнч(ЗаписьНабора.СтруктурнаяЕдиница) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
			Если ЗаписьНабора.СтруктурнаяЕдиница.ЯвляетсяСтруктурнымПодразделением = Ложь Тогда
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 не отмечено в справочнике подразделений организаций как структурное!'"), ЗаписьНабора.СтруктурнаяЕдиница);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СтруктурнаяЕдиница", "Запись", Отказ);
				
			КонецЕсли;
		КонецЕсли;
					
		// Для НДС и КПН структурным подразделением не может являться подразделение
		Если ЗаписьНабора.РазделНалоговогоУчета = Перечисления.РазделыНалоговогоУчета.НДС
				Или ЗаписьНабора.РазделНалоговогоУчета = Перечисления.РазделыНалоговогоУчета.КПН Тогда
				
			Если ТипЗнч(ЗаписьНабора.СтруктурнаяЕдиница) <> Тип("СправочникСсылка.Организации") Тогда
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 - %2 : в качестве структурной единицы может выступать только организация!'"), ЗаписьНабора.СтруктурнаяЕдиница, ЗаписьНабора.РазделНалоговогоУчета);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СтруктурнаяЕдиница", "Запись", Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
				
	КонецЦикла;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

#КонецЕсли

