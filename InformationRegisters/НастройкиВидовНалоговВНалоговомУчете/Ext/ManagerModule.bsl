#Область ПрограммныйИнтерфейс

Функция ПолучитьНастройки(ВидНалога, Период = Неопределено) Экспорт
	
	Результат = Новый Структура;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	0 КАК ЗначениеПоУмолчанию,
	|	Настройки.Период КАК Период,
	|	Настройки.ОтноситьНаВычетыВПределахУплаченнойСуммыНалога КАК ОтноситьНаВычетыВПределахУплаченнойСуммыНалога
	|ИЗ
	|	РегистрСведений.НастройкиВидовНалоговВНалоговомУчете.СрезПоследних(&Период, ВидНалога = &ВидНалога) КАК Настройки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	ДАТАВРЕМЯ(1980, 1, 1),
	|	ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗначениеПоУмолчанию");
	
	Запрос.УстановитьПараметр("Период"   , ?(ЗначениеЗаполнено(Период), Период, ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ВидНалога", ВидНалога);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Результат.Вставить("Период", Выборка.Период);
	Результат.Вставить("ОтноситьНаВычетыВПределахУплаченнойСуммыНалога",  Выборка.ОтноситьНаВычетыВПределахУплаченнойСуммыНалога);
	
	Возврат Результат
	
КонецФункции

Процедура УстановитьНастройки(ВидНалога, Настройки, Период = Неопределено) Экспорт
	
	ТекущиеНастройки = ПолучитьНастройки(ВидНалога, Период);
	
	ПризнакМодифицированностиЗаписи = Ложь;
	Для Каждого ЭлементНастройки Из Настройки Цикл
		Если Не ТекущиеНастройки.Свойство(ЭлементНастройки.Ключ) Или ТекущиеНастройки[ЭлементНастройки.Ключ] <> ЭлементНастройки.Значение Тогда
			ПризнакМодифицированностиЗаписи = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПризнакМодифицированностиЗаписи Тогда
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = Период;
		МенеджерЗаписи.ВидНалога = ВидНалога;
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Настройки);
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти