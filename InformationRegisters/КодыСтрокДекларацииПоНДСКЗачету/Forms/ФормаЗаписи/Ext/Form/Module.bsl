
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//обновление наименования кода строки
	ОбновитьПредставлениеЭлемента("КодСтрокиДекларации");

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
		
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.ФормаВыбораИзКлассификатора") Тогда
		
		 
		Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда 
			Запись.КодСтроки = ВыбранноеЗначение;
		Иначе 
			Запись.КодСтроки = ВыбранноеЗначение.КодСтроки;
			Элементы.ДекорацияРасшифровкаКодСтроки.Заголовок = ВыбранноеЗначение.Наименование;
		КонецЕсли;
		
		Модифицированность = Истина;
		
	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура КодСтрокиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИмяМакета"			  , "КодыСтрокНалоговыхДеклараций");
	ПараметрыФормы.Вставить("ИмяСекции"			  ,	"НДС_Зачет");
	ПараметрыФормы.Вставить("ПолучатьПолныеДанные", Истина);
	ТекущийКодСтроки = ?(НЕ ЗначениеЗаполнено(Запись.КодСтроки), Неопределено, СокрЛП(Запись.КодСтроки));
	ПараметрыФормы.Вставить("ТекущийКодСтроки"	  , ТекущийКодСтроки);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораИзКлассификатора", ПараметрыФормы, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КодСтрокиПриИзменении(Элемент)
	
	Запись.КодСтроки = РегламентированнаяОтчетностьКлиентСервер.ОбрезатьКодСтроки(Запись.КодСтроки);
	ОбновитьПредставлениеЭлемента("КодСтрокиДекларации");
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Запись.Период = НачалоКвартала(Запись.Период);
			
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОбновитьПредставлениеЭлемента(ИмяОбновляемогоЭлемента)
	
	Если ИмяОбновляемогоЭлемента = "КодСтрокиДекларации" Тогда
		
		Если ПустаяСтрока(СтрЗаменить(Запись.КодСтроки, ".", "")) Тогда
			Элементы.ДекорацияРасшифровкаКодСтроки.Заголовок = НСтр("ru ='<не указано>'");
		Иначе
			
			Если мМакетКодовСтрок = Неопределено Тогда
				мМакетКодовСтрок 	= УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КодыСтрокНалоговыхДеклараций");	 			
			КонецЕсли;
			мОбластьСтрок = мМакетКодовСтрок.Области.Найти("НДС_Зачет");
			
			КодОсновнойСтроки = Лев(Запись.КодСтроки,10);
			ИмяОбластиМакета = "НДС_Зачет_" + СтрЗаменить(КодОсновнойСтроки, ".", "_");
			ОбластьДополнительныхСтрок = мМакетКодовСтрок.Области.Найти(ИмяОбластиМакета);
			
			Если ОбластьДополнительныхСтрок = Неопределено Тогда
				НаименованиеСтроки = РегламентированнаяОтчетность.ПолучитьНаименованиеСтрокиКлассификатораПоКоду(мМакетКодовСтрок, мОбластьСтрок, Запись.КодСтроки);	
				
			Иначе	
				КодДополнительнойСтроки = Прав(Запись.КодСтроки, СтрДлина(Запись.КодСтроки) - 11);
				
				НаименованиеСтроки = РегламентированнаяОтчетность.ПолучитьНаименованиеСтрокиКлассификатораПоКоду(мМакетКодовСтрок, ОбластьДополнительныхСтрок, КодДополнительнойСтроки);
			КонецЕсли;
		
			Если ПустаяСтрока(НаименованиеСтроки) Тогда
				Элементы.ДекорацияРасшифровкаКодСтроки.Заголовок = НСтр("ru ='строка с кодом " + СокрЛП(Запись.КодСтроки) + " не найдена.'");
			Иначе
				Элементы.ДекорацияРасшифровкаКодСтроки.Заголовок = НаименованиеСтроки;
			КонецЕсли;
			
			
		КонецЕсли;
	
	КонецЕсли;
	       
КонецПроцедуры // ОбновитьПредставлениеЭлемента()





