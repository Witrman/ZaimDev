
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияБКВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтотОбъект);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ СПИСОК

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ОбщегоНазначенияБККлиентСервер.ВосстановитьОтборСписка(Список, Настройки, "Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если НЕ Элементы.ФормаИстория.Пометка Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("Ключ", Элемент.ТекущиеДанные.Сотрудник);
		ОткрытьФорму("Справочник.СотрудникиОрганизаций.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПриемНаРаботу(Команда)

	ОткрытьФорму("Документ.ПриемНаРаботуВОрганизацию.ФормаОбъекта");
	
КонецПроцедуры

&НаКлиенте
Процедура КадровоеПеремещение(Команда)
	
	ПараметрыЗаполнения = Новый Структура;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПараметрыЗаполнения.Вставить("Основание", ТекущиеДанные.Сотрудник);
	КонецЕсли;
	
	ОткрытьФорму("Документ.КадровоеПеремещениеОрганизаций.ФормаОбъекта", ?(ПараметрыЗаполнения.Количество() > 0, ПараметрыЗаполнения, Неопределено));
	
КонецПроцедуры

&НаКлиенте
Процедура Увольнение(Команда)
	
	ПараметрыЗаполнения = Новый Структура;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПараметрыЗаполнения.Вставить("Основание", ТекущиеДанные.Сотрудник);
	КонецЕсли;

	ОткрытьФорму("Документ.УвольнениеИзОрганизаций.ФормаОбъекта", ?(ПараметрыЗаполнения.Количество() > 0, ПараметрыЗаполнения, Неопределено));
	
КонецПроцедуры

&НаКлиенте
Процедура История(Команда)

	ИсторияОбработатьНажатиеНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ИсторияОбработатьНажатиеНаСервере()
	
	Если Элементы.ФормаИстория.Пометка Тогда
		Элементы.ФормаИстория.Пометка = Ложь;
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "РегистрСведений.РаботникиОрганизаций", "РегистрСведений.РаботникиОрганизаций.СрезПоследних");
		Список.ОсновнаяТаблица = "РегистрСведений.РаботникиОрганизаций.СрезПоследних";
	Иначе 
		Элементы.ФормаИстория.Пометка = Истина;
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "РегистрСведений.РаботникиОрганизаций.СрезПоследних", "РегистрСведений.РаботникиОрганизаций");
		Список.ОсновнаяТаблица = "РегистрСведений.РаботникиОрганизаций";
	КонецЕсли;
	
КонецПроцедуры