////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		ПроцентСН 	= ПроцедурыНалоговогоУчета.ПолучитьСтавкуНалога(Неопределено, Справочники.НалогиСборыОтчисления.СоциальныйНалог, Запись.Период);
		ПроцентСО	= ПроцедурыНалоговогоУчета.ПолучитьСтавкуНалога(Неопределено, Справочники.НалогиСборыОтчисления.ОбязательныеСоциальныеОтчисления, Запись.Период);
		ПроцентОПВ	= ПроцедурыНалоговогоУчета.ПолучитьСтавкуНалога(Неопределено, Справочники.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносы, Запись.Период);
		ПроцентОПВР = ПроцедурыНалоговогоУчета.ПолучитьСтавкуНалога(Неопределено, Справочники.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносыРаботодателя, Запись.Период);
		ПроцентООСМС= ПроцедурыНалоговогоУчета.ПолучитьСтавкуОСМС(Запись.Период, "Отчисления");
		
		Запись.СчетУчета = ПредопределенноеЗначение("ПланСчетов.Типовой.КраткосрочныеОценочныеОбязательстваПоВознаграждениямРаботникам"); // 3430
		
		// по умолчанию процент социального налога и отчислений берем за минусом ОПВ, а СН еще и за минусом соц. отчислений
		Запись.РазмерСоциальногоНалога = (ПроцентСН - ПроцентСО) * (1 - ПроцентОПВ / 100);
		Запись.РазмерСоциальныхОтчислений = ПроцентСО * (1 - ПроцентОПВ / 100);
		Запись.РазмерОтчисленийОСМС = ПроцентООСМС;
		Запись.РазмерПенсионныхВзносовРаботодателя = ПроцентОПВР;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ПроцентСН 	= ПроцедурыНалоговогоУчета.ПолучитьСтавкуНалога(Запись.Организация, Справочники.НалогиСборыОтчисления.СоциальныйНалог, Запись.Период);
	ПроцентСО	= ПроцедурыНалоговогоУчета.ПолучитьСтавкуНалога(Запись.Организация, Справочники.НалогиСборыОтчисления.ОбязательныеСоциальныеОтчисления, Запись.Период);
	ПроцентОПВ	= ПроцедурыНалоговогоУчета.ПолучитьСтавкуНалога(Запись.Организация, Справочники.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносы, Запись.Период);
	ПроцентОПВР = ПроцедурыНалоговогоУчета.ПолучитьСтавкуНалога(Запись.Организация, Справочники.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносыРаботодателя, Запись.Период);
	ПроцентООСМС= ПроцедурыНалоговогоУчета.ПолучитьСтавкуОСМС(Запись.Период, "Отчисления",,Запись.Организация);	
	
	// по умолчанию процент социального налога и отчислений берем за минусом ОПВ, а СН еще и за минусом соц. отчислений
	Запись.РазмерСоциальногоНалога = (ПроцентСН - ПроцентСО) * (1 - ПроцентОПВ / 100);
	Запись.РазмерСоциальныхОтчислений = ПроцентСО * (1 - ПроцентОПВ / 100);
	Запись.РазмерОтчисленийОСМС = ПроцентООСМС;
	Запись.РазмерПенсионныхВзносовРаботодателя = ПроцентОПВР;

КонецПроцедуры
