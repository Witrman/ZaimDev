
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	РезультатЗапросаПоНаборуЗаписей   = СформироватьЗапросПоНаборуЗаписей();
	Выборка = РезультатЗапросаПоНаборуЗаписей.Выбрать();
	
	ПредыдущийПредел = 0;
	ПредыдущаяСтрока = Неопределено;
	ПредыдущийНомерСтроки = 0;

	Пока Выборка.Следующий() Цикл
		Запись.СуммаДоходаС = Выборка.СуммаДоходаПо;
		Запись.НомерСтрокиСтавок = Выборка.НомерСтрокиСтавок + 1;
		//Расчет суммы фиксированной ставки в тенге от процентов предыдущего предела
		Если Запись.НомерСтрокиСтавок <> 1 Тогда
			Если НЕ ЗначениеЗаполнено(Запись.СуммаНалогаПредыдущегоПредела) Тогда
				Запись.СуммаНалогаПредыдущегоПредела = Окр(Выборка.СуммаНалогаПредыдущегоПредела + (Выборка.СуммаДоходаПо - Выборка.СуммаДоходаС) * Выборка.Ставка / 100, 0);				
			КонецЕсли;
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СформироватьЗапросПоНаборуЗаписей()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВидСтавокИПНиСН",	Запись.ВидСтавокИПНиСН);
	Запрос.УстановитьПараметр("Период",				Запись.Период);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СведенияОСтавкахИПНиСН.НомерСтрокиСтавок,
	|	СведенияОСтавкахИПНиСН.СуммаДоходаС,
	|	СведенияОСтавкахИПНиСН.СуммаДоходаПо КАК СуммаДоходаПо,
	|	СведенияОСтавкахИПНиСН.СуммаНалогаПредыдущегоПредела,
	|	СведенияОСтавкахИПНиСН.Ставка
	|ПОМЕСТИТЬ ВТ_НаборЗаписиОтбор
	|ИЗ
	|	РегистрСведений.СведенияОСтавкахИПНиСН КАК СведенияОСтавкахИПНиСН
	|ГДЕ
	|	СведенияОСтавкахИПНиСН.ВидСтавокИПНиСН = &ВидСтавокИПНиСН
	|	И СведенияОСтавкахИПНиСН.Период = &Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	МАКСИМУМ(ВТ_НаборЗаписиОтбор.НомерСтрокиСтавок) КАК НомерСтрокиСтавок,
	|	ВТ_НаборЗаписиОтбор.СуммаДоходаС,
	|	ВТ_НаборЗаписиОтбор.СуммаДоходаПо,
	|	ВТ_НаборЗаписиОтбор.СуммаНалогаПредыдущегоПредела,
	|	ВТ_НаборЗаписиОтбор.Ставка
	|ПОМЕСТИТЬ ВТ_НаборЗаписиПоследний
	|ИЗ
	|	ВТ_НаборЗаписиОтбор КАК ВТ_НаборЗаписиОтбор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_НаборЗаписиОтбор.СуммаДоходаС,
	|	ВТ_НаборЗаписиОтбор.СуммаДоходаПо,
	|	ВТ_НаборЗаписиОтбор.СуммаНалогаПредыдущегоПредела,
	|	ВТ_НаборЗаписиОтбор.Ставка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиСтавок УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НаборЗаписиПоследний.НомерСтрокиСтавок,
	|	ВТ_НаборЗаписиПоследний.СуммаДоходаС,
	|	ВТ_НаборЗаписиПоследний.СуммаДоходаПо,
	|	ВТ_НаборЗаписиПоследний.СуммаНалогаПредыдущегоПредела,
	|	ВТ_НаборЗаписиПоследний.Ставка
	|ИЗ
	|	ВТ_НаборЗаписиПоследний КАК ВТ_НаборЗаписиПоследний";
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить();
	
КонецФункции
