////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Период = ПолучитьПоследнийПериодЗаписей();
	УстановитьОтборыДинамическогоСписка(Список, Период);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ДекорацияСтавки2009Нажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.СведенияОСтавкахНалоговСборовОтчислений.ФормаСписка");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборыДинамическогоСписка(Список, Период)
	

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, 
																			"ВидСтавокИПНиСН", 
																			ПредопределенноеЗначение("Перечисление.ВидыСтавокИПНиСН.ПоИсчислениюИПН"), 
																			ВидСравненияКомпоновкиДанных.Равно, 
																			, 
																			Истина, 
																			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
																			
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, 
																			"Период", 
																			Период, 
																			ВидСравненияКомпоновкиДанных.Равно, 
																			, 
																			Истина, 
																			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
КонецПроцедуры

&НаСервере
Функция ПолучитьПоследнийПериодЗаписей()
	
	ПоследнийПериод = Макс(Дата(2005, 01, 01), НачалоГода(ОбщегоНазначения.ТекущаяДатаПользователя()));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаАктуальности", ПоследнийПериод);
	Запрос.УстановитьПараметр("ВидСтавокИПНиСН",  ПредопределенноеЗначение("Перечисление.ВидыСтавокИПНиСН.ПоИсчислениюИПН"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СведенияОСтавках.Период
	|ИЗ
	|	РегистрСведений.СведенияОСтавкахИПНиСН.СрезПоследних(&ДатаАктуальности, ВидСтавокИПНиСН = &ВидСтавокИПНиСН) КАК СведенияОСтавках
	|УПОРЯДОЧИТЬ ПО
	|	СведенияОСтавках.НомерСтрокиСтавок
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПоследнийПериод = Выборка.Период;
	КонецЕсли;
		
	Возврат ПоследнийПериод;
	
КонецФункции
