#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолучитьСписокВыбораВидаДохода(Запись.Период);	
	УправлениеВидимостью(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Запись.ВидДохода = Неопределено;
	ПолучитьСписокВыбораВидаДохода(Запись.Период);
	УправлениеВидимостью(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ВидНалогаПриИзменении(Элемент)
	
	Запись.ВидДохода = Неопределено;
	ПолучитьСписокВыбораВидаДохода(Запись.Период);
	УправлениеВидимостью(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокВыбораВидаДохода(Период)
	
	Элементы.ВидДохода.СписокВыбора.Очистить();
	
	Элементы.ВидДохода.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо"), НСтр("ru = 'Доходы от работодателя'"));	
	Элементы.ВидДохода.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"),  НСтр("ru = 'Доходы от налогового агента'"));
	
	Элементы.ВидДохода.ОбновитьТекстРедактирования();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостью(Форма)

	Запись		= Форма.Запись;
	Элементы	= Форма.Элементы;
	
	Если Запись.Период >= '20200101'
		И (Запись.ВидНалога = ПредопределенноеЗначение("Справочник.НалогиСборыОтчисления.ИндивидуальныйПодоходныйНалог")
		ИЛИ Запись.ВидНалога = ПредопределенноеЗначение("Справочник.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносы")) Тогда
		Элементы.ВидДохода.Видимость	  = Истина;
	Иначе
		Элементы.ВидДохода.Видимость	  = Ложь;
	КонецЕсли;
		
КонецПроцедуры


