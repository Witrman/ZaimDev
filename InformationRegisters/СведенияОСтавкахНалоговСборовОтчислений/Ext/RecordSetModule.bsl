#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Процедура - обработчик события "ПередЗаписью"
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат
	КонецЕсли;
	
	Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
		
		Если СтрокаНабора.Период < Дата('20200101')
			И ЗначениеЗаполнено(СтрокаНабора.ВидДохода) Тогда
			
			ВызватьИсключение НСтр("ru = 'Сведения о ставках налогов, сборов и отчислений в разрезе вида дохода указываются только с 01.01.2020 года.'");
			
		КонецЕсли;
		
		Если СтрокаНабора.Период >= Дата('20200101')
			И ЗначениеЗаполнено(СтрокаНабора.ВидДохода) 
			И СтрокаНабора.ВидНалога <> ПредопределенноеЗначение("Справочник.НалогиСборыОтчисления.ИндивидуальныйПодоходныйНалог")
		    И СтрокаНабора.ВидНалога <> ПредопределенноеЗначение("Справочник.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносы") Тогда
			
			ВызватьИсключение НСтр("ru = 'Сведения о ставках налогов, сборов и отчислений в разрезе вида дохода указываются только для налогов: ОПВ и ИПН с 01.01.2020 года.'");
			
		КонецЕсли;

		Если СтрокаНабора.ВидНалога = Справочники.НалогиСборыОтчисления.ЕдиныйПлатеж Тогда
			
			Если СтрокаНабора.Период < Дата('20230101') Тогда
				
				ВызватьИсключение НСтр("ru = 'Сведения о ставке единого платежа указываются с 01.01.2023 года.'");
				
			ИначеЕсли СтрокаНабора.ВидДохода = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
				
				ВызватьИсключение НСтр("ru = 'Сведения о ставке единого платежа указываются только для дохода от работодателя.'");
				
			КонецЕсли;
			
		КонецЕсли;

		Если СтрокаНабора.ВидНалога = Справочники.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносыРаботодателя Тогда
			
			Если СтрокаНабора.Период < Дата('20240101') Тогда
				
				ВызватьИсключение НСтр("ru = 'Сведения о ставке обязательных пенсионных взносов за счет работодателя указываются с 01.01.2024 года.'");
				
			КонецЕсли;
			
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли
