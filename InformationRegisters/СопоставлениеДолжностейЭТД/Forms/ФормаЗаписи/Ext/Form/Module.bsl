
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Запись, Параметры);
	
	ИспользоватьШтатноеРасписание = ЭТДСервер.ИспользоватьШтатноеРасписание();
	Элементы.ДолжностьПоШтатномуРасписанию.Видимость = ИспользоватьШтатноеРасписание;
	Элементы.ДолжностьОрганизации.Видимость = НЕ ИспользоватьШтатноеРасписание;

	ЭТДСервер.ПриСозданииНаСервереСопоставлениеДолжностейЭТД(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Если ВладелецФормы <> Неопределено Тогда
		ОповеститьОВыборе(Запись.КодДолжностиЭТД);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЭТДСервер.ПриЧтенииНаСервереРегистраСведений(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЭТДСервер.ПослеЗаписиНаСервереРегистраСведений(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодДолжностиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидКлассификатора",  "ПрофессийXML");
	ПараметрыФормы.Вставить("Заголовок",   НСтр("ru = 'Классификатор профессий'"));
	ПараметрыФормы.Вставить("Уровень",     1);
	ПараметрыФормы.Вставить("ВыборГруппы", Ложь);
	ПараметрыФормы.Вставить("ЗначениеВыбора", Запись.КодДолжностиЭТД);
	
	ОткрытьФорму("Обработка.ОбменЭТД.Форма.ФормаПодбораКлассификатора",
	ПараметрыФормы,
	,
	,
	,
	,
	Новый ОписаниеОповещения("ПослеВыбораКодаПрофессии",
	ЭтотОбъект));

КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПоШтатномуРасписаниюПриИзменении(Элемент)
	
	Запись.ДолжностьОрганизации = ПолучитьДолжностьОрганизации(Запись.ДолжностьПоШтатномуРасписанию);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВыбораКодаПрофессии(ДанныеДолжности, Параметры) Экспорт
	
	Если ДанныеДолжности = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запись.КодДолжностиЭТД = ДанныеДолжности.Код;
	Запись.НаименованиеЭТД = ДанныеДолжности.Наименование;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДолжностьОрганизации(ДолжностьПоШтатномуРасписанию)
	
	Возврат ЭТДСервер.ПолучитьДолжностьОрганизации(ДолжностьПоШтатномуРасписанию);
	
КонецФункции

#КонецОбласти

