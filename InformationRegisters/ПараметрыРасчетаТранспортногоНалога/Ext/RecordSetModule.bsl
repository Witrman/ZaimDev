#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если Количество() = 1 Тогда
		Запись = Получить(0);
		
		Если НЕ Запись.ВидТранспортногоСредства = Перечисления.ВидыТранспортныхСредств.Мотоцикл Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмеренияНалоговойБазы");
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
		Возврат;
	Иначе
		
		Если ДанныеЗаполнения = Неопределено Тогда
			ДанныеЗаполнения = Новый Структура; 			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ ДанныеЗаполнения.Свойство("Организация") Тогда
			
			ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
			ДанныеЗаполнения.Вставить("Организация", ОрганизацияПоУмолчанию);			
		КонецЕсли; 		
	КонецЕсли;  

КонецПроцедуры

#КонецЕсли