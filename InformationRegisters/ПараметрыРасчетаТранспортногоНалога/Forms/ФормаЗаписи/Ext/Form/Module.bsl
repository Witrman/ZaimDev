
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОсновнаяОрганизация = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	СписокКодовЕдиницИзмеренияТС = ПолучитьСписокКодовЕдиницИзмеренияТС();
	Для Каждого ЕдиницаИзмеренияТС Из СписокКодовЕдиницИзмеренияТС Цикл
		Элементы.ЕдиницаИзмеренияНалоговойБазы.СписокВыбора.Добавить(ЕдиницаИзмеренияТС.Значение, ЕдиницаИзмеренияТС.Представление);
	КонецЦикла;
	
	ЗаписьВыбран = РеквизитФормыВЗначение("Запись");
	Если НЕ ЗаписьВыбран.Выбран() Тогда
		Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда 
			Запись.Организация = ОсновнаяОрганизация;
		КонецЕсли;
		Если Запись.ВидТранспортногоСредства.Пустая() Тогда
			Запись.ВидТранспортногоСредства = Перечисления.ВидыТранспортныхСредств.ЛегковойАвтомобиль;
		КонецЕсли;  				
	КонецЕсли;
	
	Если Запись.СтранаПроизводитель = Справочники.КлассификаторСтранМира.Казахстан ИЛИ Запись.СтранаПроизводитель.Код = "398" Тогда
		ПроизводствоРК = 1;
	Иначе
		ПроизводствоРК = 0;
	КонецЕсли;   

	УправлениеФормой(ЭтаФорма);
	
	УстановитьВидимостьДатыВвозаНаТерриториюРК(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВидТранспортногоСредстваПриИзменении(Элемент)
	
	Если Запись.ВидТранспортногоСредства = ПредопределенноеЗначение("Перечисление.ВидыТранспортныхСредств.Мотоцикл") Тогда
		
		КодЕдиницыИзмерения = Элементы.ЕдиницаИзмеренияНалоговойБазы.СписокВыбора.НайтиПоЗначению("214");
		Если КодЕдиницыИзмерения <> Неопределено Тогда
			Запись.ЕдиницаИзмеренияНалоговойБазы = КодЕдиницыИзмерения.Значение;
		Иначе
			Запись.ЕдиницаИзмеренияНалоговойБазы = "";
		КонецЕсли;
		
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОсновноеСредствоПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.ОсновноеСредство) Тогда
		ДатаВыпуска = ОсновноеСредствоПриИзмененииНаСервереБезКонтекста(Запись.ОсновноеСредство);
		Если ЗначениеЗаполнено(ДатаВыпуска) Тогда
			Запись.ДатаВыпуска = ДатаВыпуска;
		Иначе
			Запись.ДатаВыпуска = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизведеноПриИзменении(Элемент)
	ПроизведеноПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СтранаПроизводительПриИзменении(Элемент)
	СтранаПроизводительПриИзмененииНаСервере();
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Объект   = Форма.Запись;
	Элементы = Форма.Элементы;
	
	//сначала установим видимость для всех элементов формы, как Ложь
	Элементы.ОбъемДвигателя.Видимость                  = Ложь;
	Элементы.Грузоподъемность.Видимость                = Ложь;
	Элементы.КоличествоПосадочныхМест.Видимость        = Ложь;
	Элементы.МощностьВКиловаттах.Видимость             = Ложь;
	Элементы.МощностьВЛошадиныхСилах.Видимость         = Ложь;
	Элементы.ПроизводствоСтранСНГ.Видимость            = Ложь;
	Элементы.ЭксплуатацияВРКдо1апреля1999г.Видимость   = Ложь;
	Элементы.ГруппаСведенияОПроизводителе.Видимость	   = Ложь;
	Элементы.ГруппаПараметрыТранспортногоСредстваПоНалоговойБазе.Видимость = Ложь;
	
	Если Объект.ВидТранспортногоСредства = ПредопределенноеЗначение("Перечисление.ВидыТранспортныхСредств.ЛегковойАвтомобиль") Тогда
		Элементы.ОбъемДвигателя.Видимость                  = Истина;
		Элементы.ПроизводствоСтранСНГ.Видимость            = Истина;
		Элементы.ГруппаСведенияОПроизводителе.Видимость	   = Истина;
		
		Объект.ПроизводствоСтранСНГ           			   = Ложь;
		Объект.ЭксплуатацияВРКдо1апреля1999г               = Ложь;

	ИначеЕсли Объект.ВидТранспортногоСредства = ПредопределенноеЗначение("Перечисление.ВидыТранспортныхСредств.ГрузовойАвтомобиль") Тогда
		Элементы.Грузоподъемность.Видимость                = Истина;
		Элементы.ПроизводствоСтранСНГ.Видимость            = Истина;
		
		Объект.ЭксплуатацияВРКдо1апреля1999г   = Ложь;

	ИначеЕсли Объект.ВидТранспортногоСредства = ПредопределенноеЗначение("Перечисление.ВидыТранспортныхСредств.Автобус") Тогда
		Элементы.КоличествоПосадочныхМест.Видимость        = Истина;
		
		Объект.ПроизводствоСтранСНГ            = Ложь;
		Объект.ЭксплуатацияВРКдо1апреля1999г   = Ложь;

	ИначеЕсли Объект.ВидТранспортногоСредства = ПредопределенноеЗначение("Перечисление.ВидыТранспортныхСредств.Мотоцикл") 	Тогда
		Элементы.ПроизводствоСтранСНГ.Видимость            = Истина;
		Элементы.ГруппаПараметрыТранспортногоСредстваПоНалоговойБазе.Видимость = Истина;
		
		Объект.ПроизводствоСтранСНГ            = Ложь;
		Объект.ЭксплуатацияВРКдо1апреля1999г   = Ложь;
		
	ИначеЕсли Объект.ВидТранспортногоСредства = ПредопределенноеЗначение("Перечисление.ВидыТранспортныхСредств.ЖелезнодорожныйТяговыйПодвижнойСостав") 	
		ИЛИ Объект.ВидТранспортногоСредства = ПредопределенноеЗначение("Перечисление.ВидыТранспортныхСредств.МотороВагонныйПодвижнойСостав") Тогда
		Элементы.МощностьВКиловаттах.Видимость             = Истина;
		
		Объект.ПроизводствоСтранСНГ            = Ложь;
		Объект.ЭксплуатацияВРКдо1апреля1999г   = Ложь;
		
	ИначеЕсли Объект.ВидТранспортногоСредства = ПредопределенноеЗначение("Перечисление.ВидыТранспортныхСредств.ЛетательныйАппарат") Тогда  
		Элементы.ЭксплуатацияВРКдо1апреля1999г.Видимость = Истина;
		Элементы.МощностьВКиловаттах.Видимость             = Истина;
				
		Объект.ПроизводствоСтранСНГ            = Ложь;
		
	ИначеЕсли Объект.ВидТранспортногоСредства = ПредопределенноеЗначение("Перечисление.ВидыТранспортныхСредств.Катер") Тогда
		Элементы.МощностьВЛошадиныхСилах.Видимость         = Истина;
		
		Объект.ПроизводствоСтранСНГ            = Ложь;
		Объект.ЭксплуатацияВРКдо1апреля1999г   = Ложь;

	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОсновноеСредствоПриИзмененииНаСервереБезКонтекста(ОсновноеСредство)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновноеСредство, "ДатаВыпуска");

КонецФункции

&НаСервере
Процедура ПроизведеноПриИзмененииНаСервере()
		
	Если ПроизводствоРК = 1 Тогда
		Запись.СтранаПроизводитель = Справочники.КлассификаторСтранМира.Казахстан;
		//очистим дату ввоза на РК
		Запись.ДатаВвозаНаТерриториюРК = Дата(1,1,1);
		Модифицированность = Истина;
	Иначе 
		
		КодСтраны = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.СтранаПроизводитель, "Код").Код;
		
		Если ЗначениеЗаполнено(Запись.СтранаПроизводитель) И	
			(Запись.СтранаПроизводитель = Справочники.КлассификаторСтранМира.Казахстан Или КодСтраны = "398") Тогда
		
			Запись.СтранаПроизводитель = Справочники.КлассификаторСтранМира.ПустаяСсылка();
		
		КонецЕсли;
	КонецЕсли;
					
	УстановитьВидимостьДатыВвозаНаТерриториюРК(ЭтаФорма);
	
КонецПроцедуры    

&НаСервере
Процедура СтранаПроизводительПриИзмененииНаСервере()
	
	КодСтраны = "";
	Если ЗначениеЗаполнено(Запись.СтранаПроизводитель) Тогда
		КодСтраны = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.СтранаПроизводитель, "Код").Код;
	КонецЕсли;

	//контроль соответствия переключателя и значения страны-производителя
	Если Запись.СтранаПроизводитель = Справочники.КлассификаторСтранМира.Казахстан Или КодСтраны = "398" Тогда
		ПроизводствоРК = 1;
		//очистим дату ввоза на РК
		Запись.ДатаВвозаНаТерриториюРК = Дата(1,1,1);
	Иначе
		ПроизводствоРК = 0;
	КонецЕсли;	
	
	УстановитьВидимостьДатыВвозаНаТерриториюРК(ЭтаФорма, КодСтраны);    
	
КонецПроцедуры     

&НаСервереБезКонтекста
Процедура УстановитьВидимостьДатыВвозаНаТерриториюРК(Форма, КодСтраны = "")
	
	Элементы = Форма.Элементы;
	Запись = Форма.Запись;
	
	Если Не ЗначениеЗаполнено(КодСтраны) И ЗначениеЗаполнено(Запись.СтранаПроизводитель) Тогда
		КодСтраны = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.СтранаПроизводитель, "Код").Код;
	КонецЕсли;	
		
	Элементы.ДатаВвозаНаТерриториюРК.Видимость = (Запись.ВидТранспортногоСредства = Перечисления.ВидыТранспортныхСредств.ЛегковойАвтомобиль 
										И Не (Запись.СтранаПроизводитель = Справочники.КлассификаторСтранМира.Казахстан Или КодСтраны = "398"));

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокКодовЕдиницИзмеренияТС()
	
	Список = Новый СписокЗначений;
	
	Список.Добавить("251", НСтр("ru='л. с.'"));
	Список.Добавить("214", НСтр("ru='кВт'"));
	
	Возврат Список;
	
КонецФункции // ПолучитьСписокКодовЕдиницИзмеренияТС()


 



