

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("ФизЛицо") Тогда
	Элементы.ФормаИзменитьАктивностьКартСчета.Видимость = Истина;

	УстановитьУсловноеОформление();
	КонецЕсли;

	Элементы.ФормаИзменитьАктивностьКартСчета.Доступность = РольДоступна("ДобавлениеИзменениеДанныхФизическихЛиц") ИЛИ Пользователи.ЭтоПолноправныйПользователь();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ИзменитьАктивностьКартСчета(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	АктивностьСчета = ТекущиеДанные.АктивностьСчета;
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ФизЛицо", ТекущиеДанные.ФизЛицо);
	ПараметрыОтбора.Вставить("Банк", ТекущиеДанные.Банк);
	
	ИзменитьАктивностьКартСчетаНаСервере(ПараметрыОтбора, АктивностьСчета);
	
	Элементы.Список.Обновить();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ИзменитьАктивностьКартСчетаНаСервере(ПараметрыОтбора, АктивностьСчета)
	
	НаборЗаписей = РегистрыСведений.КартСчетаФизЛиц.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ФизЛицо.Установить(ПараметрыОтбора.ФизЛицо, Истина);
	НаборЗаписей.Отбор.Банк.Установить(ПараметрыОтбора.Банк, Истина);
	НаборЗаписей.Прочитать();
	Для Каждого Запись Из НаборЗаписей Цикл
		Запись.АктивностьСчета = Не АктивностьСчета;
	КонецЦикла;
	НаборЗаписей.Записать();
	
КонецПроцедуры


	
&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Список.УсловноеОформление.Элементы.Очистить();
	Элемент = Список.УсловноеОформление.Элементы.Добавить();
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("АктивностьСчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
		
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт (, , Истина));

КонецПроцедуры
