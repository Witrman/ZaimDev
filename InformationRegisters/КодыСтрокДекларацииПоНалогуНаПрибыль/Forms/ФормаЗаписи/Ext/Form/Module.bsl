
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МакетКодовСтрок = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КодыСтрокНалоговыхДеклараций");

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	МакетКодовСтрок = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КодыСтрокНалоговыхДеклараций");
	
	ОбластьСтрок = МакетКодовСтрок.Области.Найти("НалогНаПрибыль_Расходы");

	НаименованиеСтроки = РегламентированнаяОтчетность.ПолучитьНаименованиеСтрокиКлассификатораПоКоду(МакетКодовСтрок, ОбластьСтрок, ТекущийОбъект.КодСтроки);
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.ФормаВыбораИзКлассификатора") Тогда
		Если ВыбранноеЗначение <> Неопределено Тогда
			Запись.КодСтроки = ВыбранноеЗначение;
			Модифицированность = Истина;
		КонецЕсли;
		
		НаименованиеСтрокиПриИзмененииСервер(Запись.КодСтроки);
		
	КонецЕсли;	
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	РабочаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Если Запись.Период > РабочаяДата Тогда
		
		ТекстВопроса		 = НСтр("ru='Вы действительно хотите ввести данные" + Символы.ПС + "на будущую дату?'");
		Режим 	    		 = РежимДиалогаВопрос.ДаНет;
		КнопкаПоУмолчанию	 = КодВозвратаДиалога.Нет;
		ПараметрыОповещения	 = Новый Структура("РабочаяДата", РабочаяДата);
		Оповещение  		 = Новый ОписаниеОповещения("ПослеИзмененияПериода", ЭтотОбъект, ПараметрыОповещения);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0, КнопкаПоУмолчанию);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КодСтрокиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	
	Если ЗначениеЗаполнено(Запись.ВидДоходаРасхода) И ТипЗнч(Запись.ВидДоходаРасхода) = Тип("СправочникСсылка.Доходы") Тогда
		ИмяСекции = "НалогНаПрибыль_Доходы";
	ИначеЕсли ЗначениеЗаполнено(Запись.ВидДоходаРасхода) И ТипЗнч(Запись.ВидДоходаРасхода) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
		ИмяСекции = "НалогНаПрибыль_Расходы";
	Иначе
		ИмяСекции = "НалогНаПрибыль";
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ИмяМакета"			  , "КодыСтрокНалоговыхДеклараций");
	ПараметрыФормы.Вставить("ИмяСекции"			  ,	ИмяСекции);
	ПараметрыФормы.Вставить("ПолучатьПолныеДанные", Ложь);
	ПараметрыФормы.Вставить("ТекущийКодСтроки"	  , ?(НЕ ЗначениеЗаполнено(Запись.КодСтроки), Неопределено, СокрЛП(Запись.КодСтроки)));

	ОткрытьФорму("ОбщаяФорма.ФормаВыбораИзКлассификатора", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КодСтрокиПриИзменении(Элемент)
	
	Запись.КодСтроки = РегламентированнаяОтчетностьКлиентСервер.ОбрезатьКодСтроки(Запись.КодСтроки);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект.ВидДоходаРасхода) Тогда
		ТекстСообщения = НСтр(
		"ru = 'Для корректного сбора Декларации по КПН рекомендуется заполнить поле ""Вид дохода""!'"
		);
		
		ОбщегоНазначения.СообщитьПользователю(
		ТекстСообщения, , "Запись.ВидДоходаРасхода", );				
		
	КонецЕсли;
	
	 
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПослеИзмененияПериода(Результат, ПараметрыОповещения) Экспорт
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Запись.Период = ПараметрыОповещения.РабочаяДата;
	КонецЕсли;	
КонецПроцедуры	


&НаСервере
Процедура НаименованиеСтрокиПриИзмененииСервер(КодСтроки)
	
	ОбластьСтрок = МакетКодовСтрок.Области.Найти("НалогНаПрибыль_Расходы");

	НаименованиеСтроки = РегламентированнаяОтчетность.ПолучитьНаименованиеСтрокиКлассификатораПоКоду(МакетКодовСтрок, ОбластьСтрок, КодСтроки);

КонецПроцедуры



