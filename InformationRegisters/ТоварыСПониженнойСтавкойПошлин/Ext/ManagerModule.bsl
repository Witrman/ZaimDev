#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ЗаполнитьСписокПереченьИзъятий()  Экспорт	
	
	СписокТНВЭДПереченьИзъятий = Новый СписокЗначений;
	ПереченьИзъятий =  ПолучитьАктуальныйПереченьИзъятий();
	СписокТНВЭДПереченьИзъятий.ЗагрузитьЗначения(ПереченьИзъятий);
	
	Возврат СписокТНВЭДПереченьИзъятий;
	
КонецФункции

Функция ПолучитьМакетПереченьИзъятий() Экспорт
	
	Макет = Неопределено;
	Попытка
		Макет = ЭСФСерверПовтИсп.ОбработкаОбменЭСФ().ПолучитьМакет("ПереченьИзъятий");
	Исключение
	КонецПопытки;
	
	Возврат Макет;	
			
КонецФункции

Функция ПолучитьАктуальныйПереченьИзъятий()
	
	Результат = Новый Массив;
	Макет = Неопределено;
	Попытка
		Макет = ЭСФСерверПовтИсп.ОбработкаОбменЭСФ().ПолучитьМакет("ПереченьИзъятий");
	Исключение
	КонецПопытки;
	
	Если Макет = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначениеИзСтрокиXML(Макет.ПолучитьТекст()).ВыгрузитьКолонку("КодТНВЭД");

КонецФункции

Процедура ЗаполнитьТоварыИзПеречняИзъятия(Дата,Очищать = Ложь)  Экспорт
	
	ПереченьИзъятий =  ПолучитьАктуальныйПереченьИзъятий();
	
	СписокТНВЭДПереченьИзъятий = Новый СписокЗначений();
	
	СписокТНВЭДПереченьИзъятий.ЗагрузитьЗначения(ПереченьИзъятий);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СписокКодов",СписокТНВЭДПереченьИзъятий);
	Запрос.УстановитьПараметр("ОчищатьДанные",Очищать);
	Запрос.УстановитьПараметр("Дата",Дата);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТоварыСПониженнойСтавкойПошлин.Товар,
	               |	ТоварыСПониженнойСтавкойПошлин.ВходитВПеречень
	               |ПОМЕСТИТЬ ВТ_ТоварыВРегистре
	               |ИЗ
	               |	РегистрСведений.ТоварыСПониженнойСтавкойПошлин.СрезПоследних(&Дата, ) КАК ТоварыСПониженнойСтавкойПошлин
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Номенклатура.Ссылка КАК Товар
	               |ПОМЕСТИТЬ ВТ_ТоварыВПеречне
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.КодТНВЭД В(&СписокКодов)
	               |;" 
	               + ?(НЕ Очищать, "////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ТоварыВПеречне.Товар,
	               |	ИСТИНА КАК ВходитВПеречень
	               |ИЗ
	               |	ВТ_ТоварыВПеречне КАК ВТ_ТоварыВПеречне
	               |ГДЕ
	               |	НЕ ВТ_ТоварыВПеречне.Товар В
	               |				(ВЫБРАТЬ
	               |					Т.Товар
	               |				ИЗ
	               |					ВТ_ТоварыВРегистре КАК Т)",
	               "////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ТоварыВПеречне.Товар,
	               |	ИСТИНА КАК ВходитВПеречень
	               |ИЗ
	               |	ВТ_ТоварыВПеречне КАК ВТ_ТоварыВПеречне
	               |ГДЕ
	               |	НЕ ВТ_ТоварыВПеречне.Товар В
	               |				(ВЫБРАТЬ
	               |					Т.Товар
	               |				ИЗ
	               |					ВТ_ТоварыВРегистре КАК Т
	               |				ГДЕ
	               |					Т.ВходитВПеречень = ИСТИНА)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_ТоварыВРегистре.Товар,
	               |	ЛОЖЬ
	               |ИЗ
	               |	ВТ_ТоварыВРегистре КАК ВТ_ТоварыВРегистре
	               |ГДЕ
	               |	ВТ_ТоварыВРегистре.ВходитВПеречень = ИСТИНА
	               |	И НЕ ВТ_ТоварыВРегистре.Товар В
	               |				(ВЫБРАТЬ
	               |					Т.Товар
	               |				ИЗ
	               |					ВТ_ТоварыВПЕРЕЧНЕ КАК Т)");
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	НаборЗаписей = РегистрыСведений.ТоварыСПониженнойСтавкойПошлин.СоздатьНаборЗаписей();
	
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		НаборЗаписей.Отбор.Товар.Установить(СтрокаТовара.Товар);
		НаборЗаписей.Отбор.Период.Установить(Дата);
		НаборЗаписей.Прочитать();
		Если Очищать Тогда
			НаборЗаписей.Очистить();
		КонецЕсли;
		СтрокаДвижения = НаборЗаписей.Добавить();
		СтрокаДвижения.Товар             = СтрокаТовара.Товар;
		СтрокаДвижения.ВходитВПеречень   = СтрокаТовара.ВходитВПеречень;
		СтрокаДвижения.Период            = Дата;
		
		НачатьТранзакцию();
		
		Попытка
			НаборЗаписей.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбщегоНазначения.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		НаборЗаписей.Очистить();
	КонецЦикла;

КонецПроцедуры

Процедура ОчиститьРегистрТоварыСПониженнойСтавкойПошлин() Экспорт	
	
	НаборЗаписей = РегистрыСведений.ТоварыСПониженнойСтавкойПошлин.СоздатьНаборЗаписей();
	НаборЗаписей.Очистить();
		
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ОбщегоНазначения.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
		
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИБ

Процедура ОбновитьРегистрТоварыСПониженнойСтавкойПошлин() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТоварыСПониженнойСтавкойПошлин.Товар
	|ИЗ
	|	РегистрСведений.ТоварыСПониженнойСтавкойПошлин КАК ТоварыСПониженнойСтавкойПошлин
	|ГДЕ
	|	ТоварыСПониженнойСтавкойПошлин.Период = &ПустаяДата";
	
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НаборЗаписей = РегистрыСведений.ТоварыСПониженнойСтавкойПошлин.СоздатьНаборЗаписей();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей.Отбор.Товар.Установить(Выборка.Товар);
		НаборЗаписей.Прочитать();  
		
		Если НаборЗаписей.Выбран() Тогда
			НачатьТранзакцию();
			
			Попытка
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить(Метаданные.РегистрыСведений.ТоварыСПониженнойСтавкойПошлин.ПолноеИмя());
				ЭлементБлокировки.УстановитьЗначение("Товар", Выборка.Товар);
				Блокировка.Заблокировать();
				
			Для каждого Запись Из НаборЗаписей Цикл
				Запись.Товар             = Выборка.Товар;
				Запись.ВходитВПеречень   = Истина;
				Запись.Период            = '19000101';
			КонецЦикла;
			
				НаборЗаписей.ОбменДанными.Загрузка = Истина;
				НаборЗаписей.Записать();
				ЗафиксироватьТранзакцию();
			Исключение
				ТекстСообщения = НСтр("ru = 'Не удалось обработать запись регистра Товары с пониженной ставкой пошлин (Перечень изъятий) %Ссылка% по причине: %Причина%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Товар);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.ТоварыСПониженнойСтавкойПошлин, Выборка.Товар, ТекстСообщения);
				ОтменитьТранзакцию();
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
