
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступностьИзменений = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ТоварыСПониженнойСтавкойПошлин);
	
	Элементы.СписокДобавитьТоваромИзПеречня.Доступность = ДоступностьИзменений;
	Элементы.ФормаЗаполнитьТоварыИзПеречня.Доступность  = ДоступностьИзменений;
	Элементы.ФормаОчистить.Доступность                  = ДоступностьИзменений;
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаполнитьТоваромИзПеречня(Команда)
	
	ПоказатьВводДаты(Новый ОписаниеОповещения("ВводДатыЗавершение",ЭтотОбъект,Ложь),
		ТекущаяДата(), НСтр("ru='Введите дату обновления перечня изъятий'"),ЧастиДаты.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводДатыЗавершение(Результат, ОчищатьДанные) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ЗаполнитьТоваромИзПеречняНаСервере(Результат,ОчищатьДанные);
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТоварыИзПеречня(Команда)
	
	ТекстВопроса = НСтр("ru = 'Перед заполнением текущие данные регистра будут удалены. Продолжить?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаполнить", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереченьИзъятий(Команда)
	СтандартнаяОбработка = Ложь;
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		  , Истина);
	ПараметрыФормы.Вставить("ИмяМакета"			  , "ПереченьИзъятий");
	ПараметрыФормы.Вставить("ИмяСекции"			  ,	"Классификатор");
	ПараметрыФормы.Вставить("Заголовок"			  ,	"Перечень изъятий");
		
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораИзКлассификатора", ПараметрыФормы, , Истина);

КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	ТекстВопроса = НСтр("ru = 'Все записи регистра будут удалены. Продолжить?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОчистить", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПослеЗакрытияВопросаЗаполнить(Результат, Параметры) Экспорт 
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьВводДаты(Новый ОписаниеОповещения("ВводДатыЗавершение",ЭтотОбъект,Истина),
		ТекущаяДата(), НСтр("ru='Введите дату обновления перечня изъятий'"),ЧастиДаты.Дата);
	
КонецПроцедуры

&НаСервере
Процедура  ЗаполнитьТоваромИзПеречняНаСервере (Дата,ОчищатьДанные = Ложь)
	РегистрыСведений.ТоварыСПониженнойСтавкойПошлин.ЗаполнитьТоварыИзПеречняИзъятия(Дата,ОчищатьДанные);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОчистить(Результат, Параметры) Экспорт 
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьНаСервере();
	Элементы.Список.Обновить();

КонецПроцедуры

&НаСервере
Процедура ОчиститьНаСервере()
	РегистрыСведений.ТоварыСПониженнойСтавкойПошлин.ОчиститьРегистрТоварыСПониженнойСтавкойПошлин();
КонецПроцедуры

