
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда 
		Если НЕ ЗначениеЗаполнено(Запись.Пользователь) Тогда
			Запись.Пользователь = Пользователи.ТекущийПользователь();
		КонецЕсли;
	КонецЕсли;
	
	ЗначенияНастроекПользователя = ПользователиБКВызовСервераПовтИсп.ЗначенияНастроекПользователя(
		Запись.Пользователь, "ОсновнаяОрганизация,ОсновноеСтруктурноеПодразделениеОрганизации");
	
	ЗначениеОсновнойОрганизации = ЗначенияНастроекПользователя.ОсновнаяОрганизация;
	ЗначениеОсновногоСтруктурногоПодразделения = ЗначенияНастроекПользователя.ОсновноеСтруктурноеПодразделениеОрганизации;

	УстановитьПараметрыВыбораЗначения(ЭтотОбъект);
	
	ТекущаяНастройка = Запись.Настройка;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура НастройкаПриИзменении(Элемент)
	
	Если Запись.Настройка = ТекущаяНастройка Тогда 
		Возврат;
	Иначе 
		Запись.Значение = Неопределено;
	КонецЕсли;
	
	НастройкаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеПриИзменении(Элемент)
	
	Если Запись.Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация") Тогда 
		ЗначениеОсновнойОрганизации = Запись.Значение;
	КонецЕсли;
	
	Если Запись.Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.НастройкиПользователей.ОсновноеСтруктурноеПодразделениеОрганизации") Тогда 
		ЗначениеОсновногоСтруктурногоПодразделения = Запись.Значение;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура НастройкаПриИзмененииНаСервере()
	
	ЗначенияНастроекПользователя = ПользователиБКВызовСервераПовтИсп.ЗначенияНастроекПользователя(
		Запись.Пользователь, "ОсновнаяОрганизация,ОсновноеСтруктурноеПодразделениеОрганизации");
	
	ЗначениеОсновнойОрганизации = ЗначенияНастроекПользователя.ОсновнаяОрганизация;
	ЗначениеОсновногоСтруктурногоПодразделения = ЗначенияНастроекПользователя.ОсновноеСтруктурноеПодразделениеОрганизации;
	
	УстановитьПараметрыВыбораЗначения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбораЗначения(Форма)
	
	Запись   = Форма.Запись;
	Элементы = Форма.Элементы;
	
	Если Запись.Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.НастройкиПользователей.ОсновноеПодразделениеОрганизации") Тогда
		
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Форма.ЗначениеОсновнойОрганизации));
		Если ПоддержкаРаботыСоСтруктурнымиПодразделениями() Тогда
			НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Форма.ЗначениеОсновногоСтруктурногоПодразделения));
		КонецЕсли;
		
		НовыйМассив.Добавить(Новый ПараметрВыбора("ВыбиратьПодразделенияОрганизации", Истина));
		
		Элементы.Значение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);;
		
	ИначеЕсли Запись.Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.НастройкиПользователей.ОсновноеСтруктурноеПодразделениеОрганизации") Тогда
		
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Форма.ЗначениеОсновнойОрганизации));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ЯвляетсяСтруктурнымПодразделением", Истина));
		
		Элементы.Значение.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
		
	Иначе 
		
		Элементы.Значение.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
		
	КонецЕсли;
	
КонецПроцедуры
	
&НаСервереБезКонтекста
Функция ПоддержкаРаботыСоСтруктурнымиПодразделениями()
	
	Возврат ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями");
	
КонецФункции