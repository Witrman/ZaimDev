////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПодготовитьФормуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ФизЛицоПриИзменении(Элемент)
	ЭтаФорма.Заголовок = Нстр("ru = 'Гражданство физ. лица: '") + СокрЛП(Строка(Запись.ФизЛицо));
КонецПроцедуры

&НаКлиенте
Процедура ГражданствоПриИзменении(Элемент)
	ГражданствоПриИзменениеЗначения(ЭтотОбъект, Истина);	
КонецПроцедуры

&НаКлиенте
Процедура НеЯвляетсяНалоговымРезидентомРКПриИзменении(Элемент)
	
	ПризнакНеЯвляетсяНалоговымРезидентом = Запись.НеЯвляетсяНалоговымРезидентомРК;
	
	Запись.НеИмеетПравоНаПенсию                         = ПризнакНеЯвляетсяНалоговымРезидентом;
	Запись.НеПодлежитСоциальномуМедицинскомуСтрахованию = ПризнакНеЯвляетсяНалоговымРезидентом;
	Запись.НеПодлежитСоциальномуСтрахованию             = ПризнакНеЯвляетсяНалоговымРезидентом;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <ИМЯ ТАБЛИЦЫ ФОРМЫ>

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЭтаФорма.Заголовок = Нстр("ru = 'Гражданство физ. лица: '") + СокрЛП(Строка(Запись.ФизЛицо));
	
	Если Запись.Страна = Справочники.КлассификаторСтранМира.ПустаяСсылка() Тогда
		Гражданство = 1;
	ИначеЕсли Запись.Страна = Справочники.КлассификаторСтранМира.Казахстан Тогда
		Гражданство = 2;
	Иначе
		Гражданство = 3;
	КонецЕсли;
	
	ГражданствоПриИзменениеЗначения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ГражданствоПриИзменениеЗначения(Форма, ПриИзмененииЭлемента = Ложь)
	
	Элементы = Форма.Элементы;
	Гражданство = Форма.Гражданство;
	Запись = Форма.Запись;
	
	Если Гражданство = 2 Тогда // гражданин РК	
		
		Элементы.Страна.Доступность = Ложь;
		Элементы.НомерНалоговойРегистрацииВСтранеРезидентства.Доступность = Ложь;
		Элементы.НеЯвляетсяНалоговымРезидентомРК.Доступность = Ложь;
		Элементы.ИностранныйСпециалист.Доступность = Ложь;
		Элементы.НадписьРасшифровкаЯвляетсяИностраннымСпециалистом.Доступность = Ложь;
		
		Если ПриИзмененииЭлемента Тогда
			Запись.Страна = ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.Казахстан");
			Запись.НомерНалоговойРегистрацииВСтранеРезидентства = "";
			Запись.НеЯвляетсяНалоговымРезидентомРК = Ложь;
			Запись.ИностранныйСпециалист = Ложь;
		КонецЕсли; 
		
	ИначеЕсли Гражданство = 3 Тогда	// иностранный гражданин	
		
		Элементы.Страна.Доступность = Истина;                                           		
		Элементы.НомерНалоговойРегистрацииВСтранеРезидентства.Доступность = Истина;
		Элементы.НеЯвляетсяНалоговымРезидентомРК.Доступность = Истина;
		Элементы.ИностранныйСпециалист.Доступность = Истина;
		Элементы.НадписьРасшифровкаЯвляетсяИностраннымСпециалистом.Доступность = Истина;
		
		Если ПриИзмененииЭлемента Тогда
			Запись.Страна = ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.ПустаяСсылка");
		КонецЕсли; 
		
	Иначе                           // лицо без гражданства
		
		Элементы.Страна.Доступность = Ложь;                                           		
		Элементы.НомерНалоговойРегистрацииВСтранеРезидентства.Доступность = Ложь;
		Элементы.НеЯвляетсяНалоговымРезидентомРК.Доступность = Истина;
		Элементы.ИностранныйСпециалист.Доступность = Истина;
		Элементы.НадписьРасшифровкаЯвляетсяИностраннымСпециалистом.Доступность = Истина;
		
		Если ПриИзмененииЭлемента Тогда
			Запись.Страна = ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.ПустаяСсылка");
			Запись.НомерНалоговойРегистрацииВСтранеРезидентства = "";
		КонецЕсли; 
		
	КонецЕсли;
		
	Если ПриИзмененииЭлемента Тогда
		Форма.Модифицированность = Истина;
	КонецЕсли;                      
	
КонецПроцедуры

