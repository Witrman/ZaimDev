
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Параметры.Свойство("ВедущийОбъект", ОбъектВладелец);
	Если Не ЗначениеЗаполнено(ОбъектВладелец) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если ТолькоПросмотр Тогда
		Элементы.НаборЗаписей.ТолькоПросмотр = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, 
			"ФормаКомандаОК",
			"Доступность",
			Ложь);
		Элементы.ФормаКомандаОтмена.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ГруппаИнформационная.Видимость = Ложь;
		Параметры.Свойство("ДатаЗапретаИзмененияДанных", ДатаЗапрета);
	КонецЕсли;

	Для Каждого ЗаписьНабора Из Параметры.МассивЗаписей Цикл
		НоваяСтрока = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗаписьНабора);
		НоваяСтрока.ПериодЗакрыт = (НоваяСтрока.Период <= ДатаЗапрета);
	КонецЦикла;
	
	НаборЗаписей.Сортировать("Период");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ОтменаРедактирования" Тогда
		Элементы.НаборЗаписей.ЗакончитьРедактированиеСтроки(Истина);
	ИначеЕсли ИмяСобытия = "ЗавершениеРедактирования" Тогда
		ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ТекущиеДанные, Параметр.МенеджерЗаписи);
		КонецЕсли;
		Элементы.НаборЗаписей.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ НаборЗаписей

&НаКлиенте
Процедура НаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	СписокПолей = ВладелецФормы["ГражданствоФизЛицКлючСтруктуры"];
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		
		Если НоваяСтрока Тогда
			Элемент.ТекущиеДанные.ФизЛицо = ОбъектВладелец;
			Если НаборЗаписей.Количество() > 1 Тогда
				НовыйПериод = Макс(КонецДня(НаборЗаписей.Получить(НаборЗаписей.Количество() - 2).Период) + 1, КонецДня(ДатаЗапрета) + 1, НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса()));
			Иначе
				НовыйПериод = Макс(КонецДня(ДатаЗапрета) + 1, Дата(1900, 1, 1));
			КонецЕсли; 
			
			Элемент.ТекущиеДанные.Период = НовыйПериод;
			Элемент.ТекущиеДанные.Страна = ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.Казахстан");
		КонецЕсли;
		
		СтруктураДанных = Новый Структура(СписокПолей);
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Элемент.ТекущиеДанные);
		ОткрытьФорму("РегистрСведений.ГражданствоФизЛиц.Форма.РедактированиеЗаписи", Новый Структура("СтруктураДанных, ДатаЗапрета", СтруктураДанных, ДатаЗапрета), ЭтаФорма)
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПередУдалением(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Если Элемент.ТекущиеДанные.Период <= ДатаЗапрета Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РедактированиеПериодическихСведенийКлиент.УпорядочитьНаборЗаписейВФорме(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаОК(Команда)

	РедактированиеПериодическихСведенийКлиент.ОповеститьОЗавершении(ЭтаФорма, "ГражданствоФизЛиц", ОбъектВладелец);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
