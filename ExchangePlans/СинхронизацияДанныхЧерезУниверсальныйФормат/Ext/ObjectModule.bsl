#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	// Очистка неиспользуемых реквизитов и заполнение служебных
	
	Если ВариантНастройки = "ОбменУТБП" Тогда
		ПравилаОтправкиСправочников = "НеСинхронизировать";
		ПравилаОтправкиДокументов   = "НеСинхронизировать";
		ИспользоватьОтборПоОрганизациям = Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);	
	
	Попытка
		
		Если ВариантНастройки = "ОбменЗУПБП" ИЛИ ВариантНастройки = "ОбменЗУП31БП" Тогда
			Константы.ИспользоватьОбменЗарплата3Бухгалтерия3.Установить(Истина);
			Константы.ВедетсяУчетЗарплатыИКадры.Установить(Истина);
		КонецЕсли;
		
		Если ВариантНастройки = "ОбменУТБП" Тогда
			Константы.ВедетсяРозничнаяТорговля.Установить(Истина);
			Константы.ВедетсяУчетИмпортныхТоваров.Установить(Истина);
			Константы.ВестиУчетПоДоговорам.Установить(Истина);
			Константы.ИспользоватьВалютныйУчет.Установить(Истина);
			Константы.ИспользоватьКомплектациюНоменклатуры.Установить(Истина);
		КонецЕсли;
		
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка при изменении значения константы'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
		
	УстановитьПривилегированныйРежим(Ложь);	
		
	Если ПравилаОтправкиСправочников = "НеСинхронизировать" Тогда
		
		ИспользоватьОтборПоОрганизациям = Ложь;
		РежимВыгрузкиСправочников       = Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать;
		РежимВыгрузкиПриНеобходимости   = Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать;
		
	Иначе
		
		РежимВыгрузкиПриНеобходимости    = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;
		
		Если ПравилаОтправкиСправочников = "СинхронизироватьПоНеобходимости" Тогда
			РежимВыгрузкиСправочников    = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;
		Иначе
			РежимВыгрузкиСправочников    = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПравилаОтправкиДокументов = "НеСинхронизировать" Тогда
		РежимВыгрузкиДокументов = Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать;
	ИначеЕсли ПравилаОтправкиДокументов = "ИнтерактивнаяСинхронизация" Тогда
		РежимВыгрузкиДокументов = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьВручную;
	Иначе
		РежимВыгрузкиДокументов = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию;
	КонецЕсли;


	Если Не ИспользоватьОтборПоОрганизациям И Организации.Количество() <> 0 Тогда
		Организации.Очистить();
	ИначеЕсли Организации.Количество() = 0 И ИспользоватьОтборПоОрганизациям Тогда
		ИспользоватьОтборПоОрганизациям = Ложь;
	КонецЕсли;
	
	Если ПравилаОтправкиДокументов <> "АвтоматическаяСинхронизация" Тогда
		ДатаНачалаВыгрузкиДокументов = Дата(1,1,1,0,0,0);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВерсияФорматаОбмена) Тогда
		ВерсияФорматаОбмена = "1.0";
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьОбъект(ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОбъект(ДанныеЗаполнения)
	
	Если Не ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПравилаЗагрузкиПодразделений = "НеЗагружать";
	РежимВыгрузкиПриНеобходимости = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;
	
	ИспользоватьОтборПоОрганизациям = Ложь;
	ИспользоватьОтборПоСкладам = Ложь;
	
	ДатаНачалаВыгрузкиДокументов = НачалоГода(ТекущаяДата());
	ВыгружатьАналитикуПоСкладам  = Истина;
	
	ВерсияФорматаОбмена = "1.2";
	
	ПравилаОтправкиСправочников = "АвтоматическаяСинхронизация";
	ПравилаОтправкиДокументов = "АвтоматическаяСинхронизация";
	
	Если ЗначениеЗаполнено(ВариантНастройки) Тогда
		НастроитьОтборыПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура НастроитьОтборыПоУмолчанию()
	
	Если ВариантНастройки = "ОбменЗУПБП" ИЛИ ВариантНастройки = "ОбменУТБП" Тогда
		ВерсияФорматаОбмена = "1.3";
	ИначеЕсли ВариантНастройки = "ОбменЗУП31БП" Или ВариантНастройки = "ОбменУНФБП" Тогда
		ВерсияФорматаОбмена = "1.5";
	КонецЕсли;
	
	Если ВариантНастройки = "ОбменУТБП" Тогда
		
		ПравилаОтправкиСправочников = "НеСинхронизировать";
		ПравилаОтправкиДокументов = "НеСинхронизировать";
		УстанавливатьПечатьПрефиксаИБДокументов = Истина;
		
	ИначеЕсли ВариантНастройки = "ОбменУПБП" Тогда
		
		ПравилаОтправкиСправочников = "АвтоматическаяСинхронизация";
		ПравилаОтправкиДокументов = "АвтоматическаяСинхронизация";
		
	ИначеЕсли ВариантНастройки = "ОбменЗУПБП" Тогда
		
		ПравилаОтправкиСправочников = "АвтоматическаяСинхронизация";
		ПравилаОтправкиДокументов = "АвтоматическаяСинхронизация";
		
	ИначеЕсли ВариантНастройки = "ОбменЗУП31БП" Тогда
		
		ПравилаОтправкиСправочников = "АвтоматическаяСинхронизация";
		ПравилаОтправкиДокументов = "АвтоматическаяСинхронизация";
		
	ИначеЕсли ВариантНастройки = "ОбменУНФБП" Тогда
		
		ПравилаОтправкиСправочников = "АвтоматическаяСинхронизация";
		ПравилаОтправкиДокументов = "АвтоматическаяСинхронизация";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
