#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокИсключений = Новый Массив;
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыИКадры") Тогда
		СписокИсключений.Добавить(Перечисления.ВидыОперацийППИсходящее.ПеречислениеЗаработнойПлаты);
		СписокИсключений.Добавить(Перечисления.ВидыОперацийППИсходящее.ПеречислениеПенсионныхВзносов);
		СписокИсключений.Добавить(Перечисления.ВидыОперацийППИсходящее.ПеречислениеСоциальныхОтчислений);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПрочихДоходВЦеляхНалогообложения") Тогда
		СписокИсключений.Добавить(Перечисления.ВидыОперацийППИсходящее.РасчетыПоДоходуОтРазовыхВыплатСКонтрагентами);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИсполнительныеЛисты") Тогда
		СписокИсключений.Добавить(Перечисления.ВидыОперацийППИсходящее.ПеречислениеПоИсполнительнымЛистам);
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями") Тогда
		СписокИсключений.Добавить(Перечисления.ВидыОперацийППИсходящее.ОплатаСтруктурномуПодразделению);
	КонецЕсли;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВидыОперацийППИсходящее.ЗначенияПеречисления Цикл
		
		Если ТипЗнч(Параметры.СтрокаПоиска) = Тип("Строка")
			И НЕ ПустаяСтрока(Параметры.СтрокаПоиска)
			И СтрНайти(НРег(ЗначениеПеречисления.Синоним), НРег(Параметры.СтрокаПоиска)) <> 1 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Ссылка = Перечисления.ВидыОперацийППИсходящее[ЗначениеПеречисления.Имя];
		
		Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Ссылка")
			И ТипЗнч(Параметры.Отбор.Ссылка) = Тип("ПеречислениеСсылка.ВидыОперацийППИсходящее")
			И Параметры.Отбор.Ссылка <> Ссылка Тогда
			
			Продолжить;
			
		ИначеЕсли Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Ссылка")
			И ТипЗнч(Параметры.Отбор.Ссылка) = Тип("ФиксированныйМассив")
			И Параметры.Отбор.Ссылка.Найти(Ссылка) = Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если СписокИсключений.Найти(Ссылка) <> Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ДанныеВыбора.Добавить(Ссылка, ЗначениеПеречисления.Синоним);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли