
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокИсключений = Новый Массив;
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьОбъектыПростойБухгалтерии") Тогда
		СписокИсключений.Добавить(Перечисления.РежимНалогообложения.Патент);
		СписокИсключений.Добавить(Перечисления.РежимНалогообложения.ФиксированныйВычет);
	КонецЕсли;
	
	//Если Параметры.Свойство("Период") И Параметры.Период < Дата(2023, 1, 1) Тогда
	//	СписокИсключений.Добавить(Перечисления.РежимНалогообложения.РозничныйНалог);
	//КонецЕсли;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.РежимНалогообложения.ЗначенияПеречисления Цикл
		
		Если ТипЗнч(Параметры.СтрокаПоиска) = Тип("Строка")
			И НЕ ПустаяСтрока(Параметры.СтрокаПоиска)
			И СтрНайти(НРег(ЗначениеПеречисления.Синоним), НРег(Параметры.СтрокаПоиска)) <> 1 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Ссылка = Перечисления.РежимНалогообложения[ЗначениеПеречисления.Имя];
		
		Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Ссылка")
			И ТипЗнч(Параметры.Отбор.Ссылка) = Тип("ПеречислениеСсылка.РежимНалогообложения")
			И Параметры.Отбор.Ссылка <> Ссылка Тогда
			
			Продолжить;
			
		ИначеЕсли Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Ссылка")
			И ТипЗнч(Параметры.Отбор.Ссылка) = Тип("ФиксированныйМассив")
			И Параметры.Отбор.Ссылка.Найти(Ссылка) = Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если СписокИсключений.Найти(Ссылка) <> Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ДанныеВыбора.Добавить(Ссылка, ЗначениеПеречисления.Синоним);
		
	КонецЦикла;

КонецПроцедуры
