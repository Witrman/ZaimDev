#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокИсключений = Новый Массив;
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяПроизводственнаяДеятельность") Тогда
		СписокИсключений.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку);
	КонецЕсли;
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДСЗаНерезидента") Тогда
		СписокИсключений.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПоступлениеОтНерезидента);
	КонецЕсли;
	Если НЕ ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями") Тогда
		СписокИсключений.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОтСтруктурногоПодразделения);
	КонецЕсли;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ЗначенияПеречисления Цикл
		
		Если ТипЗнч(Параметры.СтрокаПоиска) = Тип("Строка")
			И НЕ ПустаяСтрока(Параметры.СтрокаПоиска)
			И СтрНайти(НРег(ЗначениеПеречисления.Синоним), НРег(Параметры.СтрокаПоиска)) <> 1 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Ссылка = Перечисления.ВидыОперацийПоступлениеТоваровУслуг[ЗначениеПеречисления.Имя];
		
		Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Ссылка")
			И ТипЗнч(Параметры.Отбор.Ссылка) = Тип("ПеречислениеСсылка.ВидыОперацийПоступлениеТоваровУслуг")
			И Параметры.Отбор.Ссылка <> Ссылка Тогда
			
			Продолжить;
			
		ИначеЕсли Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Ссылка")
			И ТипЗнч(Параметры.Отбор.Ссылка) = Тип("ФиксированныйМассив")
			И Параметры.Отбор.Ссылка.Найти(Ссылка) = Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если СписокИсключений.Найти(Ссылка) <> Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ДанныеВыбора.Добавить(Ссылка, ЗначениеПеречисления.Синоним);
		
	КонецЦикла;

	
КонецПроцедуры

#КонецЕсли